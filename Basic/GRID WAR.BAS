7 ON BRK GOTO 840:POKE65497,0
8 '
9 'SET VARIABLES
10 HSCREEN 2:PALETTE 2,15:PALETTE 4,56:PALETTE 7,52
15 DIM GC(2,10,10),GS$(2,10,10)
20 HBUFF 1,152:HBUFF 2,152
25 S$(1)="BL5U4ER14FRFRFRFD2GLGLGLGL14HU4":NS$(1)="CRUISER"
30 S$(2)="BL5UEUERER23FRFRFRFD2GLGLGLGL23HLHUOFFHU":NS$(2)="DESTROYER"
35 S$(3)="BL5UERERERER32FRFRFRFD2GLGLGLGL32HLHLHLHU":NS$(3)="BATTLESHIP"
40 S$(4)="BL5U2ER12E2R32D2R10FD6GL56HU4":NS$(4)="CARRIER"
45 S$(5)="U3R10FRFRFGLGLGL10U3"
50 S$(6)="UEER17FRFRFGLGLGL17HHU"
55 S$(7)="ERERER22FRFRFGLGLGL22HLHLH"
60 S$(8)="ER9EER22D2R5FD3L40U3"
98 '
99 'DRAW LITTLE PAGE
100 DL=106:GOSUB1000
105 HGET(160,60)-(172,72),2:HDRAW"BM166,66NE1NF1NG1H1":HGET(160,60)-(172,72),1
110 H=5:V=3:GOSUB1100:FOR TD=1 TO 300:NEXT:H=1:V=4:GOSUB1200:H=0:GOSUB1100:FOR TD=1 TO 500:NEXT:FOR H=2 TO 4:GOSUB 1200:NEXT H
115 HCOLOR 1,2:HDRAW"S2BM47,18R22F8D8L8U4H4L14G4D32F4R14E4U4L8U8R16D16G8L22H8U40E8"
120 HDRAW"BM45,52R30F8D16G8L14F24L8H24D24L8U56BF8R18F4D8G4L18U16"
125 HDRAW"BM49,86R24D8L8D40R8D8L24U8R8U40L8U8"
130 HDRAW"BM45,120R30F8D40G8L30U56BF8R18F4D32G4L18U40"
135 HDRAW"BM256,32R8D36E12F12U36R8D56L8U12H12G12D12L8U56"
140 HDRAW"BM264,70R8F16D40L8U24L24D24L8U40E16BD8BR4F12D4L24U4E12"
145 HDRAW"BM256,108R32F8D16G8L16F24L8H24D24L8U56BF8R20F4D8G4L20U16":HDRAW"S4"
150 HPRINT(8,22),"WRITTEN BY MIKE SWITZER":FOR T=1 TO 5000:NEXT T:GOSUB 1400
198 '
199 'ENTER PLAYERS NAMES
200 HCOLOR8,2:FOR P = 1 TO 2:HPRINT(2,22),"PLAYER #":HPRINT(10,22),STR$(P):HPRINT(12,22),"<NAME>":SOUND 100,5
205 HT=20:VT=22:GOSUB 1300:P$(P)=T$:GOSUB 1400:NEXT P:P=1
298 '
299 'ENTER COORDINATES
300 GOSUB1000:H=28:V=51:Y=4:GOSUB 320:HPRINT(6,6),"A":HPRINT(3,8),NS$(4):HPRINT(2,9),"(5 GRIDS)"
305 H=248:Y=3:GOSUB 320HPRINT(33,6),"B":HPRINT(29,8),NS$(3):HPRINT(29,9),"(4 GRIDS)"
310 H=46:V=107:Y=1:GOSUB 320:HPRINT(6,13),"C":HPRINT(3,15),NS$(1):HPRINT(2,16),"(2 GRIDS)"
315 H=256:Y=2:GOSUB 320:HPRINT(33,13),"D":HPRINT(29,15),NS$(2):HPRINT(29,16),"(3 GRIDS)":GOTO 325
320 'HDRAW"BM"+STR$(H)+","+STR$(V)+S$(Y):HPAINT(H+2,V),4,8:RETURN
325 L=LEN(P$(P)):HPRINT(20-(L/2),1),P$(P):Y=1
330 HPRINT(2,22),"COORDINATES FOR":HPRINT(18,22),NS$(Y):SOUND100,5
335 IF P=1THENJ=0 ELSE J=2
340 GOSUB1500:GOSUB1400
345 HPRINT(2,22),"DIRECTION <H OR V)?":SOUND100,5
350 HT=22:VT=22:GOSUB1600:IFL$="H"THEND$(P,Y)="0" ELSE D$(P,Y)="1"
355 HS(P,Y)=H:VS(P,Y)=V:DS$(P,Y)=D$(P,Y):SV=Y
398 '
399 'CHECH IF SHIP FITS
400 IF D$(P,Y)="1" THEN 425
405 IF H+Y>9 THEN 445
410 FOR CF=H TO H+Y:IF GC(P,CF,V)<>0 THEN 445
415 NEXT CF
420 GOTO 455
425 IF V+Y>9 THEN 445
430 FOR CF=V TO V+Y:IF GC(P,H,CF)<>0 THEN 445
435 NEXT CF
440 GOTO 455
445 GOSUB 1900
450 GOTO 330
455 'HDRAW"A"+D$(P,Y)+"BM"+STR$((H*12)+106)+","+STR$((V*12)+30)+S$(Y)
460 GOSUB 1700
465 GOSUB1400:IF E=1 THEN 470 ELSE Y=Y+1:IF Y<>5 THEN 330
470 E=0:HPRINT(2,22),"IS THIS PATTERN OK <Y/N>?":SOUND 100,5
475 HT=28:GOSUB1600:IFL$="N" THEN 500
480 IF P<>2 THEN P=2:HDRAW"A0":GOTO300
485 GOSUB1800:P=1:GOTO600
498 '
499 'REPLACE SHIP
500 GOSUB1400:HPRINT(0,22),"CHANGE WHICH SHIP <A>,<B>,<C>, OR <D>?"
505 HT=39:GOSUB 1600
510 IF L$="C" THEN Y=1 ELSE IF L$="D" THEN Y=2 ELSE IF L$="B" THEN Y=3 ELSE Y=4
515 HCOLOR2,2:D$(P,Y)=DS$(P,Y):H=HS(P,Y):V=VS(P,Y):SV=0
520 HDRAW"A"+D$(P,Y)+"BM"+STR$((H*12)+106)+","+STR$((V*12)+30)+S$(Y)
525 HCOLOR8,2:GOSUB 1700:GOSUB 1400:E=1:GOTO330
598 '
599 'FIRE SHOT AT GRID
600 GOSUB1400:SOUND 100,5:HPRINT(2,22),P$(P):L=LEN(P$(P))+1:HPRINT((L+2),22),"SELECT GRID"
605 IF P=1 THEN G=2:DL=176:J=0 ELSE G=1:DL=36:J=2
610 GOSUB 1500
615 IF GS$(P,H,V)="M" OR GS$(P,H,V)="H" THEN GOSUB 1900:GOTO 600
620 IF GC(G,H,V)=0 THEN GOSUB 1100:GS$(P,H,V)="M":GOTO705
625 GOSUB 1200:GS$(P,H,V)="H":K(P,GC(G,H,V))=K(P,GC(G,H,V))+1
630 IF GC(G,H,V)=1 AND K(P,1)=2 THEN 700
635 IF GC(G,H,V)=2 AND K(P,2)=3 THEN 700
640 IF GC(G,H,V)=3 AND K(P,3)=4 THEN 700
645 IF GC(G,H,V)=4 AND K(P,4)=5 THEN 700
650 GOTO 705
698 '
699 'DID SHOT SINK SHIP
700 H(P)=H(P)+1:GOSUB2000
705 FOR TD=1 TO 1000: NEXT TD:IFH(P)=4 THEN 715 ELSE IF P=1 THEN P=2 ELSE P=1
710 GOTO 600
715 GOSUB 1400:HCOLOR 8,2:HPRINT(2,21),"YOU HAVE SUNK ALL OF":HPRINT(23,21),P$(G):L=LEN(P$(G)):HPRINT(23+L,21),"S SHIPS":FOR TD=1 TO 1000:NEXT TD:GOTO 800
798 '
799 'END OF GAME
800 FOR P=1 TO 2
805 IF P=1 THEN DL=36 ELSE DL=176
810 FOR Y=1 TO 4
815 HCOLOR 1,2
820 'HDRAW"A"+DS$(P,Y)+"BM"+STR$((HS(P,Y)*12)+DL)+","+STR$((VS(P,Y)*12)+30)+S$(Y)
825 NEXT Y,P
830 HPRINT(2,23),"THIS GAME IS OVER. PRESS ANY KEY..."
835 Y$=INKEY$:IF Y$="" THEN 835
840 CLS:POKE 65496,0:END
998 '
999 'DRAW GRID
1000 HCLS2:HCOLOR 8,2
1005 FOR V=DL-6 TO DL+114 STEP 12:HLINE(V,24)-(V,144),PSET:NEXT
1010 FOR H=24 TO 144 STEP 12:HLINE(DL-6,H)-(DL+114,H),PSET:NEXT:RETURN
1098 '
1099 'MISS ROUTINE
1100 FOR R=3 TO 6 STEP 3:HCIRCLE(H*12+DL,V*12+30),R,8:NEXT R:PM=31:GOSUB1215
1105 RETURN
1198 '
1199 'HIT ROUTINE
1200 HH=H*12+DL:VH=V*12+30:HCIRCLE(HH,VH),2,4:HPAINT(HH,VH),4,4:PM=1:GOSUB1215
1205 HCIRCLE(HH,VH),2,1:HCIRCLE(HH,VH),4,1:HPAINT(HH,VH-2),1,1:GOSUB1215
1210 HCIRCLE(HH,VH),4,7:HCIRCLE(HH,VH),5,7:HPAINT(HH,VH-4),7,7:PM=31
1215 PLAY"O1L255V31":FOR M=1 TO PM:PLAY"N10N3N5N4N2N6V-":NEXT M:RETURN
1298 '
1299 'ENTER TEXT ROUTINE
1300 T$=""
1305 L$=INKEY$:IF L$="" THEN 1305
1310 IF L$=CHR$(8) THEN L$=RIGHT$(T$,1):T$=LEFT$(T$,LEN(T$)-1):HCOLOR2,2:HPRINT(HT,VT),L$:HCOLOR8,2:HT=HT-1:GOTO 1305
1315 IF L$=CHR$(13) THEN RETURN
1320 T$=T$+L$:HT=HT+1:HPRINT(HT,VT),L$:GOTO1305
1398 '
1399 'ERASE TEXT
1400 HLINE(0,176)-(319,191),PRESET,BF:RETURN
1498 '
1499 'LOCATES CURSOR WITH JOYSTICK
1500 H=DL:V=30
1505 JS=JOYSTK(0):HH=JOYSTK(J)*1.8:VV=JOYSTK(J+1)*1.8
1510 H=DL+(INT(HH/12)*12):V=30+(INT(VV/12)*12)
1515 HGET(H-6,V-6)-(H+6,V+6),2
1520 HPUT(H-6,V-6)-(H+6,V+6),1,AND
1525 HPUT(H-6,V-6)-(H+6,V+6),2,PSET
1530 IF BUTTON(J)<>1 THEN 1505
1535 H=(H-DL)/12:V=(V-30)/12:RETURN
1540 L$=INKEY$:IF L$="" THEN 1540 ELSE HPRINT(HT,VT),L$:RETURN
1598 '
1599 'INPUT COORDINATE TEXT
1600 L$=INKEY$:IF L$="" THEN 1600 ELSE HPRINT(HT,VT),L$:RETURN
1698 '
1699 'ASSIGN SHIP TO GRID #
1700 IF D$(P,Y)="1" THEN 1715
1705 GC(P,H,V)=SV:GC(P,H+1,V)=SV:IF Y=1 THEN RETURN ELSE GC(P,H+2,V)=SV:IF Y=2 THEN RETURN
1710 GC(P,H+3,V)=SV:IF Y=3 THEN RETURN ELSE GC(P,H+4,V)=SV:RETURN
1715 GC(P,H,V)=SV:GC(P,H,V+1)=SV:IF Y=1 THEN RETURN ELSE GC(P,H,V+2)=SV:IF Y=2 THEN RETURN
1720 GC(P,H,V+3)=SV:IF Y=3 THEN RETURN ELSE GC(P,H,V+4)=SV:RETURN
1798 '
1799 'DRAW BOTH GRIDS
1800 DL=36:C=4:GOSUB1000:L=LEN(P$(1)):HPRINT(11-(L/2),1),P$(1):FOR X=1TO4:ON X GOSUB1805,1810,1815,1820:NEXT X:GOTO 1825
1805 H=30:V=152:Y=5:GOSUB2100:RETURN
1810 H=48:V=152:Y=6:GOSUB2100:RETURN
1815 H=75:V=152:Y=7:GOSUB2100:RETURN
1820 H=110:V=152:Y=8:GOSUB2100:RETURN
1825 DL=176:GOSUB1005:L=LEN(P$(2)):HPRINT(28-(L/2),1),P$(2):FORX=1TO4:ON X GOSUB1830,1835,1840,1845:NEXT X:RETURN
1830 H=170:V=152:Y=5:GOSUB2100:RETURN
1835 H=188:V=152:Y=6:GOSUB2100:RETURN
1840 H=215:V=152:Y=7:GOSUB2100:RETURN
1845 H=250:V=152:Y=8:GOSUB2100:RETURN
1898 '
1899 'TRY AGAIN ROUTINE
1900 GOSUB 1400:HPRINT(2,22),"TRY AGAIN":SOUND 200,5:SOUND 100,5:FOR TD=1 TO 1000:NEXT TD:GOSUB1400:RETURN
1998 '
1999 'SUNK SHIP ROUTINE
2000 FOR X=1 TO GC(B,H,V):PM=31:GOSUB 1215:NEXT X
2005 'GOSUB 1400:HPRINT(10,22),"YOU SUNK THE":HPRINT(23,22),NS$(GC(G,H,V)):C-2:ON G GOSUB 2010,2015:RETURN
2010 ON GC(G,H,V) GOSUB 1805,1810,1815,1820:RETURN
2015 ON GC(G,H,V) GOSUB 1830,1835,1840,1845:RETURN
2098 '
2099 'DRAW SHIP AT COORDINATES
2100 HDRAW"A0BM"+STR$(H)+","+STR$(V)+S$(Y):HPAINT(H+2,V),C,8:RETURN
