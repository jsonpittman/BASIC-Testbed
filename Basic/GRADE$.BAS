10 '$$$$$$$$$$$$$$$$$$$$$$$$$$$
20 '$$      DOLLAR$ FOR      $$
30 '$$      GOOD GRADE$      $$
40 '$$  WRITTEN IN  1985 BY  $$
50 '$$   DENNIS O. DORRITY   $$
60 '$$$$$$$$$$$$$$$$$$$$$$$$$$$
70 '###########################
80 '## INITIALIZATION & MENU ##
90 '###########################
100 POKE150,41 '1200 BAUD
110 PG$="#.##":PM$="$$#.##":BL$=STRING$(30,191) 'AESTHETIC VALUES
120 BN=10.00:SA=10.00:SB=7.00:SC=4.00:SD=1.00:SF=0 'DOLLAR VALUES
130 CLS4:TL$=" DOLLAR$ FOR  GOOD GRADE$ ":FORL=1TO26:TL=ASC(MID$(TL$,L,1)):IFTL>64THENTL=TL-64
140 POKE1026+L,TL:NEXTL
150 TL$=" WRITTEN BY DENNIS O. DORRITY ":FORL=1TO30:TL=ASC(MID$(TL$,L,1)):IFTL>64THENTL=TL-64
160 POKE1504+L,TL:NEXTL
170 GOSUB520 'NAME
180 GOSUB540 'A OR VG
190 GOSUB550 'B OR S+
200 GOSUB560 'C OR S
210 GOSUB570 'D OR S-
220 GOSUB580 'F OR N
230 PRINT" 7. START OVER
240 PRINT" P. PRINT HARD COPY
250 '##########################
260 '## CALCULATION ROUTINES ##
270 '##########################
280 GA=(A*4)+(B*3)+(C*2)+D 'GRADE ADJUSTMENT
290 GV=(A*SA)+(B*SB)+(C*SC)+(D*SD)+(F*SF) 'GRADE VALUATION
300 NC=A+B+C+D+F 'NUMBER OF CLASSES
310 IFNC=0THENPRINT@384:PRINT@417:GP=0:ME=0:GOTO340
320 GP=GA/NC 'CALCULATE GPA
330 ME=GV/NC 'MONEY EARNED
340 PRINT@384," A GRADE POINT AVERAGE OF ";:PRINTUSINGPG$;GP
350 PRINT:PRINT@417,"EARNS ";:PRINTUSINGPM$;ME;
360 IFGP=4THENPRINT" + BONUS OF ";:PRINTUSINGPM$;BN
370 '####################
380 '## CHANGE ROUTINE ##
390 '####################
400 TL$=" ANY CHANGES? ENTER <1-P> "
410 FORL=1TO26:TL=ASC(MID$(TL$,L,1)):IFTL>64THENTL=TL-64
420 POKE1346+L,TL:NEXTL
430 AC$=INKEY$:IFAC$=""THEN430
440 PRINT@320,BL$; 'BLANKS OUT PROMPT
450 IFAC$="P"GOSUB620
460 AC=VAL(AC$):IFAC<1ORAC>7THEN280
470 ON AC GOSUB 520,540,550,560,570,580,130
480 GOTO280 'RECALC GPA & $$
490 '########################
500 '## INPUT SUB-ROUTINES ##
510 '########################
520 PRINT@64:PRINT@65,"1. NAME? ";:LINEINPUTSN$
530 IFLEN(SN$)<1ORLEN(SN$)>15THEN520ELSERETURN
540 PRINT@96:PRINT@97,"2. HOW MANY A OR VG? ";:LINEINPUT A$:A=VAL(A$):PRINT@117,A:RETURN
550 PRINT@128:PRINT@129,"3. HOW MANY B OR S+? ";:LINEINPUT B$:B=VAL(B$):PRINT@149,B:RETURN
560 PRINT@160:PRINT@161,"4. HOW MANY C OR S?  ";:LINEINPUTC$:C=VAL(C$):PRINT@181,C:RETURN
570 PRINT@192:PRINT@193,"5. HOW MANY D OR S-? ";:LINEINPUTD$:D=VAL(D$):PRINT@213,D:RETURN
580 PRINT@224:PRINT@225,"6. HOW MANY F OR N?  ";:LINEINPUTF$:F=VAL(F$):PRINT@245,F:RETURN
590 '#####################
600 '## PRINTER ROUTINE ##
610 '#####################
620 IF(PEEK(65314)AND1)<>1THEN650 'DETERMINES PRINTER STATUS
630 PRINT@325," PRINTER IS NOT READY ";
640 FORL=0TO999:NEXTL:RETURN
650 PRINT#-2,TAB(50)"DOLLAR$ FOR GOOD GRADE$
660 PRINT#-2:PRINT#-2,TAB(44)"NAME OF STUDENT: "SN$
670 PRINT#-2,TAB(48)"NUMBER OF 'A's OR 'VG's: "A
680 PRINT#-2,TAB(48)"NUMBER OF 'B's OR 'S+'s: "B
690 PRINT#-2,TAB(48)"NUMBER OF 'C's OR 'S's:  "C
700 PRINT#-2,TAB(48)"NUMBER OF 'D's OR 'S-'s: "D
710 PRINT#-2,TAB(48)"NUMBER OF 'F's OR 'N's:  "F
720 PRINT#-2,TAB(44)"GRADE POINT AVERAGE: ";:PRINT#-2,USINGPG$;GP
730 PRINT#-2,TAB(44)"AMOUNT EARNED: ";:PRINT#-2,USINGPM$;ME
740 IFGP=4THENPRINT#-2,TAB(56)" + ";ELSE780
750 PRINT#-2,USINGPM$;BN;:PRINT#-2," BONUS
760 PRINT#-2,TAB(59)"------
770 PRINT#-2,TAB(59);:PRINT#-2,USINGPM$;ME+BN
780 FORL=1TO5:PRINT#-2:NEXTL
790 RETURN
