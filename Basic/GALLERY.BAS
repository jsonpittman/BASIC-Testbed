10 '****************************
20 '* LOW-RES  PICTURE GALLERY *
30 '*   WITH PALETTE CONTROL   *
40 '* VERSION: 1.0     8611.02 *
50 '****************************
60 '*  (C) 1986 BY ERIC WHITE  *
70 '****************************
80 A$=INKEY$:F=FREE(PEEK(&HEB))
90 CLS0:PCLEAR8:CLEAR2000:IF PEEK(33021)=50 THEN PMODE3,5:PCLS0 ELSE PMODE 4,5:PCLS1
100 FE$="PIC"'DISKFILE EXTENSION
110 TM=3500'TIMEOUT BETWEEN PICS
120 DIMNF$(71):X=0:FORS=3TO11
130 DSKI$ PEEK(&HEB),17,S,A$,B$
140 C$=A$+LEFT$(B$,127)
150 FORN=0TO7
160 NF$=MID$(C$,N*32+1,32)
170 IFLEFT$(NF$,1)=CHR$(0)THEN210
180 IFLEFT$(NF$,1)=CHR$(255)THENN=7:S=11:GOTO210
190 IFMID$(C$,N*32+9,3)<>FE$ THEN 210
200 NF$(X)=NF$:X=X+1
210 NEXTN,S
220 SCREEN1,1
230 FORI=0TO X-1:IF NF$(I)="" THEN CLS:PRINT"THERE IS NO PICTURES ON THIS    DISK WITH THE EXTENSION -'"FE$"'":END
240 F$=LEFT$(NF$(I),8)+"/"+FE$
250 LOADM F$
260 IF PEEK(33021)=50 THEN FORT=4TO7:PALETTE T,63:PLAY"P50":NEXTT
270 FORT=1TO4:PCOPY T TO T+4:NEXTT
280 IF PEEK(33021)=50 THEN GOSUB 310
290 FORT=1TO TM:A$=INKEY$:IF A$="" THEN NEXTT ELSE T=TM:NEXTT
300 SCREEN1,1:NEXTI:GOTO 230
310 IF MID$(NF$(I),21,4)=STRING$(4,0) THEN PALETTE 4,0:PALETTE 5,12:PALETTE 6,21:PALETTE 7,63:RETURN
320 FORT=4TO7:PALETTE T,ASC(MID$(NF$(I),17+T,1)):PLAY"P50":NEXTT:RETURN
