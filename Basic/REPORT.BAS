2 '* YEARLY REPORT PROGRAM  *
4 '*      FOR USE WITH      *
6 '* HOME FINANCIAL ANALYST *
8 '*  4/85 BY DAN PIERSMA   *
10 GOTO184
12 'DIMENSION DATAFILES & DESCRIPTIONS
14 CLEAR5500:DIMA$(100),B$(100),C$(100),D$(100),E$(100),M$(12),AD$(25),AN$(25),AN(25)
16 'SET BAUD 1200 & DEFINE NONVARIABLE STRINGS
18 POKE150,41:U$=STRING$(80,"-"):BG$=STRING$(10,"."):BP$="BUDGET PLAN FOR .../.../..."
20 'READ DESCRIPTIONS
22 FORM=1TO12:READM$(M):NEXT:FORY=1TO25:READAD$(Y):NEXT
24 'ENTER DATES
26 CLS:PRINT@9,"ANNUAL REPORT":PRINT:PRINT" ENTER BEGINNING MONTH AND YEAR  AND ENDING MONTH AND YEAR"
28 PRINT:PRINTV$:INPUT" BEGINNING MONTH (MM) ";BM:INPUT" BEGINNING YEAR (YYYY)";BY$:INPUT" ENDING MONTH    (MM) ";EM:INPUT" ENDING YEAR    (YYYY)";EY$
30 EY=VAL(EY$):BY=VAL(BY$)
32 IFBM<1ORBM>12THEN26
34 IFEM<1OREM>12THEN26
36 IFBY<1984ORBY>1988THEN26
38 'CREATE REPORT TITLE
40 IFEY<(BY)OREY>1988THEN26
42 RTITLE$=M$(BM)+"-"+BY$+" TO "+M$(EM)+"-"+EY$
44 IFEM<(BM)THENEM=(EM)+12
46 'PRINT BUDGET PLANNING FORM
48 CLS:PRINT@163,"WOULD YOU LIKE TO PRINT A       BUDGET PLANNING FORM <Y/N>"
50 L$=INKEY$:IFL$="Y"THEN54
52 IFL$="N"THEN76ELSE50
54 CLS:PRINT@201,"HARDCOPY ONLY":PRINT"IF PRINTER IS READY PRESS <P>   PRESS <C> TO SKIP TO REPORTS"
56 L$=INKEY$:IFL$="C"THEN76
58 IFL$="P"THEN60ELSE56
60 CLS:PRINT@204,"PRINTING":PRINT"      BUDGET PLANNING FORM"
62 PRINT#-2,U$:PRINT#-2,TAB(28)"BUDGET PLANNING FORM":PRINT#-2,U$:PRINT#-2:PRINT#-2,BP$TAB(40)BP$:PRINT#-2
64 FORY=1TO5:GOSUB70:NEXTY
66 PRINT#-2,U$:PRINT#-2
68 FORY=6TO25:GOSUB70:NEXTY:GOTO72
70 PRINT#-2,AD$(Y)TAB(25)BG$TAB(40)AD$(Y)TAB(65)BG$:PRINT#-2:RETURN
72 PRINT#-2,CHR$(12)
74 'GENERATE REPORTS
76 CLS0:PRINT@160,"       REPORT GENERATION":PRINT"PRINT HARD COPY REPORTS <Y/N> ?":PRINT"   PRESS <E> TO END PROGRAM"
78 L$=INKEY$:IFL$="Y"THENH=1:GOTO84
80 IFL$="N"THEN84
82 IFL$="E"THENENDELSE78
84 INPUT"ENTER COST OF LIVING RATE FOR   BUDGET PROJECTIONS IN %";I:I=(I/100)+1
86 INPUT"ENTER CHECKBOOK BALANCE";CH:INPUT"ENTER SAVINGS BALANCE  ";SA:BA=SA+CH
88 FORY=1TO25:AN$(Y)=MID$(AD$(Y),1,3):NEXTY
90 TITLE$=M$(BM)+"-"+BY$:GOSUB172:FORX=0TOC-1:IFAN$(1)=MID$(B$(X),1,3)THEN92ELSE94
92 AN(1)=VAL(C$(X))
94 NEXTX
96 FORYY=(BM)TO(EM)
98 IFYY>12THEN100ELSE102
100 S=(YY)-12:TITLE$=M$(S)+"-"+EY$:GOTO104
102 TITLE$=M$(YY)+"-"+BY$
104 GOSUB172:CLS:PRINT@195,"CALCULATING ACCOUNT TOTALS"
106 FORY=2TO25
108 FORX=0TOC-1
110 IFAN$(Y)=MID$(B$(X),1,3)THENAN(Y)=AN(Y)+VAL(C$(X))ELSE112
112 NEXTX:NEXTY:NEXTYY
114 IFH=0THEN116ELSE118
116 FORY=1TO25:PRINTAD$(Y):PRINTUSING"$#####.##";AN(Y):FORT=1TO900:NEXTT:NEXTY:GOTO124
118 PRINT#-2,TAB(30)RTITLE$:PRINT#-2:PRINT#-2,U$:PRINT#-2,TAB(3)"ACCOUNT NUMBER"TAB(37)"CREDIT"TAB(52)"DEBIT":PRINT#-2,U$:PRINT#-2
120 FORY=1TO5:PRINT#-2,AD$(Y)TAB(35):PRINT#-2,USING"$#####.##";AN(Y):NEXTY
122 FORY=6TO25:PRINT#-2,AD$(Y)TAB(50):PRINT#-2,USING"$#####.##";AN(Y):NEXTY
124 FORY=1TO5:TI=AN(Y)+TI:NEXTY
126 FORY=6TO25:TE=AN(Y)+TE:NEXTY
128 CB=TI-TE:IFH=0THEN130ELSE140
130 CLS:PRINT@160,"TOTAL INCOME          $";TI:PRINT"TOTAL EXPENSE         $";TE:PRINT"YEAR END CASH BALANCE $";CB
132 PRINT"CHECKBOOK BALANCE     $";CH:PRINT"SAVINGS BALANCE       $";SA:PRINT"TOTAL CASH ON HAND    $";BA
134 IFINT(BA)=INT(CB)THEN136ELSE138
136 PRINT" ******STATEMENT BALANCES******":FORT=1TO1500:NEXTT:GOTO154
138 PRINT"STATEMENT OUT OF BALANCE - REDO":END
140 PRINT#-2:PRINT#-2,"BEGINNING BALANCE & INCOME"TAB(35):PRINT#-2,USING"$#####.##";TI:PRINT#-2,"TOTAL EXPENSE"TAB(35):PRINT#-2,USING"$#####.##";TE
142 PRINT#-2,"END OF YEAR BALANCE"TAB(35):PRINT#-2,USING"$#####.##";CB
144 PRINT#-2:PRINT#-2,"CHECKBOOK BALANCE"TAB(35):PRINT#-2,USING"$#####.##";CH:PRINT#-2,"SAVINGS BALANCE"TAB(35):PRINT#-2,USING"$#####.##";SA:PRINT#-2,"CASH BALANCE ON HAND"TAB(35):PRINT#-2,USING"$#####.##";BA
146 IFINT(BA)=INT(CB)THEN148ELSE150
148 PRINT#-2:PRINT#-2,TAB(27)"****STATEMENT BALANCES****":PRINT#-2,CHR$(12):GOTO154
150 IFH=1THENPRINT#-2,"********STATEMENT DOES NOT BALANCE. PLEASE FIND YOUR ERROR AND REDO********":END
152 'FORMULATE BUDGET PROJECTIONS
154 A=(EM)-(BM-1)
156 FORY=2TO25:AN(Y)=AN(Y)/A:AN(Y)=AN(Y)*I:NEXTY
158 IFH=0THEN160ELSE162
160 CLS:PRINT"MONTHLY BUDGET PROJECTIONS":FORY=2TO25:PRINTAD$(Y):PRINTUSING"$#####.##";AN(Y):FORT=1TO900:NEXTT:NEXTY:GOTO168
162 PRINT#-2,TAB(28)"MONTHLY BUDGET PROJECTIONS":PRINT#-2:PRINT#-2,U$:PRINT#-2,TAB(3)"ACCOUNT NUMBER"TAB(37)"CREDIT"TAB(52)"DEBIT":PRINT#-2,U$:PRINT#-2
164 FORY=2TO5:PRINT#-2,AD$(Y)TAB(35):PRINT#-2, USING"$#####.##";AN(Y):NEXTY
166 FORY=6TO25:PRINT#-2,AD$(Y)TAB(50):PRINT#-2, USING"$#####.##";AN(Y):NEXTY:PRINT#-2:PRINT#-2,"MULTIPLIED BY"I" FOR RISE IN COST OF LIVING":PRINT#-2,CHR$(12)
168 END
170 'LOAD FILES FROM DISK SUBROUTINE
172 FORX=0TOC-1:A$(X)="":B$(X)="":C$(X)="":D$(X)="":E$(X)="":NEXTX:CLS:PRINT@140,"LOADING":PRINT@203,TITLE$:PRINT@267,"DATA FILE"
174 OPEN "I", #1, TITLE$
176 X=0
178 INPUT #1,A$(X),B$(X),C$(X),D$(X),E$(X):X=X+1
180 IFEOF(1)=-1THEN182ELSE178
182 CLOSE #1:C=X:RETURN
184 PCLEAR1:GOTO14
186 DATA JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC,100 CASH BALANCE,110 EARNED INCOME,120 INTEREST INCOME,130 MISC. TAXABLE INCOME,140 NON-TAXABLE INCOME,200 FEDERAL WITHOLDING,201 FICA,202 STATE WITHOLDING,203 LOCAL WITHOLDING
188 DATA 210 GROCERY EXPENSE,220 WATER,230 MORTGAGE,240 GAS & ELECTRIC,250 TELEPHONE,260 CAR EXPENSE,270 INSURANCE
190 DATA 280 HOUSE REPAIRS,281 ANNUITY,282 MISC. EXPENSE,283 LOAN PAYMENTS,300 MEDICAL EXPENSE,310 CONTRIBUTIONS,320 INTEREST PAID,330 TAXES PAID,340 MISC. (TAX DED.)
