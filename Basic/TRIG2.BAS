0 ' COPYRIGHT 1989 FALSOFT, INC
10 REM LISTING 2 - MULTIPLE STARS
20 PCLEAR4:REM RESERVE GRAPHICS MEMORY
30 PMODE4,1:REM SET UP HIGH RESOLUTION
40 COLOR0,1:REM SET BACKGROUND=WHITE AND FOREGROUND=BLACK
50 PCLS:REM CLEAR GRAPHICS SCREEN
53 CLS:INPUT"AMOUNT OF ROTATION (1-36)";R
55 IFR<1ORR>36THEN53
60 SCREEN1,1: REM TURN ON GRAPHICS SCREEN
70 V=57.29577951:REM DEGREES TO RADIANS CONVERSION
75 FORA=0TO72STEPR
80 X1=95*COS((90+A)/V)+128:REM X-VALUE AT POINT 1
90 Y1=-95*SIN((90+A)/V)+96:REM Y-VALUE AT POINT 1
100 X2=95*COS((162+A)/V)+128:REM X-VALUE AT POINT 2
110 Y2=-95*SIN((162+A)/V)+96:REM Y-VALUE AT POINT 2
120 X3=95*COS((234+A)/V)+128:REM X-VALUE AT POINT 3
130 Y3=-95*SIN((234+A)/V)+96:REM Y-VALUE AT POINT 3
140 X4=95*COS((306+A)/V)+128:REM X-VALUE AT POINT 4
150 Y4=-95*SIN((306+A)/V)+96:REM Y-VALUE AT POINT 4
160 X5=95*COS((378+A)/V)+128:REM X-VALUE AT POINT 5
170 Y5=-95*SIN((378+A)/V)+96:REM Y-VALUE AT POINT 5
180 LINE(X1,Y1)-(X3,Y3),PSET:REM DRAW STAR LEG 1-3
190 LINE-(X5,Y5),PSET:REM DRAW STAR LEG 3-5
200 LINE-(X2,Y2),PSET:REM DRAW STAR LEG 5-2
210 LINE-(X4,Y4),PSET:REM DRAW STAR LEG 2-4
220 LINE-(X1,Y1),PSET:REM DRAW STAR LEG 4-1
225 NEXTA
230 GOTO230:REM LOOP TO ADMIRE CREATION
