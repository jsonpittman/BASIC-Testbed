10 PCLEAR1:CLEAR5000:XB=500:GOTO50 ' *** INITIALIZE (XB -> DELAY TIME)
20 POKE65494,0:OPEN"D",#1,"HELP/SCN",17 ' *** STORE NAME OF TEST, TEST OR PRACTICE (TP) & DATE IN HELP/SCN RECORD NO. 1
30 FIELD#1,12AS A$,2AS B$,3AS C$
40 LSET A$=F$:LSET B$="0"+CHR$(TP):LSET C$=DA$:PUT#1,1:CLOSE#1:RETURN ' *** STORE NAME OF FILE
50 DIM W$(3,50),A$(3,100),S$(100),D$(68),B$(6)
60 Z1$="      ##  %                   %":Z4$=STRING$(32," ")
70 Z$="### % % #  %                   %"
100 GOSUB3110:PRINT@43,"enter date: YR/MO/DA";:PRINT@119,"--/--/--";:PRINT@119,"";:LINEINPUT"";DA$:SOUND200,1 ' *** ENTER DATE ROUTINE
110 S1=INSTR(DA$,"/"):S2=INSTR(4,DA$,"/"):IF S1=3AND S2=6THEN120ELSE PRINT@454,"wrong format ...";:SOUND120,2:GOSUB3210:GOTO100
120 YR=VAL(MID$(DA$,1,2)):MO=VAL(MID$(DA$,4,2)):DA=VAL(MID$(DA$,7,2)):DA$=CHR$(YR)+CHR$(MO)+CHR$(DA)
130 PRINT@454,USING"VERIFY ##/##/##";YR,MO,DA;:GOSUB3010:IF Y$="Y"OR Y$=CHR$(13)THEN500ELSE100
500 GOSUB3110:I=0:A$="ENTER THE WORD":GOSUB2010:A$="THEN SLASH":GOSUB2010:A$="THEN THE LEVVEL OF DIFFICULTY":GOSUB2010:A$="AS FOLLOWS":GOSUB2010 ' *** BEGIN "ENTER WORDS" ROUTINE
510 GOSUB3110:PRINT"enter: WORD/'LEVEL'";:XB=800:GOSUB3210
520 I=I+1:IF I>50THEN I=50:GOSUB3110:A$="THAT WAS FIFTY":PRINT@264,A$;:GOSUB2010:Y$="S":GOTO600
530 GOSUB3110:LINE INPUT"enter word: ";D$:IF D$=""THEN530ELSE FOR D=1 TO LEN(D$):IF MID$(D$,D,1)="/"THEN 540 ELSE NEXT D
540 L$=MID$(D$,D+1):W$(1,I)=LEFT$(D$,D-1):W$(2,I)=W$(1,I):A$=W$(2,I):XB=40*LEN(D$):GOSUB2020
550 IF LEN(L$)<1THEN PRINT@37,"level: ";:LINEINPUT"";L$
560 W$(3,I)=STRING$(11,CHR$(0)):MID$(W$(3,I),4,1)=CHR$(VAL(L$))
570 PRINT W$(1,I);" =":PRINT"        ";W$(2,I);:IF X8=1THEN XB=700:GOSUB3210:RETURN
580 PRINT@480,"<enter> <bar> <clr> <r> <s> <h>";
590 GOSUB3010:IF Y$=CHR$(12)THEN530ELSE IF Y$="H"THEN HS=8:HE=12:GOSUB4010:CLS:PRINT"verify:";:GOTO570
600 IF Y$<>"S"AND Y$<>"R"THEN610ELSE NN=I:FOR I=1TO NN:MID$(W$(3,I),5,3)=DA$:NEXT I:IF Y$="S"THEN910ELSE710
610 IF Y$=CHR$(32)THEN630
620 IF Y$=CHR$(13)THEN520ELSE590
630 PRINT@352,Z4$;Z4$;Z4$;:PRINT@352,"";:LINE INPUT"new sound: ";A$:IF A$=""THEN630ELSE W$(2,I)=A$:GOSUB2010:GOTO570
700 ' *** REVIEW WORDS SUBROUTINE
710 V=0:Z=2:UNLOAD:FOR L=0TO9 ' *** V=0 --> VOICE 'OFF'; Z=2 --> DON'T REPRONOUNCE FOR <UP> AT LINE 2090
720 X8=0:GOSUB3110:PRINT"review test words";:IF L<0THEN SOUND100,1:L=0 'V=0 SETS VOICE 'OFF'
730 FOR P=1 TO 10:I=10*L+P:IF I>NN THEN770
740 PRINT@32*(P+2),USING" ### %                   %";I,W$(1,I)
750 IF V=1THEN A$=W$(2,I):GOSUB2010 'PRONOUNCE IF V=1 --> VOICE 'ON'_
760 NEXT P
770 PRINT@484,"<e/d/s/c/v> <UP> <DN> <h>";
780 GOSUB3010:IF Y$="D"THEN B$="DELETE":GOSUB1010:GOTO720ELSE IF Y$="V"THEN V=ABS(FIX(V/2-1)):GOTO730 ' V --> TOGGLE VOICE ON/OFF
790 IF Y$="S"THEN910ELSE IF Y$=CHR$(95)THEN L=L-1:GOTO720ELSE IF Y$="E"THEN I=NN:Z=1:GOTO510ELSE IF Y$=CHR$(91)THEN NEXT L:GOTO910ELSE IF Y$="C"THEN X8=1:B$="CHANGE":GOSUB1010:GOSUB630:GOTO720
800 IF Y$="H"THEN HS=1:HE=7:GOSUB4010:GOTO720ELSE780
900 ' *** ENTER NAME OF TEST HERE AND STORE DATA TO DISK IN HELP/SCN ; THEN STORE TEST TO DISK
910 GOSUB3110:PRINT@270,"--------/TST";:PRINT@311,"(assumed)";:PRINT@256,"";:LINE INPUT"name of test: ";F$:F$=F$+"/TST"
920 PRINT@352,"test OR practice?";:PRINT@389,"<1> test";:PRINT@421,"<2> practice";:GOSUB3010:TP=VAL(Y$):IF TP<1OR TP>2THEN SOUND100,3:GOTO920
930 GOSUB20:OPEN"D",#1,F$,51
940 FIELD#1,20 AS A$,20 AS B$,11 AS C$
950 FOR I=1TO NN
960 LSET A$=W$(1,I):LSET B$=W$(2,I):LSET C$=W$(3,I)
970 PUT#1,I:NEXT I:CLOSE#1
980 CLS4:RUN"TAKETEST"
1000 ' *** DELETE AN ENTRY SUBROUTINE
1010 PRINT@479,Z4$;:A$="ENTER NUMBER TO "+B$:XB=50*LEN(A$):GOSUB2020:PRINT@480,A$;": ";:GOSUB3010
1020 IF X8=1THEN I=VAL(Y$)+10*L:RETURN
1030 IF ASC(Y$)>57OR ASC(Y$)<48THEN RETURN
1040 NN=NN-1:Y=VAL(Y$):IF Y=0THEN Y=10
1050 Y=10*L+Y:FOR I=Y TO NN:FOR P=1TO3:W$(P,I)=W$(P,I+1) :NEXT P,I:RETURN
2000 ' *** PROGRAMMABLE TIME DELAY
2010 XB=40*LEN(A$)
2020 X=&HFF00:Y=&HFF7E:POKE X+1,52:POKE X+3,63:POKE X+35,60 ' *** SPEECH SUBROUTINE ... FROM TANDY'S SS/C MANUAL
2030 POKE 65407,34:FOR M=1 TO LEN(A$) ' *** 65407 POKE TOGGLES MULTI-PAK POSITIONS ... SEE MULTI-PAK MANUAL
2040 IF PEEK(Y)AND128=0THEN2040
2050 POKE Y,ASC(MID$(A$,M,1))
2060 NEXT M
2070 IF PEEK(Y)AND128=0THEN2070
2080 POKE Y,13:GOSUB3210:POKE65407,51:RETURN
3000 ' *** STROBE KEYBOARD FOR INPUT
3010 Y$=INKEY$
3020 Y$=INKEY$:IF Y$=""THEN3020
3030 IF Y$=CHR$(94)AND Z<>2THEN GOSUB2020 ' *** REPEAT PRONUNCIATION IF UP ARROW WAS PRESSED, EXCEPT DURING REVIEW (THEN Z=2)
3040 RETURN
3100 ' *** SCREEN CLEAR SUBROUTINE
3110 CLS:SOUND200,1:RETURN
3200 ' *** VARIABLE TIME DELAY SUBROUTINE
3210 FOR X9=1TO XB:NEXT X9:XB=500:RETURN
4000 ' *** BEGIN HELP SCREEN SUBROUTINE
4010 POKE65494,0:OPEN"D",#2,"HELP/SCN",130
4020 FIELD#2,130 AS H$
4030 FOR H=HS TO HE:POKE65494,0:GET#2,H+1:POKE65495,0 ' *** SLOW DOWN POKE FOR DISK I/O
4040 CLS8:PRINT@43,"H  E  L  P";
4050 FOR HT=6TO13:PRINT@32*HT+8,STRING$(16," ");:NEXT HT ' *** DRAWS A BOX IN THE MIDDLE OF THE SCREEN
4060 H1$=MID$(H$,1,2):IF H1$<>"  "THEN PRINT@110,USING"<%%>";H1$; ' *** DISPLAY THE COMMAND STROKE, IF ANY, ABOVE THE BOX
4070 FOR H1=1TO8:PRINT@32*(4+H1)+8,MID$(H$,16*(H1-1)+3,16);:NEXT H1 ' *** DISPLAY THE MESSAGE INSIDE THE BOX
4080 PRINT@488,"<w>AIT <r>ETURN";
4090 Y$=INKEY$:FOR X9=1TO200:Y$=INKEY$:IF Y$=""THEN NEXT X9 ' *** INTERRUPT-LIKE SUBROUTINE
4100 IF Y$="R"THEN CLOSE#2:POKE65494,0:RETURN
4110 IF Y$<>"W"THEN NEXT H:GOTO4030ELSE HT=19
4120 X9=INSTR(HT,H$," "):IF X9>HT+1THEN A$=MID$(H$,HT+1,130-HT)ELSE HT=X9+1:GOTO4120
4130 A=LEN(A$):HT=INSTR(HT,A$,"  ")+1:IF HT=1THEN4140ELSE A$=MID$(A$,1,HT-1)+MID$(A$,HT+1,A-HT):GOTO4130
4140 XB=20*LEN(A$):GOSUB2020:PRINT@504,"(other)";:GOSUB3010:GOTO4100
0 ' COPYRIGHT 1989  FALSOFT,INC
10 PCLEAR1:CLEAR9000:XB=500
12 H1$="":DN$="":F1$="":F$="":YY$="":Y$="":X9=0:Q=0:H=0:H1=0:HT=0:XH=0:HS=0:HE=0:ZZ=0:Z1=0:Z2=0:N1=0:N=0:NN=0:NT=0:K=0:L=0:M=0:P=0:PP=0:J=0:JJ=0:E=0:TG=0:RB=0:II=0:X=0:Y=0:TT=0:DR=0:L1=0:JK=0
20 POKE65494,0:OPEN"D",#1,"HELP/SCN",17
30 FIELD#1,12AS A$,2AS B$,3AS C$
40 IF Q=1THEN LSET A$=F$:LSET B$="01":LSET C$=DA$:PUT#1,1:CLOSE#1:RETURN ' *** STORE NAME OF FILE
42 GET#1,1:Z=VAL(B$) ' *** GET DATE AND ACTION CODE
44 DA$=C$:YR=ASC(MID$(C$,1,1)):MO=ASC(MID$(C$,2,1)):DA=ASC(MID$(C$,3,1)):CLOSE#1
50 DIM W$(3,50),A$(3,80),S$(80),B$(6)
55 DIM T$(11,7),D$(68),N$(68),F$(68)
60 Z1$="      ##  %                   %":Z4$=STRING$(32," ")
70 Z$="### % % #  %                   %"
100 RP=0:RP$="OFF":AG=0:AG$="OFF":DD=0:DL=0:DH=10:PT=100:PC=0:NT=20:IF Q=1THEN200
110 ON Z GOTO499,120,499:Z=2:PRINT@265,"error detected";:GOTO980
120 POKE65494,0:OPEN"D",#1,"WORDLIST/DAT",51
130 FIELD#1,20AS B$(1),20AS B$(2),11AS B$(3)
140 E=LOF(1)
150 FOR J=1TO 80:S$(J)="":NEXT J
200 GOSUB3110:PRINT@131,"select words by:";:PRINT@229,"<1> PARAMETER SEARCH";:PRINT@293,"<2> BUILD A REVIEW TEST";:GOSUB3050:GOSUB3010:IF Y$="H"THEN HS=6:HE=8:GOSUB4010:GOTO200
210 IF Y$=CHR$(12)THEN IF N>0THEN340ELSE PRINT@453,"sure?";:GOSUB3010:IF Y$="Y"THEN RUN"MAINMENU"ELSE200
220 GOSUB3110:ZZ=VAL(Y$)
230 ON ZZ GOTO 300,3302:GOTO 200
300 SOUND200,1:PRINT@6,"search parameter:";:PRINT@135,USING"<1> score <= ###%";PT:PRINT@199,"<2> difficulty";:PRINT@263,"<3> repeats: ";RP$;:PRINT@327,USING"<4> NO. WORDS = ###";NT:PRINT@391,"<5> auto-test maker: ";AG$;
302 GOSUB3060:GOSUB3010:IF Y$="H"THEN HS=15:HE=22:GOSUB4010:GOSUB3110:GOTO300ELSE IF Y$=CHR$(12)THEN Q=1:GOTO100ELSE IF Y$="S"THEN I=0:GOTO314ELSE ZZ=VAL(Y$):ON ZZ GOTO304,306,308,310,312:GOTO300
304 PRINT@148,"---%";:PRINT@148," ";:LINE INPUT"";PT$:PT=VAL(PT$):SOUND180,1:IF PT=100THEN PC=0:GOTO300ELSE PC=1:GOTO300
306 DD=1:PRINT@224,Z4$;:PRINT@235,"minimum: ";:PRINT@243,"";:GOSUB3010:SOUND180,1:PRINT Y$;:DL=VAL(Y$):PRINT@245,"maximum: ";:PRINT@253,"";:GOSUB3010:PRINT Y$;:DH=VAL(Y$):SOUND180,1:GOTO300
308 IF RP=0THEN RP=1:RP$=" ON":SOUND220,1:GOTO300ELSE RP=0:RP$="OFF":SOUND180,1:GOTO300
310 PRINT@343,"  ";:PRINT@343,"";:LINEINPUT"";N$:NT=VAL(N$):IF NT>80THEN SOUND120,2:NT=80:PRINT Z4$;:PRINT@352,"MAXIMUM NO. IS 80";:XB=800:GOSUB3210:PRINT@343,Z4$;:GOTO300ELSE SOUND180,1:GOTO300
312 IF AG=0THEN AG=1:AG$=" ON":SOUND220,1:GOTO300ELSE AG=0:AG$="OFF":SOUND180,1:GOTO300
314 TN=N+NT:IF Q=3THEN RETURN
316 PRINT@0,STRING$(96,CHR$(239));:PRINT@41,"-- SEARCHING --";
320 K=K+1:IF E<K THEN336ELSE POKE65494,0:GET#1,K:POKE65495,0
322 IF DD=0THEN324ELSE D=ASC(MID$(B$(3),4,1)):IF DH>=D AND DL<=D THEN324ELSE320
324 TG=ASC(MID$(B$(3),2,1)):IF Q<3 OR RP=0THEN326ELSE RB=ASC(MID$(B$(3),3,1)):IF RB>=TG^.5-1 AND Q<3 THEN326ELSE IF Q=3 AND RB>0 THEN 326ELSE320
326 IF PC=0 OR Q=3THEN328ELSE TC=ASC(MID$(B$(3),1,1)):IF TC/(TG-.01)<=(PT+.02)/100THEN328ELSE320
328 N=N+1:I=I+1:J=N:IF N<=TN THEN330ELSE IF RND(E)<E/25THEN N=N-1:J=RND(NT)ELSE N=N-1:I=I-1:GOTO320
330 A$(1,J)=B$(1):A$(2,J)=B$(2):A$(3,J)=B$(3)
332 IF I=NT AND AG<>1 THEN336
334 IF I>=NT THEN I=0:GOTO320ELSE320
336 IF Q=3THEN RETURN
338 IF AG=1 THEN FOR I=1TO NT:FOR J=1TO3:W$(J,I)=A$(J,I):NEXT J,I:NN=NT:GOTO710
340 L=L-1
342 GOSUB3110:PRINT" J   N <#>  W O R D S";:PRINT@28,USING"##";NN:PRINT STRING$(32,"-");:IF L<-1THEN L=-1
344 Z2=0:L=L+1:IF L>=13 THEN Z=3:GOTO110
346 FOR I=1 TO 10
348 J=10*L+I
349 IF (J>TN AND Q<2)OR(Q>1ANDJ>TN) THEN PRINT@357,"end of file";:GOTO356
350 II=I:IF I=10THEN II=0
352 PRINT@32*(I+1),USING Z$;J,S$(J),II,A$(1,J)
354 NEXT I
356 Z2=0:IF Q<2THEN PRINT@484,"<g> <r> <^> <DN> <#> <h>"; ELSE PRINT@486,"<r> <^> <DN> <#> <h>";
358 PRINT@448,"select words for the test:";:GOSUB3010
359 IF Y$="H"THEN HS=9:HE=13:GOSUB4010:GOSUB3110:L=L-1:GOTO342
360 IF Y$="R"THEN710ELSE IF Y$="G"AND Q<1THEN361ELSE366
361 IF N>80-NT+1THEN362ELSE365
362 GOSUB3110:PRINT@130,"TOO MANY WORDS IN MEMORY ..";:PRINT@192,"NEED TO:";:PRINT@231," <clear> MEMORY";:PRINT@264,"<r>eview & CANCEL GET";:PRINT@324," OR <u>SE SELECTED WORDS";:PRINT@366,"& CLEAR OTHERS";:GOSUB3010
363 IF Y$="R"THEN710 ELSEIF Y$=CHR$(12)THEN 365 ELSEIF Y$<>"U"THEN SOUND120,1:GOTO363 ELSE PRINT@422,":: ok :: one moment ::";:ZZ=TT:FOR J=1 TO TN:IF S$(J)="-->"THEN ZZ=ZZ+1:W$(1,ZZ)=A$(1,J):W$(2,ZZ)=A$(2,J):W$(3,ZZ)=A$(3,J):S$(J)=""
364 A$(1,J)="":A$(2,J)="":A$(3,J)="":NEXT J:N=0:TT=ZZ:L=0
365 I=0:GOSUB3110:GOTO314
366 IF Y$=CHR$(94)THEN L=L-2:GOTO342ELSE IF Y$=CHR$(10)THEN342
368 IF Y$=CHR$(13)THEN370ELSE372
370 PRINT@448,Z4$;:GOTO358
372 IF ASC(Y$)>47 AND ASC(Y$)<58 THEN Y=VAL(Y$)ELSE SOUND 120,2:GOTO358
373 IF Y=0 THEN Y=10
374 J=10*L+Y:IF S$(J)=""THEN NN=NN+1:IF NN>50THEN399ELSEPRINT@28,USING"##";NN:W$(1,NN)=A$(1,J):W$(2,NN)=A$(2,J):W$(3,NN)=A$(3,J):S$(J)="-->"ELSE SOUND100,2:GOTO358
376 PRINT@32*(J-10*L+1)+4,"-->";:GOTO370
399 GOSUB3110:PRINT@100,"OBTAINED 50 WORDS";:XB=1200:GOSUB3210
499 I=0
500 A$="ENTER THE WORD":GOSUB2020:A$="THEN SLASH":GOSUB2010:A$="THEN THE LEVVEL OF DIFFICULTY":GOSUB2010:X9=1000:GOSUB3210:A$="AS FOLLOWS":GOSUB2010
510 GOSUB3110:PRINT"enter: WORD/'LEVEL'";:GOSUB3210
520 I=I+1:IF I>50THEN I=50:GOSUB3110:A$="THAT WAS FIFTY":PRINT@264,A$;:GOSUB2020:Y$="S":GOTO600
530 GOSUB3110:LINE INPUT"enter word: ";D$:IF D$=""THEN530ELSE FOR D=1 TO LEN(D$):IF MID$(D$,D,1)="/"THEN 540 ELSE NEXT D
540 E$=MID$(D$,D+1):W$(1,I)=LEFT$(D$,D-1):W$(2,I)=W$(1,I):A$=W$(2,I):XB=40*LEN(D$):GOSUB2020
550 IF LEN(E$)<1THEN PRINT@37,"level: ";:LINEINPUT"";E$
560 W$(3,I)=STRING$(11,CHR$(0)):MID$(W$(3,I),4,1)=CHR$(VAL(E$))
570 IF Z1=0THEN PRINT W$(1,I);" =":PRINT"        ";W$(2,I); ELSE IF Z1=2THEN PRINT W$$(1,I);"=":PRINT"        ";W$(2,I);
580 PRINT@130,"<ENTER> -> 'OK'";:PRINT@162,"<CLEAR> -> 'delete word'";:PRINT@194,"< BAR > -> 'change sound'";:IF CD=1THEN590ELSE PRINT@226,"<  S  > -> 'OK & save to disk'";
590 GOSUB3010:IF Y$=CHR$(12)THEN530
600 IF Y$<>"S"THEN610ELSE NN=I:FOR I=1TO NN:MID$(W$(3,I),5,3)=DA$:NEXT I:IF Z=1THEN F$="TEMP/WRD":GOTO710ELSE710
610 IF Y$=CHR$(32)THEN630
620 IF Y$=CHR$(13)AND CD=1THEN RETURN
625 IF Y$=CHR$(13)THEN520ELSE590
630 PRINT@320,Z4$;Z4$;Z4$;:PRINT@320,"";:LINE INPUT"new sound: ";A$:IF A$=""THEN630ELSE W$(2,I)=A$:GOSUB2020:GOTO560
710 V=0:Z2=1:L=0
720 A$=" ":GOSUB2020:GOSUB3110:PRINT"review ";:IF Z=1THEN PRINT"words";ELSE PRINT"test words";
730 FOR P=1 TO 10:I=10*L+P:IF I>NN THEN PRINT@421,"end of file";:GOTO770
740 PRINT@32*(P+2),USING" ### %                   %";I,W$(1,I)
750 IF V=1THEN A$=W$(2,I):GOSUB2010:XB=300:GOSUB3210
760 NEXT P
770 IF Q<2THEN PRINT@484,"<e/d/s/c/v> <UP> <DN> <h>";
780 GOSUB3010:IF Y$="D"THEN B$="DELETE":GOSUB1010:GOTO720ELSE IF Y$="V"THEN V=ABS(FIX(V/2-1)):GOTO730
790 IF Y$="S"THEN910ELSE IF Y$=CHR$(94)THEN L=L-1:IF L=-1THEN L=0:GOTO720ELSE720
792 IFY$="C"THEN GOSUB810:GOTO720 ELSE IF Y$=CHR$(10)THEN L=L+1:GOTO720
794 IF Y$="E"THEN N=0:TT=NN:L=0:GOSUB3110:IF Z<>1THEN314ELSE510
800 IF Y$="H"THEN HS=24:HE=28:GOSUB4010:GOTO720ELSE SOUND120,2:GOTO780
810 PRINT@416,"";:LINE INPUT"enter no. to change: ";I$:I=VAL(I$)
820 IF Z1=2THEN I=10*L+I:A$=A$(2,I)ELSE A$=W$(2,I)
830 GOSUB2020:CD=1:GOSUB3110:GOSUB570:CD=0:RETURN
910 IF Z=1THEN930ELSE GOSUB3110:PRINT@270,"--------/TST";:PRINT@311,"(assumed)";:PRINT@256,"";:LINE INPUT"name of test: ";F$:F$=F$+"/TST"
930 Q=1:GOSUB20:OPEN"D",#1,F$,51
940 FIELD#1,20 AS A$,20 AS B$,11 AS C$
950 FOR I=1TO NN
960 LSET A$=W$(1,I):LSET B$=W$(2,I):LSET C$=W$(3,I)
970 PUT#1,I:NEXT I:CLOSE#1
980 IF Z=2THEN F$="MAINMENU"ELSE F$="PUTWORDS":GOSUB3210
990 CLS4:PRINT@232,"TRANSFERRING TO";:PRINT@300,F$;:IF Z=2THEN RUN"MAINMENU"ELSE RUN"PUTWORDS"
1010 PRINT@479,Z4$;:A$="ENTER NUMBER TO "+B$:XB=50*LEN(A$):GOSUB2020:PRINT@480,A$;": ";:GOSUB3010
1020 IF X8=1THEN I=VAL(Y$)+10*L:RETURN
1030 IF ASC(Y$)>57OR ASC(Y$)<48THEN RETURN
1040 NN=NN-1:Y=VAL(Y$):IF Y=0THEN Y=10
1050 Y=10*L+Y:FOR I=Y TO NN:FOR P=1TO3:W$(P,I)=W$(P,I+1):NEXT P,I:RETURN
2010 XB=40*LEN(A$)
2020 X=&HFF00:Y=&HFF7E:POKE X+1,52:POKE X+3,63:POKE X+35,60:POKE65407,34
2030 FOR M=1 TO LEN(A$)
2040 IF PEEK(Y)AND128=0THEN2040
2050 POKE Y,ASC(MID$(A$,M,1))
2060 NEXT M
2070 IF PEEK(Y)AND128=0THEN2070
2080 POKE Y,13:GOSUB3210:POKE65407,51:RETURN
3010 YY$=INKEY$
3020 YY$=INKEY$:IF YY$=""THEN3020 ELSE Y$=YY$
3030 IF Y$=CHR$(94)AND Z2=1THEN GOSUB2020
3040 RETURN
3050 PRINT@490,"<clear>   <h>";:RETURN
3060 PRINT@485,"<clear>  <s>TART  <h>";:RETURN
3110 CLS:SOUND200,1:RETURN
3210 FOR X9=1TO XB:NEXT X9:XB=500:RETURN
3302 DR=0:DN$="0":L1=1:F1$="TST":JK=0:M=0:J=0:GOSUB3110
3304 CLS4:PRINT@235,"ONE MOMENT";:PRINT@301,"PLEASE";
3306 FOR X=3TO11:DSKI$DR,17,X,A$,B$:C$=A$+LEFT$(B$,127):FOR I=0TO7
3308 T$(X,I)=MID$(C$,I*32+1,32):C1=ASC(T$(X,I)):IF C1=255THEN3314ELSE IF C1=0THEN3312
3310 IF MID$(T$(X,I),9,3)<>F1$ THEN3312ELSE M=M+1:N$(M)=LEFT$(T$(X,I),8)+"/"+MID$(T$(X,I),9,3)
3312 NEXT I,X
3314 CLS6:PRINT@454," <enter> TO SELECT   ";:PRINT@486," <clear> TO CONTINUE ";:PRINT@2,"indicate tests: (*--------*)";:PRINT@32,"";
3316 IF M<=30THENPP=1:N1=M
3318 IF M>30THENPP=2:N1=30
3320 IF M>60THENPP=3:N1=30
3322 FOR X=L1 TO N1:PRINT"  "N$(X),:NEXT X
3324 IFPP=2ORPP=3THENPRINT@480,"PRESS <M> FOR MORE...";
3326 L$=">":R$="<":P=32:Y=1
3328 PRINT@P,L$;:PRINT@P+15,R$;
3330 FORX=338TO345:POKEX,255:NEXTX
3332 IF PEEK(338)=191THEN3366
3334 IFPEEK(339)=191THEN3368
3336 IFPEEK(341)=247THEN3358
3338 IFPEEK(342)=247THEN3360
3340 IFPEEK(343)=247THEN3362
3342 IFPEEK(344)=247THEN3364
3344 IFPEEK(343)=253THEN3348
3346 GOTO3332
3348 IFPP=2ANDY=1THENCLS:FORX=31TO M:PRINT"  "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=2:P=32:N1=M-30:GOTO3328
3350 IFPP=2ANDY=2THENY=1:P=32:N1=30:GOTO3314
3352 IFPP=3ANDY=1THENCLS:L1=31:FORX=L1TO61:PRINT"  "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=2:P=32:N1=N-59:GOTO3328
3354 IFPP=3ANDY=2THENCLS:L1=64:FORX=L1TON:PRINT"  "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=3:P=32:N1=N-61:GOTO3328
3356 IFPP=3ANDY=3THENY=1:P=32:L1=1:N1=30:GOTO3314
3358 IFP<49THEN3328ELSEP=P-32:PRINT@P+32," ";:PRINT@P+47," ";:GOTO3328
3360 IF P/16=>N1 THEN3330ELSEP=P+32:PRINT@P-32," ";:PRINT@P-17," ";:GOTO3328
3362 P=P-16:IFP<32THENP=32:GOTO3328ELSEPRINT@P+16," ";:PRINT@P+31," ";:GOTO3328
3364 IF P/16=>N1+1 THEN3330ELSEP=P+16:PRINT@P-16," ";:PRINT@P-1," ";:GOTO3328
3366 F=P/16+(Y-1)*30-1:IF D$(F)="*"THEN SOUND120,1:GOTO3328ELSE D$(F)="*":JK=JK+1:F$(JK)=N$(F)+":"+DN$:PRINT@P+1,"*";:PRINT@P+14,"*";:GOTO3328
3368 UNLOAD:Q=3:GOSUB3110:GOSUB300
3370 FOR JJ=1 TO JK:POKE65494,0:IF AG=0THEN TN=0
3372 GOSUB3110:PRINT@235,"accessing";:PRINT@296,F$(JJ);
3374 OPEN"D",#1,F$(JJ),51
3376 FIELD#1,20AS B$(1),20AS B$(2),11 AS B$(3)
3378 E=LOF(1):IF AG=0 THEN NT=E:TN=TN+E
3379 K=0:I=0:GOSUB320
3380 POKE65494,0:CLOSE#1
3382 NEXT JJ
3384 Q=2:IF AG=0THEN340ELSE338
4010 POKE65494,0:XH=0:OPEN"D",#2,"HELP/SCN",130
4020 FIELD#2,130 AS H$
4040 CLS8:PRINT@43,"H  E  L  P";
4050 FOR HT=6TO13:PRINT@32*HT+8,STRING$(16," ");:NEXT HT:IF XH=1THEN RETURN
4055 FOR H=HS TO HE:POKE65494,0:GET#2,H+1:POKE65495,0
4060 H1$=MID$(H$,1,2):PRINT@32*3+14,USING"<%%>";H1$;
4070 FOR H1=1TO8:PRINT@32*(4+H1)+8,MID$(H$,16*(H1-1)+3,16);:NEXT H1
4080 PRINT@487,"<W>ait   <R>eturn>";
4090 Y$=INKEY$:FOR X9=1TO500:Y$=INKEY$:IF Y$=""THEN NEXT X9
4095 IF Y$="W"THEN GOSUB4110:GOTO4105
4100 IF Y$="R"THEN CLOSE#2:POKE65494,0:RETURN
4105 XH=1:GOSUB4040:XH=0:NEXT H:GOTO4040
4110 PRINT@480,"  press any key to continue ...";:GOSUB3010:RETURN
