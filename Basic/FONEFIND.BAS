0 ' COPYRIGHT 1989  FALSOFT,INC
10 'FONE FINDER
20 'BY D DRIESSEN 4/89
30 GOSUB150
40 INPUT"HOW MANY DIGITS";D:D=D-1
50 PRINT"TOTAL NUMBER OF COMBINATIONS COULD BE";FIX(3^(D+1))
60 FOR X=D TO 0 STEP-1:PRINT"ENTER DIGIT";D-X+1;:INPUT D(X):NEXT X
70 INPUT"SHOW COMBINATIONS ON <S>CREEN OR <P>RINTER";Q$
80 IF Q$="P"THEN P=-2:P1=79-D ELSE P=0:P1=31-D
90 PRINT#P,"EVER WANTED A 'PERSONALIZED' PHONE NUMBER? BELOW ARE PRINTED ALL THE COMBINATIONS OF LETTERS WHICH YOUR PHONE NUMBER CAN SPELL OUT. GOOD LUCK!!":PRINT#P,"HIDDEN WORDS IN";:FORQ=D TO 0 STEP-1:PRINT#P,STR$(D(Q));:NEXTQ:PRINT#P,"":PRINT#P,"
100 X=0
110 IF X>D THEN PRINT#P,"":END
120 IF N(X)>2 OR (D(X)<2 AND N(X)>0)THEN N(X)=0:X=X+1:N(X)=N(X)+1:GOTO110 ELSE X=0:GOSUB130:N(X)=N(X)+1:GOTO120
130 FOR Y=D TO 0 STEP-1:PRINT#P,N$(D(Y),N(Y));:NEXTY:PRINT#P,"  ";:IF POS(P)>P1 THENPRINT#P,""
140 RETURN
150 FOR X1=0TO9:FORY1=0TO2:READN$(X1,Y1):NEXT Y1,X1:RETURN
160 DATA0,0,0,1,1,1,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,R,S,T,U,V,W,X,Y
