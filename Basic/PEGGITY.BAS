1 'PEGGITY
2 'WRITTEN BY GEORGE & ELLEN AFTAMONOW
3 'COPYRIGHT (C) DECEMBER 1991
4 'BY FALSOFT, INC.
5 DIMB(81)
6 'for the coco 3
10 PALETTE0,0:PALETTE5,63:HSCREEN2:HCLS0:HCOLOR5,0
15 HPRINT(10,10),"(C)MP OR (R)GB?"
20 I$=INKEY$:IFI$=""THEN20
30 IFI$="C"THEN40ELSEIFI$="R"THEN50ELSE20
40 PALETTECMP:PALETTE0,0:PALETTE1,17:PALETTE2,39:PALETTE3,58:PALETTE4,53:PALETTE5,63:PALETTE6,41:PALETTE9,7:PALETTE10,52:PALETTE12,38:PALETTE14,51:GOTO55
50 PALETTERGB:PALETTE0,0:PALETTE1,16:PALETTE2,38:PALETTE3,29:PALETTE4,55:PALETTE5,63:PALETTE6,45:PALETTE7,57:PALETTE8,44:PALETTE12,32:PALETTE13,42:PALETTE14,15:PALETTE9,32:PALETTE10,52:' 0=BLK 1=GRN 2=RED 3=BLU 4=YLW 5=WHT 6=PUR 9=DRKRED 10=LT RE
55 HCLS0:HSCREEN2:HCOLOR2
57 HPRINT(12,3),"George and Ellen":HPRINT(15,4),"present:"
60 HDRAW"BM70,120S8L6U20R14F2D2BL4L6D4R6NU4BR4NU3D2G2L8D8BR26 L16U15BUU4R16D4L10D4R4D4L4D4R10BD2D2"
61 HDRAW"BR14L12H2UBUU14E2R12F2BD2ND4L10D12R6U4L2U4R6D10G2BR16 L12H2BU16E2R12F2NU2BD2ND4L10D12R6U4L2U4R6D10NG2D2 R6U16NR6UBUU2NL6"
62 HDRAW"R18DBDD2L6ND16BL6D16R6BR18 L6U10H6BU4R2F6R2E6R2D4G6D10"
63 HPAINT(68,118),4,2
64 HLINE(1,50)-(319,150),PSET,B:HLINE(6,54)-(312,146),PSET,B:HLINE(16,60)-(300,140),PSET,B:HLINE(34,70)-(284,134),PSET,B:HPAINT(2,51),9,2:HPAINT(7,55),2,2:HPAINT(33,68),10,2
65 HCOLOR9:HLINE(0,50)-(320,150),PSET,B:HLINE(56,80)-(0,50),PSET:HLINE(0,150)-(60,120),PSET:HLINE(270,80)-(320,50),PSET:HLINE(258,120)-(320,150),PSET
90 HCOLOR2:HLINE(50,162)-(270,180),PSET,B:HPRINT(9,21),"NEED INSTRUCTIONS Y/N?"
91 I$=INKEY$:IFI$=""THEN91
92 IFI$="N"THEN99ELSEHCLS
93 HLINE(2,15)-(318,152),PSET,B:HLINE(4,17)-(316,150),PSET,B:HPRINT(3,5),"THE  OBJECT  IS TO FILL  EVERY BOX":HPRINT(3,6),"SO THAT NO TWO OF THE SAME  COLORS":HPRINT(3,7),"ARE NEXT TO EACH OTHER,VERTICALLY,":HPRINT(3,8),"HORIZONTALLY, OR DIAGONALLY."
94 HPRINT(3,9),"ONCE A COLOR IS PLACED, IT MAY NOT":HPRINT(3,10),"BE REMOVED OR CHANGED.":HPRINT(3,11),"COLORS  MUST  BE PLACED NEXT TO AN":HPRINT(3,12),"EXISTING COLORED BOX."
96 HPRINT(3,13),"USE ARROW KEY TO MOVE BLINKING BOX":HPRINT(3,14),"THEN PRESS ENTER.    HIT Q TO QUIT":HPRINT(9,17),"PRESS ENTER TO START."
98 IFINKEY$="" THEN98
99 SC=4
100 HCLS0
110 B$="L20U16R20D16BR30"
120 HDRAW"C3BM140,25S4"+B$:HDRAW"BM110,50"+B$+B$+B$
125 HDRAW"BM80,75":FORB=1TO5:HDRAW"XB$;":NEXT
130 HDRAW"BM50,100":FORB=1TO7:HDRAW"XB$;":NEXT::HDRAW"BM80,125":FORB=1TO5:HDRAW"XB$;":NEXT
135 HDRAW"BM110,150"+B$+B$+B$:HDRAW"BM140,175"+B$
140 HCOLOR2:HLINE(251,10)-(315,100),PSET,B:HLINE(249,150)-(315,180),PSET,B:HDRAW"BM45,48"+B$
150 HPRINT(0,2),"NEXT COLOR":HPRINT(33,17),"SCORE"
200 C=0:Y=20:FORZ=1TO5:X=260:C=C+1:FORX=260TO300STEP10:HCOLORC:HLINE(X,Y)-(X+6,Y+8),PSET,BF:NEXTX:Y=Y+15:NEXTZ
210 P=14:FORZ=1TO5:P(Z)=5:NEXT
220 X=138:Y=23:E=14:FORZ=1TO4:A=RND(5):AX=250+P(A)*10:AY=5+A*15:HCOLOR0:HLINE(AX,AY)-(AX+6,AY+8),PSET,BF:P(A)=P(A)-1:HPAINT(X,Y),A,3:B(E)=A:IFZ=1 THENX=48:Y=98:E=38:ELSEIFZ=2 THENX=228:Y=98:E=44:ELSEIFZ=3 THENX=138:Y=173:E=68
230 NEXT:X=140:Y=25
250 C=RND(5):IFP(C)=0 THEN250
260 XC=250+P(C)*10:YC=5+C*15:HCOLOR0:HLINE(XC,YC)-(XC+6,YC+8),PSET,BF:P(C)=P(C)-1
270 HCOLORC:HLINE(44,47)-(26,33),PSET,BF
300 PALETTE15,63:HDRAW"BM=X;,=Y;C15XB$;"
310 I$=INKEY$:IFI$="" THENPALETTE15,0:PALETTE15,63:GOTO310ELSEHDRAW"BM=X;,=Y;C3XB$;"
320 IFI$=CHR$(13) ANDB(P)=0THENIFB(P-9)<>0 ORB(P-1)<>0 ORB(P+1)<>0 ORB(P+9)<>0 THEN400
330 IFI$=CHR$(9) ANDP<>14 ANDP<>24 ANDP<>34 ANDP<>44 ANDP<>52 ANDP<>60 ANDP<>68 THENP=P+1:X=X+30:GOTO300
340 IFI$=CHR$(8) ANDP<>14 ANDP<>22 ANDP<>30 ANDP<>20 ANDP<>38 ANDP<>48 ANDP<>58 ANDP<>68 THENP=P-1:X=X-30:GOTO300
350 IFI$=CHR$(94) ANDP<>14 ANDP<>22 ANDP<>24 ANDP<>30 ANDP<>34 ANDP<>38 ANDP<>44 THENP=P-9:Y=Y-25:GOTO300
360 IFI$=CHR$(10) ANDP<>38 ANDP<>44 ANDP<>48 ANDP<>52 ANDP<>58 ANDP<>60 ANDP<>68 THENP=P+9:Y=Y+25:GOTO300
370 IFI$="Q" THEN425
380 GOTO500
400 IFB(P-10)=C ORB(P-9)=C ORB(P-8)=C ORB(P+1)=C ORB(P+10)=C ORB(P+9)=C ORB(P+8)=C ORB(P-1)=C THEN500ELSEHPAINT(X-1,Y-1),C,3:HCOLOR0:HLINE(44,47)-(26,33),PSET,BF:B(P)=C
410 HLINE(270,160)-(300,175),PSET,BF:SC=SC+1:HCOLOR4:HPRINT(34,20),SC:IFSC/25<>INT(SC/25) THEN250
420 SC=SC+4:PALETTE15,53:HCOLOR15:HPRINT(2,22),"NEXT SCREEN":FORZ=1TO5:PLAY"T250L200EAGAEAGAEAGE":PALETTE15,0:PALETTE15,53:NEXT:GOTO440
425 HCOLOR4:HPRINT(2,24),"PLAY AGAIN?":SC=4
430 I$=INKEY$:IFI$="" THEN430
435 IFI$="N" THENCLS:RGB:END:ELSEIFI$="Y" THEN440ELSE430
440 FORZ=1TO81:B(Z)=0:NEXT:GOTO100:ELSE430
500 HCOLOR4:HPRINT(6,23),"ILLEGAL":PLAY"T255L200O1BADO2BADO3BAD":FORZ=1TO50:NEXT:HCOLOR0:HPRINT(6,23),"ILLEGAL":GOTO300
