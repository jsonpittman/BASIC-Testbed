100 'BASIC DRAW LINE SUBROUTINE
110 SCREEN 1,0
120 PMODE 4,1
130 PCLS
140 X1 = 0: Y1 = 100: X2 = 255: Y2 = 95
150 GOSUB 170
160 GOTO 160
170 'DRAW A LINE SUBROUTINE FROM X1,Y1 TO X2,Y2
180 X = X1: Y = Y1
190 IF X2 - X1 >= 0 THEN MA = 1 ELSE MA = 3
200 IF Y2 - Y1 >= 0 THEN NA = 2 ELSE NA = 4
210 MD = ABS( X2 - X1 )
220 ND = ABS( Y2 - Y1 )
230 IF MD < ND THEN T = MA: MA = NA: NA = T: T = MD: MD = ND: ND = T
240 MC = MD
250 NC = MD/2
260 IF ( MD AND 1 ) = 1 AND (MA >= 3 ) THEN NC = NC - 1
270 'DRAW A LINE CORE CODE
280 FOR I = MC TO 0 STEP -1
290 PSET (X,Y)
300 ON MA GOSUB 360, 390, 420, 450
310 NC = NC + ND
320 IF NC - MD > 0 THEN NC = NC - MD: BUMP = 1 ELSE BUMP = 0
330 IF BUMP = 1 THEN ON NA GOSUB 360, 390, 420, 450
340 NEXT I
350 RETURN
360 'INCREMENT X
370 X = X + 1
380 RETURN
390 'INCREMENT Y
400 Y = Y + 1
410 RETURN
420 'DECREMENT X
430 X = X - 1
440 RETURN
450 'DECREMENT Y
460 Y = Y - 1
470 RETURN
