1 '*****COUNTED CROSS-STITCH*****
2 '*****GRAPHICS PROGRAM FOR*****
3 '*********THE COCO 3 **********
4 '******BY LARRY ANDERSON*******
5 '********PO BOX 180 ***********
6 '******BENTON, LA 71006********
7 '*******COPYRIGHT 1987*********
8 '
9 '
10 '
20 ONBRK GOTO 400
30 HSCREEN2
40 HCLS5
50 HPRINT(11,10),"COUNTED CROSS-STITCH"
60 HPRINT(4,12),"Space bar marks the stitch":HPRINT(4,13),"Arrow keys move the cursor dot":HPRINT(4,14),"ALT-arrow multiplies cursor movement"
70 HPRINT(4,15),"C and 2 digits sets stitch color":HPRINT(4,16),"D deletes the current stitch":HPRINT(4,18),"(delete any stitch by moving to it,":HPRINT(4,19)," press SPACEBAR and then D)"
80 A$=INKEY$:IFA$="" THEN80
90 FG=0:BG=15:X=162:Y=90
100 FOR SL=0TO15:READ CL:PALETTE SL,CL
110 NEXT SL
120 RESTORE
130 HCLS15
140 HCOLOR 0,15:HPRINT(3,0),"0":HCOLOR 1,15:HPRINT(5,0),"1":HCOLOR 2,15:HPRINT(7,0),"2":HCOLOR3,15:HPRINT(9,0),"3":HCOLOR4,15:HPRINT(11,0),"4"
150 HCOLOR5,15:HPRINT(13,0),"5":HCOLOR6,15:HPRINT(15,0),"6":HCOLOR7,15:HPRINT(17,0),"7":HCOLOR8,15:HPRINT(19,0),"8":HCOLOR9,15:HPRINT(21,0),"9"
160 HCOLOR10,15:HPRINT(23,0),"10":HCOLOR11,15:HPRINT(26,0),"11":HCOLOR12,15:HPRINT(29,0),"12":HCOLOR13,15:HPRINT(32,0),"13":HCOLOR14,15:HPRINT(35,0),"14"
170 FOR L=1TO320STEP54:FOR M=30TO190STEP40:HSET(L,M,0):HSET(319,M,0):NEXT M,L
180 HCOLOR FG,BG
190 HSET(X+1,Y,FG)
200 A$=INKEY$:IFA$=""THEN200 ELSE HRESET(X+1,Y)
210 D=6:E=5
220 IFPEEK(341)=191 OR PEEK(341)=183  THEN E=40:D=54
230 IF ASC(A$)=8 THEN X=X-D ELSEIF ASC(A$)=9 THEN X=X+D ELSEIF ASC(A$)=10 THEN Y=Y+E ELSEIF ASC(A$)=94 THEN Y=Y-E
240 IFX<0THEN X=0 ELSE IF X>318 THEN X=318
250 IFY<0THEN Y=0 ELSE IF Y>190THEN Y=190
260 IFA$="C"THEN GOSUB340:ELSEIFA$="D" THENGOSUB320
270 HSET(X+1,Y,FG)ELSEGOTO200
280 IFA$=CHR$(32)THEN 290ELSE 200
290 X$="BM"+STR$(X)+","+STR$(Y)+";"
300 HDRAW X$+";C"+STR$(FG)+";E3;B;L3;F3"
310 GOTO200
320 HDRAW X$+";C"+STR$(BG)+";E3;B;L3;F3"
330 RETURN
340 B$=""
350 A$=INKEY$:IFA$=""THEN350
360 B$=B$+A$:IF LEN(B$)=2 THEN FG=VAL(B$) ELSE GOTO350
370 IF FG>15 THEN FG=0
380 RETURN
390 DATA0,32,39,60,47,52,55,50,16,20,48,27,29,10,57,63
400 HPRINT(13,24),"Erase or Quit?"
410 A$=INKEY$:IFA$=""THEN410
420 IFA$="Q"THEN PALETTE RGB:NEW:ELSEGOTO90
430 A$=INKEY$:IFA$=""THEN430ELSEPRINTASC(A$);:PRINTPEEK(341)::GOTO430
