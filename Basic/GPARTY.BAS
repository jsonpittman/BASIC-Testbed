1 REM    G-PARTY 1.0  09/29/86  BY DALE R. COLLINS
2 PCLEAR4:CLEAR6000
5 GOTO9900
20 REM PLAYER BID
21 PRINT@480,"                              ";:IFPA<1THEN70
22 IFPB$="K"THEN31
23 C=440:REM GAME-BUTTON INPUT
24 J=JOYSTK(0):C=C-1:IFC<1THEN70
25 IFJ<4THEN24
26 PL=99:FORI=0TONP-1
27 IFJ<PA(I)+2ANDJ>PA(I)-2THENPL=I:PA(I)=0:PA=PA-1
28 NEXTI:IFPL=99THEN24
29 GOTO38
31 C=440:REM KEYBOARD INPUT
32 A$=INKEY$:C=C-1:IFC<1THEN70
33 IFA$=""THEN32
34 PL=999:FORI=0TONP-1
35 IFASC(A$)=PA(I)THENPL=I:PA(I)=0:PA=PA-1
36 NEXTI:IFPL=999THEN32
38 PRINT@480,P$(PL);" - PLAYER";
40 REM CHECK ANSWER
41 C=400
42 A$=INKEY$:C=C-1:IFC<1THEN60
43 IFA$=""THEN42
45 IFA$=CHR$(8)THEN60
46 IFA$<>CHR$(9)THEN42
50 REM GOOD ANSWER
51 PRINT@498,"GOOD  GOOD";
52 P2(PL)=P2(PL)+B(CO,RO):B(CO,RO)=0:TU=PL
53 FORI=1TO20:NEXTI
54 GOTO2500
60 PRINT@498,"BAD   BAD";
61 P2(PL)=P2(PL)-B(CO,RO)
62 FORI=1TO20:NEXTI
64 IFDD=1THEN20
70 REM TIME LIMIT
72 B(CO,RO)=0
73 PRINT@384,"answer  ":PRINT"   ";Q1$(CO,RO);:GOSUB502
74 GOTO2500
502 A$=INKEY$:IFA$=""THEN502
503 RETURN
2000 REM MENU
2005 CLS
2010 PRINT"         g-party"
2012 PRINT"-------------------------------"
2014 PRINT"1. PLAY ROUND 1"
2016 PRINT"2. PLAY ROUND 2"
2018 PRINT"3. PLAY FINAL"
2020 PRINT"4. IDENTIFY PLAYERS"
2022 PRINT"5. LOAD QUESTIONS"
2024 PRINT"6. SAVE QUESTIONS"
2026 PRINT"7. ENTER QUESTIONS"
2028 PRINT"8. MODIFY QUESTIONS"
2030 PRINT"9. PRINT Q&A SHEET"
2040 PRINT"-------------------------------"
2042 PRINT"    ENTER OPTION ";:
2044 INPUTOP
2050 ONOP GOTO2060,2070,4500,2090,2110,2200,4300,4200,6000
2054 GOTO2042
2060 REM ROUND 1
2061 A2$="            g-party"
2063 MX=B(0,5)
2064 CO=99
2068 GOTO2500
2070 REM ROUND 2
2071 A2$="         double g-party"
2072 FORCO=0TO4:FORRO=0TO5:B(CO,RO)=B(CO,RO)*2:NEXTRO:NEXTCO
2073 MX=B(0,5)
2074 CO=99
2078 GOTO2500
2090 NP=0:REM IDENT PLAYERS
2091 CLS:PRINT"ENTER PLAYER'S NAME ";:INPUTP$(NP):IFP$(NP)=""THEN2101
2092 IFPB$="K"THEN2098
2093 PRINT:PRINT"PRESS YOUR BUTTON TO REGISTER";
2094 P1(NP)=JOYSTK(0):IFP1(NP)<4THEN2094
2095 K=JOYSTK(0):IFK>P1(NP)THENP1(NP)=K
2096 NP=NP+1:GOTO2091
2098 PRINT"PRESS YOUR KEY TO REGISTER";
2099 GOSUB502
2100 P1(NP)=ASC(A$):NP=NP+1:GOTO2091
2101 GOSUB2300
2102 GOSUB502
2103 TU=RND(NP)-1:GOTO2000
2110 REM LOAD Q'S
2120 FORI=0TO4
2121 B(I,0)=50
2122 B(I,1)=100
2123 B(I,2)=200
2124 B(I,3)=300
2125 B(I,4)=400
2126 B(I,5)=500
2129 NEXTI
2131 CLS:PRINT"load questions from ";:IFMD$="D"THEN2140
2132 PRINT"tape":PRINT:PRINT"READY TAPE - PRESS PLAY":PRINT:PRINT"ENTER FILENAME ";:INPUTB$
2133 OPEN"I",#-1,B$
2134 FORI=0TO4:FORJ=0TO5
2135 INPUT#-1,H$(I,J),Q2$(I,J),Q$(I,J),Q1$(I,J)
2136 NEXTJ:NEXTI
2137 CLOSE#-1
2139 GOTO2000
2140 PRINT"disk":PRINT
2142 PRINT"ENTER FILENAME ";:INPUTB$:B$=B$+"/DAT"
2144 OPEN"D",#1,B$,133
2146 FIELD #1,5 AS C1$,8 AS C2$,60 AS C3$,60 AS C4$
2148 K=0
2150 FORI=0TO4:FORJ=0TO5
2152 K=K+1:GET#1,K
2154 H$(I,J)=C1$:Q2$(I,J)=C2$:Q$(I,J)=C3$:Q1$(I,J)=C4$
2156 NEXTJ:NEXTI
2160 CLOSE#1
2199 GOTO2000
2200 REM SAVE QUESTIONS
2201 CLS:PRINT"save questions to ";:IFMD$="D"THEN2210
2202 PRINT"tape":PRINT:PRINT"READY TAPE - PRESS PLAY & RECORD ":PRINT"ENTER FILENAME ";:INPUTB$
2203 OPEN"O",#-1,B$
2204 FORI=0TO4:FORJ=0TO5
2205 PRINT#-1,H$(I,J),Q2$(I,J),Q$(I,J),Q1$(I,J)
2206 NEXTJ:NEXTI
2207 CLOSE#-1
2209 GOTO2000
2210 PRINT"disk":PRINT
2212 PRINT"ENTER FILENAME ";:INPUTB$:B$=B$+"/DAT"
2214 OPEN"D",#1,B$,133
2216 FIELD#1,5 AS C1$,8 AS C2$,60 AS C3$,60 AS C4$
2218 K=0
2220 FORI=0TO4:FORJ=0TO5
2224 LSETC1$=H$(I,J):LSETC2$=Q2$(I,J):LSETC3$=Q$(I,J):LSETC4$=Q1$(I,J)
2225 K=K+1:PUT#1,K
2226 NEXTJ:NEXTI
2240 CLOSE#1
2290 GOTO2000
2300 REM DISPLAY PLAYERS
2310 CLS:PRINT" total  players":PRINT
2311 FORPA=0TONP-1:PA(PA)=PA:NEXTPA
2314 FORI=1TONP-1:FORJ=1TONP-1
2316 IFP2(PA(J))>P2(PA(J-1))THENK=PA(J-1):PA(J-1)=PA(J):PA(J)=K
2318 NEXTJ:NEXTI
2320 FORI=0TONP-1
2322 PRINTUSING"+#####  %                 % ###";P2(PA(I)),P$(PA(I)),P1(PA(I))
2324 NEXTI
2390 RETURN
2400 REM GENERAL BOARD
2410 CLS:PRINTA2$
2420 FORJ=0TO5:FORI=0TO4
2422 PRINT".";:PRINTUSING"%   %";H$(I,J);
2424 NEXTI:PRINT"."
2426 NEXTJ
2450 PRINT"----1-----2-----3-----4-----5--"
2460 FORJ=0TO5
2461 FORI=0TO4
2462 PRINT"!";:IFB(I,J)=0THENPRINT"     ";ELSEPRINTUSING"#### ";B(I,J);
2463 NEXTI:PRINTUSING"#";J+1
2464 NEXTJ
2470 PRINT"-------------------------------"
2480 PRINTP$(TU);" - select";:PRINT@504,"T A C M";
2490 RETURN
2500 REM NORMAL PLAY
2510 GOSUB2400
2520 GOSUB502
2530 IFA$="M"THEN2000
2535 IFA$="C"THEN2900
2539 IFA$<>"T"THEN2545
2540 GOSUB2300
2541 GOSUB502
2542 GOTO2500
2545 IFA$="A"THENGOSUB2700:GOTO2500
2560 IFA$<"1"THEN2520
2562 IFA$>"5"THEN2520
2564 CO=VAL(A$)-1
2570 GOSUB502
2572 IFA$<"1"THEN2520
2574 IFA$>"6"THEN2520
2576 RO=VAL(A$)-1
2578 IFB(CO,RO)=0THEN2500
2580 GOTO5000
2700 REM SHOW ANSWER
2710 IFCO=99THENRETURN
2720 CLS
2730 PRINT"question:":PRINT"   ";Q$(CO,RO):PRINT:PRINT
2740 PRINT"answer:":PRINT"   ";Q1$(CO,RO)
2780 GOSUB502
2790 RETURN
2900 REM CORRECT A PLAYER'S SCORE
2910 GOSUB2300
2920 PRINT@448,"PLAYER TO CORRECT: PLEASE IDENT";
2925 IFPB$="K"THEN2950
2930 J=JOYSTK(0):IFINKEY$<>""THEN2990
2932 IFJ<4THEN2930
2940 PL=99:FORI=0TONP-1
2942 IFJ<P1(I)+2ANDJ>P1(I)-2THENPL=I
2944 NEXTI
2946 IFPL=99THEN2930
2948 GOTO2960
2950 GOSUB502
2952 PL=999:FORI=0TONP-1
2954 IFASC(A$)=P1(I)THENPL=I
2956 NEXTI:IFPL=999THEN2950
2960 PRINT@448,"ENTER CORRECTION AMT FOR ";P$(PL);"'S SCORE";:INPUTK
2970 P2(PL)=P2(PL)+K
2980 GOSUB2300
2982 FORI=1TO1000:NEXTI
2990 GOSUB2500
3000 REM INPUT INTO A$
3010 A=0:A$=""
3015 PRINT@A9,"? ";A$;CHR$(255);" ";
3020 Z$=INKEY$:IFZ$=""THEN3020
3030 IFZ$=CHR$(13)THENRETURN
3040 IFZ$<>CHR$(8)THENA=A+1:A$=A$+Z$:GOTO3015
3050 A=A-1:IFA<1THEN3010
3060 A$=LEFT$(A$,A):GOTO3015
4020 REM DISPLAY CATAORY
4030 PRINT@32,"catagory ";CO+1
4032 PRINT@064," >-----<";
4035 FORJ=0TO5:J1=096+J*32:PRINT@J1,USING"  %    %    ";H$(CO,J);:NEXTJ
4039 PRINT@288," >-----<";
4040 RETURN
4045 REM INPUT CATAGORY
4050 FORJ=0TO5
4052 J1=096+(J*32):A9=J1:GOSUB3000
4053 IFLEN(A$)>5THENA$=LEFT$(A$,5)
4054 IFA$<>""THENH$(CO,J)=A$
4056 PRINT@J1,USING"  %    %    ";H$(CO,J);
4058 NEXTJ
4059 RETURN
4070 REM INPUT Q & A
4071 PRINT@320,USING"    %      % #  %  %  %        %";"question",RO+1,"type",Q2$(CO,RO);
4072 PRINT@352,USING"%% %                                                          %";"q",Q$(CO,RO);
4074 PRINT@416,USING"%% %                                                          %";"a",Q1$(CO,RO);
4078 A9=340:GOSUB3000:IFLEN(A$)>8THENA$=LEFT$(A$,8)
4080 IFA$<>""THENQ2$(CO,RO)=A$
4082 PRINT@340,USING"  %      %  ";Q2$(CO,RO);
4084 A9=353:GOSUB3000:IFLEN(A$)>60THENA$=LEFT$(A$,60)
4086 IFA$<>""THENQ$(CO,RO)=A$
4088 PRINT@353,USING"  %                                                           %";Q$(CO,RO);
4090 A9=417:GOSUB3000:IFLEN(A$)>60THENA$=LEFT$(A$,60)
4092 IFA$<>""THENQ1$(CO,RO)=A$
4094 PRINT@417,USING"  %                                                           %";Q1$(CO,RO);
4098 RETURN
4200 REM MODIFY 
4210 CLS:PRINT" modify screen":PRINT
4220 INPUT"qUESTION OR cATAGORY";A$
4222 IFA$<>"Q"ANDA$<>"C"THEN2000
4230 INPUT"WHICH CATAGORY (1-5)";CO
4232 IFCO<1THEN2000
4234 IFCO>5THEN2000
4236 CO=CO-1
4238 IFA$="Q"THEN4260
4240 CLS:PRINT"          modify catagory":GOSUB4020
4244 GOSUB4045:GOTO2000
4250 GOTO2000
4260 INPUT"WHICH ROW (1-6)";RO
4262 IFRO<1THEN2000
4264 IFRO>6THEN2000
4266 RO=RO-1
4280 CLS:PRINT"          modify question":GOSUB4020
4282 GOSUB4070
4290 GOTO2000
4300 REM COMPLETE ENTER
4310 CLS:PRINT"          enter new board"
4320 FORCO=O TO4
4322 GOSUB4020
4326 GOSUB4045
4330 FORRO=0TO5
4332 GOSUB4070
4340 NEXTRO
4350 NEXTCO
4390 GOTO2000
4500 REM FINAL
4505 GOSUB2300
4507 GOSUB502
4510 CLS:PRINT"          final g-party":PRINT@128,"catagory:"
4520 PRINT@160,H$(0,0);H$(0,1);H$(0,2);H$(0,3);H$(0,4);H$(0,5);
4530 GOSUB502
4540 PRINT@256,"question:"
4550 PRINT@291,Q$(0,0);
4560 GOSUB502
4575 PA=0:FORI=0TONP-1:IFP2(PA(I))>0THENPA=PA+1ELSEPS(PA(I))=0
4580 NEXTI
4600 PB=PA:FORI=PB-1TO0STEP-1:PB(PB-1-I)=PA(I):NEXTI
4610 FORA9=0TOPB-1
4620 GOSUB2300
4622 PRINT@448,P$(PB(A9));
4625 GOSUB502
4626 IFA$=CHR$(8)THENDD=-1:GOTO4630
4627 IFA$=CHR$(9)THENDD=1:GOTO4630
4628 GOTO4625
4630 PRINT"'S WAGER ";:INPUTK
4640 P2(PB(A9))=P2(PB(A9))+K*DD
4650 NEXTA9
4660 GOSUB2300
4670 PRINT@448,"congradulations ";P$(PA(0));
4680 GOSUB502
4690 GOTO2000
5000 REM DISPLAY QUESTION
5002 DD=1
5003 A$=INKEY$:IFA$<>""THENPRINT@448,"someone has their key pushed";:goto5003
5004 IFPB$="K"THEN5006
5005 J=JOYSTK(0):IFJ>1THENPRINT@448,"someone has their button pushed";:GOTO5005
5006 IFQ2$(CO,RO)>"        "THEN5100
5007 FORPA=0TONP-1:PA(PA)=P1(PA):NEXTPA
5010 CLS:PRINT"       question"
5050 PRINT@131,Q$(CO,RO)
5090 GOTO20
5100 REM DAILY DOUBLE
5105 PL=TU:DD=2
5110 CLS:PRINT@128,"***    D A I L Y     ***":PRINT@160,"      D O U B L E"
5120 IFQ2$(CO,RO)<>"A       "ANDQ2$(CO,RO)<>"D       "THENPRINT@096,"       v i d i o"
5130 IFQ2$(CO,RO)="A       "THENPRINT@096,"       a u d i o"
5140 PRINT@224,"player - ";P$(PL);
5150 PRINT@256,"total  - ";P2(PL);
5160 K=P2(PL):IFK<MX THEN K=500
5165 PRINT@320,"limit  - ";K
5170 PRINT@336,"";:INPUT"WAGER ";B(CO,RO):IFB(CO,RO)>K THEN5170
5175 CLS:PRINT"       question":PRINT@131,Q$(CO,RO);
5177 IFQ2$(CO,RO)="D       "THEN5198
5178 IFQ2$(CO,RO)="A       "THEN5190
5180 IFMD$="D"THENQ2$(CO,RO)=Q2$(CO,RO)+"/BIN":LOADMQ2$(CO,RO) ELSE CLOADMQ2$(CO,RO)
5182 PMODE4,1:SCREEN1,1:FORI=1TO100:NEXTI:GOTO5198
5190 AUDIOON:MOTORON
5191 GOSUB502
5192 AUDIOOFF:MOTOROFF
5198 GOTO40
6000 REM PRINT Q & A LIST
6010 PRINT#-2,"     LIST OF Q & A FOR FILE ";B$
6012 PRINT#-2
6020 FORCO=0TO4
6022 PRINT#-2:PRINT#-2,"***  ";
6024 FORJ=0TO5:PRINT#-2,H$(CO,J);" ";:NEXTJ:PRINT#-2," "
6026 FORRO=0TO5
6028 PRINT#-2,USING"# # %% %                                                          % %        %";CO+1,RO+1,"Q",Q$(CO,RO),Q2$(CO,RO)
6030 PRINT#-2,USING"    %% %                                                          %";"A",Q1$(CO,RO)
6032 PRINT#-2
6034 NEXTRO
6036 NEXTCO
6090 GOTO2000
9900 REM SETUP
9910 DIMB(5,6),P1(10),P2(10),PA(10),PB(10)
9920 DIMP$(10),Q$(5,6),Q1$(5,6),Q2$(5,6),H$(5,6)
9930 CLS:PRINT" WELCOME TO CO-CO g-party!"
9932 PRINT@128,"WILL YOU USE (G)AME-BUTTONS OR  (K)EYBOARD FOR PLAYER BIDDING";:INPUTPB$:IFPB$<>"G"ANDPB$<>"K"THEN9932
9938 IFPEEK(&HC000)<>&H44 THEN MD$="T":GOTO9990
9940 PRINT@256,"WILL YOU USE (D)ISK OR (T)APE   FOR FILE STORAGE";:INPUTMD$:IFMD$<>"D"ANDMD$<>"T"THEN9940
9990 GOTO2000
