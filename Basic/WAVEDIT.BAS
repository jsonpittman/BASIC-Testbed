1 '*****************************
2 '*        WAVE EDITOR        *
3 '*    BY JEREMY SPILLER      *
4 '*          (C) 1987         *
5 '*****************************
15 S$=CHR$(13)+CHR$(94)+CHR$(10)+CHR$(8)+CHR$(9)+CHR$(95)+CHR$(91)+CHR$(21)+CHR$(93)+"12345":DIMP(6,6)
20 CLS:PRINT"  WAVE EDIT   BY JEREMY SPILLER"
30 PRINT:PRINT"      1) LOAD WAVEFORMS":PRINT"      2) SAVE WAVEFORMS":PRINT"      3) EDIT WAVEFORM":PRINT
40 LINE INPUT"ENTER SELECTION: ";I$
50 ON VAL(I$) GOSUB 100,200,300:GOTO 20
100 CLS:PRINT"LOAD WAVEFORM":GOSUB 250:IF T$="T" THEN CLOADM N$ ELSE LOADM N$
110 RETURN
200 CLS:PRINT"SAVE WAVEFORM":GOSUB 250:IF T$="T" THEN GOTO 205 ELSE SAVEM N$,&HFB00,&HFDFF,&HB44A:GOTO 210
205 CSAVEM N$,&HFB00,&HFDFF,&HB44A
210 RETURN
250 PRINT:INPUT"(D)ISK OR (T)APE";T$:IFT$<>"D"ANDT$<>"T"THEN250
260 LINE INPUT"ENTER FILE NAME: ";N$:RETURN
300 CLS:LINEINPUT"WHICH WAVE FORM WOULD YOU LIKE  TO WORK ON (1-3): ";W$:W=VAL(W$):IF W<>INT(W) OR W<1 OR W>3 THEN 300
310 PMODE4,1:PCLS:SCREEN1,1:X=128:LINE(0,X)-(255,X),PSET:Y=64
320 W=&HFB00+(W-1)*256:FOR I=0 TO 255:PSET(I,PEEK(W+I)/2):NEXT
330 H=((X-3<0)+1)*(X-3):V=((Y-3<0)+1)*(Y-3):GET(H,V)-(X+3,Y+3),P,G:DRAW"BM=X;,=Y;NE3NF3NG3NH3"
340 I$=INKEY$:IF I$="" THEN 340 ELSE PUT(H,V)-(X+3,Y+3),P,PSET:ON INSTR(1,S$,I$) GOSUB 500,550,600,650,700,750,800,850,900,950,950,950,950,950:IF I$<>"M" THEN 330 ELSE RETURN
500 PRESET(X,PEEK(W+X)/2):PSET(X,Y):POKE W+X,Y*2:X=X+1:IF X>255 THEN X=255:RETURN ELSE RETURN
550 Y=Y-1:IF Y<0 THEN Y=0
560 RETURN
600 Y=Y+1:IF Y>127 THEN Y=127
610 RETURN
650 X=X-1:IF X<0 THEN X=0
660 RETURN
700 X=X+1:IF X>255 THEN X=255
710 RETURN
750 Y=Y-4:IF Y<0 THEN Y=0
760 RETURN
800 Y=Y+4:IF Y>127 THEN Y=127
810 RETURN
850 X=X-8:IF X<0 THEN X=0
860 RETURN
900 X=X+8:IF X>255 THEN X=255
910 RETURN
950 PLAY"V31L4T4W"+W$+"O"+I$+"C":RETURN
