10 REM**************************
15 REM* LIFE SKILLS MATH DRILL *
20 REM*    THE METRIC SYSTEM   *
25 REM*   MEASURING IN METERS  *
30 REM*    BY FRED B.SCERBO    *
35 REM*     60 HARDING AVE     *
40 REM*  NORTH ADAMS,MA 01247  *
45 REM*   COPYRIGHT (C) 1987   *
50 REM**************************
55 CLS0:FORI=1TO32:PRINTCHR$(188);:NEXT
60 FORI=1TO192:READA:IFA=0THENA=16
65 PRINTCHR$(A+128);:NEXT
70 FORI=1TO32:PRINTCHR$(179);:NEXT
75 DATA109,104,96,109,104,100,110,108,106,109,108,109,,,30,28,26,29,,,30,20,30,20,30,16,20,30,,21,28,29
80 DATA101,,,101,,,106,96,104,101,,100,,,26,,24,21,16,22,16,,26,,26,,16,26,,21,,20
85 DATA101,,,101,,,107,106,96,101,99,98,,,27,19,18,21,22,16,,,26,,26,,16,26,,21,19,19
90 DATA101,,,101,,,106,104,,101,,,,,,,26,21,20,18,,,26,,26,,16,26,,,,21
95 DATA101,,106,101,,,106,,,101,,97,,16,26,,26,21,,20,18,,26,,26,21,16,26,21,21,,21
100 DATA103,99,106,103,98,97,107,,,103,99,103,,,27,19,26,23,18,,27,17,27,17,27,23,17,27,23,21,19,23
105 PRINT@293,"  THE METRIC SYSTEM   ";
110 PRINT@325," MEASURING IN METERS  ";
115 PRINT@357,"   BY FRED B.SCERBO   ";
120 PRINT@389,"  COPYRIGHT (C) 1987  ";
125 PRINT@453,"  (R)EVIEW OR (Q)UIZ  ";
130 DIMA$(50),B$(50),C(7)
135 Q=143:FORI=1TO7:C(I)=Q+(I*16):NEXT
140 FORI=1TO7:READP$(I),P(I):NEXTI
145 DATA MILLIMETERS,1,CENTIMETERS,10,DECIMETERS,100,METERS,1000,DECAMETERS,10000,HECTOMETERS,100000,KILOMETERS,1000000
150 SW=30
155 GOTO190
160 IF LEN(JK$)<=SW THEN180
165 FOR T=SW TO 0STEP-1:IF MID$(JK$,T,1)=" "THEN175
170 NEXT T:GOTO180
175 L$=LEFT$(JK$,T):W$=L$:GOSUB185:JK$="   "+RIGHT$(JK$,(LEN(JK$))-T):GOTO160
180 W$=JK$:PRINTW$:RETURN
185 PRINTW$:RETURN
190 FORJ=1TO50:READ A$(J),B$(J):IF A$(J)="END" THEN200
195 NEXTJ
200 X$=INKEY$:IFX$=""THEN200
205 IFX$="R"THEN220
210 IFX$="Q"THEN255
215 MU=RND(-TIMER):GOTO200
220 CLS0:FORI=1TOJ-1:C=RND(7):FORY=1TO32:PRINTCHR$(C(C));:NEXTY:PRINT@64,"";:JK$="   "+A$(I):GOSUB160
225 FORY=1TO32:PRINTCHR$(128);:NEXTY:FORY=1TO32:PRINTCHR$(C(C));:NEXTY:FORY=1TO32:PRINTCHR$(128);:NEXTY
230 IFINKEY$<>CHR$(13)THEN230
235 JK$="   "+B$(I):GOSUB160
240 FORY=1TO32:PRINTCHR$(128);:NEXT:FORY=1TO32:PRINTCHR$(C(C));:NEXTY
245 IFINKEY$<>CHR$(13)THEN245
250 CLS0:NEXT
255 CLS0:N(1)=RND(7)
260 N(2)=RND(7):IFN(2)=N(1)THEN260
265 C=RND(7)::FORI=1TO32:PRINTCHR$(C(C));:NEXTI:FORI=1TO32:PRINTCHR$(128);:NEXTI:PRINT
270 PRINT"   WHICH OF THESE IS GREATER ?"
275 V=RND(9):NN(1)=P(RND(4))*V:NN(2)=P(RND(4))*V
280 NV(1)=P(N(1))*NN(1):NV(2)=P(N(2))*NN(2):IFNV(1)=NV(2)THEN275
285 PRINT:PRINT"     A) ";NN(1);P$(N(1))
290 PRINT:PRINT"     B) ";NN(2);P$(N(2)):PRINT
295 IFNV(1)>NV(2)THENR$="A"
300 IFNV(2)>NV(1)THENR$="B"
305 FORI=1TO32:PRINTCHR$(128);:NEXTI:FORI=1TO32:PRINTCHR$(C(C));:NEXTI:FORI=1TO32:PRINTCHR$(128);:NEXTI
310 X$=INKEY$:IFX$=""THEN310
315 IFX$="@"THEN520
320 IFX$="A"THEN335
325 IFX$="B"THEN335
330 GOTO310
335 IFX$=R$THEN345
340 PRINT"  SORRY, BUT THAT IS NOT RIGHT!":NW=NW+1:GOTO350
345 PRINT"  YES, YOUR ANSWER IS CORRECT!":NC=NC+1
350 FORI=1TO32:PRINTCHR$(128);:NEXT:FORI=1TO32:PRINTCHR$(C(C));:NEXT
355 IFR$="A"THEN LC=161
360 IFR$="B"THEN LC=225
365 X$=INKEY$
370 PRINT@LC,")=>";:FORI=1TO50:NEXTI:PRINT@LC,"   ";:FORI=1TO50:NEXTI
375 IFX$="@"THEN520
380 IFX$=CHR$(13)THEN255
385 GOTO365
390 DATA OUR CENTRAL UNIT OF MEASURE IN THE METRIC SYSTEM IS CALLED THE METER.,IT IS EQUAL TO 39.37 INCHES
395 DATA ONE METER IS ALSO EQUAL TO 3.28 FEET.,ONE METER IS ALSO EQUAL TO 1.09 YARDS.
400 DATA MOST COUNTRIES IN THE WORLD OTHER THAN THE UNITED STATES USE THE METRIC SYSTEM.,THE METRIC SYSTEM IS EASY TO USE BECAUSE ALL MEASURING UNITS ARE IN POWERS OF TEN (10).
405 DATA NOW WE WILL LOOK AT THE DIFFERENT UNITS WHICH MAKE UP THE METRIC SYSTEM.,WE WILL DEAL ONLY WITH UNITS OF LENGTH BASED ON THE METER.
410 DATA WE CAN DIVIDE A METER UP INTO TEN (10) EQUAL PARTS.,EACH ONE OF THESE PARTS IS CALLED A DECIMETER.
415 DATA THE ROOT 'DECI' MEANS 'ONE-TENTH' SO A DECIMETER IS ONE-TENTH OF A METER.,THERE ARE TEN (10) DECIMETERS IN A METER.
420 DATA WE COULD ALSO DIVIDE A METER UP INTO ONE HUNDRED (100) EQUAL PARTS.,EACH ONE OF THESE PARTS IS CALLED A CENTIMETER.
425 DATA THE ROOT 'CENT' MEANS 'ONE-HUNDREDTH' SO A CENTIMETER IS ONE-HUNDREDTH OF A METER.,THERE ARE ONE HUNDRED (100) CENTIMETERS IN A METER.
430 DATA WE COULD ALSO DIVIDE A METER UP INTO ONE THOUSAND (1000) EQUAL PARTS.,EACH ONE OF THESE PARTS IS CALLED A MILLIMETER.
435 DATA THE ROOT 'MILLI' MEANS 'ONE-THOUSANDTH' SO A MILLIMETER IS ONE-THOUSANDTH OF A METER.,THERE ARE ONE THOUSAND (1000) MILLIMETERS IN A METER.
440 DATA LET'S LOOK AT THE RELATIONSHIPS BETWEEN THESE UNITS., 1 METER =   10 DECIMETERS 1 METER =  100 CENTIMETERS 1 METER = 1000 MILLIMETERS
445 DATA WE CAN CHANGE METERS (M) TO DECIMETERS (DM) BY MULTIPLYING BY TEN (10).,5 M X 10 = 50 DM
450 DATA WE CAN CHANGE DECIMETERS (DM) TO CENTIMETERS (CM) BY MULTIPLYING BY TEN (10).,7 DM X 10 = 70 CM
455 DATA WE CAN ALSO CHANGE CENTIMETERS (CM) TO MILLIMETERS (MM) BY MULTIPLYING BY TEN (10).,4 CM X 10 = 40 MM
460 DATA WE HAVE UNITS WHICH ARE LARGER THAN A METER.,TEN (10) METERS IS KNOWN AS A DECAMETER.
465 DATA THE ROOT 'DECA' OR 'DEKA' MEANS TEN.,DECAMETER OR DEKAMETER MEANS TEN METERS.
470 DATA THE ROOT 'HECTO' MEANS ONE HUNDRED (100).,HECTOMETER MEANS ONE HUNDRED METERS.
475 DATA THE ROOT 'KILO' MEANS ONE THOUSAND.,A KILOMETER IS EQUAL TO ONE THOUSAND (1000) METERS.
480 DATA TO FIND HOW MANY METERS IN A NUMBER OF DECAMETERS SIMPLY MULTIPLY BY TEN (10).,5 DECAMTERS X 10 EQUALS 50 METERS.
485 DATA TO FIND HOW MANY METERS IN A NUMBER OF HECTOMETERS SIMPLY MULTIPLY BY ONE HUNDRED (100).,7 HECTOMETERS X 100 EQUALS 700 METERS
490 DATA TO FIND HOW MANY METERS IN A NUMBER OF KILOMETERS SIMPLY MULTIPLY BY ONE THOUSAND (1000).,3 KILOMETERS X 1000 EQUALS 3000 METERS.
495 DATA HERE IS A COMPARISON CHART.,KILOMETER  =   1000 METERS HECTOMETER =    100 METERS DECAMETER  =     10 METERS METER      =      1 METER  DECIMETER  =   1/10 METER  CENTIMETER =  1/100 METER  MILLIMETER = 1/1000 METER
500 DATA HERE IS A BRIEF QUIZ ABOUT WHAT WE HAVE JUST COVERED.,YOU WILL BE GIVEN TWO METRIC VALUES ON THE SCREEN.
505 DATA YOU WILL BE ASKED TO PICK THE VALUE WHICH IS GREATER.,SIMPLY PRESS THE LETTER (A) OR (B) NEXT TO YOUR CHOICE.
510 DATA YOU MAY STOP THE QUIZ ANYTIME BY PRESSING THE <@> KEY.,PRESS <ENTER> TO BEGIN THIS SHORT METRIC QUIZ.
515 DATA END,END
520 CLS:PRINT@101,"YOU TRIED"NC+NW"PROBLEMS &":PRINT@165,"ANSWERED"NC"CORRECTLY"
525 PRINT@229,"WHILE DOING"NW"WRONG."
530 NQ=NC+NW:IF NQ=0THEN NQ=1
535 MS=INT(NC/NQ*100)
540 PRINT@293,"YOUR SCORE IS"MS"%."
545 PRINT@357,"ANOTHER TRY (Y/N/C) ?";
550 X$=INKEY$:IFX$="Y"THEN RUN
555 IFX$="N"THENCLS:END
560 IFX$="C"THEN255
565 GOTO550
