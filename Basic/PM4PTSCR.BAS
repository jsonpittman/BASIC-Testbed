0 'SAVE PMODE4 PART SCREENS
1 'WRITTEN BY KEIRAN KENNY
2 'COPYRIGHT (C) DECEMBER 1991
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
10 DIMD(1228)
20 CLS:PRINT@160,"1. SAVE A PARTPIX",,,"2. LOAD A PARTPIX":PRINT:PRINTTAB(4)"PRESS 1 OR 2"
30 K$=INKEY$:IFK$<>"1"ANDK$<>"2"THEN30
40 IFK$="2"THEN390
50 CLS:PRINT@224,"IS FULL PIX IN MEMORY? Y/N"
60 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN60
70 IFK$="Y"THEN90
80 PRINT:INPUT"LOAD PIX:";PX$
90 PMODE4,1:COLOR0,5
100 IFPEEK(&H15F)=196THENDV=1ELSEDV=-1
110 IFK$="Y"THEN130
120 IFDV=1THENLOADMPX$ ELSECLOADMPX$
130 SCREEN1,1
140 X=128:Y=96:N=1
150 IFPEEK(341)=247THENY=Y-N
160 IFPEEK(342)=247THENY=Y+N
170 IFPEEK(343)=247THENX=X-N
180 IFPEEK(344)=247THENX=X+N
190 IFPEEK(338)=247THENN=6ELSEN=1
200 IFPEEK(135)=85THENX1=X:Y1=Y
210 IFP ANDPEEK(135)=83THEN340ELSEIFP=0ANDPEEK(135)=83THENX=X1:Y=Y1:GOTO340
220 IFPEEK(135)=76THENH=X-X1:V=Y-Y1:GET(X1,Y1)-(X1+H,Y1+V),D,G:PCLS:PUT(X1,Y1)-(X1+H,Y1+V),D,PSET:POKE135,0
230 IFPEEK(135)=77THENP=1:PCLS:PUT(X,Y)-(X+H,Y+V),D,PSET:POKE135,0
240 IFX<0THENX=0
250 IFX+H>255THENX=255-H
260 IFX>255THENX=255
270 IFY<0THENY=0
280 IFY+V>191THENY=191-V
290 IFY>191THENY=191
300 DR=PPOINT(X,Y):IFDR=0THENCL=5ELSECL=0
310 PSET(X,Y,CL):FORD=1TO100:NEXT:PSET(X,Y,DR):GOTO320
320 GOTO150
330 GOTO330
340 POKE135,0:CLS:PRINT@224,"SAVE PARTPIX NAME:";:INPUTPP$
350 SCREEN1,1
360 IFDV=1THENSAVEMPP$,3584,9727,40999:GOTO380
370 CSAVEMPP$,1536,7679,40999
380 CLS:PRINT@224,"PARTPIX "+CHR$(34)+PP$+CHR$(34)+" SAVED.":END
390 CLS:PRINT@224,"LOAD PARTPIX NAME:";:INPUTPP$
400 PMODE4,1:COLOR0,5:PCLS:SCREEN1,1
410 IFPEEK(&H15F)=196THENLOADMPP$ ELSECLOADMPP$
420 SOUND128,4
430 EXEC44539:CLS:END
