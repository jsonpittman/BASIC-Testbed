5 CLEAR2000
10 DIMA(16)
12 CLS:PRINT"THIS PROGRAM WILL START A NEW   GAME OF GALACTIC CONFLICT":PRINT"BE SURE THE PROPER DISKETTE IS  INSERTED IN DRIVE 0.":PRINT
15 INPUT"HOW MANY LEVELS";LV:INPUT"ENTER GALAXY SIZE (13-25)";SZ:IFSZ<13 OR SZ>25 THENPRINT"TRY AGAIN!":GOTO15
16 PRINT"HOLD ON A SEC..."
17 DIMG(SZ,SZ)
20 FORZ=1TO16:A(Z)=0:NEXT:FORZ=0TO9:B(Z)=0:NEXT
2100 'PUT RECORD
2102 D1$="":C1$="":FOR Z=1TO16:C1$=C1$+MKN$(A(Z)):NEXT:FORZ=0 TO 9:D1$=D1$+MKN$(B(Z)):NEXT:AT$="/":D1$=D1$+AT$
2105 IFL=0 THENA1$="\":B1$="\"
2106 IF R=42 THEND1$="":FORF=1TO5:D1$=D1$+"NONAME    "+MKN$(0)+MKN$(0):NEXTF
2110 OPEN"D",#1,"GALAXUSR/DAT",200
2120 FIELD#1,10 AS A$,10 AS B$,80 AS C$,100 AS D$
2130 LSET A$=A1$:LSET B$=B1$:LSET C$=C1$:LSET D$=D1$
2135 IF L=0 THENFORR=1TO42
2140 PUT#1,R
2145 IF L=0 THENNEXT
2150 CLOSE#1
3000 IF L=0 THEN L=1:R=42:A1$="Noname":B1$="Cadet":A(1)=0:A(2)=0:A(3)=SZ:A(4)=LV:GOTO2100
3010 CLOSE#1
4000 FOR LL=1 TO LV:PRINT"LEVEL";LL
4010 INPUT"# OF ROBOTOIDS";RO:INPUT"# OF STARTGATES";ST:INPUT"# OF CARGO";CA:INPUT"# OF ??";Q
4020 Z=SZ*SZ:Z1=RO+Q+ST+CA:IFZ<=Z1 THENPRINT"START OVER..":PRINT"YOU CAN ONLY HAVE";Z;"ITEMS TOTAL":GOTO4010
4021 PRINT"HOLD ON WHILE I CREATE THE LEVEL"
4022 FOR Z=1 TO Q
4024 X=RND(SZ):Y=RND(SZ):IFG(X,Y)=0 THEN G(X,Y)=90+RND(8) ELSE4024
4026 NEXTZ
4030 FOR Z=1TORO
4040 X=RND(SZ):Y=RND(SZ):IF G(X,Y)=0 THEN G(X,Y)=40+RND(9) ELSE 4040
4050 NEXTZ
4060 FOR Z=1 TO ST
4070 X=RND(SZ):Y=RND(SZ):IF G(X,Y)=0 THEN G(X,Y)=50 ELSE4070
4080 NEXTZ
4090 FOR Z=1 TO CA
4100 X=RND(SZ):Y=RND(SZ):IF G(X,Y)=0 THEN G(X,Y)=RND(7)*1000 ELSE 4100
4110 NEXT
4120 Z$="LEVEL"+STR$(LL)
4130 GOSUB4140:NEXTLL:GOTO4999
4140 G(1,1)=99:G(SZ,SZ)=99:G(1,SZ)=99:G(SZ,1)=99
4150 PRINT"MAKING ";Z$
4160 OPEN"O",#1,Z$
4170 FOR X=1TOSZ:FOR Y=1 TOSZ
4180 WRITE #1,G(X,Y)
4190 NEXTY,X
4200 CLOSE#1:FOR TR=1 TO SZ:FOR TQ=1 TO SZ:G(TR,TQ)=0:NEXT TQ,TR:RETURN
4999 OPEN"O",#1,"RULES/TXT":PRINT#1,"The rules go here!":CLOSE#1
5000 PRINT"NEWGAME COMPLETE":PRINT"NOW ALL YOU HAVE TO DO IS:":PRINT"LOADM REMOTE2":PRINT"EXEC":PRINT"PCLEAR1":PRINT"RUN GALACTIC"
