0 'EDUCATION NOTES
1 'WRITTEN BY STEVE BLYN
2 'PUBLISHED JANUARY 1991
10 REM"MAILING LETTERS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1990"
30 CLEAR 1000:J=RND(-TIMER):CLS5:LINEINPUT"SELECT ANY NAME ";N$
40 IF LEN(N$)>12 OR LEN(N$)<2 THEN 30 
50 A=3+RND(6):B=6+RND(6):C=(A*.25)+(B*.15):D=10-C:P$=STRING$(32,255)
60 CLS0:PRINT@7,"MAILING    LETTERS";:PRINT@32,P$;
70 PRINT@64,N$;" WENT TO THE POST"
80 PRINT"OFFICE TO MAIL SOME LETTERS."
90 PRINT@160,"THE MAIL FOR THIS TRIP WAS ..."
100 PRINT@192,A;"REGULAR LETTERS AND":PRINTB;"POST CARDS."
110 PRINT@128,P$;:GOSUB 280:PRINT@256,P$;
120 PRINT@288,"WHAT WAS THE COST OF";
130 GOSUB 280:PRINT@325," ":PRINT@325,"1. THE LETTERS?    $";
140 LINEINPUT A$:AA=VAL(A$)
150 IF INT((100*AA)+.01)=INT(A*25) THEN PLAY"L50CDEGG" ELSE 130
160 GOSUB 280:PRINT@355," ":PRINT@355,"2. THE POST CARDS?    ";
170 LINEINPUT B$:BB=VAL(B$)
180 IF INT((100*BB)+.01)=INT(B*15) THEN PLAY"L50CDEGG" ELSE 160
190 GOSUB 280:PRINT@385," ":PRINT@385,"3. THE TOTAL OF BOTH?   ";
200 LINEINPUT C$:CC=VAL(C$)
210 IF INT((100*CC)+.01)=INT(C*100) THEN PLAY"L50CDEGG" ELSE 190
220 PRINT@416,P$;:GOSUB 280:PRINT@448," ":PRINT@448,"WHAT CHANGE FROM $10.00? ";
230 LINEINPUT D$:DD=VAL(D$)
240 IF INT((100*DD)+.01)=INT(D*100) THEN PLAY"L50CDEGG" ELSE 220
250 PRINT@484,"PRESS enter TO CONTINUE";
260 EN$=INKEY$
270 IF EN$=CHR$(13) THEN RUN ELSE IF EN$="E" THEN 290 ELSE 260
280 PLAY"L8O2L20CDEFGAB;O3C":RETURN
290 CLS:END
