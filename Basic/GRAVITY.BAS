0 'COPYRIGHT 1989, FALSOFT INC.
10 CLEAR300:PCLEAR8:SOUND155,1
20 PMODE4,1:PCLS(0):PMODE4,5:PCLS(0):CLS(4):COLOR5,0
30 '****************************
32 '       constants
34 S1$="####.#":S2$="######.#":S3$="###":PI=3.1416:G=9.8
36 '****************************
38 '           <TITLE>
39 PRINT@108,"GRAVITY";
40 PRINT@168,"parabolic";:PRINT@178,"motion";
50 PRINT@238,"BY";:PRINT@264," HECTOR CUADRA";:PRINT@296,"1336 E.MIFFLIN";:PRINT@330,"MADISON WI";:PRINT@365,"53703";
70 IFN$="7"THENENDELSEJ=0:GOSUB1200
90 FORN=1TO10:PLAY"L255;O5;CEGC":NEXT:J=1:POKE135,0:GOSUB6000
100 '***************************
110 '        instructions
120 CLS:PRINT"    kinematics of a particle":PRINT STRING$(32,246);
130 PRINT" PATH OF A NON-SELFPROPELLED    OBJECT NEAR THE EARTH'S SURFACE":PRINT"WITH NO AIR OR WIND RESISTANCE.";
140 PRINT" YOU CHOOSE THE INITIAL VELOCITY":PRINT"AND THE ELEVATION ANGLE 'A'":PRINT"ABOVE THE HORIZON."
150 PRINT"THE HORIZONTAL COMPONENT OF THE":PRINT"INITIAL VELOCITY IS V*COS(A),":PRINT"THE VERTICAL ONE IS V*SIN(A)."
160 PRINT"HORIZONTAL DISTANCE TRAVELED IS":PRINT"V*(COS(A)*T WHILE ITS":PRINT"HEIGHT IS V*SIN(A)*T-.5*G*T*T."
170 PRINT STRING$(32,246);
180 GOSUB 1530
200 CLS:PRINTSTRING$(32,246);
210 PRINT:PRINT"YOUR CANNON IS AT THE LEFT SIDE OF THE SCREEN."
220 PRINT"YOUR TARGET IS THE LITTLE BOX","ABOVE THE ARROW."
230 PRINT"PLEASE, DO NOT HIT THE LARGE","BUILDING AT YOUR RIGHT."
240 PRINT"THE HORIZONTAL SCALE AT THE ","BOTTOM OF THE SCREEN IS","CALIBRATED IN 10 METERS/LINE."
242 PRINT"FOR HELP ENTER 'H' AT 'VIEW'.   ...GOOD LUCK!"
250 GOSUB 1530
255 '***************************
258 '       game status
260 IF N$="4"THEN 950
285 IF ZZ=1 THEN1330:'new target
295 ZZ=0:'reset record of target
300 SOUND 200,1:SOUND 255,1:IFTR=1GOSUB1330
305 '***************************
308 '        data input
310 CLS4:PRINT@0,STRING$(32,182);
320 PRINT@42,"input data";:PRINT@64, STRING$(32,166);
330 PRINT"    [MKS-SYSTEM, METERS/SEC.]"
340 PRINT"YOUR TARGET IS";XT-7;"METERS AWAY ":PRINT"AT A HEIGHT OF ";:PRINTUSINGS3$;155-YT;:PRINT" METERS.":PRINTSTRING$(32,45);
342 INPUT"WANT A VIEW (Y/N/H)";B$
345 IFB$="Y"THEN GOTO 1424
346 IFB$="H"THEN1700
347 B$="N"
348 SOUND200,1
350 PRINT@256,"INITIAL LAUNCH velocity? =";:LINE INPUTV$:SOUND200,1:IFV$=""THEN350
355 V=ASC(V$):IFV<48ORV>57THEN350
360 PRINT@288,"ELEVATION angle(0-90 DEG)=";:LINE INPUT A$:SOUND200,1:IFA$=""THEN360
365 A=ASC(A$):IF(A<48ORA>57)ORVAL(A$)>90THEN360
370 V=VAL(V$):A=VAL(A$)
380 PRINT@320,"LEAVE PATH?";:INPUT"(Y/N)";Q$:SOUND200,1:IFQ$=""THENQ$="Y":PRINT@337,"y"
390 IFQ$=""THEN400ELSEIFQ$<>"Y"ANDQ$<>"N"THEN380
400 PRINT@352,"IS DATA OK?";:INPUT"(Y/N)";A$:SOUND200,1:IFA$=""THENA$="Y":PRINT@369,"y":FORP=1TO500:NEXT
410 IF A$="N"THEN310
415 TL=TL+1:'number of chances
420 IF A$="Y" THEN 450 ELSE 400
430 '***************************
440 '        display
450 PMODE4,1:SCREEN1,1:POKE65495,0:COLOR5,0
470 '***************************
475 'degrees to radians
510 T=0:B=(PI/180)*A:A1=B
520 TM=(V*SIN(A1))/G:'half time
530 '***************************
540 '         plotting
545 '         formulas
548 GOSUB 3000
549 'time intervals
550 PLAY"L255;O5;C":IF V>100 THEN T=T+.01 ELSE T=T+.1
555 'motion formulas
560 Y=T*V*SIN(B)-(G*T*T)/2
570 X=(V*COS(B))*T
575 'screen mapping formulas
590 Y=Y1-(Y*.792)
600 X=X1+X
610 IF Y=<0 AND X=<255 THEN 550
620 IF X>=255 OR Y>=180 THEN 810:'overshoot!
630 IF (X>=XT AND X<XT+7) AND (YT-6<Y AND Y<=YT) THEN GOTO 1110
640 IF PPOINT(X,Y)=5 GOSUB 690:'checking for obstacles
650 PSET(X,Y,5):'plot
660 IF Q$="N" THEN GOSUB 1570
670 GOTO 550
680 '***************************
690 '         obstacles
700 IF (X>204 AND X=<233) AND Y>=145 THEN 740
710 IF PPOINT(X+1,Y)=5 AND PPOINT(X-1,Y)=5 THEN 1575
720 RETURN
730 '***************************
740 '       wrong target
750 FORPM=1TO4:SCREEN1,1:PMODEPM,1:SOUNDPM*50,1:NEXTPM:CO=5:SCREEN1,1
755 IF ZZ=1 THEN 1135
760 FORR=1TO40:CIRCLE(219,150),R,CO:GOSUB780:NEXT R:GOTO790
780 CIRCLE(219,150),R,0:RETURN
790 POKE65494,0:CLS(4):PRINT@234,"*wrong*target*";:PLAY"L4;O1;CCE;L1;C":TR=1:TT=TT-100
800 '***************************
810 '         results
820 SOUND200,1:POKE65494,0
830 CLS:PRINT"           *results*":PRINT STRING$(32,150);
840 PRINT"INITIAL VELOCITY=";V;"M/S":PRINT"ELEVATION  ANGLE=  ";A;"DEGREES"
850 PRINT"HORIZONTAL RANGE=";:PRINT USING S1$;X-7;:PRINT" METERS"
860 PRINT"ALTITUDE @ RANGE=";:PRINT USING S1$;155-Y;:PRINT" METERS"
880 PRINT"MAXIMUN HEIGHT    =";:PRINT USINGS2$;V*SIN(A1)*TM-(G*TM*TM)/2;:PRINT" M.":PRINT"POSSIBLE MAX RANGE=";:PRINT USING S2$;2*TM*V*COS(A1);:PRINT" M."
900 PRINTSTRING$(32,190);:PRINT"TRAVEL TIME    :";:PRINT USING S2$;T;:PRINT" SECS."
910 PRINT"PEAK REACHED AT:";:PRINT USING S2$;TM;:PRINT" SECS."
912 PRINT"YOU MISSED";TL;"OUT OF 3"
914 PRINT"YOUR SCORE IS";TT
916 PRINT;3-TL;"TRIES LEFT":PRINTSTRING$(32,150);
920 SOUND1,5
930 GOSUB1530
932 IFN$="5"THEN950
935 IF TL<3 THEN 258
940 '***************************
950 '           menu
952 TL=0
960 CLS:PRINT@12,"*menu*":PRINT STRING$(32,150);:PRINT
970 PRINT" (1) TO VIEW PLOT AND ANY KEY ","       TO RETURN TO HERE."
980 PRINT" (2) TRY AGAIN, SAME TARGET?"
990 PRINT" (3) ERASE OLD TRAJECTORY"
1000 PRINT" (4) REVIEW INSTRUCTIONS"
1010 PRINT" (5) REVIEW RESULTS"
1020 PRINT" (6) NEW TARGET POSITION"
1030 PRINT" (7) QUIT"
1035 PRINT" (8) SOLUTION"
1038 PRINT"                score";TT
1040 PRINT STRING$(32,150);:PRINT
1050 PRINT"CHOOSE A NUMBER, FROM (1-8)";
1060 N$=INKEY$:IF N$=""THEN GOTO 1060
1070 ON VAL(N$) GOTO 1424,258,1330,100,810,1330,2000,1700
1080 SOUND1,1:GOTO950
1110 '*************************
1120 '        on target
1130 PSET(X,Y,5):SOUND1,1:ZZ=1:GOTO750
1135 FOR R=1 TO 10:CIRCLE(XT+3,YT),R,5:GOSUB1140:NEXT R:GOTO1150
1140 CIRCLE(XT+3,YT),R,0:RETURN
1145 POKE65494,0
1150 PLAY"L4;O3;CEGFACGB":FORN=1TO200STEP10:CLS(2):PRINT@234,"* right on *";:SOUNDN,1:NEXTN
1160 TT=TT+100:TL=TL-1:GOTO800
1190 '**************************
1200 '     picture drawing
1210 PCLS:PMODE4,1:POKE65495,0
1214 '**************************
1215 '       landscape
1220 DRAW"BM0,155;R60;E5;R4;E4;R2;U1;R2;E3;R6;D1;R1;E3;R1;E5;R2;U1;R2;U1;R7;E7;R2;D1;R3;D1;F9;R1;R2;D1;R3;D2;R5;D1;R1;F10;R3;F4;R20;D2;F8;R2;U2;E4;R3;E3;R60"
1230 PAINT(10,191),5,5
1240 DRAW"C0;BM0,155;D36;R255;U36":DRAW"BM0,180;R255"
1250 FORST=0TO255STEP10:ST$=STR$(ST):DRAW"BM0"+ST$+",185;U10":NEXTST
1252 CIRCLE(10,188),3:LINE(110,190)-(110,185),PSET:LINE(210,190)-(210,185),PSET
1255 FORS=1TO255STEP10:PSET(S,RND(140),5):NEXTS
1260 '**************************
1270 '       building
1280 DRAW"C5;BM205,155;U10;R10;D5;R15;D5":PAINT(210,150),5,5:DRAW"C0;BM205,147;R10;U1;L10"
1290 CIRCLE(210,145),5,5,1,.5:PAINT(210,143),5,5:DRAW"C0;BM205,155;R25"
1300 DRAW"C0;BM5,155;D5R2E7"
1305 '**************************
1308 '  picture duplication
1310 PCOPY1TO5:PCOPY2TO6:PCOPY3TO7:PCOPY4TO8
1320 '**************************
1330 '     random target
1332 PCOPY8TO4:PCOPY7TO3:PCOPY6TO2:PCOPY5TO1
1334 SCREEN1,1:TR=0:IFN$="3"ORN$="4"THEN1370
1335 XT=RND(230)+20:YT=170:IF XT>198 AND XT<235 THEN1335
1350 IFPPOINT(XT,YT)=5THENYT=YT-1
1360 IFPPOINT(XT,YT)=5THEN1350
1370 TA$="BM"+STR$(XT)+","+STR$(YT)+";U3R2U2R2D2R2D10U7L5U1;C0;R4U1L4"
1380 DRAW "C5;"+TA$
1400 DRAW"C0;BM"+STR$(XT+3)+","+STR$(YT+10)+";G3E3F3H3D80"
1405 GOSUB 3000
1406 IFQQ=1THENQQ=0:RETURN
1408 POKE65494,0:IFJ=0THEN90
1410 IFN$="6"ORN$="3"THEN 1424
1420 GOTO295
1422 '**************************
1424 '         view
1425 SOUND150,1:SCREEN1,1
1430 '*************************
1490 '    checking keyboard
1500 SOUND240,1:SOUND100,1
1510 A$=INKEY$:IF A$=""THEN GOTO1510 ELSE 1515
1515 IFB$="Y"THEN 308
1520 SOUND200,1:GOTO950
1530 PRINT@480,"press any key to continue";
1540 EXEC44539:POKE135,0:SOUND200,1:RETURN
1550 '**************************
1560 '     no path shown
1570 SOUND255,1:PSET(X,Y,0):RETURN
1571 '**************************
1572 '      ground contact
1575 SOUND1,20:GOTO810
1580 '**************************
1700 '     solution
1705 CLS(0):PRINT"possible solution":PRINT
1708 PRINT@32,"target at";XT-7;"meters";
1709 PRINT@64,"and";:PRINTUSINGS3$;155-YT;:PRINT"meters height";
1710 PRINT@128,">";:INPUT"YOUR DISTANCE FROM TARGET";DI
1720 PRINT:INPUT"HEIGHT OF TARGET=";HE
1730 PRINT:INPUT"SHOOTING ANGLE (0-90DEG)";AN
1735 AN=AN*(3.1416/180)
1736 HE=HE-X1+7
1740 T=-9.8*DI*DI:B=2*HE*COS(AN)*COS(AN)-2*DI*COS(AN)*SIN(AN)
1742 IF B=0 THEN PRINT"***DIVISION BY 0 !, TRY AGAIN***";:SOUND1,30:GOTO1700
1745 IF (T/B)<0 THEN PRINT"***SQUARE ROOT OF NEG. NUMBER!**":SOUND1,30:GOTO1700
1750 PRINT"TRY THIS VELOCITY...";:PRINT USING S1$;SQR(T/B)
1790 GOSUB1530
1792 IF B$="H"THEN308ELSE960
2000 '**************************
2010 CLS(0):PRINT@11,"*thank*you*";:SOUND200,1:GOTO40
3000 '**************************
3050 '       cannon
3100 A2=A:COLOR 0,0
3105 IF J=0 THEN LINE(7,151)-(11,151),PSET ELSE LINE(7,151)-(X1,Y1),PSET
3110 A2=(3.14/180)*A2
3120 X1=7+(7*COS(A2)):Y1=151-(7*SIN(A2))
3125 COLOR5,0
3130 CIRCLE(7,151),4,5:LINE(0,155)-(7,151),PSET:LINE-(X1,Y1),PSET
3135 RETURN
6000 'subroutine of parabola w/ vectors
6030 SCREEN1,1
6060 V=45:A=45
6080 T=0:PI=3.1416:G=9.80:B=(PI/180)*A
6090 T=T+.2
6100 V=45:A=45
6110 X=T*V*COS(B)
6120 Y=T*V*SIN(B)-(G*T*T)/2
6130 X=X+10:Y=155-Y
6140 XP=X:YP=Y
6150 IFY>155THEN7000
6160 XF=X+V*COS(B)
6170 YF=Y-(V*SIN(B)-G*T)
6180 IF YF<0THEN YF=Y+V*SIN(B)+G*T)
6190 PSET(X,Y,2):SOUND200,1
6200 GOSUB 6250
6210 GOTO 6090
6240 '--------------------------
6250 LINE(X,Y)-(X,YF),PSET
6260 LINE(X,Y)-(XF,Y),PSET
6270 PSET(XP,YP,2)
6280 RETURN
7000 '**************************
7010 '   BACK TO INSTRUCTIONS
7020 QQ=1:GOTO1330
