10 GOTO 2190
20 'MODIFIED FROM REFUND
30 'APRIL 1986 RAINBOW, PAGE 95
40 'BY DALE LEISTICO
50 '313 SOMERSET PLACE
60 'LOMPOC, CALIF,93436
70 CLEAR 3000:DIMS$(300),EX(300)
80 CLS(RND(8)):PRINT@32*6+10,"PAPER ROUTE"
90 FOR Q=1TO1000:NEXT Q:CLS
100 GOSUB 1720
110 CLS
120 PRINT:PRINT"   ******select choice******":PRINT
130 PRINT" (1)input checks to list"
140 PRINT" (2)add to the check list"
150 PRINT" (3)delete checks from list"
160 PRINT" (4)print entire list"
170 PRINT" (5)save check list "
180 PRINT" (6)load check list "
190 PRINT" (7)alphabetize list"
200 PRINT" (8)search check list"
210 PRINT" (9)directory" 
220 PRINT"(10)end session"
230 PRINTTAB(8);"SELECTION (1-10)";
240 INPUT M
250 IF M<0 OR M>10 THEN 110
260 ON M GOSUB 290,410,510,700,1070,1230,1380,1620,1760,1560
270 GOTO 110
280 ' ROUTINE TO INPUT/ADD ITEMS
290 IF Y=0 THEN 400
300 PRINT" do YOU wish TO erase THE "
310 PRINT:PRINT " records IN THE computer ?"
320 LINEINPUT "<Y>ES OR <N>O..";W$
330 IF W$="Y" THEN 370
340 IF W$="N" THEN RETURN
350 CLS:GOTO 300
360 FOR Q=1 TO 1000: NEXT Q:RETURN 
370 CLS:PRINT@32*3+5,"OLD RECORDS ERASED"
380 PRINT@32*6+5,"START NEW RECORD"
390 FOR Q=1 TO 1000:NEXT Q
400 Y=1
410 CLS:PRINT"input/add items routine"
420 GOSUB 2170
430 PRINT:PRINT "CUSTOMER NAME" Y;
440 LINEINPUT S$(Y)
450 IF LEN(S$(Y))>15 THEN PRINT"PLEASE RE-ENTER A SHORTER NAME":GOTO 440
460 IF S$(Y)="" THEN RETURN
470 PRINT"CHECK AMOUNT $";:INPUT EX(Y)
480 Y=Y+1
490 GOTO 430
500 REM ROUTINE TO DELETE ITEMS
510 N=0
520 CLS:PRINT"delete items routine"
530 GOSUB 2170
540 PRINT"NOTE-->delete from highest":PRINT"         item number to ":PRINT"       lowest item number!"
550 PRINT:INPUT "CUSTOMER NUMBER TO DELETE";N
560 IF N>Y-1 THEN 550
570 IF N=0 THEN RETURN
580 PRINT N;:PRINT"  ";:PRINTS$(N);:PRINTEX(N)
590 PRINT"DO YOU WISH TO DELETE"
600 LINE INPUT "<Y>ES OR <N>O..";W$
610 IF W$="Y" THEN 620 ELSE 520
620 PRINT"  ITEM DELETED":GOSUB 360
630 FOR X=N TO Y-2
640 S$(X)=S$(X+1)
650 EX(X)=EX(X+1)
660 NEXT X
670 Y=Y-1
680 GOTO 510
690 REM ROUTINE TO PRINT ITEMS
700 EB=0:EC=0
710 CLS:PRINT"LIST ITEMS ON SCREEN(S) OR ON   PRINTER(P)":PRINT"---ENTER 'S' OR 'P'..";:LINE INPUT W$
720 IF W$="P" THEN D=-2:T=10:GOSUB 2070:GOTO 750
730 IF W$="S" THEN D=0:T=0:GOTO 750
740 GOTO 710
750 CLS:PRINT" ENTER COUPON TOTAL ":LINEINPUT CO$
760 PRINT" ENTER CASH TURN IN ":LINEINPUT CA$
770 PRINT "ENTER YOUR BILL":LINEINPUT EB$
780 CO=VAL(CO$):CA=VAL(CA$):EB=VAL(EB$)
790 CLS
800 FOR X=1 TO Y-1 STEP 13
810 FOR Z=X TO X+12
820 IF D=-2 THEN PRINT@32*8," printing---stand by!!!!!!!!!!!":IF S$(Z)="" THEN 860
830 PRINT#D,TAB(T):PRINT#D,USING"###";Z;:PRINT#D,TAB(T+5)S$(Z);
840 PRINT#D,TAB(T+20):PRINT#D,USING "##.##";EX(Z)
850 EC=EC+EX(Z)
860 NEXT Z
870 IF D=0 GOSUB 1750
880 NEXT X
890 PRINT#D,TAB(T+17)"========"
900 PRINT#D,TAB(T)"CUST CHK TOT";
910 PRINT#D,TAB(T+17):PRINT #D,USING"$####.##";EC
920 PRINT#D,TAB(T)"COUPON";
930 PRINT#D,TAB(T+17):PRINT#D,USING"$####.##";CO
940 PRINT#D,TAB(T)"CASH";
950 PRINT#D,TAB(T+17):PRINT#D,USING"$####.##";CA
960 ET=EC+CO+CA
970 IF D=0 THEN NN$="YOUR CHECK"
980 PRINT#D,TAB(T)NN$;
990 PRINT#D,TAB(T+17):PRINT#D,USING"$####.##";EB-ET
1000 PRINT#D,TAB(T+17)"========"
1010 PRINT#D,TAB(T)"TOTAL";
1020 PRINT#D,TAB(T+17):PRINT#D,USING"$####.##";EB
1030 IF D=0 THEN GOSUB 1750
1040 RETURN
1050 REM ROUTINE TO SAVE ITEMS TO DISK OR TAPE
1060 IF Y=0 THEN PRINT"NO RECORDS IN FILE":FOR Q=1 TO 1000:NEXT Q:RETURN
1070 GOSUB 1870
1080 CLS(8):PRINT@135,"save items on "GG$;
1090 SOUND 200,3:SOUND 200,3
1100 IF CC$="D" THENPRINT@32*8+8,"insert data disk":SOUND 200,3:
1110 GOSUB 1750
1120 PRINT@161,"FILE NAME TO SAVE..."
1130 LINEINPUT FF$
1140 IF CC$="T" THEN PRINT"PRESS play AND record" :GOSUB 1750
1150 CLS(0):PRINT@224,"saving" FF$ " on "GG$"---stand by!";:SOUND 200,3
1160 PRINT FF$: PRINT HH
1170 OPEN "O",#HH,FF$
1180 FOR X=1 TO Y-1
1190 IF CC$="T" THEN PRINT#-1,S$(X),EX(X) ELSE WRITE#1, S$(X),EX(X)
1200 NEXT X
1210 CLOSE #HH:SOUND 200,3:RETURN
1220 REM ROUTINE TO LOAD FROM DISK OR TAPE
1230 GOSUB 1870
1240 CLS(4):PRINT@134,"load data from "GG$;:SOUND 200,3
1250 IF FF$="D" THEN PRINT@32*8+8,"insert data disk!";
1260 IF FF$="T" THEN PRINT @32*5+8,"press play on recorder";
1270 GOSUB 1750
1280 PRINT@161,"FILE NAME TO LOAD..."
1290 LINEINPUT FF$
1300 CLS(0):PRINT@224,"loading "FF$" from "GG$"-stand by";:SOUND 200,3
1310 OPEN"I",#HH,FF$
1320 Y=1
1330 IF EOF(HH) THEN 1370
1340 INPUT #HH, S$(Y),EX(Y)
1350 Y=Y+1
1360 GOTO 1330
1370 CLOSE #HH:SOUND 200,3:RETURN
1380 REM ALPHABETIZE ROUTINE
1390 IF Y=0 THEN PRINT"NO records IN file":FOR Q=1 TO 1000:NEXT Q:RETURN
1400 CLS(RND(8)):PRINT@32*8,"stand by----alphabetizing list!"
1410 L=Y
1420 L=INT(L/2)+2 
1430 FOR I=1 TO Y-L
1440 IF S$(I+L)="" THEN 1500
1450 IF S$(I)<S$(I+L)THEN 1490
1460 T$=S$(I):F=EX(I)
1470 S$(I)=S$(I+L):EX(I)=EX(I+L)
1480 S$(I+L)=T$:EX(I+L)=F
1490 NEXT I
1500 IF L=1 THEN 1540
1510 IF L>5 THEN 1420
1520 L=L-1:GOTO 1430
1530 GOTO 1420
1540 CLS(3):PRINT@32*8,"alphabetizing completed!!!!!!!!":SOUND 200,3:FORQ=1TO500:NEXTQ:RETURN
1550 'END WARNING
1560 CLS:FOR B=1 TO 8:CLS(RND(8)):SOUND 200,2:NEXT B
1570 PRINT@32*3,"before ending this session,":PRINT"be sure to save all changes":PRINT"on your disk or tape file!!!":PRINT:PRINT"if you are sure you want to":PRINT"end the session, then press 'E'":PRINT"otherwise, press and <enter> anyother";
1580 PRINT" key to return to the main menu"
1590 INPUT R$
1600 IF R$="E" THEN CLS:END
1610 RETURN
1620 LINE INPUT"NAME TO SEARCH FOR...";NN$
1630 CLS(5)
1640 FOR I=1 TO Y-1
1650 FT=INSTR(1,S$(I),NN$)
1660 IF FT>0 THEN PRINTS$(I);:PRINT"   ";:PRINTUSING "###.##";EX(I)
1670 IF FT>0 THEN GOSUB 1750
1680 NEXT I
1690 PRINT "END OF SEARCH"
1700 GOSUB 1750
1710 RETURN
1720 PRINT"THIS PROGRAM WILL KEEP A FILE OFYOUR PAPER ROUTE CHECKS.":PRINT"YOU WILL HAVE ROOM FOR ABOUT 150-300 ENTRIES(DEPENDING ON THEIR LENGTH)!"
1730 PRINT"  THERE WILL ALSO BE A PRINTER    OPTION FOR YOUR LIST WHEN YOU     WANT A HARD COPY."
1740 PRINT"note:  DO NOT USE COMMAS WHEN          ENTERING ITEMS!"
1750 PRINT@32*13+5,"PRESS enter TO CONTINUE";:LINE INPUT R$:CLS:RETURN
1760 IF PEEK(49152) = 68 THEN 1800
1770 CLS: PRINT"THIS FEATURE IS DISABLED FOR    NON DISK SYSTEMS"
1780 GOSUB 1750
1790 RETURN
1800 CLS:DIR
1810 PRINT @((32*12)+25),"press"
1820 PRINT@((32*13)+23),"<ENTER>"
1830 PRINT@((32*14)+28),"to"
1840 PRINT@((32*15)+22),"CONTINUE"
1850 LINEINPUT R$:CLS:RETURN
1860 RETURN
1870 PRINT"SELECT <D>ISK OR <T>APE"
1880 LINEINPUT CC$
1890 IF PEEK(49152)=68 THEN 1910
1900 CC$="T"
1910 IF CC$="D" THEN GG$="disk":HH=1:RETURN
1920 IF CC$="T" THEN 1940
1930 GOTO 1880
1940 GG$="tape":HH=-1
1950 CLS(RND(8))
1960 PRINT"SETUP TAPE AND RECORDER"
1970 PRINT"PRESS <play> ON THE RECORDER"
1980 PRINT"THE MOTOR WILL GO ON WHEN"
1990 PRINT"enter IS PRESSED"
2000 PRINT"PRESS enter  TO TURN IT OFF"
2010 GO SUB 1750
2020 MOTOR ON:AUDIO ON
2030 GO SUB 1750
2040 MOTOR OFF:AUDIO OFF
2050 RETURN
2060 'PRINTER SUBROUTINE
2070 PS=PEEK(65314):IF (PS)/2=INT((PS)/2) THEN 2120
2080 CLS:PRINT@71,"SET up the PRINTER"
2090 SOUND 200,3
2100 GOSUB 1750
2110 GOTO 2070
2120 PRINT" ENTER CARRIER NUMBER  ":INPUT MN
2130 PRINT" ENTER CARRIER NAME ":LINEINPUT NN$
2140 PRINT#-2,TAB(10)"CARRIER ROUTE..."MN
2150 PRINT#-2,TAB(10)"CARRIER NAME."NN$
2160 RETURN   
2170 PRINT @34,"PRESS enter WHEN FINISHED"
2180 RETURN
2190 PCLEAR1:GOTO70
