10 L=20:VM=100
20 DIM U(L,L),UU(L,L),V(L,L),VV(L,L)
30 READ R,VR,VE,GU,GD,KR,KE
40 RR=(2*R+1)*(2*R+1)
50 PMODE0,1:PCLS:SCREEN1,1
60 FOR Y=0 TO L:FOR X=0 TO L/2
70 U(X,Y)=1:UU(X,Y)=1
80 PSET(X+X,Y+Y):NEXT X,Y
90 FOR X=R TO L-R:FOR Y=R TO L-R
100 U=U(X,Y):UU(X,Y)=U
110 V=V(X,Y):VV(X,Y)=V
120 IF U=1 THEN 200
130 'TEST 0
140 IF V=>VE THEN 180
150 GOSUB 350
160 IF E>KE THEN UU(X,Y)=1:GOTO  290
170 'TEST 0A
180 V=V-GD:IF V<0 THEN V=0
190 VV(X,Y)=V:GOTO 290
200 'TEST 1
210 IF V<=VR THEN 260
220 IF V=VM THEN UU(X,Y)=0:GOTO  290
230 GOSUB 350
240 EE=RR-E
250 IF EE>KR THEN UU(X,Y)=0:GOTO 290
260 'TEST 1A
270 V=V+GU:IF V>VM THEN V=VM
280 VV(X,Y)=V
290 NEXT Y,X
300 FOR X=0 TO L:FOR Y=0 TO L
310 U=UU(X,Y):U(X,Y)=U
320 V=VV(X,Y):V(X,Y)=V
330 PSET(X+X,Y+Y,U)
340 NEXT Y,X:GOTO 90
350 E=0
360 FOR I=X-R TO X+R
370 FOR J=Y-R TO Y+R
380 IF U(I,J)=1 THEN E=E+1
390 NEXT J,I:RETURN
400 DATA 3,95,80,24,19,9,5
