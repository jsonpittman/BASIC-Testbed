10 CLS4:C=2:X=3
20 IF C>3 THEN C=2:IF X<2 THEN X=3
30 FOR H=0 TO 63 STEP 4:FOR V=0 TO 1
40 SET (H,V,C):SET(H+1,V,C)
50 SET (H+2,V,X):SET(H+3,V,X)
60 NEXT V,H
70 FOR H= 62 TO 63:FOR V= 2 TO 29 STEP 4
80 SET (H,V,C):SET(H,V+1,C)
90 SET (H,V+2,X):SET(H,V+3,X)
100 NEXT V,H
110 FOR H=63 TO 0 STEP -4:FOR V=30 TO 31
120 SET(H,V,C):SET(H-1,V,C)
130 SET(H-2,V,X):SET(H-3,V,X)
140 NEXT V,H
150 FOR H=0 TO 1:FOR V=29 TO 2 STEP -4
160 SET (H,V,C):SET(H,V-1,C)
170 SET (H,V-2,X):SET(H,V-3,X)
180 NEXT V,H
190 PRINT@ (138)," LOAN DATA ";
200 PRINT@(237)," BY: ";
210 PRINT@(329)," KEN JOHNSON ";
220 PRINT@(364)," 1985 ";
230 FOR DLAY=1 TO 1500:NEXT DLAY
240 U$="######.##":V$="       "
250 CLS:PRINT@(72),"<<<LOAN DATA>>>":PRINT
260 PRINT@(140),"-MENU-":PRINT:PRINT
270 PRINT"      (C)ALCULATE PAYMENT":PRINT
280 PRINT"      (S)CHEDULE PRINTOUT":PRINT:PRINT
290 PRINT"      MAKE SELECTION: C/S"
300 I$=INKEY$:IF I$="C" THEN 310 ELSE IF I$="S" THEN 600 ELSE 300
310 CLS:PRINT:PRINT:PRINT"          DATA ENTRIES:":PRINT
320 L=0:IT=0:P=0:W=0
330 INPUT" LOAN BALANCE";A
340 INPUT" INTEREST RATE";B
350 INPUT" TERM IN MONTHS";C
360 PRINT:PRINT:PRINT"        CHECK ENTRIES...."
370 PRINT:PRINT"   PRESS (C)ONTINUE OR (R)EDO"
380 CE$=INKEY$:IF CE$="C"THEN 390 ELSE IF CE$="R" THEN 310 ELSE 380
390 IF B<1 THEN B=B*100
400 I=(B/100)/12
410 M=I/((1+I)^(C)-1)+I:D=M*A
420 CLS:PRINT:PRINT:PRINT" LOAN: $";:PRINT USINGU$;A
430 PRINT" RATE:";B;"%"
440 PRINT" TERM:";C;"MONTHS"
450 PRINT" PYMT: $";:PRINT USINGU$;D
460 PRINT:PRINT" CALCULATING TOTAL INTEREST...."
470 K=A
480 FORY=1 TO C
490 R=B/100:DD=D
500 I=(K*R)/12
510 W=W+I
520 IT=W
530 P=DD-I:NB=K-P
540 K=NB
550 NEXT Y
560 PRINT:PRINT " TOTAL INTEREST: $";:PRINT USINGU$;IT
570 PRINT:PRINT:PRINT" (T)RY ANOTHER (M)AKE PRINTOUT"
580 PRINT" (E)ND PROGRAM (R)ETURN TO MENU"
590 I$=INKEY$:IF I$="T"THEN 310 ELSE IF I$="M" THEN 710 ELSE IF I$="R" THEN 250 ELSE IF I$="E" THEN CLS:END ELSE 590
600 CLS:PRINT:PRINT:PRINT"     AMORTIZATION SCHEDULE":PRINT
610 PRINT
620 INPUT" ENTER LOAN BALANCE ";A
630 INPUT" ENTER INTEREST RATE ";B
640 INPUT" ENTER TERM IN MONTHS ";C
650 INPUT" ENTER AMOUNT OF PYMT. ";D
660 PRINT" DAYS FIRST PYMT DELAYED."
670 INPUT" IF N/A PRESS <ENTER>";E
680 PRINT:PRINT:PRINT"        CHECK ENTRIES...."
690 PRINT:PRINT"   PRESS (C)ONTINUE OR (REDO)"
700 CP$=INKEY$:IF CP$="C"THEN 710 ELSE IF CP$="R" THEN 600 ELSE 700
710 CLS:PRINT@ (102),"MAKE PRINTER READY !":PRINT:PRINT
720 PRINT"PRESS: (P)RINT  (R)E-ENTER DATA"
730 PRINT"       (A)BORT  (E)ND PROGRAM"
740 I$=INKEY$:IFI$="P"THEN 750ELSE IF I$="R"THENCLS:GOTO600ELSE IFI$="A"THEN GOTO250 ELSE IF I$="E"THEN END ELSE 740
750 QQ=PEEK(65314):IF QQ><4 THEN CLS:PRINT@ (102),"PRINTER NOT READY !"
760 PRINT#-2:PRINT#-2:L=2:PN=1
770 CLS:PRINT#-2,"                         <<< AMORTIZATION SCHEDULE >>>":PRINT#-2:PRINT#-2:PRINT#-2:L=L+4:Y=0
780 CLS:PRINT@ (103), "PRINTING SCHEDULE"
790 PRINT#-2,"  LOAN: ";:PRINT#-2,USINGU$;A;:PRINT#-2,"    INT.RATE:"B;:PRINT#-2,"%";:PRINT#-2,"     TERM:"C;:PRINT#-2,"MONTHS";:PRINT#-2,"     PAYMENT:";:PRINT#-2,USINGU$;D;:PRINT#-2:PRINT#-2:L=L+3:GOSUB 970
800 K=A:IF B<1 THEN B=B*100
810 FOR Y= 1 TO C
820 R=B/100:DD=D
830 IF Y=1 AND E<>0 THEN I=((R*A)/365)*E ELSE I=(K*R)/12
840 P=DD-I:NB=K-(DD-I)
850 IF Y=C THEN DD=K+I:P=K:NB=0:GOTO880
860 K=NB
870 IF Y=C THEN DD=NB+I:P=NB:NB=0 
880 PRINT#-2,USING"###. ";Y;:PRINT #-2," <.............> ";:PRINT#-2,USINGU$;DD;:PRINT#-2,V$;:PRINT#-2,USINGU$;I;:PRINT#-2,V$;:PRINT#-2,USINGU$;P;:PRINT#-2,V$;:PRINT#-2,USINGU$;NB:L=L+1
890 IF C<25 THEN PRINT#-2:L=L+1
900 IF L=62 AND Y<C THEN GOSUB 1010
910 NEXT Y
920 PRINT#-2:PRINT#-2:PRINT#-2,TAB(34)"<LAST PAGE>"
930 CLS:PRINT:PRINT:PRINT:PRINT:PRINT"PRESS: (A)NOTHER COPY"
940 PRINT"       (R)E-RUN PROGRAM"
950 PRINT"       (Q)UIT; CLEAR MEMORY"
960 I$=INKEY$:IF I$="A" THEN CLS:GOTO 720 ELSE IF I$="R" THEN RUN ELSE IF I$="Q" THEN POKE 113,0:EXEC 40999 ELSE 960
970 IF Y=0 THEN 980 ELSE IF Y>1 THEN PRINT#-2:PRINT#-2:PRINT#-2:L=L+3
980 PRINT#-2,STRING$(80,45):L=L+1
990 PRINT#-2," PYMT.#    PYMT.DATE     AMOUNT          INT.         PRINCIPLE       BALANCE":L=L+1
1000 PRINT#-2,STRING$(80,45):PRINT #-2:L=L+2:RETURN
1010 PRINT#-2:PRINT#-2:PRINT#-2,TAB(36)"PAGE"PN:PN=PN+1:PRINT#-2:PRINT#-2:PRINT#-2:L=2:GOSUB 970:RETURN
