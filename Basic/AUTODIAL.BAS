10 CLEAR 10000:GOSUB 430:REM AUTODIALER FOR THE COCO
15 REM SEAN M. BOSSINGER
20 REM 16220 SW 282 ST
30 REM HOMESTEAD, FLORIDA 33033
40 CLS(4)
50 PRINT"    WELCOME TO THE AUTODIALER"
60 PRINT"  FOR LOCAL, AND LONG DISTANCE"
70 PRINT"      BY SEAN BOSSINGER."
80 PRINT"   USING THE HAYES SMARTMODEM."
90 FOR X=1 TO 3000:NEXT X:CLS
100 INPUT"INSTRUCTIONS?(Y/N):";A$:IF A$="Y" THEN GOSUB 60170
110 FORX=1 TO 200:NEXT X:CLS:PRINT"ENTER:":PRINT"1) FOR INTERNATIONAL":PRINT"2) FOR LOCAL AND NATIONAL.":INPUT"1 OR 2 PLEASE:";A:IF A =1 OR A=2 THEN 120 ELSE 110
120 ON A GOTO 130,60000
125 REM BEGIN INTERNATIONAL             AUTODIAL HERE
130 RESTORE
140 INPUT"ENTER THE COUNTRY NAME:";CN$
150 READ A$,B:IF A$=CN$ THEN 190 ELSE 160
160 IF A$="END" THEN 180
170 GOTO 150
180 PRINT"TRY AGAIN, COUNTRY NOT FOUND":GOTO 130
190 PRINT"COUNTRY CODE IS:";B
200 CC=B
210 INPUT"ENTER THE CITY NAME:";CN$
220 IF CN$="NONE" THEN 230 ELSE 250
230 INPUT"ENTER THE CITY CODE";CC(1):IF CC(1)=0 THEN 310
240 GOTO 310
250 RESTORE
260 READ A$,B:IF A$=CN$ THEN 300 ELSE 270
270 IF A$="END" THEN 290
280 GOTO 260
290 PRINT"NO CITY BY THAT NAME FOUND":GOTO 210
300 CC(1)=B
310 INPUT"ENTER THE PARTY NUMBER";PN$:IF CC(1)=0 THEN 50000
320 PRINT"DIALING 011";CC;"-";CC(1);PN$
330 PRINT#-2,"ATDT011";CC;" ";CC(1);PN$
335 REM TIMER ROUTINE
340 PRINT"HIT <ENTER> TO START TIMER, AND TO DISCONECT MODEM"
350 INPUT A$
360 PRINT#-2," "
370 S=0:M=0:CLS
380 FOR X=1 TO 456:NEXT X:S=S+1
390 PRINT@0,S;":SECONDS"
400 A$=INKEY$:IF A$="" THEN 380 ELSE 410
410 CLS:PRINT"TOTAL TIME ON PHONE WAS:";:PRINTUSING"##.##";S/60:PRINT"MINUTES."
420 INPUT"ANOTHER NUMBER(Y/N)";A$:IF A$="Y" THEN 110 ELSE IF A$="N" THEN END ELSE 420
425 REM SET UP MODEM FOR DIALING
430 REM SETUP THE MODEM PORT
440 W=65314:X=W+1
450 POKE 149,0:POKE 150,180:POKE X,48:POKE W,249:POKE X,52:POKE W,0:PRINT#-2,"ATS11=40"
460 RETURN
465 REM CITY AND COUNTRY CODES
470 DATA ALGERIA,213,AMERICAN SAMOA,684,ANDORRA,33,ALL,078,ARGENTINA,54,BUENOS AIRES,1,CORDOBA,51,ATLANTIC OCEAN,871,AUSTRAILIA,61,MELBOURNE,3,SYDNEY,2
480 DATA AUSTRIA,43,INNSBRUCK,5222,VIENNA,222,BAHRAIN,973,BELGIUM,32,ANTWERP,3,BRUSSELS,2,GHENT,91,BELIZE,501,BOLIVIA,591,LA PAZ,2,SANTA CRUZ,33,BRAZIL,55,BRASILIA,61,RIO DE JANEIRO,21,SAO PAULO,11
490 DATA CAMEROON,237,CHILE,56,SANTIAGO,2,VALPARAISO,31,COLUMBIA,57,CALI,3,COSTA RICA,506,CYPRUS,357,NICOSIA,21,CZECHOSLOVAKIA,42,DENMARK,45,AALBORG,8,COPENHAGEN,1,ECUADOR,593,CUENCA,4,QUITO,2,EGYPT,20
500 DATA EL SALVADOR,503,ETHIOPIA,251,FIJI,679,FINLAND,358,HELSINKI,0,FRANCE,33,BORDEAUX,56,MARSEILLE,91,NICE,93,PARIS,1,FRENCH ANTILLES,596,FRENCH POLYNESIA,689,GABON,241,EAST GERMANY,37
510 DATA WEST GERMANY,49,STUTTGART,711,BERLIN,30,FRANKFURT,611,MUNICH,89,SCHWEINFURT,9721,GREECE,30,ATHENS,1,RHODES,241,GUAM,671,GUANTANAMO BAY,53,GUATEMALA,502,GUATEMALA CITY,2,GUYANA,592,GEORGETOWN,02,HAITI,509,PORT AU PRINCE,1
520 DATA HONDURAS,504,HONG KONG,852,KOWLOON,3,HUNGARY,36,ICELAND,354,AKUREYRI,6,HAFNARFIJOROUR,1,INDIA,91,INDONESIA,62,JAKARTA,21,IRAN,98,TEHERAN,21,IRAQ,964,BAGHDAD,1,IRELAND,353,DUBLIN,1,GALWAY,91,ISRAEL,972,HAIFA,4,JERUSALEM,2,TEL AVIV,3,ITALY,39
530 DATA FLORENCE,55,NAPLES,81,ROME,6,VENICE,41,IVORY COAST,225,JAPAN,81,HIROSHIMA,822,TOKYO,3,YOKOHAMA,45,JORDAN,962,KENYA,254,REPUBLIC OF KOREA,82,PUSAN,72,SEOUL,2,KUWAIT,965,LIBERIA,231,LIBYA,218,TRIPOLI,21
540 DATA LIECHTENSTEIN,41,ALL CITIES,75,LUXEMBOURG,352,MALAWI,265,MALAYSIA,60,KUALA LUMPUR,3,MEXICO,52,MEXICO CITY,5,TIJUANA,668,MONACO,33,ALL CITIES,93,MOROCCO,212
550 DATA NAMIBIA,264,NETHERLANDS,31,AMSTERDAM,20,THE HAGUE,70,NETHERLAND ANTILLIES,599,ARUBA,8,NEW CALEDONIA,687,NEW ZEALAND,64,AUCKLAND,9,WELLINGTON,4,NICARAGUA,505,MANAGUA,2,NIGERIA,234,LAGOS,1,NORWAY,47,BERGEN,5,OSLO,2
560 DATA OMAN,968,PACIFIC OCEAN,872,PAKISTAN,92,PANAMA,507,PAPUA NEW GUINEA,675,PARAGUAY,595,ASUNCION,21,PERU,51,AREQUIPA,54,LIMA,14,PHILIPPINES,63,MANILA,2
570 DATA POLAND,48,PORTUGAL,351,LISBON,1,QATAR,974,ROMANIA,40,BUCURESTI,0,SAIPAN,670,SAN MARINO,39,ALL POINTS,541
580 DATA SAUDI ARABIA,966,RIYADH,1,SENEGAL,221,SINGAPORE,65,SOUTH AFRICA,27,CAPE TOWN,21,JOHANNESBURG,11,PRETORIA,12,SPAIN,34,BARCELONA,3,LAS PALMAS,28,MADRID,1,SEVILLE,54,SRI LANKA,94,KANDY,8,SURINAME,597,SWEDEN,46,GOTEBORG,31,STOCKHOLM,8
590 DATA SWITZERLAND,41,BERNE,31,GENEVA,22,LUCERNE,41,ZURICH,1,TAIWAN,886,TAINAN,62,TAIPEI,2,THAILAND,66,BANGKOK,2,TUNISIA,216,TUNIS,1,TURKEY,90,ISTANBUL,11,IZMIR,51,UNITED ARAB EMIRATES,971,ABU DHABI,2,AJMAN,6,AL AIN,3,AWEIR,49,DUBAI,4
600 DATA FUJAIRAH,91,RAS AL KHAIMAH,7,SHARJAH,6,UMM AL QUWAIN,6,UNITED KINGDOM,44,BELFAST,232,CARDIFF,222,EDINBURGH,31,GLASGOW,41,LIVERPOOL,51,LONDON,1,URUGUAY,598,CANELONES,598,MERCEDES,532,MONTEVIDEO,2
610 DATA VATICAN CITY,39,ALL CITIES,6,VENEZUELA,58,CARACAS,2,MARACAIBO,61,YEMEN,967,YUGOSLAVIA,38,BELGRADE,11
620 REM TELEPHONE DIRECTORY             BEGINS HERE!
630 DATA MCI,3728501,DOWPHONE,18002220248,RAINBOW,15022284492,END,0
49999 REM DIAL NUMBER WITHOUT             CITY CODE
50000 PRINT"DIALING 011 ";CC;" ";PN$
50010 PRINT#-2,"ATDT 011";CC;" ";PN$
50020 GOTO 340
59999 REM LOCAL, U.S., AND                CANADIAN DIALING
60000 CLS
60010 PRINT"ENTER:"
60020 PRINT"1) FOR LOCAL NUMBERS"
60030 PRINT"2) FOR NATIONAL LONG DISTANCE"
60040 INPUT"1 OR 2 ONLY PLEASE:";A
60050 IF A=1 OR A=2 THEN 60070 ELSE 60060
60060 GOTO 60010
60070 ON A GOTO 60080,60110
60075 REM MENU FOR LOCAL DIALING
60080 CLS:PRINT"1>MANUAL ENTER OR 2>AUTO DIAL";:INPUT A:IF A=1 THEN 60090 ELSE 60380
60090 INPUT"ENTER THE NUMBER TO DIAL";A$
60100 PRINT"DIALING ";A$:PRINT#-2,"ATDT ";A$:GOTO 340
60110 CLS
60120 INPUT"1>MANUAL DIAL OR 2>AUTO DIAL";A
60130 IF A=1 THEN 60140 ELSE 60380
60140 INPUT"ENTER THE AREA CODE";AC$:INPUT"ENTER THE TELEHONE NUMBER";TN$
60150 PRINT"DIALING ";AC$;"+";TN$
60160 PRINT#-2,"ATDT1 ";AC$+TN$:GOTO 340
60165 REM INSTRUCTION SCREENS
60170 PRINT"INSTRUCTIONS FOR AUTODIAL***"
60180 PRINT"INTERNATIONAL CALLING:"
60181 PRINT"HIT <1>,  <ENTER> AT THE FIRST  PROMPT, FOLLOWED BY COUNTRY     NAME, FOLLOWED BY CITY NAME     (IF APPLICABLE), FOLLOWED       BY TELEPHONE NUMBER"
60182 PRINT"IF NO CITY CODE IS NEEDED, THEN TYPE 'NONE' AT THE CITY NAME    PROMPT, FOLLOWED BY 0 AT THE    CITY CODE PROMPT, IF AN ALL     CITY CODE IS NEEDED, FOLLOW THE ABOVE INSTRUCTIONS, EXCEPT, AT  THE CITY CODE PROMPT,  ENTER THEALL POINT CODE.":
60183 INPUT"PRESS <ENTER> TO CONTINUE";A$:CLS
60184 PRINT"FOR LOCAL CALLING:"
60185 PRINT"ENTER <2> AT THE FIRST PROMPT   FOLLOWED BY <1> AT THE NEXT     MENU."
60186 PRINT"FOR DIAL BY NAME ENTER <2> AND  THEN WHEN THE COMPUTER ASKS     FOR IT, ENTER THE PARTY  NAME."
60187 PRINT"FOR MANUAL KEYBOARD ENTRY,      ENTER <1> AND WHEN THE COMPUTER ASKS FOR IT,  ENTER THE PARTY   NAME."
60188 INPUT"<ENTER> TO CONTINUE";A$:CLS
60189 PRINT"U.S. AND CANADIAN DIALING"
60190 PRINT"ENTER <2> AT THE FIRST MENU,    FOLLOWED BY <2> AT THE SECOND   MENU."
60191 PRINT"FOR MANUAL DIALING, ENTER <1>   AND WHEN THE COMPUTER ASKS FOR  THEM, ENTER THE AREA CODE, AND  THE TELEPHONE NUMBER"
60192 PRINT"FOR AUTO-DIAL BY NAME, ENTER <2>AT THE MENU, THEN WHEN THE      COMPUTER ASKS FOR IT, ENTER     THE PARTY NAME":INPUT"<ENTER> TO START PROGRAM";A$:CLS:RETURN
60375 REM LOCAL, U.S. AND CANADA          DIALING BY NAME
60380 CLS:INPUT"ENTER THE NAME OF THE PARTY:";NP$
60390 RESTORE
60400 READ A$,B$:IF A$=NP$ THEN 60440 ELSE 60410
60410 IF A$="END" THEN 60420 ELSE 60430
60420 PRINT"ENTRY NOT FOUND":FOR X=1 TO 1000:NEXT X:CLS:PRINT"RETURNING TO LOCAL,LONG DISTANCEMENU.":FORX=1TO1000:NEXT X:CLS:GOTO60000
60430 GOTO 60400
60440 CLS:PRINT"DIALING ";A$:PRINT#-2,"ATDT ";B$:GOTO 340
