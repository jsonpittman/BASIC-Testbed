1 'BEAT THE BOMB
2 'BY JOHN A. SAYA
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
40 HBUFF1,150:HBUFF2,150
50 POKE65497,0:CLEAR1000:DIMZ$(50),P$(5),PP$(5),Q(20)
60 ON BRK GOTO 50
70 WIDTH32:CLS0:FORN=0TO510:PRINTCHR$(166);:NEXTN:PRINT@0,"<e> A S Y";:PRINT@169,"<m> E D I U M";:PRINT@342,"<h> A R D";
80 PLAY"T255O2ABCDFGO1ABCDFG"
90 A$=INKEY$:IFA$=""THEN90
100 IF A$="E" THEN ZX=30
110 IF A$="M" THEN ZX=20
120 IF A$="H" THEN ZX=10
130 CLS3:PRINT@0,"PLEASE WAIT...";:PLAY"T255O2ABCDFGO1ABCDFG"
140 Z$(1)="SCHOOL":Z$(2)="COMPUTER"
150 Z$(3)="DINOSAUR":Z$(4)="BUILDING"
160 Z$(5)="NUMBERS":Z$(6)="OCTOPUS"
170 Z$(7)="EASTER":Z$(8)="CHRISTMAS"
180 Z$(9)="THANKSGIVING":Z$(10)="SUMMER"
190 Z$(11)="WINTER":Z$(12)="SPRING"
200 Z$(13)="PUMPKIN":Z$(14)="RAILROAD"
210 Z$(15)="VIBRATE":Z$(16)="CHEMISTRY"
220 Z$(17)="SCIENCE":Z$(18)="HISTORY"
230 Z$(19)="ENGLISH":Z$(20)="HOUSE"
240 Z$(21)="MULTIPLY":Z$(22)="SUBTRACT"
250 Z$(23)="DEMAND":Z$(24)="VEGETABLE"
260 Z$(25)="ANIMALS":Z$(26)="MARATHON"
270 Z$(27)="DOCTOR":Z$(28)="LIZARD"
280 Z$(29)="CROCODILE":Z$(30)="FINGERS"
290 Z$(31)="PIRATE":Z$(32)="BREATH"
300 Z$(33)="EXAMINE":Z$(34)="HORSE"
310 Z$(35)="FLOATING":Z$(36)="LEARN"
320 Z$(37)="TELEPHONE":Z$(38)="PORCH"
330 Z$(39)="GLOWING":Z$(40)="TRAIN"
340 Z$(41)="CRACKLE":Z$(42)="GROAN"
350 Z$(43)="DREAMING":Z$(44)="BIRDS"
360 Z$(45)="ORANGE":Z$(46)="NAPKIN"
370 Z$(47)="IMPORTANT":Z$(48)="WRITE"
380 Z$(49)="JUNIOR":Z$(50)="MUSCLE"
390 I=1:FORG=1TO5
400 O=RND(50)
410 FORH=1 TO 5:IF O=Q(H) THEN 400 ELSE NEXTH
420 P$(G)=Z$(O):Q(I)=O:I=I+1:NEXTG:FORG=1TO5:Q(I)=0:NEXTG:I=1
430 FORG=1TO5:FORH=1 TO LEN(P$(G))
440 V=RND(LEN(P$(G)))
450 FORT=1 TO LEN(P$(G)):IF V=Q(T) THEN 440 ELSE NEXTT
460 T$=T$+MID$(P$(G),V,1)
470 Q(I)=V:I=I+1:NEXTH:PP$(G)=T$:T$="":I=1:FORU=1 TO LEN(P$(G)):Q(U)=0:NEXTU:NEXTG
480 HSCREEN2:HCLS6:HCOLOR8
490 HCIRCLE(155,115),75
500 HLINE(95,80)-(215,100),PSET,B
510 HPAINT(97,83),11,8
520 HPAINT(155,115),8,8
530 HLINE(153,0)-(158,70),PSET,B
540 HPAINT(154,2),7,8
550 HLINE(190,5)-(310,30),PSET,B:HPAINT(192,6),11,8
560 HGET(0,0)-(30,8),1
570 HDRAW"BM10,0;C8U1R1U1R1U1R2D1R1D1R1D1R2U1R1U1R1U1R2D1R1D1R1D1R5D1L1D1L1D1L2D1L1D1L1D1L2U1L1U1L1U1L2D1L1D1L1D1L2U1L1U1L1U1L5U1R1U1R1U1R2U5"
580 HPAINT(11,2),1,8
590 HGET(0,0)-(30,8),2
600 HPUT(0,0)-(30,8),1:HPUT(0,5)-(30,13),1:X=0:Y=8:R=0:B=1:F=24:HCOLOR3:HPRINT(16,15),"B E A T":HPRINT(18,17),"THE":HPRINT(16,19),"B O M B"
610 HCOLOR8:HLINE(0,0)-(90,25),PSET,B:HPAINT(2,2),11,8:HCOLOR8:HPRINT(1,1),"SOLVED:":HPRINT(7,1),B-1
620 A$=INKEY$:IF A$="" THEN 630 ELSE GOSUB710
630 HPUT(138,X)-(168,Y),2
640 HCOLOR2:HPRINT(12,11),PP$(B)
650 PLAY"T255O1AB"
660 HPUT(138,X)-(168,Y),1
670 R=R+1:IF R=ZX THEN R=0:X=X+1:Y=Y+1
680 IF X=33 AND Y=41 THEN 700
690 GOTO620
700 HCOLOR11:HPRINT(24,2),Q$:FORN=1TO10:HCOLOR2:HPRINT(24,2),P$(B):PLAY"T200O1CCCCCCCCCCCCCCC":HCOLOR11:HPRINT(24,2),P$(B):PLAY"T200O1CCCCCCCCCCCCCCC":NEXTN:HCOLOR5:HPRINT(26,5),"YOU LOSE !!":PLAY"T50O5FFFO4FFFO3FFFO2FFFO1FFF":FORN=1TO500:NEXTN:GOTO780
710 HCOLOR2:HPRINT(F,2),A$:Q$=Q$+A$
720 PLAY"T175O3CDF"
730 IF A$=CHR$(13) THEN HCOLOR11:HPRINT(24,2),Q$:HCOLOR2:Q$="":F=23
740 IF Q$=P$(B) THEN FORN=1TO10:HCOLOR2:HPRINT(24,2),P$(B):PLAY"T200O1AAAO2AAA":HCOLOR11:HPRINT(24,2),P$(B):PLAY"T200O3AAAO4AAAO5AAA":NEXTN:Q$="":HPRINT(12,11),PP$(B):B=B+1:F=23:GOTO770
750 IF B>5 THEN GOTO770
760 F=F+1:RETURN
770 HCOLOR11:HPRINT(1,1),"SOLVED:":HPRINT(7,1),B-2:HCOLOR8:HPRINT(1,1),"SOLVED:":HPRINT(7,1),B-1:IF B>5 THEN 830 ELSE 750
780 T$=INKEY$:IFT$=""THEN790 ELSE820
790 HCOLOR6:HPRINT(1,20),"PLAY AGAIN":PLAY"T15O1ABAB"
800 HCOLOR1:HPRINT(1,20),"PLAY AGAIN":PLAY"T15O1ABAB"
810 GOTO780
820 IF T$="Y" THEN 50 ELSE POKE65496,0:CLS:END
830 HCOLOR5:HPRINT(25,5),"CONGRATULATIONS":PLAY"T25O1DDDDO2DDDDO3DDDDO4DDDDO5DDDD":FORN=1TO500:NEXTN:GOTO780
