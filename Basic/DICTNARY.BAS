10 REM "BREAK-IT-UP! DICTIONARY SKILL"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1989"
30 L=25:Z=RND(-TIMER)
40 DIM A$(L)
50 FOR W=1 TO L:READ A$(W):NEXT W
60 CLS0:PRINT@9,"BREAK  IT  UP!";
70 R=RND(L)
80 C=C+1:IF C>10 THEN 230
90 A$=A$(R)
100 A$(R)=A$(L)
110 L=L-1
120 B$=LEFT$(A$,1)
130 B=ASC(B$)
140 IF B<68 THEN D=1
150 IF B>67 AND B<77 THEN D=2
160 IF B>76 AND B<83 THEN D=3
170 IF B>82 THEN D=4
180 PRINT@32*C+64,C;".";A$,"1,2,3 OR 4 ";
190 PRINT@32*C+91,"";:INPUT E
200 IF E=D THEN PLAY"L100CDEFGGG":J=J+1
210 IF E<>D THEN PLAY"L4C#":PRINT@32*C+91,D
220 RESTORE:GOTO 70
230 FOR T=1 TO 3:PLAY"L50GFEDCCC":NEXT T
240 PRINT@455,"YOUR SCORE IS ";J*10;"%";
250 EN$=INKEY$
260 IF EN$="E" THEN END
270 IF EN$=CHR$(13) THEN RUN
280 GOTO 250
290 DATA ALGAE,AMEBA,BACILLI,BACTERIA,CELLS
300 DATA DISEASE,DRUG,ECOLOGY,FLOWERS,GENES
310 DATA HABIT,IMAGE,LARVA,MAMMAL,NEURON
320 DATA NUCLEUS,ORGAN,PEPSIN,REFLEX,SALIVA
330 DATA SEPALS,THEORY,VEIN,XYLEM,YEAST
