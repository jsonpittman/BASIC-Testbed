1000 '**************************
1010 '*   YARDSALE INVENTORY   *
1020 '*  &  PRICE TAG PROGRAM  *
1030 '**************************
1040 '* (C) 1987 BY ERIC WHITE *
1050 '*  ALL RIGHTS RESERVED.  *
1060 '* VERSION: 1.00  8701.02 *
1070 '**************************
1080 GOSUB1880'DRAW SCREEN
1090 GOSUB 1970' OPEN FILES
1100 GOSUB1910:GOSUB2030
1110 PRINT@480,WB$;
1120 GOSUB2040
1130 PRINT@480," ";:ON INSTR("ILFTE",A$) GOTO 1180,2070,1490,2430,2090
1140 GOTO 1110
1150 '**************************
1160 '      ITEM DATA INPUT 
1170 '**************************
1180 A$="INPUT":GOSUB2060:R=LOF(1)
1190 LSET SF$=MKN$(0)
1200 LSET ST$="I"
1210 PRINT@481,"SELLER ID CODE ? "WB$;:C$=""
1220 GOSUB2040:IF A$=CHR$(13) THEN 1260
1230 IF A$=CHR$(8) AND LEN(C$) > 0 THEN C$=LEFT$(C$,LEN(C$)-1):PRINT@498,C$WB$" ";:SOUND1,1
1240 IF INSTR(RIGHT$(VK$,26),A$) AND LEN(C$)<2 THEN C$=C$+A$:PRINT@498,C$WB$;:SOUND1,1
1250 GOTO 1220
1260 LSET ID$=C$
1270 R=R+1:PRINT@205,ID$;:PRINTUSING".####";R/10000;
1280 LSET IN$=MKN$(R)
1290 GOSUB2030
1300 PRINT@269,WB$;:PRINT@482,"PRESS [CLEAR] TO ABORT INPUT";:C$=STRING$(36,32):C=1
1310 GOSUB2040:IF A$=CHR$(13) THEN1350
1320 IF A$=CHR$(8) THEN C=C-1:MID$(C$,C,1)=" ":IF C<19 THEN PRINT@268+C,WB$:PRINT@301,RIGHT$(C$,18) ELSE PRINT@300+C-18,WB$" ";
1330 IF INSTR((VK$+" !#$%&'()*:-=@<>?,/"),A$) AND C<37 THEN MID$(C$,C,1)=A$:C=C+1:PRINT@269,LEFT$(C$,18):PRINT@301,RIGHT$(C$,18):IF C<19 THEN PRINT@268+C,WB$ ELSE PRINT@300+C-18,WB$;
1340 GOTO 1310
1350 LSET DI$=C$
1360 PRINT@269,LEFT$(C$,18):PRINT@301,RIGHT$(C$,18):C$="":C=0
1370 X=364:SP=1000:GOSUB1430:SP=VAL(C$)/100:LSET SP$=MKN$(SP):PRINT@372," ";
1380 X=375:C$="":GOSUB1430:LSET MP$=MKN$(VAL(C$)/100):PRINT@383," ";
1390 PUT #1,R
1400 GOSUB2030:PRINT@481,"ANY MORE ITEMS TO ENTER (Y/N)";
1410 GOSUB2040:IF A$="Y" THEN GOSUB1910:GOTO1270
1420 GOTO 1100
1430 PRINT@X,USING"$$###.##";VAL(C$)/100;:PRINTWB$;:PRINT@373,"min";
1440 GOSUB2040:IF A$=CHR$(13) THEN RETURN
1450 IF A$=CHR$(8) AND LEN(C$)>0 THEN C$=LEFT$(C$,LEN(C$)-1):GOTO1430
1460 IF INSTR(LEFT$(VK$,10),A$) AND LEN(C$)<5 AND (VAL(C$+A$)/100) < SP+.01 THEN C$=C$+A$:GOTO1430
1470 GOTO 1440
1480 '**************************
1490 '     ITEM DATA OUTPUT
1500 '**************************
1510 A$="FIND":GOSUB2060:PRINT@481,"IDEM CODE NUMBER ?"WB$;:C$="":L$=C$
1520 GOSUB2040
1530 SOUND1,1:IF A$=CHR$(13) THEN 1580
1540 IF A$=CHR$(8) THEN PRINT@500,"       ";:GOTO1510
1550 IF INSTR(VK$,A$) AND LEN(L$)<7 THEN PRINTCHR$(8)A$WB$;:L$=L$+A$
1560 IF INSTR(LEFT$(VK$,10),A$) AND LEN(C$)<4 THEN C$=C$+A$
1570 GOTO 1520
1580 PRINTCHR$(8);:R=VAL(C$):C$="":IF R < 1 OR R > LOF(1) THEN 1100
1590 GET #1,R
1600 PRINT@205,ID$;:PRINTUSING".####";CVN(IN$)/10000;
1610 IF ST$="I" THEN PRINT"   NOT SOLD" ELSE PRINTUSING" SOL$$###.##";CVN(SF$);:PRINT@213,"SOLD";
1620 PRINT@269,LEFT$(DI$,18):PRINT@301,RIGHT$(DI$,18)
1630 PRINT@375,USING"$$###.##";CVN(MP$);:PRINT@373,"min";
1640 PRINT@364,USING"$$###.##";CVN(SP$);
1650 GOSUB2030:IF ST$="I" THEN 1670 ELSE PRINT@481,"RE-INVENTORY ITEM (Y/N) ?"WB$;
1660 GOSUB2040:IF A$="Y" THEN LSET ST$="I":LSET SF$=MKN$(0):PUT #1,R:GOTO 1590 ELSE GOTO 1100
1670 PRINT@481,"SELL THIS ITEM (Y/N) ?";
1680 GOSUB2040
1690 IF A$<>"Y" THEN 1100
1700 GOSUB2030
1710 PRINT@481,"SELL FOR HOW MUCH ?  $"WB$;:C$=""
1720 GOSUB2040
1730 IF A$=CHR$(8) THEN C$="":GOTO1710
1740 IF INSTR(LEFT$(VK$,11),A$) AND LEN(C$)<6 THEN C$=C$+A$:PRINT@503,C$WB$;
1750 IF A$=CHR$(13)THEN1760 ELSE 1720
1760 IF VAL(C$)<.01 THEN 1100
1770 IF VAL(C$)+1> CVN(MP$) THEN GOTO1830 ELSE GOSUB2030
1780 PRINT@480,STRING$(31,32);:PRINT@480,USING"$$###.##";VAL(C$);:PRINT" IS LOWER THAN MINIMUM.";
1790 A$=INKEY$:PLAY"V5;T200ABCD":IF A$="" THEN 1780
1800 GOSUB2030:PRINT@481,"SELL AT ";:PRINTUSING"$$###.##";VAL(C$);:PRINT" ANYWAY (Y/N)?";
1810 GOSUB2040
1820 IF A$="Y" THEN 1830 ELSE 1700
1830 LSET SF$=MKN$(VAL(C$)):LSET ST$="S":PUT #1,R
1840 GOTO1100
1850 '**************************
1860 '       DRAW SCREEN
1870 '**************************
1880 CLS0
1890 FORX=1TO96:PRINT@X-1,CHR$(ASC(MID$("J;J;<J=;JGF;HIH=;RWW;LZM;Z;;;ZWWH>J;J;J;J>I;@E;J;OWV;Z;Z;Z;;;ZWW>>I;JGJ;J;J;BFBC;XNY;ZWZ;ZNN;ZNN",X))+117);:NEXT
1900 FOR X=128 TO 479:POKE1024+X,32:NEXTX:RETURN
1910 PRINT@192,"item";:PRINT@197,"number":POKE1227,58
1920 PRINT@256,"description":POKE1291,58
1930 PRINT@300," "
1940 PRINT@353,"price";:PRINT@359,"list          min":POKE1387,58
1950 PRINT@416,"Input";:PRINT@423,"Labels";:PRINT@431,"Find";:PRINT@437,"Tally";:PRINT@444,"Exit";
1960 RETURN
1970 OPEN "D",#1,"YARDSALE/DAT",59
1980 FIELD #1,2 AS ID$,5 AS IN$,36 AS DI$,5 AS SP$,5 AS MP$,1 AS ST$,5 AS SF$
1990 VK$="0123456789.ABCDEFGHIJKLMNOPQRSTUVWXYZ":WB$=CHR$(207)
2000 NM$=CHR$(27)+CHR$(19)'NORMAL PRINT
2010 CP$=CHR$(27)+CHR$(20)'COMPRESSED PRINT
2020 RETURN
2030 PRINT@128,"  COPYRIGHT 1987 BY ERIC WHITE":FORX=1535 TO 1504 STEP-1:POKEX,207:PLAY"P200":POKEX,96:NEXTX:RETURN
2040 A$=INKEY$:IF A$=""THEN2040
2050 IF A$=CHR$(12) THEN 1100 ELSE RETURN
2060 PRINT@128,"":B$=A$:X=INT(32/(LEN(A$)+2)):FORY=2 TO X:B$=B$+"  "+A$:NEXTY:PRINT@128+(32-LEN(B$))/2,B$:RETURN
2070 GOTO2110
2080 PRINT@481,"TALLY";:GOTO 1120
2090 CLOSE #1:CLS:END
2100 '**************************
2110 '   PRINT PRICES LABELS
2120 '**************************
2130 A$="LABELS":GOSUB2060:PRINT@481,"TITLE ?  "WB$;:C$=""
2140 GOSUB 2040:IF A$=CHR$(13) THEN T$="*"+STRING$(31,32)+"*":MID$(T$,18-(LEN(C$)+9)/2,LEN(C$)+9)=C$+" YARDSALE":C=1:C$=RIGHT$(STR$(LOF(1)),LEN(STR$(LOF(1)))-1):PRINTCHR$(8);:GOTO2180
2150 IF A$=CHR$(8) AND LEN(C$)>0THEN C$=LEFT$(C$,LEN(C$)-1):PRINT@490,C$WB$" ";
2160 IF INSTR((VK$+" !#$%&'()*:-=@<>?,/"),A$) AND LEN(C$)<20 THEN C$=C$+A$:PRINT@490,C$WB$;
2170 GOTO 2140
2180 GOSUB2030:PRINT@481,"PRINT RANGE"C"- "C$" (Y/N)";
2190 GOSUB2040:IF A$="Y" THEN 2300
2200 GOSUB2030:PRINT@481,"ENTER RANGE,FROM: "WB$"   TO:";:C$=""
2210 GOSUB 2040:IF A$=CHR$(13) AND VAL(C$)>0 AND VAL(C$)<(LOF(1)+1) THEN 2250
2220 IF A$=CHR$(8) AND LEN(C$)>0THEN C$=LEFT$(C$,LEN(C$)-1):PRINT@499,C$WB$" ";
2230 IF INSTR(LEFT$(VK$,10),A$) AND LEN(C$)<3 THEN C$=C$+A$:PRINT@499,C$WB$;
2240 GOTO 2210
2250 PRINT@499+LEN(C$)," ";:PRINT@507,WB$;:C=VAL(C$):C$=""
2260 GOSUB2040:IF A$=CHR$(13) AND VAL(C$)+1>C AND VAL(C$)<LOF(1)+1 THEN 2180
2270 IF A$=CHR$(8) AND LEN(C$)>0 THEN C$=LEFT$(C$,LEN(C$)-1):PRINT@507,C$WB$" ";
2280 IF INSTR(LEFT$(VK$,10),A$) AND LEN(C$)<3 THEN C$=C$+A$:PRINT@507,C$WB$;
2290 GOTO 2260
2300 GOSUB2030:PRINT@481,"IS THE PRINTER READY ? (Y/N)";:GOSUB2040
2310 IF A$<>"Y" THEN 1100
2320 FOR X=C TO VAL(C$)
2330 PRINT#-2,STRING$(33,"*")
2340 PRINT#-2,T$
2350 PRINT#-2,STRING$(33,"*")
2360 GET #1,X
2370 PRINT#-2,CP$"DESCRIPTION:  "DI$
2380 PRINT#-2,"ID#:  "NM$ID$;:PRINT#-2,USING".####";X/10000;:PRINT#-2,"          "CP$"PRICE: "NM$;:PRINT#-2,USING"$$###.##";CVN(SP$)
2390 PRINT#-2,NM$:NEXTX:GOTO1100
2400 '**************************
2410 '      PRINT TALLY
2420 '**************************
2430 A$="TALLY":GOSUB2060:PRINT@481,"TALLY TO SCREEN/PRINTER (S/P)?";
2440 GOSUB 2040:IF A$=CHR$(13) THEN 1100
2450 IF A$="S" THEN DV=0:GOTO2480
2460 IF A$="P" THEN DV=2:GOTO2480
2470 GOTO 2440
2480 FORX=1 TO LOF(1)
2490 GET #1,X:A=0
2500 IF I$(A)=ID$ THEN 2540
2510 IF I$(A)="" THEN I$(A)=ID$:GOTO2540
2520 IF A>9 THEN 2540
2530 A=A+1:GOTO 2500
2540 NEXTX:IF DV=0 THEN CLS
2550 FORY=0 TO A:TL=0:FORX=1 TO LOF(1)
2560 GET #1,X:IF ID$=I$(Y) AND ST$="S" THEN 2620
2570 NEXTX:IF TL THEN PRINT#-DV,STRING$(53,"-"):PRINT#-DV,STRING$(36,32)I$(Y)" TOTAL";:PRINT#-DV,USING"$$####.##";TL:PRINT#-DV:PRINT#-DV
2580 IF DV=0 AND TL>0 THEN GOSUB2040:CLS
2590 NEXTY
2600 IF DV=0 THEN GOSUB1880
2610 GOTO1100
2620 PRINT#-DV,I$(Y);:PRINT#-DV,USING".####";X/10000;
2630 PRINT#-DV,"  "DI$;:PRINT#-DV,USING"$$###.##";CVN(SF$)
2640 TL=TL+CVN(SF$):GOTO2570
2650 '**************************
2660 '*   YARDSALE INVENTORY   *
2670 '*  &  PRICE TAG PROGRAM  *
2680 '**************************
2690 '* (C) 1987 BY ERIC WHITE *
2700 '*  ALL RIGHTS RESERVED.  *
2710 '* VERSION: 1.00  8701.02 *
2720 '**************************
