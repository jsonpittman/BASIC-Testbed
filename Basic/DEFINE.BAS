7 PRINTCHR$(27);" ";:POKE&HFFDE,0:CLEAR200,&H34FF:LOADM"COLRTEXT":EXEC:DIM X,A,Y,A$,I$(7):DEF FNC(A)=&H3880+((A-32)*8)
8 JP$=CHR$(13)+CHR$(8)+CHR$(9)+CHR$(10)+CHR$(94)+CHR$(12)
9 F$=CHR$(254):E$=CHR$(255)
10 PMODE 4,1:SCREEN 1,1
20 PMODE 3,1:COLOR 0,1:CLS:COLOR 2,0
30 PRINT "* Color text -                  ";:COLOR 3,0:PRINT "  * Character Define Module     ";
40 PMODE 4,1
50 PRINT@128,"Select..."
60 RESTORE:X=0:PRINT
70 READ A$:IF A$<>"." THEN X=X+1:PRINT TAB(7);RIGHT$(STR$(X),1);") ";A$:GOTO 70
80 DATA Design characters,Save character set,Save as loader,Load character set,Exit (also CLEAR),Warm boot,Cold boot,Directory,.
100 A$=INKEY$:IF A$=CHR$(12) THEN A=5 ELSE A=VAL(A$):IF A<1 OR A>X THEN 100
105 IF A=6 THEN PRINT CHR$(27);:EXEC &HA027 ELSE IF A=7 THEN POKE &H71,0:EXEC &HA027
107 IF A<>8 THEN 110 ELSE COLOR 0,1:CLS:SCREEN 1,0:DIR:
108 IF INKEY$="" THEN 108 ELSE 10
110 IF A=5 THEN COLOR 0,1:CLS:END
120 IF A=2 THEN GOSUB 200:SAVEM NM$,&H3880,&H3FFF,&H3500
130 IF A=3 THEN GOSUB 200:SAVEM NM$,&H3500,&H3FFF,&H3500
140 IF A=4 THEN GOSUB 200:LOADM NM$:GOTO 10
150 GOTO 500
199 '
200 'get and verify a file name
210 '
220 PRINT@480,"";:LINEINPUT "FILE NAME =->";NM$
225 IF NM$="" THEN SOUND 200,1:GOTO 220
230 IF INSTR(NM$,"/")=0 AND INSTR(NM$,".")=0 THEN NM$=NM$+"/BIN"
240 FOR X=1 TO LEN(NM$):A=ASC(MID$(NM$,X,1)):IF A>96 AND A<123 THEN MID$(NM$,X,1)=CHR$(A-32):A=A-32
250 NEXT X:RETURN
500 '
510 'select a character
520 '
530 PMODE 4,1:SCREEN 1,1:COLOR 0,1:CLS:PRINT "Select a character...":PRINT
540 FOR X=32 TO 255:PRINT CHR$(X);:NEXT
560 PRINT@416,"By moving the cursor with the   arrow keys, and then pressing   ENTER. (Press CLEAR to return)  ";
600 PRINT@CX+(CY+2)*32,CHR$(6);
610 A$=INKEY$:IF A$="" THEN 610
620 PRINT@CX+(CY+2)*32,CHR$(6);
635 FOR Y=341 TO 344:POKE Y,255:NEXT
640 X=INSTR(JP$,A$):IF X=0 THEN 600
650 ON X GOTO 1000,660,670,680,690,10
660 CX=CX-1:IF CX<0 THEN CX=31
665 GOTO 600
670 CX=CX+1:IF CX=32 THEN CX=0
675 GOTO 600
680 CY=CY+1:IF CY>6 THEN CY=0
685 GOTO 600
690 CY=CY-1:IF CY<0 THEN CY=6
695 GOTO 600
1000 '
1001 'define/modify a character
1002 '
1010 C=32+CX+(CY*32):CA=FNC(C)
1020 PMODE 0,1:SCREEN 1,1:COLOR 0,1:CLS236
1030 PRINT "  * DEFINER *   ";
1050 PRINT@32,"";:GOSUB 2000 'get image to I$
1060 COLOR 1,0:FOR X=0 TO 7:PRINT@36+(X*16),I$(X);:NEXT X
1070 DX=0:DY=0
1100 PRINT@36+(DY*16)+DX,CHR$(6);:A$=INKEY$:PRINT@36+(DY*16)+DX,CHR$(6);
1105 IF A$=" " THEN 1200
1110 IF A$="" THEN 1100
1130 X=INSTR(JP$,A$):IF X=0 THEN 1100
1140 ON X GOTO 3000,1150,1160,1170,1180,500
1150 DX=DX-1:IF DX<0 THEN DX=7
1155 GOTO 1100
1160 DX=DX+1:IF DX=8 THEN DX=0
1165 GOTO 1100
1170 DY=DY+1:IF DY=8 THEN DY=0
1175 GOTO 1100
1180 DY=DY-1:IF DY<0 THEN DY=7
1185 GOTO 1100
1200 'reverse character
1210 IF MID$(I$(DY),DX+1,1)=F$ THEN MID$(I$(DY),DX+1,1)=E$ ELSE MID$(I$(DY),DX+1,1)=F$
1220 PRINT@36+(DY*16),I$(DY);:GOTO 1100
2000 'Get image into I$(0-7)
2010 FOR X=0 TO 7:A=PEEK(CA+X):I$(X)="":IF A=255 THEN I$(X)=STRING$(8,E$):NEXT X:RETURN
2020 FOR Y=7 TO 0 STEP -1
2030 IF (A AND 2^Y)=0 THEN I$(X)=I$(X)+F$ ELSE I$(X)=I$(X)+E$
2040 NEXT Y:PRINT@36+(X*16),I$(X):NEXT X:RETURN
2999 '
3000 'set character
3001 '
3010 COLOR 0,1:FOR X=0 TO 7:PRINT@36+(X*16),I$(X);
3020 A=0:FOR Y=7 TO 0 STEP-1
3030 IF MID$(I$(X),8-Y,1)<>F$ THEN A=A+2^Y
3040 NEXT Y:POKE CA+X,A:NEXT X:GOTO 500
