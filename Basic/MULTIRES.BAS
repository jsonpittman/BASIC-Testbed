0 '*****************************   **       MULTIRES          **
1 '** (C) 1988 BY R. STANWOOD **
2 '*****************************
3 ' COPYRIGHT 1989  FALSOFT,INC
10 HSCREEN4:HSCREEN0:CLS:'Provides HCLS on initialization only
20 PALETTE CMP:'Fix monitor type
30 PALETTE 0,48:PALETTE 1,0:PALETTE 2,6:PALETTE 3,9:'Change palette to match PMODE4 artifact colors
40 DIM H(256),V(192)
50 PRINTTAB(8)"MULTI-RESOLUTION":PRINTTAB(8)"GRAPHICS UTILITY":PRINTTAB(4)"(C) 1988 BY R.STANWOOD"
60 PRINT:PRINTTAB(5)"(1) LOAD HI RES":PRINTTAB(5)"(2) SAVE HI RES":PRINTTAB(5)"(3) LOAD/DUMP LOW TO HI":PRINT:PRINTTAB(9)"SELECT 1-3":EXEC44539:A$=INKEY$:A=VAL(A$):IFA<1 OR A>3 THEN 60
70 CLS:ON A GOTO 280,390,80
80 HSCREEN 0:PRINT:PRINTTAB(6)"LOAD WHICH PICTURE?":PRINTTAB(7)"(D) FOR DIRECTORY":INPUT P$:IF P$="D" THEN DIR:GOTO80
90 PRINT"MAKE INTO:":PRINTTAB(4)"(1) 320 X 192 4 COLOR":PRINTTAB(4)"(2) 320 X 192 16 COLOR":PRINTTAB(4)"(3) 640 X 192 2 COLOR":PRINTTAB(4)"(4) 640 X 192 4 COLOR"
100 PRINT:PRINTTAB(9)"SELECT 1-4":EXEC44539:A$=INKEY$:S=VAL(A$):IFS<1 OR S>4 THEN 90
110 PRINT:PRINTTAB(8)"(1) FLUSH LEFT":PRINTTAB(8)"(2) FLUSH RIGHT":PRINTTAB(8)"(3) CENTRE":PRINT:PRINTTAB(10)"SELECT 1-3":EXEC44539:L$=INKEY$:L=VAL(L$):IF L<1 OR L>3 THEN 110:'Select hi-res screen positioning
120 IF L=2 AND S<3 THEN L=64 ELSE IF L=2 THEN L=384 ELSE IF L=3 AND S<3 THEN L=32 ELSE IF L=3 THEN L=192 ELSE L=0
130 IF P$=""THEN150
140 PMODE4,1:SCREEN1,1:LOADM P$:FOR X=1TO2000:NEXT
150 PRINT:INPUT"SWAP RED/BLUE (Y/N)";SW$:IF SW$="Y" THEN PALETTE 2,9:PALETTE 3,6:'Switch colorset if requested
160 POKE65497,0:'Set to 2Mhz
170 POKE&HE6C6,33:'Allow for return to HSCREEN with picture intact
180 V=0
190 FORH=0TO255
200 PMODE3,1
210 H(H)=PPOINT(H,V):NEXT
220 HSCREEN S:FOR X=0TO255:HSET(X+L,V,H(X)):NEXT
230 V=V+1
240 IF V<192 THEN 190
250 POKE 65496,0
260 EXEC44539:'Press any key to continue
270 RUN
280 POKE&HE6C6,141:CLS:PRINT:PRINTTAB(2)"LOAD FILENAME (NO EXTENSION)":INPUTF$:'Return to coldstart configuration before saving screen
290 PRINTTAB(5);:INPUT"HSCREEN NUMBER";H
300 PRINT:INPUT"SWAP RED/BLUE (Y/N)";SW$:IF SW$="Y"THEN PALETTE 2,9:PALETTE 3,6
310 HSCREEN H:FOR M=&H70 TO &H73
320 POKE &HFFA2,M:'Manipulate MMU to load into low memory
330 FI$=F$+"/HR"+HEX$(M-&H70)
340 LOADM FI$
350 NEXT
360 POKE&HFFA2,&H7A:'Return MMU to coldstart setting
370 EXEC44539
380 POKE&HE6C6,33:RUN
390 POKE&HE6C6,141:CLS:PRINT:PRINTTAB(2)"SAVE FILENAME (NO EXTENSION)":INPUT F$
400 IF FREE(0)<16 THEN PRINT"NOT ENOUGH SPACE - CHANGE DISKS":PRINT"  (PRESS ANY KEY TO CONTINUE)":EXEC44539:GOTO400
410 FOR M=&H70 TO &H73
420 POKE &HFFA2,M
430 FI$=F$+"/HR"+HEX$(M-&H70)
440 PRINT"saving: "+FI$
450 SAVEM FI$,&H4000,&H5FFF,&H4000
460 NEXT
470 POKE&HFFA2,&H7A
