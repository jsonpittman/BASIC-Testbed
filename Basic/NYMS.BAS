10 REM"REVIEW OF SYNONYMS-HOMONYMS-ANTONYMS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1988"
30 N=40
40 DIM A$(N),A(N)
50 P$=STRING$(32,191)
60 FOR T=1 TO N
70 READ A$(T),A(T):NEXT T
80 CLS:X=X+1
90 PRINT@2,"homonyms-antonyms-synonyms";
100 PRINT@64,P$
110 R=RND(N)
120 PRINT@98,A$(R)
130 PRINT@128,P$
140 PRINT@160,"1.BOTH ARE SYNONYMS"
150 PRINT@196,"2.BOTH ARE HOMONYMS"
160 PRINT@232,"3.BOTH ARE ANTONYMS"
170 PRINT@268,"4.NONE OF THE ABOVE"
180 PRINT@288,P$
190 PRINT@321,"WHICH TYPE ARE THESE WORDS? ";
200 EN$=INKEY$
210 IF EN$="1" OR EN$="2" OR EN$="3" OR EN$="4" THEN 220 ELSE 200
220 PRINTEN$
230 IF VAL(EN$)=A(R) THEN PRINT@395,"CORRECT":C=C+1:GOTO 250
240 PRINT@389,"SORRY, THE ANSWER IS";A(R)
250 PRINT@454,"PRESS ENTER TO GO ON";
260 AN$=INKEY$
270 IF AN$=CHR$(13) THEN 80 ELSE IF AN$="E" THEN 280 ELSE 260
280 PRINT@452,"YOUR FINAL SCORE IS";INT(C/X*100);"%.":END
290 DATA COARSE-COURSE,2
300 DATA METE-MEET,2
310 DATA SCENT-CENT,2
320 DATA HAPPY-JOYOUS,1
330 DATA NICHE-NOOK,1
340 DATA NOOK-KNOCK,4
350 DATA PARE-PEAR,2
360 DATA KNIGHT-AFTERNOON,4
370 DATA SLIM-THIN,1
380 DATA SNEER-FROWN,1
390 DATA STEAK-STAKE,2
400 DATA SITE-CITE,2
410 DATA REIN-REIGN,2
420 DATA THEIR-THOSE,4
430 DATA FOR-FOUR,2
440 DATA THEIR-THERE,2
450 DATA THERE-HERE,3
460 DATA DIRTY-SPOTLESS,3
470 DATA COLD-FRIGID,1
480 DATA FRIGID-WARM,3
490 DATA ICY-FRIGID,1
500 DATA FRIGID-RIGID,4
510 DATA OPEN-SHUT,3
520 DATA SHUT-SHOOT,4
530 DATA SHOOT-CHUTE,2
540 DATA BEACH-BIRCH,4
550 DATA BEACH-BEECH,2
560 DATA SECURE-SAFE,1
570 DATA LEAVE-EXIT,1
580 DATA LEAVE-LIVE,4
590 DATA KING-SOVEREIGN,1
600 DATA KING-PEASANT,3
610 DATA SOVEREIGN-PEASANT,3
620 DATA KING-RING,4
630 DATA ROUGH-SMOOTH,3
640 DATA ROUGH-SLEEK,3
650 DATA SLEEK-SMOOTH,1
660 DATA UNFAILING-CERTAIN,1
670 DATA AMBITIOUS-LAZY,3
680 DATA INDUSTRIOUS-LAZY,3
690 DATA AMBITIOUS-INDUSTRIOUS,1
700 DATA HARMFUL-HARMLESS,3
