10 '*** CIRCLE CYCLE DRAW ***
20 '*** BY BECKY MATTHEWS ***
30 ON BRK GOTO 920
40 ON ERR GOTO 60
50 POKE 65497,0
60 '*TITLE PAGE
70 HSCREEN 2
80 HCLS 8
90 HCOLOR 4,8
100 HPRINT (11,4)," CIRCLE CYCLE DRAW "
110 'SMALL CIRCLES
120 C=1
130 FORX=15TO315STEP14
140 C=C+2:IFC>15THENC=1
150 FORY=9TO182STEP173
160 HCIRCLE(X,Y),5,C
170 HPAINT(X,Y),C,C
180 NEXTY:SOUND X/2,1:NEXTX
190 'MEDIUM CIRCLES
200 C=1
210 FORY=30TO160STEP26
220 C=C+2
230 FORX=10TO310STEP300
240 HCIRCLE(X,Y),10,C
250 HPAINT(X,Y),C,C
260 NEXTX:SOUND Y/2,1:NEXTY
270 'LARGE CIRCLES
280 C=1
290 FORY=45TO160STEP49
300 C=C+2
310 FORX=45TO275STEP230
320 HCIRCLE(X,Y),20,C
330 HPAINT(X,Y),C,C
340 NEXTX:SOUND Y/2,1:NEXTY
350 'MENU
360 HPRINT (10,11)," CHOOSE CIRCLE SIZE"
370 HPRINT (14,14)," 1 - SMALL "
380 HPRINT (14,15)," 2 - MEDIUM"
390 HPRINT (14,16), " 3 - LARGE "
400 HPRINT (13,20)," (ENTER 1-3)"
410 A$=INKEY$:IF A$=""THEN410
420 A=VAL(A$)
430 IF A<1 OR A>3 THEN 400
440 'SET CIRCLE RADIUS
450 R=A*5
460 '*INSTRUCTION PAGE
470 HCLS4
480 HCOLOR 8,4
490 HPRINT (14,2),"INSTRUCTIONS"
500 HPRINT (2,7),"USE THE RIGHT JOYSTICK TO DRAW."
510 HPRINT (2,10),"PRESS JOYSTICK BUTTON TO CLEAR SCREEN."
520 HPRINT (2,11),"(RIGHT BUTTON 1 ON NEW JOYSTICK)"
530 HPRINT (2,14),"PRESS SPACEBAR TO PAUSE DRAWING AND"
540 HPRINT (2,15),"SEE FAST CYCLE."
550 HPRINT (2,18),"PRESS SPACEBAR AGAIN TO RESUME."
560 HPRINT (7,22),"(PRESS ANY KEY TO START)"
570 A$=INKEY$:IFA$=""THEN570
580 X=148:Y=96
590 T=1:C=1
600 'SET BACKGROUND TO BLACK
610 PALETTE 0,0
620 HCLS0
630 'BUTTON CHECK
640 IF BUTTON(0)=1 THEN 620
650 'SPACEBAR CHECK
660 A$=INKEY$:IF A$=CHR$(32) GOSUB 1220
670 'GOSUB ADVANCE PALETTE
680 GOSUB 850
690 'JOYSTICK CHECK
700 H=JOYSTK(0)
710 IF H>41 THEN X=X+4
720 IF H<20 THEN X=X-4
730 IF X<10 THEN X=10
740 IF X>310 THEN X=310
750 V=JOYSTK(1)
760 IF V>41 THEN Y=Y+4
770 IF V<20 THEN Y=Y-4
780 IF Y>182 THEN Y=182
790 IF Y<10 THEN Y=10
800 IF H<160 AND V<96 THEN 980
810 IF H>160 AND V<96 THEN 1040
820 IF H<160 AND V>96 THEN 1100
830 IF H>160 AND V>96 THEN 1160
840 GOTO 630
850 '*ADVANCE PALETTE SUB
860 FOR P=1 TO 15
870 PALETTE P,T+P
880 NEXT
890 T=T+5:IFT>49 THEN T=1
900 C=C+1:IFC>15 THEN C=1
910 RETURN
920 '*END
930 PALETTE RGB:CLS
940 PRINT"BYE FOR NOW."
950 POKE65496,0
960 FORN=100TO1STEP-5:SOUND N,1:NEXT
970 END
980 '*QUADRANT 1 SUB
990 HCIRCLE(X,Y),R,C:HPAINT(X,Y),C,C
1000 HCIRCLE(320-X,Y),R,C:HPAINT(320-X,Y),C,C
1010 HCIRCLE(X,192-Y),R,C:HPAINT(X,192-Y),C,C
1020 HCIRCLE(320-X,192-Y),R,C:HPAINT(320-X,192-Y),C,C
1030 GOTO630
1040 '*QUADRANT 2 SUB
1050 HCIRCLE(X,Y),R,C:HPAINT(X,Y),C,C
1060 HCIRCLE(320-X,Y),R,C:HPAINT(320-X,Y),C,C
1070 HCIRCLE(320-X,192-Y),R,C:HPAING(320-X,192-Y),C,C
1080 HCIRCLE(X,192-Y),R,C:HPAINT(X,192-Y),C,C
1090 GOTO630
1100 '*QUADRANT 3 SUB
1110 HCIRCLE(X,Y),R,C:HPAINT(X,Y),C,C
1120 HCIRCLE(X,192-Y),R,C:HPAINT(X,192-Y),C,C
1130 HCIRCLE(320-X,192-Y),R,C:HPAINT(320-X,192-Y),C,C
1140 HCIRCLE(320-X,Y),R,C:HPAINT(320-X,Y),C,C
1150 GOTO630
1160 '*QUADRANT 4 SUB
1170 HCIRCLE(X,Y),R,C:HPAINT(X,Y),C,C
1180 HCIRCLE(X,192-Y),R,C:HPAINT(X,192-Y),R,C
1190 HCIRCLE(320-X,Y),R,C:HPAINT(320-X,Y),C,C
1200 HCIRCLE(320-X,192-Y),R,C:HPAINT(320-X,192-Y),C,C
1210 GOTO630
1220 '*CYCLE ONLY SUB
1230 FOR P = 1 TO 15
1240 PALETTE P,T+P
1250 NEXT
1260 T=T+4:IFT>49 THEN T=1
1270 'SPACEBAR CHECK
1280 IF INKEY$="" THEN 1230
1290 RETURN
