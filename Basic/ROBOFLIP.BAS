1 REM ************************
2 REM *      ROBOFLIP        *
3 REM *   BY FRED B.SCERBO   *
4 REM *    60 HARDING AVE.   *
5 REM * NORTH ADAMS,MA 01247 *
6 REM *  COPYRIGHT (C) 1986  *
7 REM ************************
10 CLS0:FORI=1TO32:PRINTCHR$(252);:NEXT
15 FORY=128TO192STEP32:RESTORE:FORI=1TO128:READA:PRINTCHR$(A+Y);:NEXTI,Y:FORI=1TO32:PRINTCHR$(252);:NEXT
20 DATA28,30,28,29,,30,28,29,28,29,28,28,30,28,30,28,29,16,44,46,44,44,42,45,40,,36,46,36,46,44,45
25 DATA,27,19,23,16,26,,21,,21,19,19,26,,26,,21,,,43,39,32,40,37,,,,42,,43,35,39
30 DATA,26,,26,16,26,,21,,21,,,26,,26,,21,,,42,36,,,37,,37,32,42,,42,,
35 DATA28,24,,28,28,28,28,28,,28,28,28,24,,28,28,28,,44,44,,,,44,44,44,36,44,36,44,,
40 PRINT@454,"  BY FRED B.SCERBO  ";
45 PRINT@486," COPYRIGHT (C) 1986 ";
50 IFINKEY$<>CHR$(13)THEN50
55 CLS0
60 R$=CHR$(128)
65 FORI=3TO7:SET(I,5,2):SET(I+53,5,2):NEXT
70 FORI=2TO8:SET(I,6,6):SET(I+53,6,4):NEXT
75 FORI=1TO9:FORY=7TO10:SET(I,Y,6):SET(I+53,Y,4):NEXTY,I
80 FORI=2TO8:SET(I,11,6):SET(I+53,11,4):NEXT
85 FORI=0TO10:FORY=12TO18:SET(I,Y,8):SET(I+53,Y,3):NEXTY,I
90 FORI=2TO7:FORY=20TO24STEP2:SET(I,Y,3):SET(I+53,Y,6):NEXTY,I
95 FORI=1TO8:SET(I,26,7):SET(I+53,26,2):NEXT
100 PRINT@227,CHR$(236)CHR$(235)CHR$(227)CHR$(227)CHR$(232);
105 PRINT@248,CHR$(196)CHR$(195)CHR$(195)CHR$(199)CHR$(204);
110 PRINT@131,CHR$(206);:PRINT@133,CHR$(194);
115 PRINT@154,CHR$(193);:PRINT@156,CHR$(205);
120 GOTO135
125 FORI=175TO399STEP32:PRINT@I,CHR$(153);:SOUND2,1:PRINT@I,CHR$(128);:PRINT@I+32,CHR$(150);:SOUND2,1:PRINT@I+32,CHR$(128);:NEXT
130 RETURN
135 REM START GAME
140 PRINT@449,"you";:PRINT@472,"computer";
145 PP=RND(10):IFPP=>6THENCC=1:GOTO155
150 CC=0
155 YU=10:CP=10:TT=RND(2)
160 GOSUB165:FORI=1TO1000:NEXT:GOTO170
165 PRINT@34,"";:PRINTUSING"##";YU;:PRINT@60,"";:PRINTUSING"##";CP;:RETURN
170 IF TT=1THENGOSUB240:GOSUB280:TT=2:GOTO180
175 IF TT=2THENGOSUB280:GOSUB240:TT=1:GOTO180
180 BP=BP+2:IF KK=LL THEN200
185 PRINT@5,R$R$R$"neither"R$"one"R$"wins"R$R$R$;
190 PRINT@110,BP;
195 GOTO210
200 IFKK=223THENPRINT@5,R$R$"you"R$"win"R$"this"R$"time"R$R$R$;:PRINT@110,R$R$R$R$;:YU=YU+BP:BP=0:GOTO210
205 IFKK=191THENPRINT@5,R$R$"the"R$"computer"R$"wins"R$R$R$;:PRINT@110,R$R$R$R$;:CP=CP+BP:BP=0
210 IF YU=0THEN320
215 IF CP=0THEN320
220 GOSUB165
225 FORI=1TO1500:NEXT:PRINT@461,R$R$R$R$R$;
230 GOTO170
235 GOTO235
240 PRINT@5,"press"R$"spacebar"R$"to"R$"flip";
245 X$=INKEY$:FORI=1TO30:NEXT:PRINT@231,CHR$(191);:AK=RND(-TIMER):FORI=1TO30:NEXT:PRINT@231,CHR$(223);:IFX$<>CHR$(32)THEN245
250 PRINT@200,R$;:YU=YU-1:GOSUB165
255 ZZ=12:FORI=14TO9STEP-1:ZZ=ZZ+3:SET(ZZ,I,5):FORKY=1TO30:NEXTKY:RESET(ZZ,I):NEXTI:GOSUB125
260 KK=RND(50):IFKK=>26THENKK=223
265 IFKK<=25THENKK=191
270 PRINT@461,CHR$(KK);
275 RETURN
280 PRINT@5,"the"R$"computer"R$"flips"R$"now";
285 CP=CP-1:GOSUB165
290 FORI=1TO10:A=RND(-TIMER):PRINT@248,CHR$(191);:FORII=1TO30:NEXTII:PRINT@248,CHR$(223);:FORII=1TO30:NEXTII:NEXTI
295 ZZ=51:FORI=14TO9STEP-1:ZZ=ZZ-3:SET(ZZ,I,5):FORKY=1TO30:NEXTKY:RESET(ZZ,I):NEXTI:GOSUB125
300 LL=RND(50):IFLL=>26THENLL=191
305 IFLL<=25THENLL=223
310 PRINT@465,CHR$(LL);
315 RETURN
320 IF KK<>LL THEN BP=BP/2:YU=YU+BP:CP=CP+BP:BP=0:FORI=1TO1000:NEXTI:PRINT@5,R$"you"R$"both"R$"split"R$"them"R$R$;
325 IF KK<>LL THENPRINT@110,R$R$R$R$;:PRINT@461,R$R$R$R$R$;:FORI=1TO1000:NEXT:GOTO220
330 IF YU=0 THEN PRINT@5,"you"R$"have"R$"lost"R$"the"R$"game";:GOSUB165
335 IF CP=0 THEN PRINT@5,R$"the"R$"computer"R$"has"R$"lost"R$;:GOSUB165
340 X$=INKEY$:IFX$<>CHR$(13)THEN340
345 PRINT@110,R$R$R$R$;
350 PRINT@461,R$R$R$R$R$;
355 GOTO135
