10 'COPYRIGHT 1988
20 'BY DAVID H. TURNER
30 'POB 5062
40 'LITTLE ROCK, ARK.  72225
50 CLEAR1000
60 VERIFYON
70 CLS
80 INPUT"<L>OAD PREVIOUS DATA OR         <C>REATE NEW DATA";R$:IFR$="L"THEN1580ELSEIFR$="C"THEN90ELSE70
90 Z=0
100 Z=Z+1
110 IFZ>6THEN230
120 CLS
130 PRINT"ROOMMATE #"Z
140 LINEINPUT"NAME ";NA$(Z)
150 INPUT"% PAY FOR RENT";RC(Z)
160 INPUT"% PAY FOR PHONE";PC(Z)
170 INPUT"% PAY FOR CATV";CC(Z)
180 INPUT"% PAY FOR WATER ";WA(Z)
190 INPUT"% PAY FOR GAS ";GA(Z)
200 INPUT"% PAY FOR TRASH";SC(Z)
210 INPUT"% PAY FOR APL";AC(Z)
220 GOTO100
230 CLS
240 INPUT"DO YOU WANT A PRINTOUT  Y/N:";R$
250 IFR$="Y"THENJ=(-2)ELSEJ=0
260 PRINT#J,"RMMT*RENT *PHONE *CATV *APL      *WATER     *GAS  *TRASH"
270 Z=0
280 Z=Z+1
290 IFZ>6THEN380
300 PRINT#J,Z"  ";:PRINT#J,USING"###";RC(Z);
310 PRINT#J,"     ";:PRINT#J,USING"###";PC(Z);
320 PRINT#J,"   ";:PRINT#J,USING"###";CC(Z);
330 PRINT#J,"  ";:PRINT#J,USING"###";AC(Z);
340 PRINT#J,"         ";:PRINT#J,USING"###";WA(Z);
350 PRINT#J,"      ";:PRINT#J,USING"###";GA(Z);
360 PRINT#J,"     ";:PRINT#J,USING"###";SC(Z)
370 GOTO280
380 PRINT:PRINT:
390 INPUT"DO YOU WANT TO CHANGE A ROOMMATEY/N";R$
400 IFR$="Y"THEN410ELSEIFR$="N"THEN510ELSE390
410 PRINT"NAME               #"
420 Z=0
430 Z=Z+1
440 IFZ>6THEN460
450 PRINTNA$(Z);:PRINTTAB(18)Z:GOTO430
460 INPUT"WHICH ONE";Z
470 LINEINPUT"NAME ";NA$(Z):INPUT"% PAY RENT";RC(Z):INPUT"% PAY PHONE";PC(Z):INPUT"% PAY CATV";CC(Z):INPUT"% PAY APL";AC(Z)
480 INPUT"% PAY WATER ";WA(Z):INPUT"% PAY GAS ";GA(Z):INPUT"% PAY TRASH";SC(Z)
490 INPUT"ANOTHER CHANGE  Y/N";R$:IFR$="Y"THEN410ELSE500
500 GOTO230
510 '
520 'loaded data and preparation for printing new statement
530 Z=0
540 CLS:INPUT"DO YOU WANT TO CHANGE % PAYS";R$
550 IFR$="Y"THEN410ELSEIFR$="N"THEN560ELSE540
560 INPUT"RENT:";RE
570 INPUT"PHONE:";PH
580 INPUT"CATV:";CA
590 INPUT"WATER/SEWER:";WA
600 INPUT"GAS:";GA
610 INPUT"TRASH:";TR
620 INPUT"ELECTRICITY:";KU
630 LINEINPUT"DATE BILL IS DUE ";DD$
640 Z=0
650 Z=Z+1
660 IFZ>6THEN700
670 IF NA$(Z)="" THEN 690
680 CLS:PRINTNA$(Z):INPUT"AMOUNT PAID:";PD(Z)
690 GOTO650
700 'printing statement
710 Z=0
720 Z=Z+1:IFZ>6THEN1450
730 IFNA$(Z)=""THEN1440
740 PRINTNA$(Z);:INPUT" PRINTOUT  Y/N";HC$
750 IFHC$="Y"THENJ=(-2)ELSEJ=0
760 PRINT#J,NA$(Z)
770 PRINT#J,"STATEMENT DUE DATE IS: "DD$
780 PRINT#J,"RENT";
790 PRINT#J,TAB(23);
800 PRINT#J,USING"$$,####.##";((RC(Z)*RE)/100)
810 PRINT#J,"PHONE";
820 PRINT#J,TAB(26);
830 PRINT#J,USING"$$##.##";((PC(Z)*PH)/100)
840 PRINT#J,"CATV";
850 PRINT#J,TAB(25);
860 PRINT#J,USING"$$###.##";((CC(Z)*CA)/100)
870 PRINT#J,"WATER/SEWER ";:PRINT#J,TAB(25);:PRINT#J,USING"$$###.##";((WA(Z)*WA)/100)
880 PRINT#J,"GAS ";:PRINT#J,TAB(25);:PRINT#J,USING"$$###.##";((GA(Z)*GA)/100)
890 PRINT#J,"TRASH";:PRINT#J,TAB(26);:PRINT#J,USING"$$##.##";((SC(Z)*TR)/100)
900 PRINT#J,"ELECTRICITY";
910 PRINT#J,TAB(23);
920 PRINT#J,USING"$$,####.##";(KU*(AC(Z)/100))
930 PRINT#J,"TOTAL ";
940 PRINT#J,TAB(23);
950 X(Z)=RC(Z)*RE/100
960 W(Z)=PC(Z)*PH/100
970 V(Z)=CC(Z)*CA/100
980 U(Z)=KU*(AC(Z))/100
990 L(Z)=WA(Z)*WA/100
1000 M(Z)=GA(Z)*GA/100
1010 O(Z)=SC(Z)*TR/100
1020 PRINT#J,USING"$$,####.##";(X(Z)+W(Z)+V(Z)+U(Z)+L(Z)+M(Z)+O(Z))
1030 T(Z)=X(Z)+W(Z)+V(Z)+U(Z)+L(Z)+M(Z)+O(Z)
1040 PRINT#J,"":PRINT#J,"UNPAID FROM LAST BILL";:PRINT#J,TAB(25);:PRINT#J,USING"$$###.##";(BB(Z));:IF BB(Z)<0THENPRINT#J," CREDIT"ELSEPRINT#J," "
1050 PRINT#J,"":PRINT#J,"PAYMENT";
1060 PRINT#J,TAB(23);
1070 PRINT#J,USING"$$,####.##";PD(Z)
1080 PRINT#J,"":PRINT#J,"TOTAL NOW DUE";
1090 PRINT#J,TAB(23);
1100 BA(Z)=(T(Z))-(PD(Z))+(BB(Z))
1110 BB(Z)=BA(Z)
1120 PRINT#J,USING"$$,####.##";(BA(Z));
1130 IF (BA(Z))>0THENPRINT#J," NOW DUE"
1140 IF (BA(Z))<0THENPRINT#J," CREDIT"
1150 PRINT#J,""
1160 PRINT#J,NA$(Z)" TO DATE TOTALS"
1170 RT(Z)=RT(Z)+X(Z)
1180 PR(Z)=PR(Z)+W(Z)
1190 CT(Z)=CT(Z)+V(Z)
1200 UT(Z)=UT(Z)+U(Z)
1210 PT(Z)=PT(Z)+PD(Z)
1220 F(Z)=F(Z)+L(Z)
1230 G(Z)=G(Z)+M(Z)
1240 H(Z)=H(Z)+O(Z)
1250 IFHC$<>"Y"THEN720
1260 PRINT#J,TAB(2)"PAYMENTS";
1270 PRINT#J,TAB(16)"RENT";
1280 PRINT#J,TAB(25)"PHONE";
1290 PRINT#J,TAB(36)"CATV";
1300 PRINT#J,TAB(42)"ELECTRIC";
1310 PRINT#J,TAB(55)"WATER";
1320 PRINT#J,TAB(67)"GAS";
1330 PRINT#J,TAB(75)"TRASH"
1340 PRINT#J,USING"$$,####.##";(PT(Z));
1350 PRINT#J,TAB(10);:PRINT#J,USING"$$,####.##";(RT(Z));
1360 PRINT#J,TAB(19);:PRINT#J,USING"$$,####.##";(PR(Z));
1370 PRINT#J,TAB(26);:PRINT#J,USING"$$,####.##";(CT(Z));
1380 PRINT#J,TAB(40);:PRINT#J,USING"$$,####.##";(UT(Z));
1390 PRINT#J,TAB(49);:PRINT#J,USING"$$#,###.##";(F(Z));
1400 PRINT#J,TAB(57);:PRINT#J,USING"$$#,###.##";(G(Z));
1410 PRINT#J,TAB(65);:PRINT#J,USING"$$#,###.##";(H(Z))
1420 PRINT#J,STRING$(80,36)
1430 PRINT#J,"":PRINT#J,""
1440 GOTO720
1450 'save to disk
1460 CLS:INPUT"READY TO SAVE TO DISK  Y/N";R$:IFR$="Y"THEN1470ELSEEND
1470 INPUT"LAST THREE LETTERS FOR PASSCODE";LT$
1480 FP$="MATE"
1490 PC$=FP$+LT$
1500 VERIFYON:INPUT"PRESS <ENTER> WHEN READY";R$
1510 OPEN "O",#1,PC$
1520 FORZ=1TO6
1530 WRITE#1,NA$(Z),PT(Z),RT(Z),PR(Z),CT(Z),UT(Z),BB(Z),F(Z),G(Z),H(Z),RE,PH,CA,AP,WA,GA,TR,RC(Z),PC(Z),CC(Z),AC(Z),WA(Z),GA(Z),SC(Z)
1540 NEXTZ
1550 CLOSE#1
1560 INPUT"READY TO DELETE OLD FILE  Y/N";RD$:IFRD$="Y"THENINPUT"NAME OF OLD FILE:";OF$:OF$="MATE"+OF$+"/DAT":KILL OF$
1570 END
1580 'load old data from disk
1590 INPUT"ENTER LAST THREE LETTERS OF CODE";LC$
1600 EC$="MATE":LE$=EC$+LC$
1610 OPEN"I",#1,LE$
1620 FORZ=1TO6
1630 IFEOF(1)THEN1660
1640 INPUT#1,NA$(Z),PT(Z),RT(Z),PR(Z),CT(Z),UT(Z),BB(Z),F(Z),G(Z),H(Z),RE,PH,CA,AP,WA,GA,TR,RC(Z),PC(Z),CC(Z),AC(Z),WA(Z),GA(Z),SC(Z)
1650 NEXTZ
1660 CLOSE#1
1670 GOTO520
