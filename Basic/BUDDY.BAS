1 PCLEAR1:HSCREEN0:WIDTH32:PRINT@202,"INITIALIZING":POKE&HE6B7,&H39:HSCREEN2:POKE&HE6B7,&H20
2 POKE&HFFD9,0:ONBRKGOTO1:CMP:PALETTE9,15:HCLS5:HCOLOR8:HDRAW"BM100,40R120F5D80G5L120H5U80E5":HDRAW"BM110,50R100F5D60G5L100H5U60E5":HDRAW"BM120,65E5R15BR40E5F10":HDRAW"BM135,65G5F5R5U5":HDRAW"BM180,70D5R5E5H5":HDRAW"BM165,65D25L5"
3 HCIRCLE(160,93),35,,.15,0,.5:HCIRCLE(160,93),35,,.5,0,.5
4 HPAINT(120,70),0,8:HPAINT(160,108),4,8:HPAINT(97,70),4,8
5 HLINE(100,130)-(85,145),PSET:HLINE-(235,145),PSET:HLINE-(220,130),PSET:HLINE(100,135)-(95,140),PSET:HLINE-(225,140),PSET:HLINE-(220,135),PSET:HLINE-(100,135),PSET
6 HPAINT(101,132),4,8:HPAINT(106,138),9,8:HCOLOR3:HLINE(101,133)-(120,133),PSET:HCOLOR8:HLINE(85,145)-(235,150),PSET,B:HPAINT(86,146),4,8
7 HPRINT(4,20),"THE FRIENDLY PROGRAMMING UTILITY"
8 HCOLOR4:HPRINT(4,22),"(C) MCMLXXXVIII BY MARC CAMPBELL"
9 HCOLOR8:HDRAW"BM35,10D20R50U5H5E5U5NL50D20R50U20L20D10L10U10L20":HDRAW"BM135,10D20R45E5U10H5L45":HDRAW"BM185,10D20R45E5U10H5L45":HDRAW"BM235,10D10R20D10R10U10R20U10L20D5L10U5L20"
10 HPAINT(36,11),1,8:HPAINT(86,11),1,8:HPAINT(136,11),1,8:HPAINT(186,11),1,8:HPAINT(236,11),1,8
11 POKE&HE6E4,&HE6:HSCREEN2:POKE&HE6E4,&HE7:A$=INKEY$:A$=""
12 IFINKEY$=""THENPALETTE1,Q:Q=Q+1:IFQ=64THENQ=0:GOTO12ELSE12
13 ATTR0,0:WIDTH40:CMP:CLS1:CLEAR2500:LOCATE2,10:PRINT"Are you using (1) 128K or (2) 512K?":ONBRKGOTO13
14 EXEC44539:A$=INKEY$:IFA$="1"THENL=196:S=393216ELSEIFA$="2"THENL=1769:S=0ELSEPLAY"V31L35O1C":GOTO14
15 PLAY"L30O3C":DIMLL(L-1):PT=S:W=40:E$="V31L35O1C":P$="V31L255O1C":POKE&HFFD9,0:ONERRGOTO48:VERIFYON:CH=0:BK=18:CW=63:ONBRKGOTO104
16 ATTR0,0:PT=S+(250*LU):L$=STRING$(250,0):EL=1:LP=1:X=0:Y=3:WIDTHW:GOSUB54:IFAL=1ANDZ=0THENZ=LU+1:L$=STR$(SR)ELSEIFAL=1ANDZ>0THENL$=STR$(SR+IC*((LU+1)-Z))
17 IFAL=1THENL$=RIGHT$(L$,LEN(L$)-1)+" ":EL=LEN(L$)+1:LP=EL:LOCATEX,Y:PRINTL$;:HSTATQ$,Q,X,Y:L$=L$+STRING$(250-LEN(L$),0)
18 LOCATEX,Y:A$=INKEY$:IFA$=""THEN18ELSEPLAYP$:IFASC(A$)>31ANDASC(A$)<123ANDA$<>"^"ANDPEEK(343)<>191ANDPEEK(341)<>191THEN33
19 IFA$=CHR$(12)THENA$="^":GOTO33
20 IFA$=CHR$(214)ANDLP<EL THENPRINTSTRING$(7,13):MID$(L$,LP,250-LP)=STRING$(250,0):EL=LP:IFED=0THENLL(LU)=EL:GOTO18ELSELL(LX)=EL:GOTO18
21 IFA$=CHR$(13)THENPRINTSTRING$(6,13):LOCATE(W-40)/2,11:PRINT"Are you finished editing this line (Y/N)":EXEC44539:A$=INKEY$:IFA$="Y"ORA$="y"THEN39ELSELOCATE(W-40)/2,11:PRINT:A$="":GOTO18
22 IFA$=CHR$(103)ANDEL>1ANDLP<>EL ANDLP<249THENLF$=LEFT$(L$,LP):RT$=MID$(L$,LP+1,250):L$=LEFT$(LF$,LP-1)+RT$+CHR$(0):LOCATE0,3:PRINTL$:EL=EL-1:GOSUB54:GOTO18ELSEIFPEEK(343)=191THENPLAYE$:GOTO18
23 IFA$=CHR$(4)ANDEL<249ANDLP<>EL THENLF$=LEFT$(L$,LP-1):RT$=MID$(L$,LP,250):L$=LF$+" "+RT$:L$=LEFT$(L$,250):LOCATE0,3:PRINTL$:EL=EL+1:GOSUB54:GOTO18ELSEIFPEEK(344)=191THENPLAYE$:GOTO18
24 IFA$=CHR$(189)THEN59
25 IFPEEK(341)=191ANDI=0THENI=1:GOSUB54:GOTO18ELSEIFPEEK(341)=191ANDI=1THENI=0:GOSUB54:GOTO18
26 IFA$=CHR$(8)THENLP=LP-1:IFLP<1THENLP=1:PLAYE$:GOTO18ELSEGOSUB54:X=X-1:IFX<0THENX=W-1:Y=Y-1:GOTO42ELSE42
27 IFA$=CHR$(9)THENLP=LP+1:IFLP>EL THENLP=EL:PLAYE$:GOTO18ELSEIFLP=251THENLP=250:PLAYE$:GOTO18ELSEGOSUB54:X=X+1:IFX>W-1 THENX=0:Y=Y+1:GOTO42ELSE42
28 IFA$=CHR$(10)THENLP=LP+W:IFLP>EL THENLP=LP-W:PLAYE$:GOTO18ELSEIFLP=251THENLP=250:PLAYE$:GOTO18ELSEGOSUB54:Y=Y+1:IFY>24THENY=24:GOTO42ELSE42
29 IFA$=CHR$(94)THENLP=LP-W:IFLP<0THENLP=LP+W:PLAYE$:GOTO18ELSEGOSUB54:Y=Y-1:IFY<0THENY=0:GOTO42ELSE42
30 IFA$=CHR$(21)THENLOCATE0,3:PRINTL$:GOTO18
31 IFA$=CHR$(1)THENWIDTH32:PRINTL$:EXEC44539:A$=INKEY$:A$="":WIDTHW:GOSUB54:PRINT:PRINTL$;:GOTO18
32 PLAYE$:GOTO18
33 IFLU+1>L THENPLAYE$:GOTO18ELSEIFI=1THEN36ELSEMID$(L$,LP,1)=A$:LP=LP+1:IFLP>EL THENEL=LP
34 IFLP=251THENPRINTA$;:PLAYE$:L$=LEFT$(L$,250):LP=250:GOTO18
35 PRINTA$;:HSTATQ$,Q,X,Y:LOCATE0,0:ATTR0,0,U:PRINT:GOSUB54:GOTO18
36 IFEL<249ANDLP<>EL THENLF$=LEFT$(L$,LP-1):RT$=MID$(L$,LP,250):L$=LF$+A$+RT$:L$=LEFT$(L$,250):LOCATE0,3:PRINTL$:EL=EL+1:MU=MU+1:X=X+1:LP=LP+1:GOSUB37:GOSUB54:GOTO18ELSEPLAYE$:GOTO18
37 IFX>W-1 THENX=0:Y=Y+1
38 RETURN
39 IFED=1THENED=0:LL(LX)=LP:LX=0ELSELL(LU)=LP:LU=LU+1
40 MID$(L$,LP,250)=CHR$(13)+STRING$(250-LP,0):FORQ=1TO250:LPOKEPT,ASC(MID$(L$,Q,1)):PT=PT+1:NEXT
41 GOTO16
42 LOCATEX,Y:PLAYP$
43 IFPEEK(343)=247THEN26
44 IFPEEK(344)=247THEN27
45 IFPEEK(342)=247THEN28
46 IFPEEK(341)=247THEN29
47 GOTO18
48 GOSUB147:CLOSE#-1:POKE&HFFD9,0:ATTR0,0,U:LOCATE0,0:PRINT"WARNING: ";:IFERNO=4THENPRINT"Out of Range":GOTO105
49 IFERNO=5THENPRINT"Overflow":GOTO105
50 IFERNO=20THENPRINT"Input/Output Error":GOTO105
51 IFERNO=23THENPRINT"Not in ASCII Format":GOTO106
52 IFERNO>25ANDERNO<38THENPRINT"Disk Error":GOTO105
53 WIDTH32:PRINTERNO,ERLIN:END
54 LOCATE0,0:ATTR0,0,U:PRINT:LOCATE0,1:PRINT"BUDDY ";L-LU;250-LP;
55 IFPEEK(282)=0THENPRINT"L";ELSEPRINT"U";
56 IFI=1THENPRINT" I";ELSEPRINT" O";
57 IFED=1THENPRINT" E"ELSEPRINT
58 ATTR0,0:RETURN
59 IFED=1THENPLAYE$:GOTO18ELSELOCATE0,11:ATTR0,4:PRINTSTRING$(12,13);:LOCATE(W-19)/2,11:PRINT"Select a command: ";:EXEC44539:A$=INKEY$:PRINTA$
60 IFA$="W"ORA$="w"THENC$="Alter Screen Width":GOSUB96:INPUT"Would you like 40 or 80 columns";Q:Q=INT(Q):IFQ=40THENW=40:GOTO104ELSEIFQ=80THENW=80:GOTO104
61 IFA$="H"ORA$="h"THENC$="Alter Colors":GOSUB96:PRINT"Current Colors:"CH;BK;CW:INPUT"Characters, background, command window";CH,BK,CW:CH=INT(CH):BK=INT(BK):CW=INT(CW):PALETTE0,BK:PALETTE8,CH:PALETTE4,CW:GOTO104
62 IFA$="Q"ORA$="q"THENC$="Key Click":GOSUB96:INPUT"Key click (1) on or (2) off";Q:Q=INT(Q):IFQ=1THENP$="V31L255O1C":GOTO105ELSEIFQ=2THENP$="":GOTO105
63 IFA$="E"ANDLU>0ORA$="e"ANDLU>0THENC$="Edit Line":GOSUB96:GOTO108
64 IFA$="B"ORA$="b"THENC$="Return to BASIC":GOSUB96:PRINT"Are you sure you want to exit? (Y/N)";:EXEC44539:I$=INKEY$:IFI$="Y"ORI$="y"THENWIDTH32:POKE&HFFD8,0:ATTR0,0:CMP:ENDELSE105
65 IFA$="M"ORA$="m"THENMR=0:C$="Memory Used":GOSUB96:FORQ=0TO195:MR=MR+LL(Q):NEXT:PRINT"The program uses"MR"bytes.":GOSUB114:GOTO105
66 IFA$="S"ANDLU>0ORA$="s"ANDLU>0THENIFPEEK(188)=14THENLN$="":XX=0:C$="Disk Save":GOSUB96:DV=1:GOSUB118:GOTO104
67 IFA$="T"ANDLU>0ORA$="t"ANDLU>0THENLN$="":XX=0:C$="Cassette Save":GOSUB96:DV=-1:GOSUB118:GOTO104
68 IFA$="L"ORA$="l"THENIFPEEK(188)=14THENLN$="":XX=0:C$="Disk Load":GOSUB96:DV=1:GOSUB120:GOTO104
69 IFA$="U"ORA$="u"THENLN$="":XX=0:C$="Cassette Load":GOSUB96:DV=-1:GOSUB120:GOTO104
70 IFA$="K"ORA$="k"THENIFPEEK(188)=14THENC$="Kill Disk File":GOSUB96:LINEINPUT"What is the filename? ";F$:F$=LEFT$(F$,8)+".BAS":POKE&HFFD8,0:KILLF$:GOTO104
71 IFA$="G"ANDLU>0ORA$="g"ANDLU>0THENC$="Global Replace":GOSUB96:HSTATQ$,Q,MX,MY:LINEINPUT"Target text: ";T$:LOCATEMX,MY:PRINTSTRING$(7,13):LOCATEMX,MY:LINEINPUT"Replacement: ";R$:GOTO80
72 IFA$="D"ORA$="d"THENIFPEEK(188)=14THENC$="Disk Directory":GOSUB96:GOSUB114:ATTR0,0:WIDTHW:POKE&HFFD8,0:DIR:PRINT"Free granules:"FREE(0):POKE&HFFD9,0:GOSUB115:GOTO104
73 IFA$="C"ORA$="c"THENC$="Cut & Paste":GOSUB96:CP=1:GOSUB108:EE=LL((Q-S)/249)-1:PRINT"Line"LN"found.":LN$="":FORQQ=Q TO Q+249:LN$=LN$+CHR$(LPEEK(QQ)):NEXT:GOTO128
74 IFA$="R"ANDLU>0ORA$="r"ANDLU>0THENC$="Replace Text":GOSUB96:GOTO89
75 IFA$="F"ANDLU>0ORA$="f"ANDLU>0THENC$="Find Text":GOSUB96:LINEINPUT"Target text: ";T$:GOTO97
76 IFA$="N"ORA$="n"THENC$="Clear Memory":GOSUB96:LOCATE(W-20)/2,22:PRINT"Are you sure? (Y/N)";:EXEC44539:A$=INKEY$:IFA$="Y"ORA$="y"THEN13
77 IFA$="A"ORA$="a"THENC$="Automatic Line Numbering":GOSUB96:INPUT"Would you like it (1) on or (2) off";Q:Q=INT(Q):IFQ=1THENAL=1:GOTO106ELSEIFQ=2THENAL=0:GOTO105
78 IFA$="P"ANDLU>0ORA$="p"ANDLU>0THENC$="Output to Printer":GOSUB96:INPUT"What is your printer's width";C:C=INT(C):IFC<1THEN79ELSEGOSUB112:GOTO105
79 PLAYE$:GOTO59
80 NC=0:PS=S:FORQ=S TO S+(250*LU)-250STEP250
81 LN$="":FORQQ=Q TO Q+LL((Q-S)/250)-1:LN$=LN$+CHR$(LPEEK(QQ)):NEXT:P=1:LC=0
82 IR=INSTR(P,LN$,T$):IFIR=0THEN84
83 LC=LC+1:LT$=LEFT$(LN$,IR-1):RT$=MID$(LN$,IR+LEN(T$),250):IFLEN(LT$)+LEN(R$)+LEN(RT$)>250THENPS=PS+250:LC=0:GOTO87ELSELN$=LT$+R$+RT$:P=IR+LEN(R$):GOTO82
84 LL((Q-S)/250)=LEN(LN$):IFLEN(LN$)=250THEN86
85 LN$=LN$+STRING$(250-LEN(LN$),0)
86 FORZZ=1TO250:LPOKEPS,ASC(MID$(LN$,ZZ,1)):PS=PS+1:NEXT
87 NC=NC+LC:NEXT
88 PRINT"Number of occurrences changed:"NC:GOSUB114:GOTO104
89 HSTATQ$,XQ,MX,MY:CP=1:GOSUB108:PRINT"Line"LN"found.":LN$="":FORQQ=Q TO Q+LL((Q-S)/250)-1:LN$=LN$+CHR$(LPEEK(QQ)):NEXT:LOCATE0,3:ATTR0,0:PRINTSTRING$(7,13):LOCATE0,3:PRINTLN$
90 LOCATEMX,MY:ATTR0,4:PRINTSTRING$(7,13):LOCATEMX,MY:LINEINPUT"Target text: ";T$:LOCATEMX,MY:PRINTSTRING$(7,13):LOCATEMX,MY:LINEINPUT"Replacement: ";R$:P=1
91 IR=INSTR(P,LN$,T$):IFIR=0THEN93
92 LT$=LEFT$(LN$,IR-1):RT$=MID$(LN$,IR+LEN(T$),250):IFLEN(LT$)+LEN(R$)+LEN(RT$)>250THEN93ELSELN$=LT$+R$+RT$:P=IR+LEN(R$):GOTO91
93 LL((Q-S)/250)=LEN(LN$):IFLEN(LN$)=250THEN95
94 LN$=LN$+STRING$(250-LEN(LN$),0)
95 FORZZ=1TO250:LPOKEQ,ASC(MID$(LN$,ZZ,1)):Q=Q+1:NEXT:GOSUB114:GOTO104
96 LOCATE(W-LEN(C$))/2,12:PRINTC$:PRINT:RETURN
97 FORQ=S TO S+(250*LU)-250STEP250
98 L$="":FORQQ=Q TO Q+LL((Q-S)/250)-1:L$=L$+CHR$(LPEEK(QQ)):NEXT:IFLEN(L$)=250THEN99ELSEL$=L$+STRING$(250-LL((Q-S)/250),0)
99 IR=INSTR(1,L$,T$):IFIR=0THEN103
100 ATTR0,0:LOCATE0,3:PRINTSTRING$(7,13):LOCATE0,3:PRINTL$:ATTR0,4:LOCATE(W-30)/2,22:PRINT"Do you want to continue? (Y/N)";
101 A$=INKEY$:IFA$=""THEN101ELSEIFA$="Y"ORA$="y"THEN103
102 ED=1:EL=LL((Q-S)/249):PT=Q:GOTO104
103 NEXT:ATTR0,0:GOTO16
104 POKE&HFFD9,0:CLOSE#-1:GOSUB147:CLS1:ATTR0,0:LP=1:X=0:Y=3:WIDTHW:GOSUB54:LOCATEX,Y:PRINTL$:GOTO18
105 LOCATE0,11:ATTR0,0:PRINTSTRING$(12,13);:GOTO18
106 INPUT"Start, increment";SR,IC:SR=INT(SR):IC=INT(IC)
107 IFSR+IC*L>63999THENPLAYE$:GOTO106ELSE16
108 NB$="":INPUT"What is the line number";LN:LN=INT(LN):LN$=STR$(LN):LN$=RIGHT$(LN$,LEN(LN$)-1)+" "
109 FORQ=S TO S+(250*LU)STEP250:FORQQ=Q TO Q+LEN(LN$)-1:NB$=NB$+CHR$(LPEEK(QQ)):NEXT:IFLN$=NB$ THEN111ELSENB$="":NEXT
110 ED=0:CP=0:PRINT"Line"LN"does not exist.":GOSUB114:GOTO105
111 IFCP=1THENCP=0:RETURNELSEED=1:LX=(Q-S)/249:EL=LL(LX):PRINT"Line"LN"found.":PT=Q:L$="":FORQQ=Q TO Q+249:L$=L$+CHR$(LPEEK(QQ)):NEXT:GOTO104
112 POKE&HFFD8,0:PRINT#-2:FORQ=S TO PT-1:IFLPEEK(Q)=0THEN113ELSEPRINT#-2,CHR$(LPEEK(Q));:IFPOS(-2)=C THENPRINT#-2
113 NEXT:POKE&HFFD9,0:RETURN
114 LOCATE(W-26)/2,22:PRINT"Press any key to continue.";
115 IFINKEY$=""THEN115ELSERETURN
116 LOCATE(W-20)/2,22:PRINT"Are you sure? (Y/N)";
117 IFINKEY$="Y"ORINKEY$="y"THENRETURNELSEIFINKEY$="N"ORINKEY$="n"THEN105ELSE117
118 LINEINPUT"What is the filename? ";F$:POKE&HFFD8,0:F$=LEFT$(F$,8):IFDV=1THENF$=F$+".BAS"
119 OPEN"O",#DV,F$:ATTR0,0:WIDTHW:PRINT"File being saved: "F$:FORQ=S TO S+(250*LU)-250STEP250:LN$="":FORQQ=Q TO Q+LL((Q-S)/250):LN$=LN$+CHR$(LPEEK(QQ)):NEXT:PRINTLN$;:PRINT#DV,LN$:NEXT:GOSUB147:CLOSE#-1:POKE&HFFD9,0:RETURN
120 LINEINPUT"What is the filename? ";F$:POKE&HFFD8,0:F$=LEFT$(F$,8):IFDV=1THENF$=F$+".BAS"
121 FORQ=0TO LU:LL(Q)=0:NEXT:PT=S:OPEN"I",#DV,F$:ATTR0,0:WIDTHW:PRINT"File loading: ";F$
122 POKE&HFFD8,0:IFEOF(DV)=-1THEN127ELSELINEINPUT#DV,LN$:POKE&HFFD9,0
123 IFLN$=""THEN122
124 LN$=LN$+CHR$(13):LL(XX)=LEN(LN$):IFLEN(LN$)=250THEN125ELSELN$=LN$+STRING$(250-LEN(LN$),0)
125 PRINTLN$;:FORQ=1TO250:LPOKEPT,ASC(MID$(LN$,Q,1)):PT=PT+1:NEXT
126 XX=XX+1:IFXX<L THENGOTO122ELSEXX=XX-1:GOTO127
127 LU=XX:POKE&HFFD9,0:EL=LL(XX):RETURN
128 ATTR0,0:WIDTHW:ATTR0,0,U:PRINT:PRINT:LOCATE(W-11)/2,1:PRINT"Cut & Paste":ATTR0,0:PL=1:X=0:Y=3:LOCATEX,Y:PRINTLN$:ST=1:EN=EE:LOCATE(W-30)/2,12:PRINT"Position"PL"Start"ST"End"EN
129 EXEC44539:A$=INKEY$
130 IFA$=CHR$(8)THENPL=PL-1:IFPL<1THENPL=1:PLAYE$:GOTO129ELSEX=X-1:IFX<0THENX=W-1:Y=Y-1:GOTO142ELSE142
131 IFA$=CHR$(9)THENPL=PL+1:IFPL>EE THENPL=EE:PLAYE$:GOTO129ELSEIFPL=251THENPL=250:PLAYE$:GOTO129ELSEX=X+1:IFX>W-1 THENX=0:Y=Y+1:GOTO142ELSE142
132 IFA$=CHR$(10)THENPL=PL+W:IFPL>EE THENPL=PL-W:PLAYE$:GOTO129ELSEIFPL=251THENPL=250:PLAYE$:GOTO129ELSEY=Y+1:IFY>24THENY=24:GOTO142ELSE142
133 IFA$=CHR$(94)THENPL=PL-W:IFPL<0THENPL=PL+W:PLAYE$:GOTO129ELSEY=Y-1:IFY<0THENY=0:GOTO142ELSE142
134 IFA$="S"ANDPL<EN ORA$="s"ANDPL<EN THENPLAY"L25O3C":ST=PL
135 IFA$="E"ANDPL>ST ORA$="e"ANDPL>ST THENPLAY"L25O3C":EN=PL
136 IFA$="M"ANDPL>=EN ORA$="M"ANDPL<ST ORA$="m"ANDPL>=EN ORA$="m"ANDPL<ST THEN138
137 GOTO129
138 PLAY"L25O3C":MV$=MID$(LN$,ST,EN-ST+1):IFPL>=EN THENLT$=LEFT$(LN$,ST-1):MD$=MID$(LN$,EN+1,PL-EN):RT$=MID$(LN$,PL+1,EE-EN):CL$=LT$+MD$+MV$+RT$ ELSELT$=LEFT$(LN$,PL):MD$=MID$(LN$,PL+1,ST-PL-1):RT$=MID$(LN$,EN+1,EE-EN):CL$=LT$+MV$+MD$+RT$
139 LOCATE0,3:PRINTCL$:LOCATE0,12:PRINT:LOCATE(W-14)/2,12:PRINT"Like so? (Y/N)":EXEC44539:A$=INKEY$:IFA$="Y"ORA$="y"THENLN$=CL$ ELSEA$="":LOCATE0,12:PRINT:LOCATE0,3:PRINTLN$:GOTO142
140 LN$=LN$+CHR$(13):IFLEN(LN$)=250THEN141ELSELN$=LN$+STRING$(250-EE,0):LN$=LEFT$(LN$,250)
141 AZ=Q:FORQQ=1TO250:LPOKEAZ,ASC(MID$(LN$,QQ,1)):AZ=AZ+1:NEXT:GOTO104
142 LOCATE(W-30)/2,12:PRINT"Position"PL"Start"ST"End"EN:LOCATEX,Y:PLAYP$:IFPEEK(343)=247THEN130
143 IFPEEK(344)=247THEN131
144 IFPEEK(342)=247THEN132
145 IFPEEK(341)=247THEN133
146 GOTO129
147 IFPEEK(188)=14THENCLOSE#1:RETURNELSERETURN
