5 'HOUSE INVENTORY
10 'WRITTEN BY BOB GRIFFARD
15 'COPYRIGHT JUNE 1991
20 'BY FALSOFT, INC.
25 'RAINBOW MAGAZINE
30 GOTO 1280
35 CLEAR 8000
40 DIM I$(500),L$(500)
45 DIMD(500),C(500):Z$=CHR$(134)
50 T=91:P=.05:H=18
55 DATA LR,DR,DE,KI,UT,B1,B2,CR,SE,N1,N2,N3,HA,GA,AT,ST,PO,YD
60 '----------T=CURRENT YEAR P=ANNUAL INFLATION RATE H=NUMBER OF ROOMS
65 '--------ROOM CODES ARE IN       LINES 1230 & 1240 AND IN        LINES 2070 & 2080
70 '-------HARDCOPY PRINT FORMATTED FOR 4 1/2 INCH ROLL PAPER
95 '---------MAIN MENU
100 CLS:PRINT"        HOUSE INVENTORY":PRINTSTRING$(32,134);
110 PRINT"ITEMS IN INVENTORY= ";N
120 PRINT" DO YOU WANT TO:"
130 PRINT:      PRINT TAB(3)"1> ADD ITEMS TO INVENTORY"
140 PRINTTAB(3)"2> DELETE OR CHANGE AN ITEM"
150 PRINTTAB(3)"3> SEE INVENTORY"
160 PRINTTAB(3)"4> PRINT INVENTORY"
170 PRINTTAB(3)"5> LOAD INVENTORY FROM DISK"
180 PRINTTAB(3)"6> SAVE INVENTORY TO DISK"
190 PRINTTAB(3)"7> QUIT"
200 PRINT:PRINTSTRING$(32,131);:INPUT" INPUT CHOICE (1-7) ";A
210 ON A GOSUB 260,340,640,950,1180,1220,240
220 GOTO100
230 '----------QUIT PROGRAM
240 CLS:PRINT"       QUIT THE PROGRAM":PRINTSTRING$(32,134):INPUT" ARE YOU SURE y/n ";R$:IF R$="Y" THEN CLS:END:ELSE RETURN
250 '----------ADD ITEMS TO LIST
260 CLS:PRINT"       ADD ITEMS TO LIST":PRINTSTRING$(32,131);
270 PRINT" TYPE end TO STOP ENTRIES."
280 N=N+1:IF N=500 THEN GOTO 320
290 PRINTZ$"item"Z$"no"Z$;N:PRINT:INPUT" ROOM LOCATION: ";E$:IF E$="END" THEN 310
300 L$(N)=E$:INPUT" ITEM NAME: ";I$(N):INPUT" YEAR PURCHASED: ";D(N):INPUT" PURCHASE PRICE: $";C(N):GOTO 280
310 N=N-1:RETURN
320 PRINTZ$Z$Z$Z$"sorry"Z$Z$Z$"the"Z$"file"Z$"is"Z$"full"Z$Z$Z$;Z$=CHR$(128):SOUND150,3:FORX=1TO3000:NEXT:GOTO 310
330 '----------DELETE AN ITEM
340 CLS:PRINT"     DELETE OR CHANGE ITEM":PRINTSTRING$(32,131);
350 PRINT:PRINT"YOU MUST KNOW THE ITEM NUMBER   TO EITHER DELETE OR CHANGE AN   ITEM."
360 PRINT:PRINT" DO YOU WISH TO:"
370 PRINT:PRINT"   <1> RETURN TO MASTER MENU" 
380 PRINT:PRINT"   <2> DELETE AN ITEM"
390 PRINT:PRINT"   <3> CHANGE AN ITEM"
400 PRINT:INPUT" INPUT YOUR CHOICE (1-3) ";S:IF S=1 THEN RETURN ELSE IF S=2 THEN 430 ELSE IF S=3 THEN 510
410 IF S=1 THEN RETURN ELSE IF S=2 THEN 430 ELSE IF S=3 THEN 510
420 '----------CHOICE: DELETE AN ITEM
430 CLS:PRINT"        DELETE AN ITEM":PRINTSTRING$(32,131):PRINT" ITEM NUMBERS HIGHER THAN THE    DELETED ITEM CHANGE WHEN THE    ITEM IS DELETED."
440 PRINT:PRINT" DELETE HIGHEST NUMBERED ITEM    FIRST."
450 PRINT:INPUT" ITEM NUMBER TO BE DELETED";K:IF K>N THEN PRINT"INVALID ITEM NUMBER":FOR TT=1 TO 1200:NEXT:GOTO470
460 B=K:PRINT" DELETED ITEM, ";I$(B)",":PRINT" WAS LOCATED IN ";L$(B):FOR B=K TO N:I$(B)=I$(B+1):L$(B)=L$(B+1):D(B)=D(B+1):C(B)=C(B+1):NEXT B:N=N-1
470 INPUT" TYPE <1> TO DELETE ANOTHER ITEM,     <2> TO RETURN TO MENU";J:IF J=1 THEN 430
480 IF J=2 THEN RETURN
490 IF J<>1 OR J<>2 THEN 470
500 '---------CHANGE AN ITEM
510 CLS:PRINT"        CHANGE AN ITEM":PRINTSTRING$(32,131);:INPUT" ITEM NUMBER TO BE CHANGED";X:IF X>N THEN PRINT"INVALID ITEM NUMBER":FOR TT=1 TO 1200:NEXT:RETURN
520 B=X:PRINTB;L$(B);I$(B);D(B);C(B):PRINT" DO YOU WISH TO CHANGE":PRINT:PRINT"   <L> LOCATION                    <I> ITEM NAME                   <Y> YEAR OF PURCHASE            <K> COST":PRINT:INPUT" YOUR CHOICE IS: ";X$
530 IFX$="L"THEN 580
540 IF X$="I" THEN 590
550 IF X$="Y" THEN 600
560 IF X$="K" THEN 610
570 IF X$<>"L"ORX$<>"I"OR X$<>"Y"OR X$<>"K"THEN RETURN
580 INPUT" NEW LOCATION: ";C$:L$(B)=C$:GOTO620
590 INPUT" NEW ITEM NAME: ";C$:I$(B)=C$:GOTO620
600 INPUT" NEW YEAR OF PURCHASE: ";G:D(B)=G:GOTO620
610 INPUT" NEW COST OF ITEM: ";G:C(B)=G
620 PRINT" PRESS <1> TO CHANGE ANOTHER     ITEM":PRINT" PRESS <2> TO RETURN TO MENU":INPUT S:IF S=1 THEN 510 ELSE RETURN
630 '----------VIEW THE FILE
640 CLS:PRINT"         VIEW THE FILE":PRINT STRING$(32,131);:PRINT:PRINT" DO YOU WANT TO SEE:":PRINT:PRINT"    <E> THE FILE AS ENTERED         <L> FILE BY ROOM LOCATION":PRINT:INPUT J$:IF J$="L" THEN GOTO 760
650 '----------PRINT OF FILE AS LISTED
660 CLS:PRINTZ$"inventory"Z$"listing"Z$
670 PRINT"ROOM";TAB(8)"ITEM";TAB(18)"YR";TAB(22)"COST";TAB(27)"REPL"
680 FOR B=1 TO N:GOSUB1260
690 PRINT L$(B);TAB(4)I$(B);TAB(17)D(B);TAB(20)C(B);TAB(24)R
700 IF B/12=INT(B/12) THEN 710 ELSE750
710 PRINT@480,"    cONTINUE    mENU  ";
720 R$=INKEY$:IF R$=""THEN720
730 IF R$="M"THEN RETURN 
740 CLS
750 NEXT B:PRINTZ$"end"Z$"of"Z$"file"Z$:INPUT" PRESS enter TO RETURN TO MENU    ";R$:RETURN
760 CLS:PRINT"      INVENTORY BY ROOM":PRINTSTRING$(32,131):PRINT" <1> ALL ROOMS":PRINT:PRINT" <2> A SPECIFIC ROOM":INPUT Z:IF Z=1 THEN 800
770 CLS:PRINT"        SELECT THE ROOM":PRINTSTRING$(32,131);:PRINT:PRINT"YOU MUST USE 2 LETTER ROOM CODE"
780 PRINTSTRING$(32,140);: PRINT"LR-LIVING RM","CR-COMPUTER RM","DR-DINING RM","SE-SEWING RM","DE-DEN","N1-MASTER BATH","KI-KITCHEN","N2-HALL BATH","UT-UTILITY RM","N3-HALF BATH","B1-BEDROOM 1","HA-HALLWAYS"
790 PRINT"B2-BEDROOM 2","GA-GARAGE","AT-ATTIC","ST-STORAGE","PO-PORCH","YD-YARD":PRINTSTRING$(32,131);:INPUTR$:F$=R$
800 TC=0:CR=0:RC=0:TR=0:IF Z=2 THEN 820
810 RESTORE:FOR E=1 TO H:READ F$
820 V=0:GOSUB830:IF Z=2 THEN RETURN ELSE NEXT E:GOTO930
830 FOR B=1 TO N:IF L$(B)<>F$ THEN 890
840 GOSUB1260:PRINT B;TAB(5)L$(B);TAB(9)I$(B);TAB(22)D(B);TAB(26)C(B):V=V+1:CR=CR+C(B):TC=TC+C(B):RC=RC+R:TR=TR+R:IF V>=12 THEN 850 ELSE 890
850 V=0:PRINT@480,"   cONTINUE      mENU";
860 R$=INKEY$:IF R$=""THEN 860
870 IF R$="M"THEN Z=2 :RETURN
880 CLS
890 NEXT B:IF CR=0 THEN 920
900 PRINT" TOTAL COST FOR THIS ROOM WAS     $";CR:PRINT" REPLACEMENT COST FOR THIS ROOM   IS  $";RC:PRINT@480,"  cONTINUE     mENU";:INPUT R$:IF R$="M"THEN Z=2
910 CLS:CR=0:RC=0
920 RETURN
930 PRINT" TOTAL FOR ALL ROOMS WAS         $";TC:PRINT" REPLACEMENT COST ALL ROOMS IS   $";TR:PRINT:INPUT" PRESS enter TO RETURN TO MENU";R$:RETURN
940 '----------PRINT THE INVENTORY LISTING
950 CLS:PRINT:PRINT" DO YOU WANT:":PRINT:PRINT"   <1> ALL ROOMS":PRINT:PRINT"   <2> A SPECIFIC ROOM":INPUT Z:IF Z=1 THEN 970
960 PRINT "WHICH ROOM ?-NOTE YOU MUST USE  TWO LETTER ROOM CODE":INPUT R$:F$=R$:GOTO1060
970 PRINT:INPUT" PRESS enter WHEN READY ";R$
980 INPUT" INPUT MONTH/YEAR OF LISTING ";M$
990 PRINT #-2," HOUSEHOLD INVENTORY AS OF       ";M$
1000 PRINT#-2," REPLACEMENT COST COMPUTED AT    ";P*100;"% 
1010 PRINT#-2," PER YEAR FROM YEAR OF PURCHASE."
1020 PRINT#-2
1030 RESTORE
1040 TC=0:CR=0:RC=0:TR=0
1050 FOR E=1 TO H:READ F$
1060 PRINT#-2,"RM";TAB(10)"ITEM";TAB(23)"YR";TAB(27)"COST";TAB(33)"REPLACE"
1070 GOSUB1080:IF Z=2 THEN 1160 ELSE NEXT E:GOTO1130
1080 FOR B=1 TO N:IF L$(B)<>F$ THEN 1100
1090 GOSUB1260:PRINT#-2,L$(B);TAB(5)I$(B);TAB(22)D(B);TAB(26);:PRINT#-2,USING"$####";C(B);:PRINT#-2,TAB(34);:PRINT#-2,USING"$####";R:CR=CR+C(B):TC=TC+C(B):RC=RC+R:TR=TR+R
1100 NEXT B:IF CR=0 THEN 1120
1110 PRINT#-2:PRINT#-2," TOTAL COST FOR THIS ROOM WAS    $";:PRINT#-2, USING"$#####.##";CR:PRINT#-2," REPLACEMENT COST THIS ROOM IS   $";:PRINT#-2,USING"$#####.##";RC:CR=0:RC=0:PRINT#-2
1120 RETURN
1130 PRINT#-2:PRINT#-2," TOTAL FOR ALL ROOMS WAS         $";:PRINT#-2, USING"$#####.##";TC:PRINT#-2," REPLACEMENT COST ALL ROOMS IS   $";:PRINT#-2,USING"$#####.##";TR
1140 PRINT#-2:PRINT#-2," LR=LIVING ROOM,  DR=DINING ROOM,  DE=DEN":PRINT#-2," KI=KITCHEN,  UT=UTILITY" :PRINT#-2," B1=MASTER BEDROOM,   B2=GUEST BEDROOM":PRINT#-2, " SE=SEWING ROOM,  N1=MASTER BATH":PRINT#-2," N2=HALL BATH,  N3=1/2 BATH,  HA=HALL"
1150 PRINT#-2," GA=GARAGE,  AT=ATTIC,  ST=STORAGE":PRINT#-2," PO=PORCH,  YD=YARD  CR=COMPUTER RM"
1160 PRINT :INPUT" PRESS enter TO RETURN TO MENU";R$:RETURN
1170 '----------LOAD DATA FROM DISK
1180 CLS:PRINT"      LOAD DATA FROM DISK":PRINTSTRING$(32,131);:PRINT:INPUT" WHAT IS DATA SOURCE DRIVE       NUMBER: ";DN:DRIVE(DN):PRINT:INPUT" PRESS enter WHEN READY ";R$:OPEN"I",#1,"HOUSEINV/DAT":IF EOF (1)=-1 THEN 1200
1190 INPUT #1,N:FOR B=1 TO N:CLS:PRINT@233,Z$"loading"Z$"data"Z$:LINEINPUT#1,I$(B):LINEINPUT#1,L$(B):INPUT#1,D(B):INPUT#1,C(B):NEXT B
1200 CLOSE#1:RETURN
1210 '----------SAVE DATA TO DISK
1220 CLS:PRINT"       SAVE FILE ON DISK":PRINTSTRING$(32,131);:PRINT" WHAT IS DESTINATION DRIVE";:INPUT DN:DRIVE(DN):PRINT:PRINT" DATA WILL BE SAVED AS           -HOUSEINV/DAT-":PRINT:INPUT" PRESS enter WHEN READY";R$
1230 OPEN "O",#1,"HOUSEINV/DAT":PRINT#1,N
1240 FOR B=1 TO N:CLS:PRINT@235,"saving data"
1250 PRINT#1,I$(B):PRINT#1,L$(B):PRINT#1,D(B):PRINT#1,C(B):NEXT B:CLOSE#1:CLS:PRINT" DATA HAS BEEN SAVED TO DISK":PRINT:INPUT" PRESS enter FOR MENU";R$:RETURN
1260 I=(T-D(B))*P:R=INT((I*C(B))+C(B)):RETURN
1270 END
1280 POKE182,0:POKE183,PEEK(188):POKE184,0:POKE185,16:POKE186,PEEK(188):POKE187,0:POKE188,PEEK(188)-6:PCLEAR1:POKE183,PEEK(183)+6:POKE188,PEEK(188)+6:GOTO35
