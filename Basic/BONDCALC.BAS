10 'BONDCALC
20 'WRITTEN BY DALE TINKLEPAUGH
30 'COPYRIGHT FEBRUARY 1991
40 'BY FALSOFT, INC.
50 'RAINBOW MAGAZINE
105 CLEAR 200,16127  'SAVE SPACE FOR TEXT DUMP CODE
107 POKE 150,18  'SET PRINTER TO 2400
108 ' TEXT DUMP ROUTINE
110 DATA &H86,&HFE,&HB7,0,&H6F,&H8E,4,0,&H5F,&H5C,&HA6,&H80,&H81,&H60
115 DATA &H2B,2,&H84,&HBF,&HAD,&H9F,&HA0,2,&HC1,&H20,&H2B,&HEF,&H86,&H0D
120 DATA &HAD,&H9F,&HA0,2,&H8C,6,0,&H2B,&HE3,&H7F,0,&H6F,&H39
125 FOR I=16128 TO 16128+40
130 READ A:POKE I,A
135 NEXT I
140 DEFUSR0=&H3F00
202 F2$="#####":F3$="####.###":BL$="                               "
205 F1$="$$#######.##":P$="PRESENT VALUE IS"
207 'MAIN BODY
210 CLS
230 PRINT@11,"BONDCALC"
235 PRINT@40,"COPYRIGHT 1988"
240 PRINT@70,"BY DALE TINKLEPAUGH"
250 PRINT@131,"SELECT CONVERSION RESULT"
260 PRINT@194,"1 CURRENT YIELD"
270 PRINT@226,"2 YIELD TO MATURITY"
280 PRINT@258,"3 PRICE FOR A CURRENT YIELD""
290 PRINT@290,"4 PRICE FOR YIELD TO MATURITY""
295 PRINT@322,"5 EXIT PROGRAM"
300 PRINT@385,"PRESS A NUMBER BETWEEN 1 AND 5"
310 A$=INKEY$:IF A$=""GOTO 310
320 CASE=INSTR(1,"12345",A$)
330 ON CASE GOSUB 5000,6000,7000,8000,500
340 GOTO 210
500 STOP
600 'END OF MAIN BODY
1140 'CHECK TO SEE IF PRINTER IS ON LINE
1150 PR=PEEK(65314):IF INT(PR/2)=PR/2 THEN QQ=1 ELSE QQ=0
1160 RETURN
5000 'CURRENT YIELD
5010 CLS:PRINT@98,"CURRENT YIELD"
5020 PRINT" ":INPUT"ENTER COST OF BOND";CPSH
5030 INPUT"*INTEREST RATE (%)";CUPN:IF CUPN<0 GOSUB 9000 'TO SCRATCHPAD
5032 IF CUPN<0 THEN PRINT@192,BL$;
5035 IF CUPN<0 THEN CUPN=POP*100:PRINT@192,"INTEREST RATE= ";CUPN;" %"
5040 INPUT"NUMBER OF BONDS";NSHRS
5050 INPUT"PAR VALUE";PAR
5060 INPUT"*COMMISSION";COMM:IF COMM<0 GOSUB 9000
5062 IF COMM<0 THEN PRINT@288,BL$;
5065 IF COMM<0 THEN COMM=POP:PRINT@288,"COMMISSION= ";COMM
5070 PUPR=CPSH*NSHRS+COMM
5080 DIV=CUPN*PAR*NSHRS/100
5090 CRYLD=DIV/PUPR
5100 PRINT"CURRENT YIELD IS";
5110 PRINTUSING F3$; CRYLD*100;:PRINT" %"
5120 PRINT"DO ANOTHER? (Y OR N)"
5135 GOSUB 1150
5140 IF QQ=1 THEN PRINT" (PRESS P TO PRINT OUT SCREEN)";
5150 GOSUB 1150:A$=INKEY$:IF A$="" THEN GOTO 5150
5160 IF A$="P" AND QQ=1 THEN A=USR0(0)
5170 IF A$="Y" THEN GOTO 5010 ELSE IFA$="N" THEN RETURN ELSE GOTO 5150
5180 RETURN
6000 ' YIELD TO MATURITY
6010 ' FIRST GET INPUT DATA AND
6015 ' FIGURE APPROXIMATE SOLUTION
6017 ' BY FORMULA
6030 CLS:PRINT@98,"YIELD TO MATURITY"
6040 PRINT" ":INPUT"ENTER COST OF BOND";CPSH
6045 INPUT"COMPOUNDING PERIODS/YEAR";NP
6050 INPUT"*INTEREST RATE (%)";CUPN:IF CUPN<0 GOSUB 9000
6052 IF CUPN<0 THEN PRINT@224,BL$;
6055 IF CUPN<0 THEN CUPN=POP*100:PRINT@224,"INTEREST RATE= ";CUPN;" %"
6060 INPUT"NUMBER OF BONDS";NSHRS
6070 INPUT"PAR VALUE";PAR
6080 INPUT"YEARS TO MATURITY";YRS
6090 INPUT"*COMMISSION";COMM:IF COMM<0 GOSUB 9000
6092 IF COMM<0 THEN PRINT@352,BL$;
6095 IF COMM<0 THEN COMM=POP:PRINT@352,"COMMISSION= ";COMM
6100 DIV=CUPN*PAR*NSHRS/100/NP
6110 API=(PAR*NSHRS+NSHRS*CPSH+COMM)/2
6115 'PREMIUM OR DISCOUNT
6120 PORD=CPSH*NSHRS+COMM-PAR*NSHRS
6130 AYTM=(NP*DIV-PORD/YRS)/API:GOSUB 6400
6140 PRINT"YIELD TO MATURITY"
6150 PRINTUSING F3$;AYTM*100;:PRINT" %"
6155 PRINT"DO ANOTHER? (Y OR N)"
6157 GOSUB 1150
6160 IF QQ=1 THEN PRINT" (PRESS P TO PRINT OUT SCREEN)";
6170 GOSUB 1150:A$=INKEY$:IF A$="" THEN GOTO 6170
6175 IF A$="P" AND QQ=1 THEN A=USR0(0)
6180 IF A$="Y" THEN GOTO 6030 ELSE IF A$="N" THEN RETURN ELSE GOTO 6170
6190 RETURN
6400 'PLUG APPROXIMATION INTO FORMULA & ITERATE UNTIL WITHIN $.005
6405 RVAL=PAR*NSHRS:INC=.01:OLDIR=0
6410 FP=(1+AYTM/NP)^(YRS*NP)
6420 FA=(((1+AYTM/NP)^(YRS*NP))-1)/(AYTM/NP)
6430 FVAL=(CPSH*NSHRS+COMM)*FP-FA*DIV
6435 'PRINT"INC= ";INC;" DR= ";DR,"AYTM= ";AYTM;" FVAL= ";FVAL;" RVAL= ";RVAL:STOP  'FOR DEBUGGING
6440 IF FVAL>RVAL GOTO 6600
6450 IF FVAL<RVAL GOTO 6800
6460 RETURN '= SO DONE
6600 'APPROXIMATION TOO HIGH
6602 IF ABS(FVAL-RVAL)<.005 THEN RETURN
6605 UB=AYTM:DR=-1
6620 IF(DR<>OLDIR)AND (OLDIR<>0)THEN INC=INC/10:IF ABS(INC)<.000001 THEN RETURN  'CLOSE ENOUGH
6625 AYTM=AYTM-INC
6630 OLDIR=DR:GOTO 6410
6800 'APPROX TOO LOW
6810 IF ABS(FVAL-RVAL)<.005 THEN RETURN
6815 LB=AYTM:DR=1
6830 IF(DR<>OLDIR)AND(OLDIR<>0) THEN INC=INC/10:IF ABS(INC)<.000001 THEN RETURN
6835 AYTM=AYTM+INC
6840 OLDIR=DR:GOTO 6410
7000 'PRICE FOR A CURRENT YIELD
7010 CLS:PRINT@98,"PRICE FOR A CURRENT YIELD"
7020 PRINT" ":INPUT"DESIRED CURRENT YIELD (%)";C:CRYLD=C/100
7030 INPUT"*INTEREST RATE (%)";CUPN:IF CUPN<0 GOSUB 9000
7032 IF CUPN<0 THEN PRINT@192,BL$
7035 IF CUPN<0 THEN CUPN=POP*100:PRINT@192,"INTEREST RATE= ";CUPN;" %"
7040 INPUT"NUMBER OF BONDS";NSHRS
7050 INPUT"PAR VALUE";PAR
7060 INPUT"*COMMISSION";COMM:IF COMM<0 GOSUB 9000
7062 IF COMM<0 THEN PRINT@288,BL$;
7065 IF COMM<0 THEN PRINT@288,"COMMISSION= ";COMM
7070 DIV=CUPN*PAR*NSHRS/100
7080 CPSH=(DIV/CRYLD-COMM)/NSHRS
7090 PRINT"PRICE PER SHARE IS"
7100 PRINTUSING F1$;CPSH
7120 PRINT"DO ANOTHER? (Y OR N)"
7125 GOSUB 1150
7130 IF QQ=1 THEN PRINT" (PRESS P TO PRINT OUT SCREEN)";
7140 GOSUB 1150:A$=INKEY$:IF A$="" THEN GOTO 7140
7150 IF A$="P" AND QQ=1 THEN A=USR0(0)
7160 IF A$="Y" THEN GOTO 7010 ELSE IF A$="N" THEN RETURN ELSE GOTO 7140
7170 RETURN
8000 'PRICE FOR A YIELD TO MATURITY
8010 CLS:PRINT@98,"PRICE FOR YIELD TO MATURITY"
8015 PRINT" ":INPUT"COMPOUNDING PERIODS/YEAR";NP
8020 INPUT"YIELD TO MATURITY (%)";Y:YTM=Y/100/NP
8030 INPUT"*INTEREST RATE (%)";CUPN:IF CUPN<0 GOSUB 9000
8032 IF CUPN<0 THEN PRINT@224,BL$;
8035 IF CUPN<0 THEN CUPN=POP*100:PRINT@224,"INTEREST RATE= ";CUPN;" %"
8040 INPUT"NUMBER OF BONDS";NSHRS
8050 INPUT"PAR VALUE";PAR
8060 INPUT"YEARS TO MATURITY";YRS
8070 INPUT"*COMMISSION";COMM:IF COMM<0 GOSUB 9000
8072 IF COMM<0 THEN PRINT@352,BL$;
8075 IF COMM<0 THEN COMM=POP:PRINT@352,"COMMISSION= ";COMM
8080 DIV=CUPN*PAR*NSHRS/100/NP
8090 FP=(1+YTM)^(YRS*NP) 'FUTURE VALUE OF PRESENT SUM FACTOR
8100 FA=(((1+YTM)^(YRS*NP))-1)/YTM  'FUTURE VALUE OF UNIFORM SERIES FACTOR
8110 CPSH=(((PAR*NSHRS+FA*DIV)/FP)-COMM)/NSHRS
8115 PRINT"PRICE/BOND"
8120 PRINTUSING F1$;CPSH
8130 PRINT"DO ANOTHER? (Y OR N)"
8135 GOSUB 1150
8140 IF QQ=1 THEN PRINT"(PRESS P TO PRINT OUT SCREEN)";
8150 GOSUB 1150:A$=INKEY$:IF A$=" " THEN GOTO 8150
8155 IF A$="P" AND QQ=1 THEN A=USR0(0)
8160 IF A$="Y" THEN GOTO 8010 ELSE IF A$="N" THEN RETURN ELSE GOTO 8150
8170 RETURN
9000 'SCRATCH PAD
9010 PRINT@384,"SCRATCH PAD INVOKED"
9015 PRINT@416,"";:INPUT"ENTER NUMBER  ";POP
9017 PRINT@448,BL$;
9020 PRINT@448,"ENTER OPERATION (+-*/R)";
9030 B$=INKEY$:IF B$="" GOTO 9030
9040 CASE=INSTR(1,"+-*/R",B$):IF CASE=0 GOTO 9030
9045 IF CASE=5 THEN PRINT@384,BL$;:PRINT@416,BL$;:PRINT@448,BL$;:PRINT@480,BL$;: RETURN
9055 PRINT@448,BL$;:PRINT@448,B$;" ";:INPUT"ENTER NUMBER";NUM
9060 ON CASE GOTO 9100,9200,9300,9400,9500
9100 POP=POP+NUM:PRINT@416,BL$;:PRINT@431,POP;:GOTO 9020
9200 POP=POP-NUM:PRINT@416,BL$;:PRINT@431,POP;:GOTO 9020
9300 POP=POP*NUM:PRINT@416,BL$;:PRINT@431,POP;:GOTO 9020
9400 POP=POP/NUM:PRINT@416,BL$;:PRINT@431,POP;:GOTO 9020
9500 RETURN
