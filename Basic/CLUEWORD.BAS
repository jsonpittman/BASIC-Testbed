1 '**********************
3 '*BY JAMES PH LAMONICA*
4 '*& MARY JEAN LAMONICA*
5 '*   EL PASO, TEXAS   *
6 '*      (C) 1987      *
7 '**********************
10 PMODE0:PCLEAR1:CLEAR12000:DIMD$(5,300),PN$(6),PS(6),DU(300)
20 V$="WELCOME TO CLUEWORD. YOU WILL   BE GIVEN 4 WORDS, ONE AT A TIME DETERMIN WHAT THE WORDS HAVE IN COMMON AND TYPE IN THE ANSWER.  THE FEWER CLUEWORDS YOU NEED,   THE MORE POINTS YOU WILL GET."
21 CLS(RND(6)):PRINT@64,V$
25 V$="PRESS A KEY TO BEGIN":GOSUB500:PRINT@448,V$:EXEC44539:PRINT@448,"LOADING clueword FILE FROM DISK"
30 OPEN"I",#1,"CLUEDATA"
40 FORX=1TO300
50 IFEOF(1)=-1THEN70
60 FORY=1TO5:INPUT#1,D$(Y,X):DS=X:NEXTY
65 NEXTX
70 CLOSE#1
80 CLS(RND(6)):V$="HOW MANY PLAYERS (1 TO 6)":GOSUB500:PRINT@64,V$;:INPUTNP
90 V$="ENTER PLAYERS BELOW":GOSUB500:FORX=1TONP:PRINT@128,"NAME OF PLAYER";X;:INPUTPN$(X):PRINT@128,"":NEXTX
95 FOR GL=1TOINT(DS/NP)
100 FORG=1TONP:CLS(RND(6)):FORX=1TONP:PRINT@17+(X*16),PN$(X);PS(X);:NEXTX:PRINT@16+(G*16),CHR$(62);
110 Q=RND(DS):IF DU(Q)=Q THEN110
111 PA=272
115 FOR L=1TO4
120 DU(Q)=Q:PRINT@PA-INT(.5*LEN(D$(L,Q))),D$(L,Q);:V$=D$(L,Q):GOSUB500
130 S=INT(2^(5-L))
140 PRINT@448,"":PRINT@448,"FOR ";S;"POINTS";:INPUTA$
145 IF A$="Q"THENGOTO300
150 IF A$=D$(5,Q) THEN PS(G)=PS(G)+S:GOSUB400:GOTO220
190 PA=PA+32
200 NEXT L
210 GOSUB460
220 NEXTG:NEXTGL
300 CLS:V$="THE GAME IS OVER AND THESE ARE THE FINAL SCORES":GOSUB500
310 FOR X=1TONP:PRINT@(X*32),PN$(X);PS(X)
320 NEXTX
399 END
400 V$="YOU ARE CORRECT"+PN$(G):GOSUB500
405 V$="PRESS A KEY FOR THE NEXT SET":GOSUB500
410 EXEC44539:RETURN
460 V$="THE CORRECT ANSWER WAS "+D$(5,Q):GOSUB500
465 PRINT@448,"":PRINT@464-INT(.5*LEN(D$(5,Q))),D$(5,Q)
470 V$="PRESS A KEY FOR THE NEXT SET":GOSUB500
480 EXEC44539:RETURN
500 XX=&HFF00:YY=&HFF7E:POKEXX+1,52:POKEXX+3,63:POKEXX+35,60
501 FOR I=1TOLEN(V$)
502 IFPEEK(YY)AND128=0THEN502
503 POKEYY,ASC(MID$(V$,I,1))
504 NEXTI
505 IFPEEK(YY)AND128=0THEN505
506 POKE YY,13
507 RETURN
