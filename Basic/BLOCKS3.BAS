10 'BUILDING BLOCKS 3 (C)1988       FROM BILL BERNICO SOFTWARE                                      GRAPHICS SAVE/LOAD ROUTINE      COURTESY OF LARRY BOELDT                                        LOADS FILES WITH /BB3 EXT       
20 RGB:A$="U4R4D4L4":H=0:V=4:HSCREEN2:HCLS4:HCOLOR8,4:ONBRKGOTO330
30 PALETTE7,52:PALETTE9,56:PALETTE10,32:PALETTE11,20:PALETTE12,40:PALETTE13,12:PALETTE14,60:PALETTE15,48:POKE&HE6,2:POKE&HE6C6,18:POKE&HE6C7,18
40 GOSUB480:GOSUB490
70 HDRAW"BM=H;,=V;C4"+A$:HDRAW"BM=H;,=V;C8"+A$
80 I$=INKEY$
90 IF I$=CHR$(8)THEN H=H-4
100 IF H<0 THEN H=312
110 IF I$=CHR$(9)THEN H=H+4
120 IF H>312 THEN H=0
130 IF I$=CHR$(10)THEN V=V+4
140 IF V>156 THEN V=4
150 IF I$=CHR$(94)THEN V=V-4
160 IF V<4 THEN V=156
170 IF I$="Z"THEN HPAINT(H+2,V-2),10,8
180 IF I$="X"THEN HPAINT(H+2,V-2),11,8
190 IF I$="C"THEN HPAINT(H+2,V-2),12,8
200 IF I$="V"THEN HPAINT(H+2,V-2),13,8
210 IF I$="B"THEN HPAINT(H+2,V-2),14,8
220 IF I$="N"THEN HPAINT(H+2,V-2),15,8
230 IFI$>"/"AND I$<":"THEN P=VAL(I$):HPAINT(H+2,V-2),P,8
240 IF I$="E"THEN 320
250 IF I$="R"THEN HCOLOR8:HPRINT(23,23),"Hit R to restart":HCOLOR9:HPRINT(23,23),"RESTART?? (Y/N)":FORX=1TO9:SOUND191,1:NEXT
260 IF I$="R"THENX$=INKEY$:IFX$="Y"THEN20ELSEIFX$="N"THENHCOLOR8:HPRINT(23,23),"RESTART?? (Y/N)":GOTO60ELSE260
270 IF I$="Q"THEN HCOLOR8:HPRINT(23,21),"Hit Q to quit":HCOLOR9:HPRINT(23,21),"QUIT?? (Y/N)":FORX=1TO9:SOUND191,1:NEXT
280 IF I$="Q"THENX$=INKEY$:IFX$="Y"THEN330ELSEIFX$="N"THENHCOLOR8:HPRINT(23,21),"QUIT?? (Y/N)":GOTO60ELSE280
290 IF I$="S"THEN WIDTH40:GOTO340
300 IF I$="L"THEN WIDTH40:GOTO420
310 GOTO 70
320 HLINE(H,V)-(H+4,V-4),PRESET,BF:HDRAW"BM=H;,=V;C4"+A$:HDRAW"BM=H;,=V;C8"+A$:GOTO 80
330 POKE65496,0:WIDTH32:CLS:RGB:END
340 INPUT "NAME";N$
350 HSCREEN2
360 POKE65497,0
370 PT=3584:FOR Y=2 TO 158 STEP 4
380 FORX=2 TO 318 STEP 4
390 Z=HPOINT(X,Y):POKE PT,Z:PT=PT+1
400 NEXTX,Y:POKE65496,0:SAVEM N$+".BB3",3584,6784,44537
410 GOTO 80
420 INPUT"NAME";N$:LOADM N$+".BB3":PT=3584
430 HSCREEN2:POKE65497,0:HLINE(0,0)-(315,156),PRESET,BF:GOSUB480
440 FOR Y=2 TO 158 STEP 4
450 FOR X=2 TO 318 STEP 4
460 HPAINT(X,Y),PEEK(PT),8:PT=PT+1:NEXT X,Y:POKE65496,0
470 GOTO80
480 FORX=0TO156STEP4:HLINE(0,X)-(315,X),PSET:NEXTX:FORX=0TO316STEP4:HLINE(X,0)-(X,156),PSET:NEXTX:RETURN
490 HPAINT(318,1),8,8:HCOLOR0:HPRINT(0,20),"Hit L to load":HCOLOR6:HPRINT(23,20),"Hit S to save":HCOLOR2:HPRINT(0,21),"Move with arrow keys":HCOLOR1:HPRINT(0,22),"Paint with keys (0-8)":HPRINT(0,23),"OR row one (keys Z-N)"
500 HCOLOR3:HPRINT(23,21),"Hit Q to quit":HCOLOR4:HPRINT(23,22),"Hit E to erase":HCOLOR5:HPRINT(23,23),"Hit R to restart":RETURN
