1 REM***************************
2 REM*   REALTIME CALCULATOR   *
3 REM*   COPYRIGHT (C) 1989    *
4 REM*    BY FRED B. SCERBO    *
5 REM*    60 HARDING AVENUE    *
6 REM*  NORTH ADAMS, MA 01247  *
7 REM***************************
8 CLEAR1000
9 CLS0
10 PRINTSTRING$(32,188)STRING$(32,204);
15 FORI=1TO 160 :READ A:PRINTCHR$(A+128);:NEXT
20 PRINTSTRING$(32,195)STRING$(32,179);
25 PRINT@358," REALTIME CALCULATOR ";
30 PRINT@390,"  BY FRED B. SCERBO  ";
35 PRINT@422," COPYRIGHT (C) 1989  ";
40 DATA94,92,94,92,90,93,88,93,92,93,92,93,88,93,92,92,90,21,28,28,26,30,28,29,20,30,,,21,28,28,29
45 DATA88,80,90,,88,85,,85,,85,,85,,85,,80,82,21,,,,26,,21,,26,,,21,,,
50 DATA,,90,,,85,,85,,85,,85,,85,92,92,90,21,,,,30,28,29,,26,,,21,,,
55 DATA,,90,,,85,,85,,85,,85,,85,,,80,21,,,,26,,21,,26,,,21,,,
60 DATA,81,91,,,87,82,87,82,,,87,82,87,83,83,90,21,19,19,26,27,16,23,17,27,19,23,21,19,19,23
65 MM=1
70 IFINKEY$<>CHR$(13)THEN70
75 A$(4)="00:00"
80 A$(1)="00:00":A$(2)="00:00":A$(3)="00:00"
85 CLS:GOSUB90:GOTO165
90 PRINT@0,"";:PRINT:PRINT
95 PRINTTAB(5)"TIME ADDING CALCULATOR"
100 PRINT
105 IF MM=2THENPRINTTAB(5)"LAST ENTRY       "A$(1):GOTO115
110 PRINTTAB(5)"FIRST ENTRY:     "A$(1)
115 PRINT
120 IFMM=2THENPRINTTAB(5)"NEXT ENTRY:      "A$(2):GOTO130
125 PRINTTAB(5)"SECOND ENTRY:    "A$(2)
130 PRINT
135 PRINTTAB(5)"TIME ELAPSED:    "A$(3)
140 PRINT
145 PRINT
150 PRINTTAB(5)"PRESS SPACEBAR FOR MORE"
155 PRINTTAB(5)"PRESS CLEAR FOR RESTART"
160 RETURN
165 REM START CALCULATIONS
170 FORQ= MM TO2
175 FORI=1TO4
180 X$=INKEY$:IFX$=""THEN180
185 IFX$=CHR$(13)THEN240
190 IFX$=" "THEN80
195 IFX$=CHR$(12)THENZ$="":MM=1:GOTO75
200 X=ASC(X$)-48:IFX<0THEN180
205 IFX>9THEN180
210 Z$=Z$+X$
215 IFI=1THENA$(Q)="00:0"+Z$
220 IFI=2THENA$(Q)="00:"+Z$
225 IFI=3THENA$(Q)="0"+LEFT$(Z$,1)+":"+RIGHT$(Z$,2)
230 IFI=4THENA$(Q)=LEFT$(Z$,2)+":"+RIGHT$(Z$,2)
235 GOSUB90:NEXTI
240 Z$=""
245 CH=VAL(MID$(A$(Q),4,1)):IF CH=>6THEN Z$="":A$(Q)="00:00":GOSUB90:GOTO175
250 NEXTQ
255 A(2,2)=VAL(LEFT$(A$(2),2)):A(2,1)=VAL(RIGHT$(A$(2),2))
260 A(1,2)=VAL(LEFT$(A$(1),2)):A(1,1)=VAL(RIGHT$(A$(1),2))
265 A(3,1)=A(1,1)+A(2,1):IF A(3,1)=>60 THEN A(1,2)=A(1,2)+1:A(3,1)=A(3,1)-60
270 A(3,2)=A(1,2)+A(2,2)
275 C$=STR$(A(3,2)):IF LEN(C$)=2 THEN C$="0"+RIGHT$(C$,1) ELSE C$=RIGHT$(C$,2)
280 D$=STR$(A(3,1)):IF LEN(D$)=2 THEN D$="0"+RIGHT$(D$,1) ELSE D$=RIGHT$(D$,2)
285 A$(3)=C$+":"+D$
290 GOSUB90
295 C(1)=VAL(C$):D(1)=VAL(D$)
300 C(2)=VAL(LEFT$(A$(4),2)):D(2)=VAL(RIGHT$(A$(4),2))
305 D(3)=D(1)+D(2):IF D(3)=>60 THEN D(3)=D(3)-60:C(1)=C(1)+1
310 C(3)=C(1)+C(2)
315 C$=STR$(C(3)):IFLEN(C$)=2 THEN C$="0"+RIGHT$(C$,1) ELSE C$=RIGHT$(C$,2)
320 D$=STR$(D(3)):IFLEN(D$)=2 THEN D$="0"+RIGHT$(D$,1) ELSE D$=RIGHT$(D$,2)
325 A$(4)=C$+":"+D$
330 GOSUB90
335 X$=INKEY$:IFX$=""THEN335
340 IFX$=" "THEN355
345 IFX$=CHR$(12)THEN MM=1:GOTO75
350 GOTO335
355 A$(1)=A$(3):MM=2:A$(2)="00:00":A$(3)="00:00":GOTO85
