0 'COPYRIGHT 1989, FALSOFT INC.
10 '===========================
15 '  C O N C E N T R A T E !
20 '
25 '  Written By Eric A. Wolf
30 '    Copyright (C) 1989
35 '===========================
40 POKE65497,0:PCLEAR1:WIDTH32
45 CLEAR1000:DIM B(10,5),C(16)
50 HSCREEN2:HSCREEN0
55 POKE&HA27E,0
60 '
65 ' Set monitor type HERE!
70 '
75 MO=1  '(1=RGB and 0=CMP/TV)
80 '
85 ' Display Title Screen
90 '
95 A1$="":A2$="":FORZ=1TO8:A1$=A1$+CHR$(159)+CHR$(159)+CHR$(175)+CHR$(175):A2$=A2$+CHR$(175)+CHR$(175)+CHR$(159)+CHR$(159):NEXTZ
100 IF MO=1 THEN RGB ELSE CMP
105 WIDTH32:POKE359,57:POKE&HFF22,80
110 PRINT@0,A1$:PRINT@260,"Written By Eric A. Wolf":PRINT@294,"Copyright (C) 1989":PRINT@448,A1$;
115 PLAY"T200L200":T$="CONCENTRATE!":FORY=1 TO 12:FORX=64 TO 160 STEP 32:POKE1026+(Y*2)+X-32,96:PRINT@2+(Y*2)+X,MID$(T$,Y,1):PLAYSTR$(Y):NEXTX:FORZ=1TO50:NEXTZ,Y
120 FORY=6TO12:FORX=Y TO 12:PLAY STR$(X):NEXTX,Y
125 FORY=0TO2:PRINT@384,STRING$(31,32);:POKE1439,96:FORZ=1TO200:NEXTZ:PRINT@384,"< Press any key to begin game!";:POKE1439,126:FORZ=1TO200:NEXTZ,Y:R=1
130 D=20:IF R=1 THEN PRINT@0,A1$;:PRINT@448,A1$;: ELSE PRINT@0,A2$;:PRINT@448,A2$;
135 R=1-R
140 D=D-1:IF D=0 THEN 130
145 A$=INKEY$:IFA$=""THEN140 ELSE FORY=1TO16:PRINT@480:FORZ=1TO50:NEXTZ,Y
150 PRINT@32,"Will the first player please    enter their name below...":I=128:GOSUB175:N$(0)=N$
155 PRINT@192,"Thank you.  Now, will the secondplayer enter their name.":I=288:GOSUB175:N$(1)=N$
160 CLS:A$="Welcome "+N$(0)+" & "+N$(1):PRINT@16-LEN(A$)/2,A$:PRINT@34,"to the game of CONCENTRATE !":PRINTSTRING$(32,"=")
165 P=0:GOSUB180:P=1:GOSUB180
170 GOTO 200
175 PRINT@I,"";:LINEINPUTN$:N$=LEFT$(N$,10):RETURN
180 FORY=96TO448STEP32:PRINT@Y,STRING$(32,32);:NEXTY:PRINT@128,N$(P);", please select your":PRINT@160,"method of input...":PRINT
185 PRINT"1) Right Joystick":PRINT"2) Left Joystick":PRINT"3) Keyboard"
190 A$=INKEY$:IFA$<"1"ORA$>"3"THEN190ELSEP(P)=VAL(A$)-1
195 RETURN
200 CLS:PRINT@266,"Please wait..."
205 '
210 ' Select correct palettes
215 '
220 IF MO=1 THEN PA$="00095663075200326040542518345636" ELSE PA$="00113263165300075626364518213207"
225 '
230 ' Store Flip Card Animation
235 '
240 POKE&HE6,2:LPOKE &H6E000,&HFF:LPOKE&H6E001,&HFF:POKE&HE0D4,&H37:FORY=1TO17:HBUFFY,455:NEXTY
245 POKE&HE0D4,&H34:FORY=1TO17:HBUFFY,455:NEXTY
250 BL=0:FOR L=0 TO 15 STEP 4:FORY=0TO29STEP2:HSET(Y,0,1):HSET(Y+1,1,1):HSET(Y+1,0,2):HSET(Y,1,2):NEXTY:HGET(0,0)-(29,1),BL+1:FORY=2TO29 STEP2:HPUT(0,Y)-(29,Y+1),BL+1,PSET:NEXT
255 GOSUB265:BL=BL+1:HGET(0,0)-(29,29),BL:NEXTL
260 FORL=12 TO 0 STEP-4:HCOLOR3:HLINE(0,0)-(29,29),PSET,BF:GOSUB265:BL=BL+1:HGET(0,0)-(29,29),BL:NEXTL:GOTO295
265 IFL=0 THEN 275
270 FORL1=0 TO L-1:HLINE(0,L1)-(29,L1),PRESET:HLINE(0,29-L1)-(29,29-L1),PRESET:NEXTL1
275 HDRAW"C0;BM0,"+STR$(L)+";ND1R1;BM0,"+STR$(29-L)+";NU1R1;BM29,"+STR$(L)+";ND1L1;BM29,"+STR$(29-L)+";NLU1U1":RETURN
280 '
285 ' Store 16 Card Faces
290 '
295 HCLS:POKE&HE0D4,&H34:HPUT(0,0)-(29,29),1,PSET:POKE&HE0D4,&H37:HGET(0,0)-(29,29),1
300 BL=1:FORY=1TO16:POKE&HE0D4,&H34:HPUT(0,0)-(29,29),8,PSET:POKE &HE0D4,&H37:READ D1$,D2$,P1,P2:HDRAW D1$:HPAINT(P1,P2):HDRAW D2$:BL=BL+1:HGET(0,0)-(29,29),BL:NEXTY
305 '
310 ' Start the game!
315 '
320 GOSUB485:P=1:TF=0:S(0)=0:S(1)=0
325 SW=0:P=1-P:HLINE(0,176)-(319,191),PRESET,BF:A$=N$(P)+", use "
330 IF P(P)=0 THEN A$=A$+"right joystick" ELSE IF P(P)=1 THEN A$=A$+"left joystick" ELSE A$=A$+"Keyboard"
335 A$=A$+" to select":HCOLOR2:HPRINT(20-LEN(A$)/2,23),A$
340 B=0:O1=-1:O2=-1:X1=4:Y1=2
345 ON P(P)+1 GOTO 350,355,360
350 B=PEEK(65280) AND 1:B=1-B:X1=JOYSTK(0)/7:Y1=JOYSTK(1)/12.8:GOTO390
355 B=PEEK(65280) AND 2:B=(B/2)*1:B=1-B:X1=JOYSTK(0):X1=JOYSTK(2)/7:Y1=JOYSTK(3)/12.8:GOTO390
360 IF O1<0 THEN 390 ELSE A$=INKEY$
365 B=0:IF A$=" " THEN B=1
370 X1=X1-11:Y1=Y1-11:X1=INT(O1/32):Y1=INT(O2/32):IF A$=CHR$(8) THEN X1=X1-1:IFX1<0 THEN X1=0
375 IFA$=CHR$(9)THENX1=X1+1:IFX1>9THENX1=9
380 IFA$=CHR$(94)THENY1=Y1-1:IFY1<0 THEN Y1=0
385 IFA$=CHR$(10)THENY1=Y1+1:IFY1>4 THEN Y1=4
390 X1=INT(X1)*32+11:Y1=INT(Y1)*32+11:IF X1=O1 THEN IF Y1=O2 THEN 405
395 POKE&HE0D4,&H34:IF O1>-1 THEN HPUT(O1,O2)-(O1+7,O2+7),9,PSET
400 O1=X1:O2=Y1:HGET(X1,Y1)-(X1+7,Y1+7),9:HCOLOR2:HLINE(X1,Y1)-(X1+7,Y1+7),PSET,BF
405 IF B=0 THEN 345
410 X=X1-11:Y=Y1-11:X=INT(X/32)+1:Y=INT(Y/32)+1
415 IF B(X,Y)=0 THEN 345
420 SW=SW+1:IF SW=1 THEN C1=X:C2=Y:GOSUB605:GOTO340
425 IF C1=X AND C2=Y THEN SW=SW-1:GOTO340 ELSE C3=X:C4=Y
430 GOSUB605:IF B(C1,C2)<>B(C3,C4) THEN HLINE(0,176)-(319,183),PRESET,BF:HCOLOR15:HPRINT(7,22),"Sorry, no match that time!":PLAY"T150L150":FORY=12TO1STEP-1:PLAY STR$(Y):NEXTY:FORZ=1TO100:NEXTZ:X=C1:Y=C2:GOSUB610:X=C3:Y=C4:GOSUB610:GOTO325
435 C1=C1-1:C2=C2-1:C3=C3-1:C4=C4-1
440 PLAY"T200L200":HCOLOR3:HPRINT(7,22),"Great Match!   Keep going!"
445 FORZ1=1 TO 12 STEP3:FORZ=Z1 TO 12:PLAY STR$(Z):NEXTZ,Z1:HLINE(C1*32,C2*32)-(C1*32+30,C2*32+30),PRESET,BF:HLINE(C3*32,C4*32)-(C3*32+30,C4*32+30),PRESET,BF
450 S(P)=S(P)+1:B(C1+1,C2+1)=0:B(C3+1,C4+1)=0:SW=0:HLINE(160,160)-(176,167),PRESET,BF:HLINE(304,160)-(319,167),PRESET,BF:HPRINT(19,20),S(0):HPRINT(37,20),S(1)
455 TF=TF+1:IF TF<25 THEN 340
460 IF S(0)>S(1) THEN W=0 ELSE W=1
465 GOTO560
470 '
475 ' Make & Draw a new board
480 '
485 FORY=1TO16:PALETTE Y-1,VAL(MID$(PA$,(Y-1)*2+1,2)):NEXTY
490 HSCREEN2:HCOLOR2:HPRINT(11,12),"Shuffling Cards...":TC=0
495 FORY=0TO15:C(Y)=0:NEXTY
500 F=RND(16):IF C(F-1)>0 THEN 500 ELSE C(F-1)=1
505 X=RND(10):Y=RND(5):IF B(X,Y)>0 THEN 505 ELSE B(X,Y)=F
510 X=RND(10):Y=RND(5):IF B(X,Y)>0 THEN 510 ELSE B(X,Y)=F
515 TC=TC+1:IF TC=16 THEN FORW=0TO15:C(W)=0:NEXTW
520 IF TC<25 THEN 500
525 HLINE(88,96)-(258,103),PRESET,BF:POKE&HE0D4,&H37:FORY=0TO4:FORX=0TO9:HPUT(X*32,Y*32)-(X*32+29,Y*32+29),1,PSET:NEXTX,Y
530 HCOLOR10:HPRINT(36,20),"-":HPRINT(18,20),"-":HPRINT(0,20),"Score:"
535 HCOLOR0:HPRINT(0,23),N$(0):HCOLOR10:HPRINT(7,20),N$(0):HPRINT(25,20),N$(1)
540 RETURN
545 '
550 ' We have a winner!
555 '
560 PLAY"T200L200":FORY=1TO12:FORX=Y TO12:PLAY STR$(X):NEXTX,Y:HSCREEN0:IF MO=1 THEN RGB ELSE CMP
565 HSCREEN0:CLS:PRINT@39,"We have a winner !":PRINT@138,"Final Score":PRINT@170,STRING$(11,"-")
570 PRINT@202,N$(0):PRINT@214,S(0):PRINT@234,N$(1):PRINT@246,S(1)
575 PRINT:PRINT:A$=N$(W)+" is our winner!":PRINTTAB(16-LEN(A$)/2);A$
580 PRINT@483,"Care to play again (Y/N) ?";
585 A$=INKEY$:IFA$="N" THEN POKE359,126:RGB:END ELSE IFA$="Y"THENRUN ELSE 585
590 '
595 ' Flip a specific card
600 '
605 X1=(X-1)*32:Y1=(Y-1)*32:PLAY"T20L20":POKE&HE0D4,&H34:FORZ=1TO8:HPUT(X1,Y1)-(X1+29,Y1+29),Z,PSET:PLAY STR$(Z):FORZ1=1TO50:NEXTZ1,Z:POKE&HE0D4,&H37:HPUT(X1,Y1)-(X1+29,Y1+29),B(X,Y)+1:RETURN
610 X1=(X-1)*32:Y1=(Y-1)*32:PLAY"T20L20":POKE&HE0D4,&H34:FORZ=7 TO 1 STEP-1:HPUT(X1,Y1)-(X1+29,Y1+29),Z,PSET:PLAY STR$(Z):FORZ1=1TO50:NEXTZ1,Z:RETURN
615 '
620 ' Card Face Grfx Data
625 '
630 DATA "BM13,7;C15;E3R5F3D6G10H10U6E3R5F3","",15,15,"BM14,4;C14;G10F10E10H10","",14,14,"BM12,8;C13;D16RU16RD16RU16;BM14,2;C12;M+12,+19;L24;M+12,-19;","",14,15
635 DATA "BM15,13;C13;L12R24H1L22ER20HL18ER16HL14ER12HL10ER8HL6;BM15,26;C11;R12U12L24D12R12L2U6R4D6;L1;C13;U5LD5LU5;BM6,4;D6RU6RD6;C11","",15,15
640 DATA "BM15,4;C10;R6F6D10G6L12H6U10E6R12;BM9,8;D5R3U5L2D1;C0;D3RU3;BM18,8;C10;D5R3U5L2D1;C0;D3RU3;C10;BM5,17;R2F4R8E4R2G6L8H6R2;D1;C0;F4R8E4;C10","",15,15
645 DATA "BM3,4;C1;M+8,+20;R16;M-8,-20;L16","",8,5,"BM2,18;C9;R24U4;M-2,-4;H2;M-4,-2;L8;M-4,+2;G2;M-2,+4;D4;","",15,15
650 DATA "C8;BM9,4;D24R4;M+4,-2;E4;M+2,-4;U6;M-2,-4;H4;M-4,-2;L4D2;","",15,15,"C13;BM3,4;R22D6H2L6D16L6U16L6G2U6;","",15,15
655 DATA "C7;BM4,3;R22D5L18D4R18D5L18D4R18D5L22U23;","",6,6,"C0;BM10,5;D16GL3HU3ER3FU11R12UL12UR12D16GL3HU3ER3FL4DR4DL4DR4","C3;BM6,19;E1;BM18,19;E1",8,20
660 DATA "C2;BM4,4D20RU20R1;C15;R20DL20BD2R20DL20BD2R20DL20BD2R20DL20;BM6,4;C1;D5RU5RD5RU5RD5RU5;C0","",0,0
665 DATA "BM4,4;C0;R22D22L22U22","C3;BM26,7;LDR1;BM15,15;NL2NR2U1NL2NR2U1NL1NR1D3NR2NL2D1NL1R1;BD2;BL1D6EU4L2D4",15,15
670 DATA "C0;BM3,4;D20RU20RD20RU20RD20RU20RD20RU20R1;C4;D20RU20RD20RU20RD20RU20RD20RU20R1;C14;D20RU20RD20RU20RD20RU20RD20RU20;C0","",0,0
675 DATA "C9;BM3,5;E3R18F3D21H3G3H3G3H3G3H3G3U21;","C8;BM8,11;R4EL6UR6UL6ER4;C10;L1GR3DL3DNR3FR1;C8;BM18,11;R4EL6UR6UL6ER4;C10;L1GR3DL3DNR3FR1",8,8
680 DATA "C5;BM4,4;D22R22H22","",6,16
685 '
690 END
