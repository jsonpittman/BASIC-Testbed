10 B$="32621CAF7EADA5":Y=&HF8
20 FORR=1TOLEN(B$)STEP2
30 C$="&H"+MID$(B$,R,2)
40 V=VAL(C$):POKEY,V:Y=Y+1:NEXTR
50 POKE&H19A,&H39:POKE&H19B,0:POKE&H19C,&HF8:POKE&H19A,&H7E
60 CLS3
70 FORT=1TO500:NEXT
80 CLEAR5000
90 DIMFD(9),PA(5),PD(5),FD$(9),MD$(9),MD(9),NA$(23),PF(9),PM(9)
100 Q=100:FORX=1TO9:READFD(X):NEXT
110 DATA111.11,222.22,333.33,444.44,555.55,666.66,777.77,888.88,999.99
120 FORX=1TO9:READFD$(X):NEXTX
130 DATALOAN#1,LOAN#2,LOAN#3,LOAN#4,LOAN#5,LOAN#6,LOAN#7,LOAN#8,LOAN#9
140 FORDA=1TO23:READNA$(DA):NEXTDA
150 DATA W,R,I,T,T,E,N, ,B,Y, ,D,A,V,I,D, ,V., ,H,A,A,S
160 CLS3:FORT=1024TO1055:POKET,36:EXEC43359:NEXTT:FORT=1504TO1535:POKET,36:EXEC43359:NEXTT
170 FORDA=1TO23:PRINT@Q+1,NA$(DA);:EXEC43359:IFDA=8 THENGOSUB210:Q=Q+1:NEXTDA:POKE65494,0 ELSEQ=Q+1:NEXTDA
180 CT=1:PRINT@226,"    1- START NEW MONTH     ";
190 PRINT@258,"    2- LOAD CURRENT DATA   ";
200 GOTO220
210 PRINT@361,"JUNE 15, 1986";:SOUND250,1:FORT=1TO500:NEXT:RETURN
220 W$=INKEY$:IFW$="" THEN220 ELSEIFW$="1" THEN230 ELSEIFW$="2" THEN1840 ELSE220
230 CLS
240 S9=1:PRINTTAB(225):PRINT"ENTER 1ST 3 LETTERS             OF CURRENT MONTH":PRINTTAB(2):INPUTCM$:GOSUB2390
250 IFCM$=""THENCLS:GOTO240 ELSEIFLEN(CM$)<>3 THEN260 ELSE270
260 CLS3:PRINT@230,"PLEASE USE 3 LETTERS!!";:FORT=1TO1000:NEXT:CLS:GOTO240
270 CLS
280 FORX=0TO31:PRINT@X,CHR$(143):NEXT
290 A$="$$ FILE'S BUDGET $$ "
300 FORC=32TO63:PRINT@C,CHR$(175);:NEXTC
310 IFS9=1 THENPRINT"THIS IS A MONTHLY BUDGET FOR USEON A TAPE BASED SYSTEM. IT WILL STORE UP TO NINE FIXED MONTHLY  BILLS AND ALSO UP TO NINE OTHER BILLS. YOU CAN ENTER UP TO FIVE PAYCHECKS
320 IFS9=1 THENPRINT@234,"AND IT KEEPS A CURRENTBALANCE OF  ALL PAYCHECKS MINUS ANY BILLS THAT YOU'VE DEDUCTED. WHEN A BILL IS DEDUCTED, IT IS  LOCKED OUT WITH A <-paid! FLAG.                                         (PRESS ANY KEY)"
330 IFS9=2 THENPRINT"THESE FLAGS WILL BE STORED ALONGWITH ALL OTHER DATA ON YOUR DATATAPE TO INDICATE ALL PREVIOUSLY PAID BILLS NEXT TIME. AFTER YOU REVIEW PAID BILLS, RESET FLAGS  AND YOU ARE READY FOR CURRENT   WEEK'S DEDUCTIONS."
340 IFS9=2 THENPRINT@275,"SAVE NEW DATATO TAPE EACH TIME IT'S UPDATED. SAVE ALL DATA TO A SEPARATE TAPEAT THE END OF EACH MONTH SO YOU CAN REFER BACK TO ANY PAST MONTHIF NECESSARY.                           (PRESS ANY KEY)"
350 IFS9=3 THENPRINT"THIS PROGRAM ALSO HAS A PRINT   FUNCTION THAT PRINTS OUT THE    AMOUNT OF YOUR PAY BEFORE AND   AFTER EACH BILL IS DEDUCTED FOR THE CURRENT WEEK. YOU CAN CHECK OFF EACH BILL AS YOU MAIL IT!          (PRESS ANY KEY)"
360 FORI=1TO30:SO$=LEFT$(A$,I):PRINT@31-I,SO$:Z$=INKEY$:IFZ$=""THEN370 ELSE390
370 FORT=1TO100:NEXTT:NEXTI:FORI=20TO0STEP-1:SO$=RIGHT$(SO$,I):PRINT@1,SO$:Z$=INKEY$:IFZ$=""THEN380 ELSE390
380 FORT=1TO100:NEXTT:NEXTI:GOTO360
390 S9=S9+1:IFS9=4 THENCLS:GOTO400 ELSE270
400 PRINT@4,"PRESS NUMBER OF SELECTION"
410 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
420 PRINT@68,"1. ENTER NEW PAYCHECKS"
430 PRINT@100,"2. LOAD MISC EXPENSES"
440 PRINT@132,"3. DEDUCT FIXED EXPENSES"
450 PRINT@164,"4. DEDUCT MISC EXPENSES"
460 PRINT@196,"5. SAVE DATA TO TAPE"
470 PRINT@228,"6. LOAD DATA FROM TAPE"
480 PRINT@260,"7. SKIPF PROGRAM/SET GAP"
490 PRINT@292,"8. OUTPUT TO PRINTER"
500 PRINT@324,"9. QUIT / RESET paid FLAGS"
510 FORX=352TO383:PRINT@X,CHR$(175):NEXTX
520 PRINT@388,"CURRENT MONTH:    "CX$"  
530 PRINT@450,"PRESENT BALANCE: ":PRINT@468,USING"$ ####.##";CB
540 Z$=INKEY$:IFZ$=""THEN540
550 IFVAL(Z$)<1 THEN540 ELSE IFVAL(Z$)>9 THEN540
560 ON VAL(Z$) GOTO570,1140,800,1360,1640,1840,2160,2510,2050
570 CLS
580 PRINT@0,"CHECK #1.. ":PRINT@11,USING"$ ###.##";PA(1):PRINT@24,"-"CM$PD(1)
590 PRINT@32,"CHECK #2.. ":PRINT@43,USING"$ ###.##";PA(2):PRINT@56,"-"CM$PD(2)
600 PRINT@64,"CHECK #3.. ":PRINT@75,USING"$ ###.##";PA(3):PRINT@88,"-"CM$PD(3)
610 PRINT@96,"CHECK #4.. ":PRINT@107,USING"$ ###.##";PA(4):PRINT@120,"-"CM$PD(4)
620 PRINT@128,"CHECK #5.. ":PRINT@139,USING"$ ###.##";PA(5):PRINT@152,"-"CM$PD(5)
630 PRINT
640 PRINT"<E>NTER PAYCHECK  <M>AIN MENU"
650 FORX=384TO415:PRINT@X,"*":NEXTX
660 PRINT@422,"PAYCHECK'S WILL LOAD            IN SEQUENCE (1 TO 5)"
670 Z$=INKEY$:IFZ$=""THEN670
680 IF Z$="M" THENCLS:GOTO400
690 IFZ$="E" THEN700 ELSE670
700 PRINT@295,"ARE YOU SURE (Y/N)"
710 Z$=INKEY$:IFZ$="" THEN710 ELSEIFZ$="Y" THEN720 ELSE570
720 CLS:PRINT@7,"paycheck entry mode"
730 FORX=32TO63:PRINT@X,"X":NEXTX
740 IFPA(1)=0 THENINPUT"ENTER AMOUNT";PA(1) :INPUT"DAY OF MONTH";PD(1):CB=CB+PA(1):GOTO570
750 IFPA(2)=0 THENINPUT"ENTER AMOUNT";PA(2):INPUT"DAY OF MONTH";PD(2):CB=CB+PA(2):GOTO570
760 IFPA(3)=0 THENINPUT"ENTER AMOUNT";PA(3):INPUT"DAY OF MONTH";PD(3):CB=CB+PA(3):GOTO570
770 IFPA(4)=0 THENINPUT"ENTER AMOUNT";PA(4):INPUT"DAY OF MONTH";PD(4):CB=CB+PA(4):GOTO570
780 IFPA(5)=0 THENINPUT"ENTER AMOUNT";PA(5):INPUT"DAY OF MONTH";PD(5):CB=CB+PA(5):GOTO570
790 PRINT@134,"all paychecks loaded":FORT=1TO2000:NEXTT:GOTO570
800 CLS:POKE65495,0
810 PRINT@6,"$$ FIXED DEDUCTIONS $$"
820 DC$="<-paid!"
830 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
840 PRINT@64,"1. "FD$(1):PRINT@78,USING"$ ###.##";FD(1):IFCK(1)=1 THENPRINT@88,DC$
850 PRINT@96,"2. "FD$(2):PRINT@110,USING"$ ###.##";FD(2):IFCK(2)=1 THENPRINT@120,DC$
860 PRINT@128,"3. "FD$(3):PRINT@142,USING"$ ###.##";FD(3):IFCK(3)=1 THENPRINT@152,DC$
870 PRINT@160,"4. "FD$(4):PRINT@174,USING"$ ###.##";FD(4):IFCK(4)=1 THENPRINT@184,DC$
880 PRINT@192,"5. "FD$(5):PRINT@206,USING"$ ###.##";FD(5):IFCK(5)=1 THENPRINT@216,DC$
890 PRINT@224,"6. "FD$(6):PRINT@238,USING"$ ###.##";FD(6):IFCK(6)=1 THENPRINT@248,DC$
900 PRINT@256,"7. "FD$(7):PRINT@270,USING"$ ###.##";FD(7):IFCK(7)=1 THENPRINT@280,DC$
910 PRINT@288,"8. "FD$(8):PRINT@302,USING"$ ###.##";FD(8):IFCK(8)=1 THENPRINT@312,DC$
920 PRINT@320,"9. "FD$(9):PRINT@334,USING"$ ###.##";FD(9):IFCK(9)=1 THENPRINT@344,DC$
930 PRINT@384,"CURRENT CASH BALANCE:":PRINT@407,USING"$ ####.##";CB
940 PRINT:FORX=1TO9:P=P+CK(X):NEXTX:IFP=9 THEN970 ELSEP=0
950 PRINT@450," <D>EDUCT BILL  <M>AIN MENU"
960 GOTO980
970 PRINT"           <M>AIN MENU":GOTO1000
980 Z$=INKEY$:IFZ$=""THEN980
990 IFZ$="D" THEN1010 ELSEIFZ$="M" THENPOKE65494,0:CLS:GOTO400 ELSE980
1000 IF INKEY$="M" THENPOKE65494,0:CLS:GOTO400 ELSE1000
1010 PRINT"WHICH BILL TO DEDUCT (1-9)"
1020 Z$=INKEY$:IFZ$=""THEN1020
1030 IF Z$="0" THEN1020
1040 IFZ$="1"THENIFCK(1)=0 THENCB=CB-FD(1):CK(1)=1:GOTO800ELSE SOUND1,3:GOTO800
1050 IFZ$="2"THENIFCK(2)=0 THENCB=CB-FD(2):CK(2)=1:GOTO800 ELSESOUND1,3:GOTO800
1060 IFZ$="3"THENIFCK(3)=0 THENCB=CB-FD(3):CK(3)=1:GOTO800 ELSESOUND1,3:GOTO800
1070 IFZ$="4"THENIFCK(4)=0 THENCB=CB-FD(4):CK(4)=1:GOTO800 ELSESOUND1,3:GOTO800
1080 IFZ$="5"THENIFCK(5)=0 THENCB=CB-FD(5):CK(5)=1:GOTO800 ELSESOUND1,3:GOTO800
1090 IFZ$="6"THENIFCK(6)=0 THENCB=CB-FD(6):CK(6)=1:GOTO800 ELSESOUND1,3:GOTO800
1100 IFZ$="7"THENIFCK(7)=0 THENCB=CB-FD(7):CK(7)=1:GOTO800 ELSESOUND1,3:GOTO800
1110 IFZ$="8"THENIFCK(8)=0 THENCB=CB-FD(8):CK(8)=1:GOTO800 ELSESOUND1,3:GOTO800
1120 IFZ$="9"THENIFCK(9)=0 THENCB=CB-FD(9):CK(9)=1:GOTO800 ELSESOUND1,3:GOTO800
1130 CLS:GOTO400
1140 CLS3:PRINT@66,"1. CLEAR ALL MISC BILLS    ";
1150 PRINT@98,"2. KEEP PREVIOUS MISC BILLS";:PRINT@130,"3. RETURN TO MENU          ";
1160 PRINT@194,NB" BILLS CURRENTLY LOADED ";
1170 IFNB=9 THENPRINT@265,"MISC IS FULL!";
1180 Z$=INKEY$:IFZ$="1" THEN1190 ELSEIFZ$="2" THENCT=NB+1:GOTO1220 ELSEIFZ$="3"THENCLS:GOTO400 ELSE1180
1190 FORX=1TO9:MD$(X)="":MD(X)=0:NEXTX:CT=1:NB=0
1200 GOTO1220
1210 CLS3:PRINT@235,"MISC FULL";:FORT=1TO2000:NEXT:CLS:GOTO400
1220 IFNB=9 THEN1210 ELSE CLS3:PRINT" HOW MANY NEW BILLS TO ADD?"
1230 Z$=INKEY$:IFZ$=""THEN1230
1240 IFZ$="0" THENCLS:GOTO400 ELSEIFNB+VAL(Z$)>9 THEN1250 ELSENB=NB+VAL(Z$):GOTO1270
1250 CLS3:PRINT@226,"MISC ONLY HOLDS 9 ENTRIES!!";:FORT=1TO1500:NEXT:CLS:GOTO1220
1260 CLS:PRINT@7," $$ MISC LOADING $$":FORX=32TO63:PRINT@X,CHR$(175):NEXT:GOTO1290
1270 CLS:FORY=1 TO VAL(Z$):PRINT@7," $$ MISC LOADING $$"
1280 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
1290 PRINTCT". ":INPUT"NAME OF BILL";MD$(CT):IFMD$(CT)=""THEN1290
1300 IFLEN(MD$(CT))>8 THEN1320
1310 IFLEN(MD$(CT))<8 THENMD$(CT)=MD$(CT)+CHR$(32):GOTO1310 ELSE1330
1320 CLS3:PRINT@228,"EIGHT (8) LETTERS MAX!";:FORT=1TO1500:NEXT:CLS:GOTO1260
1330 :INPUT"AMOUNT OF BILL";MD(CT)
1340 CLS:CT=CT+1:NEXTY
1350 CLS:GOTO400
1360 DC$="<-paid!":CLS:POKE65495,0
1370 PRINT@5," $$ MISC DEDUCTIONS $$     "
1380 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
1390 IFMD$(1)<>"" THENPRINT@64,"1. "MD$(1):PRINT@78,USING"$ ###.##";MD(1):IFCL(1)=1 THENPRINT@88,DC$
1400 IFMD$(2)<>"" THENPRINT@96,"2. "MD$(2):PRINT@110,USING"$ ###.##";MD(2):IFCL(2)=1 THENPRINT@120,DC$
1410 IFMD$(3)<>"" THENPRINT@128,"3. "MD$(3):PRINT@142,USING"$ ###.##";MD(3):IFCL(3)=1 THENPRINT@152,DC$
1420 IFMD$(4)<>"" THENPRINT@160,"4. "MD$(4):PRINT@174,USING"$ ###.##";MD(4):IFCL(4)=1 THENPRINT@184,DC$
1430 IFMD$(5)<>"" THENPRINT@192,"5. "MD$(5):PRINT@206,USING"$ ###.##";MD(5):IFCL(5)=1 THENPRINT@216,DC$
1440 IFMD$(6)<>"" THENPRINT@224,"6. "MD$(6):PRINT@238,USING"$ ###.##";MD(6):IFCL(6)=1 THENPRINT@248,DC$
1450 IFMD$(7)<>"" THENPRINT@256,"7. "MD$(7):PRINT@270,USING"$ ###.##";MD(7):IFCL(7)=1 THENPRINT@280,DC$
1460 IFMD$(8)<>"" THENPRINT@288,"8. "MD$(8):PRINT@302,USING"$ ###.##";MD(8):IFCL(8)=1 THENPRINT@312,DC$
1470 IFMD$(9)<>"" THENPRINT@320,"9. "MD$(9):PRINT@334,USING"$ ###.##";MD(9):IFCL(9)=1 THENPRINT@344,DC$
1480 PRINT@384,"CURRENT CASH BALANCE:":PRINT@407,USING"$ ####.##";CB
1490 PRINT@450," <D>EDUCT BILL  <M>AIN MENU"
1500 Z$=INKEY$:IFZ$=""THEN1500
1510 IFZ$="D" THEN1520 ELSEIFZ$="M" THENPOKE65494,0:CLS:GOTO400 ELSE1500
1520 PRINT"WHICH BILL TO DEDUCT (1-9)"
1530 Z$=INKEY$:IFZ$=""THEN1530
1540 IFZ$="1"THENIFCL(1)=0ANDMD(1)>0  THENCB=CB-MD(1):CL(1)=1:GOTO1360   ELSE SOUND1,3:GOTO1360
1550 IFZ$="2"THENIFCL(2)=0ANDMD(2)>0 THENCB=CB-MD(2):CL(2)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1560 IFZ$="3"THENIFCL(3)=0ANDMD(3)>0 THENCB=CB-MD(3):CL(3)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1570 IFZ$="4"THENIFCL(4)=0ANDMD(4)>0 THENCB=CB-MD(4):CL(4)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1580 IFZ$="5"THENIFCL(5)=0ANDMD(5)>0 THENCB=CB-MD(5):CL(5)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1590 IFZ$="6"THENIFCL(6)=0ANDMD(6)>0 THENCB=CB-MD(6):CL(6)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1600 IFZ$="7"THENIFCL(7)=0ANDMD(7)>0 THENCB=CB-MD(7):CL(7)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1610 IFZ$="8"THENIFCL(8)=0ANDMD(8)>0 THENCB=CB-MD(8):CL(8)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1620 IFZ$="9"THENIFCL(9)=0ANDMD(9)>0 THENCB=CB-MD(9):CL(9)=1:GOTO1360 ELSESOUND1,3:GOTO1360
1630 CLS:GOTO400
1640 CLS:PRINT"PREPARE RECORDER..PRESS ANY KEY"
1650 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
1660 Z$=INKEY$:IFZ$=""THEN1660
1670 FORX=0TO31:PRINT@X,CHR$(175):NEXTX:PRINT@74,"SAVING DATA"
1680 FORX=96TO127:PRINT@X,CHR$(175):NEXTX
1690 OPEN "O", #-1,"DATA"
1700 FORQ8=1TO9:PRINT #-1,CK(Q8):NEXTQ8
1710 FORQ9=1TO9:PRINT #-1,CL(Q9):NEXTQ9
1720 PRINT #-1,CB:PRINT #-1,CM$
1730 PRINT #-1,CX$:PRINT #-1,NB
1740 FORX=1TO5:PRINT #-1,PA(X):NEXTX
1750 FORX2=1TO5:PRINT #-1,PD(X2):NEXTX2
1760 FORX3=1TO9:PRINT #-1,FD$(X3):NEXTX3
1770 FORX4=1TO9:PRINT #-1,FD(X4):NEXTX4
1780 FORX5=1TO9:PRINT #-1,MD$(X5):NEXTX5
1790 FORX6=1TO9:PRINT #-1,MD(X6):NEXTX6
1800 CLOSE #-1
1810 CLS3
1820 PRINT@299,"DATA SAVED";
1830 FORX=1TO2000:NEXT:CLS:GOTO400
1840 CLS:PRINT@0,"PREPARE RECORDER..PRESS ANY KEY"
1850 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
1860 Z$=INKEY$:IFZ$=""THEN1860
1870 FORX=0TO63:PRINT@X,CHR$(175):NEXTX
1880 PRINT@74,"LOADING DATA"
1890 FORX=96TO127:PRINT@X,CHR$(175):NEXTX
1900 OPEN "I", #-1,"DATA"
1910 FORQ8=1TO9:INPUT #-1,CK(Q8):NEXTQ8
1920 FORQ9=1TO9:INPUT #-1,CL(Q9):NEXTQ9
1930 INPUT #-1,CB:INPUT #-1,CM$
1940 INPUT #-1,CX$:INPUT #-1,NB
1950 FORX=1TO5:INPUT #-1,PA(X):NEXTX
1960 FORX2=1TO5:INPUT #-1,PD(X2):NEXTX2
1970 FORX3=1TO9:INPUT #-1,FD$(X3):NEXTX3
1980 FORX4=1TO9:INPUT #-1,FD(X4):NEXTX4
1990 FORX5=1TO9:INPUT #-1,MD$(X5):NEXTX5
2000 FORX6=1TO9:INPUT #-1,MD(X6):IF EOF (-1) THEN2010 ELSENEXTX6NEXTX6
2010 CLOSE #-1
2020 CLS3
2030 PRINT@299,"DATA LOADED";
2040 FORS=1TO2000:NEXT:CLS:W$="":GOTO400
2050 CLS3:PRINT@0,"<Q>UIT.............<R>ESET FLAGS"
2060 Z$=INKEY$:IFZ$="" THEN2060
2070 IFZ$="Q" THEN2120 ELSEIFZ$="R" THEN2080 ELSE2060
2080 CLS3:PRINT@233,"CLEARING FLAGS";
2090 FORX=1TO9:CK(X)=0:NEXTX
2100 FORY=1TO9:CL(Y)=0:NEXTY
2110 FORT=1TO1000:NEXT:CLS:GOTO400
2120 CLS3:PRINT@224,"DID YOU SAVE UPDATED DATA (Y/N)?";
2130 Z$=INKEY$:IFZ$="" THEN2130
2140 IFZ$="N" THENCLS:GOTO400 ELSE2150
2150 CLS:POKE359,60:PRINT"THANK YOU FOR USING MY BUDGET!!!":POKE359,126:END
2160 CLS:PRINT@5,"  <S>KIPF....<G>AP":FORT=32TO63:PRINT@T,CHR$(175):NEXTT
2170 Z$=INKEY$:IFZ$=""THEN2170
2180 IFZ$="S" THEN2190 ELSEIFZ$="G" THEN2320 ELSE2170
2190 CLS:PRINT@0,"PREPARE RECORDER..PRESS ANY KEY";
2200 FORT=32TO63:PRINT@T,CHR$(175):NEXTT
2210 Z$=INKEY$:IFZ$=""THEN2210
2220 CLS:PRINT@8,"SKIPPING PROGRAM"
2230 FORX=32TO63:PRINT@X,CHR$(175):NEXT
2240 SKIPF
2250 CLS:PRINT@8,"PROGRAM SKIPPED!"
2260 FORX=32TO63:PRINT@X,CHR$(175):NEXT
2270 FORX=1TO1500:NEXT:CLS
2280 PRINT@2,"  <S>ET GAP.....<M>AIN MENU"
2290 FORX=32TO63:PRINT@X,CHR$(175):NEXT
2300 Z$=INKEY$:IFZ$=""THEN2300
2310 IFZ$="M" THENCLS:GOTO400 ELSEIFZ$="S" THEN2320 ELSE2310
2320 CLS:PRINT@0,"PREPARE RECORDER..PRESS ANY KEY"
2330 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
2340 Z$=INKEY$:IFZ$=""THEN2340
2350 MOTORON:CLS:PRINT@0,"MOTOR IS ON..TAP ANY KEY TO STOP"
2360 FORX=32TO63:PRINT@X,CHR$(175):NEXTX
2370 Z$=INKEY$:IFZ$=""THEN2370
2380 MOTOROFF:CLS:GOTO400
2390 IFCM$="JAN" THENCX$="JANUARY":RETURN
2400 IFCM$="FEB" THENCX$="FEBRUARY":RETURN
2410 IFCM$="MAR" THENCX$="MARCH":RETURN
2420 IFCM$="APR" THENCX$="APRIL":RETURN
2430 IFCM$="MAY" THENCX$=CM$:RETURN
2440 IFCM$="JUN" THENCX$="JUNE":RETURN
2450 IFCM$="JUL" THENCX$="JULY":RETURN
2460 IFCM$="AUG" THENCX$="AUGUST":RETURN
2470 IFCM$="SEP" THENCX$="SEPTEMBER":RETURN
2480 IFCM$="OCT" THENCX$="OCTOBER":RETURN
2490 IFCM$="NOV" THENCX$="NOVEMBER":RETURN
2500 IFCM$="DEC" THENCX$="DECEMBER":RETURN ELSECM$="":RETURN
2510 CLS3:PRINT@8,"OUTPUT TO PRINTER";
2520 PRINT@231,"ARE YOU SURE (Y/N)";
2530 Z$=INKEY$:IFZ$="" THEN2530
2540 IFZ$="N" THENCLS:GOTO400
2550 IFZ$="Y" THENCLS:GOTO2560 ELSE2530
2560 CLS3:INPUT"DAY OF MONTH";DM:INPUT"AMOUNT OF PAY THIS WEEK";AP:CLS3:PRINT"*VERIFY PRINTER IS ON AND READY*";
2570 PRINT@234,"PRESS ANY KEY";
2580 Z$=INKEY$:IFZ$="" THEN2580
2590 CLS3:PRINT@233,"STARTING BUDGET";
2600 PRINT#-2,CHR$(27);CHR$(14);CHR$(27);CHR$(84);CHR$(48);TAB(23);"=> BUDGET <=";CHR$(10);CHR$(13)
2610 PRINT#-2,TAB(17);CX$;DM", 1986";TAB(128);CHR$(27);CHR$(84);CHR$(49);"(CHECK)"
2620 PRINT#-2,CHR$(27);CHR$(84);CHR$(54);CHR$(28);CHR$(45) "*";CHR$(10);CHR$(13)
2630 PRINT#-2,CHR$(27);CHR$(84);CHR$(50)
2640 PRINT#-2,TAB(15);"PAYCHECK $ ";AP
2650 PRINT#-2,CHR$(27);CHR$(84);CHR$(48)
2660 FORX=1TO9:CLS3:PRINT@40,"FIXED DEDUCTIONS";
2670 PRINT@233,FD$(X)"  "FD(X);
2680 PRINT@294,"PRINT THIS BILL (Y/N)";
2690 Z$=INKEY$:IFZ$="" THEN2690
2700 IFZ$="N" THENNEXTX ELSEIFZ$="Y" THEN2720 ELSE2690
2710 GOTO2730
2720 PRINT#-2,TAB(15);CHR$(27);CHR$(84);CHR$(49);"- ";FD(X);" ";FD$(X);" <F>    ()";CHR$(27);CHR$(84);CHR$(50);CHR$(10);CHR$(13):AP=AP-FD(X):PRINT#-2,TAB(15);"---------------":PRINT#-2,TAB(15):PRINT#-2,USING"$ ###.##";AP:PRINT#-2,"BALANCE":NEXTX
2730 FORX=1TO9:CLS3:PRINT@40,"MISC DEDUCTIONS";:IFMD$(X)="" THEN2790 ELSEPRINT@233,MD$(X)"  "MD(X);
2740 PRINT@294,"PRINT THIS BILL (Y/N)";
2750 Z$=INKEY$:IFZ$="" THEN2750
2760 IFZ$="N" THENNEXTX ELSEIFZ$="Y" THEN2780 ELSE2750
2770 GOTO2790
2780 PRINT#-2,TAB(15);CHR$(27);CHR$(84);CHR$(49);"- ";MD(X);"  ";MD$(X);"<M>    ()";CHR$(27);CHR$(84);CHR$(50);CHR$(10);CHR$(13):AP=AP-MD(X):PRINT#-2,TAB(15);"---------------":PRINT#-2,TAB(15):PRINT#-2,USING"$ ###.##";AP:PRINT#-2,"BALANCE":NEXTX
2790 CLS:PRINT#-2,CHR$(27);CHR$(84);CHR$(54);CHR$(28);CHR$(45)"*":FORX=1TO3:PRINT#-2,CHR$(10);CHR$(13):NEXTX:GOTO400
2 '*HOME FINANCIAL ANALYST*
4 '* 4/85 BY DAN PIERSMA  *
5 '*   32K-ECB & 1 DISK   *
6 GOTO448
8 'DIMENSION DATAFILES & DESCRIPTIONS
10 CLEAR5500:DIMA$(100),B$(100),C$(100),D$(100),E$(100),M$(12),AD$(25),AN$(25)
12 'SET BAUD 1200 & DEFINE NONVARIABLE STRINGS
14 W=8:POKE150,41:V$=" "+STRING$(30,"*"):U$=STRING$(72,"-"):W$=CHR$(32):BA$=" DAY (DD) ":BB$=" ACCOUNT# ":BC$=" AMOUNT   ":BD$=" CHECK #  ":BE$=" COMMENTS ":BF$="REC #"
16 'ENTER DATE
18 CLS:PRINT@9,"BUDGET MANAGER":PRINT"ENTER MONTH AND YEAR THAT YOU   WILL BE ENTERING DATA ON. ":INPUT"MONTH (MM)  ";MM:INPUT"YEAR (YYYY) ";YY$
20 IFMM<1ORMM>12THEN18
22 YY=VAL(YY$):IFYY<1984ORYY>1988THEN18
24 'READ DESCRIPTIONS
26 FORM=1TO12:READM$(M):NEXT:FORAD=1TO25:READAD$(AD):NEXT
28 'CREATE DISK FILE TITLE
30 MM$=M$(MM):TITLE$=MM$+"-"+YY$
32 L=LEN(TITLE$)/2:Z=207-L
34 'CREATE MAIN MENU
36 H=0:CLS:PRINT@9,"BUDGET MANAGER":PRINT"      SELECT A NUMBER 1-5"
38 PRINT@128,V$:PRINT" 1> FILE MAINTENANCE":PRINT" 2> SEARCH FILE":PRINT" 3> GENERATE REPORTS":PRINT" 4> SAVE FILE":PRINT" 5> LOAD FILE":PRINTV$
40 L$=INKEY$
42 L=VAL(L$):IFL<1ORL>5THEN40
44 ONL GOTO48,212,276,392,420
46 'FILE MAINTENANCE MENU
48 CLS3:PRINT@32,"       SELECT A NUMBER 1-4":PRINT@160,V$:PRINT" 1> ADD AN ENTRY":PRINT" 2> DELETE AN ENTRY":PRINT" 3> EDIT OR VIEW A FILE":PRINT" 4> LIST CHART OF ACCOUNTS":PRINT" R> RETURN TO MAIN MENU":PRINTV$
50 L$=INKEY$:IFL$="R"THENGOTO36
52 L=VAL(L$):IFL<1ORL>4THEN50
54 ONL GOTO58,112,128,200
56 'ADD A FILE
58 CLS:PRINT@11,"ADD A FILE":GOSUB452:PRINT@136,"FILE NUMBER: "C+1:PRINT
60 PRINTBA$":";:LINEINPUTA$(C)
62 IFLEN(A$(C))<(W)THENA$(C)=A$(C)+W$ELSE66
64 GOTO62
66 PRINTBB$":";:LINEINPUTB$(C)
68 IFLEN(B$(C))<(W)THENB$(C)=B$(C)+W$ELSE72
70 GOTO68
72 PRINTBC$":";:LINEINPUTC$(C)
74 IFLEN(C$(C))<(W)THENC$(C)=C$(C)+W$ELSE78
76 GOTO74
78 PRINTBD$":";:LINEINPUTD$(C)
80 IFLEN(D$(C))<(W)THEND$(C)=D$(C)+W$ELSE84
82 GOTO80
84 PRINTBE$":";:LINEINPUTE$(C)
86 IFLEN(E$(C))<(W*2)THENE$(C)=E$(C)+W$ELSE90
88 GOTO86
90 IFLEN(A$(C))>(W)THEN58
92 IFLEN(B$(C))>(W)THEN58
94 IFLEN(C$(C))>(W)THEN58
96 IFLEN(D$(C))>(W)THEN96
98 IFLEN(E$(C))>(W*2)THEN58
100 X=C:GOSUB446
102 PRINT"IS THIS CORRECT <Y/N>"
104 L$=INKEY$:IFL$="Y"THEN108
106 IFL$="N"THEN58ELSE104
108 C=C+1:GOTO48
110 'DELETE A FILE
112 CLS:PRINT@128,V$:PRINT" *        DELETE A FILE       *":PRINT" * PRESS <R> TO RETURN TO MENU*":PRINT" *     OR <C> TO CONTINUE     *":PRINTV$
114 L$=INKEY$:IFL$="C"THEN118
116 IFL$="R"THEN36ELSE114
118 INPUT"RECORD # TO BE DELETED :";D
120 FORX=(D-1)TOC-1
122 A$(X)=A$(X+1):B$(X)=B$(X+1):C$(X)=C$(X+1):D$(X)=D$(X+1):E$(X)=E$(X+1)
124 NEXTX:CLS:PRINT@232,"FILE DELETED":FORT=1TO800:NEXTT:C=C-1:GOTO36
126 'EDIT A FILE
128 CLS:GOSUB452:PRINT:INPUT" ENTER FILE NUMBER :";E:X=E-1:IF(X)>(C)THEN128ELSEGOSUB446
130 PRINT"     EDIT WHICH FIELD <1-5>":PRINT"     OR PRESS <R> TO RETURN"
132 L$=INKEY$:IFL$=""THEN132
134 IFL$="R"THEN36
136 L=VAL(L$):IFL<1ORL>5THENGOTO132
138 ONL GOTO140,156,166,176,186
140 PRINTBA$":";:LINEINPUTA$(X)
142 IFLEN(A$(X))<(W)THENA$(X)=A$(X)+W$ELSE146
144 GOTO142
146 IFLEN(A$(X))>(W)THEN128ELSE148
148 GOSUB446
150 PRINT"IS THIS CORRECT <Y/N>"
152 L$=INKEY$:IFL$="Y"THEN36
154 IFL$="N"THEN130ELSE152
156 PRINTBB$":";:LINEINPUTB$(X)
158 IFLEN(B$(X))<(W)THENB$(X)=B$(X)+W$ELSE162
160 GOTO158
162 IFLEN(B$(X))>(W)THEN128ELSE164
164 GOSUB446:GOTO150
166 PRINTBC$":";:LINEINPUTC$(X)
168 IFLEN(C$(X))<(W)THENC$(X)=C$(X)+W$ELSE172
170 GOTO168
172 IFLEN(C$(X))>(W)THEN128ELSE174
174 GOSUB446:GOTO150
176 PRINTBD$":";:LINEINPUTD$(X)
178 IFLEN(D$(X))<(W)THEND$(X)=D$(X)+W$ELSE182
180 GOTO178
182 IFLEN(D$(X))>(W)THEN128ELSE184
184 GOSUB446:GOTO150
186 PRINTBE$":";:LINEINPUTE$(X)
188 IFLEN(E$(X))<(W*2)THENE$(X)=E$(X)+W$ELSE192
190 GOTO188
192 IFLEN(E$(X))>(W*2)THEN128ELSE194
194 GOSUB446:GOTO150
196 GOTO48
198 'LIST CHART OF ACCOUNTS
200 CLS0:PRINT@192,"DO YOU WANT A HARD COPY <Y/N> ?"
202 L$=INKEY$:IFL$="Y"THEN206
204 IFL$="N"THEN208ELSE202
206 PRINT#-2,"LIST OF ACCOUNTS":FORY=1TO25:PRINT#-2,AD$(Y):NEXTY:GOTO36
208 CLS:PRINT@7,"CHART OF ACCOUNTS":FORY=1TO25:PRINTAD$(Y):FORT=1TO500:NEXTT:NEXTY:GOTO36
210 'SEARCH FUNCTION
212 CLS:PRINT"        SEARCH FUNCTION"
214 PRINT"DO YOU WANT A HARD COPY <Y/N>"
216 H$=INKEY$:IFH$="N"THEN224
218 IFH$="Y"THEN220ELSE216
220 H=1
222 IFH=1THENGOSUB440
224 LINEINPUT"DATA TO LOOK FOR:";T$
226 IFLEN(T$)<(W)THENT$=T$+W$ELSE230
228 GOTO226
230 PRINT:PRINT"SELECT FIELD TO SEARCH"
232 PRINT" 1> ";BA$:PRINT" 2> ";BB$:PRINT" 3> ";BC$:PRINT" 4> ";BD$:PRINT" R> RETURN TO MAIN MENU"
234 L$=INKEY$:IFL$=""THEN234
236 IFL$="R"THEN36
238 L=VAL(L$)
240 IFL<1ORL>4THEN234ELSE242
242 ONL GOTO244,254,258,262
244 FORX=0TOC-1:IFA$(X)=T$THENGOSUB266ELSE246
246 NEXTX
248 CLS:PRINT@232,"NO MORE FILES":FORT=1TO800:NEXTT
250 IFH=1THENPRINT#-2,CHR$(12)
252 GOTO36
254 FORX=0TOC-1:IFB$(X)=T$THENGOSUB266ELSE256
256 NEXTX:GOTO248
258 FORX=0TOC-1:IFC$(X)=T$THENGOSUB266ELSE260
260 NEXTX:GOTO248
262 FORX=0TOC-1:IFD$(X)=T$THENGOSUB266ELSE264
264 NEXTX:GOTO248
266 IFH=1THENGOSUB270ELSE272
268 RETURN
270 PRINT#-2,A$(X)TAB(10)B$(X)TAB(20)"$"C$(X)TAB(30)D$(X)TAB(40)E$(X)TAB(59)X+1:RETURN
272 GOSUB446:FORT=1TO800:NEXTT:RETURN
274 'REPORT SETUP & MENU
276 CLS0:PRINT@192,"DO YOU WANT A HARD COPY <Y/N> ?"
278 L$=INKEY$:IFL$="Y"THENH=1:GOTO282
280 IFL$="N"THEN282ELSE278
282 CLS3:PRINT"        REPORT GENERATION":PRINT@160,V$:PRINT" 1> REPORT FOR EACH ACCOUNT #":PRINT" 2> REPORT OF ACCOUNT TOTALS":PRINT" 3> REPORT OF ALL TRANSACTIONS      (OR LIST ALL FILES BY #)":PRINT" R> RETURN TO MAIN MENU":PRINTV$
284 FORY=1TO25:AN$(Y)=MID$(AD$(Y),1,3):NEXTY
286 L$=INKEY$:IFL$="R"THEN36
288 L=VAL(L$):IFL<1ORL>3THEN286
290 ONL GOTO294,338,386
292 'REPORT EACH ACCOUNT
294 IFH=1THENGOSUB440:PRINT#-2
296 FORY=1TO25
298 IFH=1ANDY=6THENGOSUB308:GOTO310
300 IFH=1ANDY=10 THENGOSUB308:GOTO310
302 IFH=1ANDY=17THENGOSUB308:GOTO310
304 IFH=1ANDY=21THENGOSUB308:GOTO310
306 GOTO310
308 PRINT#-2,CHR$(12):GOSUB440:PRINT#-2:RETURN
310 IFH=1THENPRINT#-2,TAB(22)AD$(Y)
312 FORX=0TOC-1
314 IFAN$(Y)=MID$(B$(X),1,3)THEN316ELSE322
316 IFH=1THENGOSUB266
318 IFH=0THENGOSUB272
320 U=U+VAL(C$(X))
322 NEXTX:IFU=0THEN324ELSE328
324 IFH=1THENPRINT#-2
326 GOTO332
328 IFH=1THENPRINT#-2,"TOTAL FOR ACCOUNT"TAB(20):PRINT#-2,USING"$#####.##";U:PRINT#-2:U=0
330 IFH=0THENPRINT"TOTAL FOR ACCOUNT ";AN$(Y);" $"U:U=0:FORT=1TO1000:NEXTT
332 NEXTY:IFH=1THENPRINT#-2,CHR$(12)
334 GOTO36
336 'CHART OF ACCOUNTS
338 IFH=1THENPRINT#-2,TAB(25)TITLE$;" CHART OF ACCOUNTS":PRINT#-2:PRINT#-2,U$:PRINT#-2,"ACCOUNT #"TAB(32)"CREDIT"TAB(52)"DEBIT":PRINT#-2,U$
340 INPUT"ENTER CHECKBOOK BALANCE";CH:INPUT"ENTER SAVINGS BALANCE  ";SA:BA=SA+CH
342 CLS:FORY=1TO5:V=30:GOSUB348
344 I=U+I:U=0:NEXTY
346 V=50:GOTO360
348 FORX=0TOC-1
350 IFAN$(Y)=MID$(B$(X),1,3)THENU=U+VAL(C$(X))
352 NEXTX
354 IFH=1THENPRINT#-2,AD$(Y)TAB(V):PRINT#-2,USING"$#####.##";U
356 IFH=0THENPRINTAN$(Y);" $";U
358 RETURN
360 FORY=6TO25:GOSUB348
362 D=D+U:U=0:NEXTY
364 CB=I-D:IFH=0THEN366ELSE374
366 PRINT"TOTAL INCOME      $";I:PRINT"TOTAL EXPENSE     $";D:PRINT"CASH BALANCE      $";CB:PRINT
368 PRINT"CHECKBOOK BALANCE $";CH:PRINT"SAVINGS BALANCE   $";SA:PRINT"CASH BALANCE      $";BA
370 IFINT(BA)=INT(CB)THENPRINT" ******STATEMENT BALANCES******"ELSEPRINT"??DOES NOT BALANCE PLEASE REDO??"
372 FORT=1TO1000:NEXTT:GOTO382
374 IFH=1THENPRINT#-2:PRINT#-2,"TOTAL CREDITS"TAB(30):PRINT#-2,USING"$#####.##";I:PRINT#-2,"LESS TOTAL DEBITS"TAB(30):PRINT#-2,USING"$#####.##";D:PRINT#-2,"CASH BALANCE"TAB(30):PRINT#-2,USING"$#####.##";CB:PRINT#-2
376 PRINT#-2,"CHECKBOOK BALANCE"TAB(30):PRINT#-2,USING"$#####.##";CH:PRINT#-2,"PLUS SAVINGS BALANCE"TAB(30):PRINT#-2,USING"$#####.##";SA:PRINT#-2,"CASH BALANCE"TAB(30):PRINT#-2,USING"$#####.##";BA:PRINT#-2
378 IFINT(CB)=INT(BA)THENPRINT#-2,TAB(20)"*****STATEMENT BALANCES*****"ELSEPRINT#-2,TAB(20)"??TOTALS DO NOT ADD PLEASE REDO??"
380 PRINT#-2,CHR$(12)
382 D=0:I=0:GOTO36
384 'LIST OF DATA FILES
386 IFH=1THENGOSUB440:GOTO442
388 FORX=0TOC-1:GOSUB446:FORT=1TO950:NEXTT:NEXTX:GOTO36
390 'SAVE TO DISK
392 CLS3:PRINT@192," INSERT DATA DISK IN DRIVE #0 ":PRINT" PRESS <C> TO CONTINUE OR <R>    TO RETURN TO MAIN MENU.         "
394 L$=INKEY$:IFL$="C"THEN398
396 IFL$="R"THEN36ELSE394
398 PRINT"SAVE TO DISK-ARE YOU SURE <Y/N>"
400 L$=INKEY$:IFL$="Y"THENGOTO404
402 IFL$="N"THENGOTO36ELSE400
404 CLS:PRINT@140,"SAVING":PRINT@Z,TITLE$:PRINT@267,"DATA FILE"
406 OPEN"O", #1, TITLE$
408 X=0
410 IFA$(X)=""THEN416
412 WRITE#1,A$(X),B$(X),C$(X),D$(X),E$(X)
414 X=X+1:GOTO410
416 CLOSE#1:GOTO36
418 'LOAD FROM DISK
420 CLS3:PRINT@192," INSERT DATA DISK IN DRIVE #0":PRINT" PRESS  <C> TO LOAD DATA OR <R>  TO RETURN TO MAIN MENU."
422 L$=INKEY$:IFL$="C"THEN426
424 IFL$="R"THENGOTO36ELSE422
426 CLS:PRINT@140,"LOADING":PRINT@Z,TITLE$:PRINT@267,"DATA FILE"
428 OPEN "I", #1, TITLE$
430 X=0
432 INPUT #1,A$(X),B$(X),C$(X),D$(X),E$(X):X=X+1
434 IFEOF(1)=-1THEN436ELSE432
436 CLOSE #1:C=X:N=C:GOTO36
438 'PRINTER SUBROUTINE
440 PRINT#-2,TAB(32)TITLE$:PRINT#-2,U$:PRINT#-2,BA$TAB(10)BB$TAB(20)BC$TAB(30)BD$TAB(40)BE$TAB(59)BF$:PRINT#-2,U$:RETURN
442 FORX=0TOC-1:PRINT#-2,A$(X)TAB(10)B$(X)TAB(20)"$"C$(X)TAB(30)D$(X)TAB(40)E$(X)TAB(59)X+1:NEXTX:PRINT#-2,CHR$(12):GOTO36
444 'PRINTER SUBROUTINE
446 CLS:PRINT"     THIS IS FILE NUMBER "X+1:PRINT:PRINTA$(X):PRINTB$(X):PRINTC$(X):PRINTD$(X):PRINTE$(X):RETURN
448 PCLEAR1:GOTO10
450 'DATA OVERFLOW SUBROUTINE
452 PRINT" FIRST 4 ENTRIES ARE LIMITED TO  8 CHARACTERS EACH, AND COMMENTS ARE LIMITED TO 16 CHARACTERS.":RETURN
454 'DATA DESCRIPTIONS
456 DATA JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC,100 CASH BALANCE,110 EARNED INCOME,120 INTEREST INCOME,130 MISC. TAXABLE INCOME,140 NON-TAXABLE INCOME,200 FEDERAL WITHOLDING,201 FICA,202 STATE WITHOLDING,203 LOCAL WITHOLDING 
458 DATA 210 GROCERY EXPENSE,220 WATER,230 MORTGAGE,240 GAS & ELECTRIC,250 TELEPHONE,260 CAR EXPENSE,270 INSURANCE
460 DATA 280 HOUSE REPAIRS,281 ANNUITY,282 MISC. EXPENSE,283 LOAN PAYMENTS,300 MEDICAL EXPENSE,310 CONTRIBUTIONS,320 INTEREST PAID,330 TAXES PAID,340 MISC. (TAX DED.)
10 'HOME BUDGET ANALYSIS
15 'WRITTEN BY GLEN DUFUR
20 'COPYRIGHT FEBRUARY 1991
25 'BY FALSOFT, INC.
30 'RAINBOW MAGAZINE
40 CLS:PRINT" WAIT...":GOTO3690
50 '--- SURE CURSOR
60 GOSUB160:PRINT@466,"y/n":
70 POKE(1024+466),32:POKE(1024+468),32:K$=INKEY$:POKE(1024+466),25:POKE(1024+468),14
80 IFK$="" THEN70 ELSERETURN
90 '--- SELECT CURSOR
100 PRINT@448,STRING$(6,CHR$(128));:K$=INKEY$:PRINT@448,"select";:IFK$="" THEN100 ELSEK=VAL(K$):RETURN
110 '--- SPEED POKES
120 GOSUB150:POKE65495,0:GOSUB150:RETURN
130 GOSUB150:POKE65494,0:GOSUB150:RETURN
140 '--- SOUNDS
150 FORSS=1TO2:SOUND220,1:NEXTSS:RETURN
160 FORSS=1TO9:SOUND220,1:NEXTSS:RETURN
170 '--- BLANKING
180 PRINT@448,STRING$(63," ");:RETURN
190 '--- CALCULATE 
200 IFH$<>"Y" THENRETURN ELSEH$="":GOSUB150:GOSUB180:PRINT@459,"calculating";
210 LB=0:FORX=1TO NE:DT(X)=0:CT(X)=0
220 FORY=1TO DL:DT(X)=DT(X)+DR(Y,X):NEXTY:FORY=1TO CL:CT(X)=CT(X)+CR(Y,X):NEXTY:BL(X)=CT(X)-DT(X):LB=LB+BL(X):NEXTX:RETURN
230 '--- LIST TOTALS
240 PP=384:FORX=I(0)TO I(0)+2:PP=PP+8:PRINT@PP,USING N8$;BL(X);:NEXTX:RETURN
250 '--- LIST DATES
260 P1=I(0)+48:P2=I(0)+1+48:P3=I(0)+2+48:POKE(1024+13),P1:POKE(1024+21),P2:POKE(1024+29),P3:PRINT@41,USINGA5$;PD$(I(0));:PRINT@49,USINGA5$;PD$(I(0)+1);:PRINT@57,USINGA5$;PD$(I(0)+2);:RETURN
270 '--- GET ITEM DATA
280 GOSUB150:GOSUB180:PRINT@449,"      nAME bAL iNCREASE/dECLINECHANGE aVG-AMT  yTD-TOT  rETURN";:GOSUB100
290 IFK$="I" THENGOSUB570:GOTO280
300 IFK$="D" THENGOSUB570:GOTO280
310 IFK$="N" THENGOSUB370:GOTO280
320 IFK$="A" THENGOSUB410:GOTO280
330 IFK$="B" THENGOSUB490:GOTO280
340 IFK$="Y" THENGOSUB460:GOTO280
350 IFK>0 AND K<NE+1 THENRETURN
360 IFK$="R" THENRETURN ELSE280
370 GOSUB180:PRINT@449,"DESC: ";:GOSUB150:LINEINPUT"";XD$:GOSUB180:PRINT@449,"INFO: ";:GOSUB150:LINEINPUT"";XI$:IFXD$="" THENIFIE$="e" THENXD$=DR$(I) ELSEXD$=CR$(I)
380 XD$=XD$+S5$+S5$:XX$="":XX$=LEFT$(XD$,7)+" "+LEFT$(XI$,19):XD$=XX$:PRINT@02,STRING$(28," ");
390 IFIE$="e" THENDR$(I)=XD$:GOSUB600: ELSECR$(I)=XD$:GOSUB640
400 RETURN
410 GOSUB180:PRINT@449,"AVG AMOUNT";:GOSUB150:INPUTXA:IFXA=0 THENXF=1:GOTO430
420 GOSUB180:PRINT@449,"FREQ(1-9)";:GOSUB150:INPUTXF
430 IFXF>9 THEN420 ELSEIFXF<1 THENXF=1
440 IFIE$="e" THENDA(I)=XA:DF(I)=XF:GOSUB530: ELSECA(I)=XA:CF(I)=XF:GOSUB550
450 RETURN
460 GOSUB180:PRINT@449,"YTD BAL";:GOSUB150:INPUTXY
470 IFIE$="e" THENDY(I)=XY ELSECY(I)=XY
480 PRINT@65,USINGN9$;XY;:RETURN
490 GOSUB180:PRINT@449," BEGIN BAL";:GOSUB150:INPUTXB
500 IFIE$="e" THEN520DB(I,0)=XB:XT=0:GOSUB600 ELSECB(I,0)=XB:XT=0:GOSUB640
510 RETURN
520 DB(I,0)=XB:XT=0:GOSUB600:RETURN
530 XX=0:FORX=1TO NE:XX=XX+DR(I,X):NEXTX:IFXX>0 THENGOSUB600:RETURN
540 FORX=1TO NE STEPDF(I):DR(I,X)=DA(I):NEXTX:H$="Y":GOSUB600:RETURN
550 XX=0:FORX=1TO NE:XX=XX+CR(I,X):NEXTX:IFXX>0 THENGOSUB640:RETURN
560 FORX=1TO NE STEPCF(I):CR(I,X)=CA(I):NEXTX:H$="Y":GOSUB640:RETURN
570 GOSUB150:IFIE$="e" THENDB$(I)=K$:GOSUB890:GOSUB600: ELSECB$(I)=K$:GOSUB890:GOSUB640 
580 RETURN
590 '--- LIST DR AMT/BAL
600 PRINT@02,DR$(I);:PRINT@65,USINGN9$;DY(I);:PRINT@75,USINGN7$;DA(I);:POKE1024+51,(DF(I)+48):PRINT@85,USINGN9$;DB(I,0);
610 PP=128:FORX=1TO NE:IFDB$(I)="I" THENDB(I,X)=DB(I,X-1)+DR(I,X) ELSEDB(I,X)=DB(I,X-1)-DR(I,X)
620 PRINT@PP+1,USINGN1$;X;:PRINT@PP+2,">";:PRINT@PP+4,USINGA5$;PD$(X);:PRINT@PP+11,USINGN8$;DR(I,X);:PRINT@PP+21,USINGN9$;DB(I,X);:PP=PP+32:NEXTX:RETURN
630 '--- LIST CR AMT/BAL
640 PRINT@02,CR$(I);:PRINT@65,USINGN9$;CY(I);:PRINT@75,USINGN7$;CA(I);:POKE1024+51,(CF(I)+48):PRINT@85,USINGN9$;CB(I,0);
650 PP=128:FORX=1TO NE:IFCB$(I)="I" THENCB(I,X)=CB(I,X-1)+CR(I,X) ELSE CB(I,X)=CB(I,X-1)-CR(I,X)
660 PRINT@PP+1,USINGN1$;X;:PRINT@PP+2,">";:PRINT@PP+4,USINGA5$;PD$(X);:PRINT@PP+11,USINGN8$;CR(I,X);:PRINT@PP+21,USINGN9$;CB(I,X);:PP=PP+32:NEXTX:RETURN
670 '--- DELETE DR ITEM 
680 GOSUB180:PRINT@460,"SURE?";:GOSUB60:IFK$<>"Y" THENK$="":GOTO1450
690 GOSUB180:PRINT@457,"deleteing item";:DR$(I)="":DB$(I)="":DB(I,0)=0:DA(I)=0:DY(I)=0:DF(I)=0:DL=DL-1
700 FORZ=1TO NE:DR(I,Z)=0:NEXTZ:FORX=I TO DL:IFDR$(X+1)="" THEN720
710 DR$(X)=DR$(X+1):DR$(X+1)="":DA(X)=DA(X+1):DA(X+1)=0:DY(X)=DY(X+1):DY(X+1)=0:DF(X)=DF(X+1):DF(X+1)=0:DB$(X)=DB$(X+1):DB$(X+1)="":DB(X,0)=DB(X+1,0):DB(X+1,0)=0:FORZ=1TO NE:DR(X,Z)=DR(X+1,Z):DR(X+1,Z)=0:NEXTZ
720 NEXTX:H$="Y":GOSUB200:GOTO1050
730 '--- DELETE CR ITEM
740 GOSUB180:PRINT@460,"SURE?";:GOSUB60:IFK$<>"Y" THENK$="":GOTO2080
750 GOSUB180:PRINT@457,"deleteing item";:CR$(I)="":CB$(I)="":CB(I,0)=0:CA(I)=0:CY(I)=0:CF(I)=0:CL=CL-1
760 FORZ=1TO NE:CR(I,Z)=0:NEXTZ:FORX=I TO CL:IFCR$(X+1)="" THEN780
770 CR$(X)=CR$(X+1):CR$(X+1)="":CA(X)=CA(X+1):CA(X+1)=0:CY(X)=CA(X+1):CA(X+1)=0:CF(X)=CF(X+1):CF(X+1)=0:CB$(X)=CB$(X+1):CB$(X+1)="":CB(X,0)=CB(X+1,0):CB(X+1,0)=0:FORZ=1TO NE:CR(X,Z)=CR(X+1,Z):CR(X+1,Z)=0:NEXTZ
780 NEXTX:H$="Y":GOSUB200:GOTO1660
790 '--- GET ITEM AMOUNT
800 GOSUB180:PRINT@449,"PERIOD"K"AMOUNT";:GOSUB150:INPUTXX:IFIE$="e" THENDR(I,K)=XX ELSECR(I,K)=XX
810 H$="Y":RETURN
820 '--- INIT DISPLAY SCREEN
830 CLS:IFIE$="e" THENX1$=CHR$(252):X2$=CHR$(243):XX$="expense" ELSEX1$=CHR$(204):X2$=CHR$(195):XX$="income"+BB$
840 PRINT@0,STRING$(32,BB$);:PRINT@352,STRING$(32,X1$);:PRINT@416,STRING$(32,X2$);
850 PRINT@0,XX$;:PRINT@7," ";:PRINT@15," ";:PRINT@23," ";:PRINT@31," ";:PRINT@09,"per";:PRINT@17,"per";:PRINT@25,"per";
860 PRINT@40,CHR$(133);:PRINT@46,CHR$(138);:PRINT@48,CHR$(133);:PRINT@54,CHR$(138);:PRINT@56,CHR$(133);:PRINT@62,CHR$(138);
870 PRINT@32,"display";:PRINT@384,BB$;"avlbl";BB$;:PRINT@449,"      arrws a-i nEW sAVE oPN x  mENU  SCRLL SEL ITM pRNT PER ^";:POKE(1024+462),45:RETURN
880 '--- INIT ITEM SCREEN
890 CLS:XX$=STRING$(8,BB$):PRINT@1,CHR$(141);:PRINT@30,CHR$(142);:PRINT@33,XX$;:PRINT@41,CHR$(131);:PRINT@34,"ytd";:PRINT@38,"tot";
900 PRINT@52,CHR$(130);:PRINT@53,STRING$(10,BB$);:PRINT@116,CHR$(138);:PRINT@117,STRING$(10,BB$);
910 PRINT@42,STRING$(10,BB$);:PRINT@106,STRING$(10,BB$);:PRINT@97,STRING$(8,BB$);
920 PRINT@98,"period";:PRINT@108,"amount";:PRINT@118,"balance";:PRINT@42,"avg";:PRINT@46,"evry";:IFIE$="e" THEN940
930 IFCB$(I)="I" THEN950 ELSE960
940 IFDB$(I)="I" THEN950 ELSE960
950 PRINT@53,"increasing";:GOTO970
960 PRINT@53,"declining";
970 PRINT@416,STRING$(32,X2$);:PRINT@448,"              cHANGE nEW ITEM   ACTION    PER dELETE rETURN";
980 POKE(1024+459),45:POKE(1024+458),49:POKE(1024+460),57:RETURN
990 '--- FILE FULL
1000 GOSUB180:PRINT@458,"file full";:SOUND1,7:RETURN
1010 '--- SCROLL DR MONEY
1020 FORY=I(0) TO (I(0)+2):IFDR(X,Y)<>0 THENPRINT@PX,USING N8$;DR(X,Y);
1030 PX=PX+8:NEXTY:RETURN
1040 '--- DEBIT DISPLAY
1050 IE$="e":GOSUB830:GOSUB260:GOSUB240
1060 IFDL=0 THENPRINT@163,"enter"BB$;"expense"BB$"items";:PRINT@229,"hit"BB$"n"BB$"to"BB$"begin";:GOTO1110
1070 PP=64:PX=PP:IX=1:FORX=I(1) TO I(1)+(SE-1):PRINT@PP+0,STRING$(32,CHR$(143));
1080 IFX<(EN+1) THEN1090 ELSE1100
1090 PX=PP:PRINT@PP+0,IX$(IX);:PX=PX+1:PRINT@PX,USINGA6$;DR$(X);:PX=PX+7:GOSUB1020
1100 I(IX)=X:IX=IX+1:PP=PP+32:NEXTX:SOUND200,1
1110 I=0:GOSUB100
1120 IFK$=CHR$(09) THENIFI(0)=NE-2 THEN1430 ELSE1380
1130 IFK$=CHR$(08) THENIFI(0)=1 THEN1430 ELSE1390
1140 IFK$=CHR$(21) THENIFI(0)=1 THEN1430 ELSEI(0)=1:GOTO1400
1150 IFK$=CHR$(93) THENIFI(0)=NE-2 THEN1430 ELSEI(0)=NE-2:GOTO1400
1160 IFK$=CHR$(95) THENIFI(1)=1 THEN1430 ELSEI(1)=1:GOTO1070
1170 IFK$=CHR$(91) THENIFI(1)+(SE-1)=DL THEN1430 ELSEIFDL<=(SE-1) THEN1430 ELSEI(1)=DL:GOTO1330
1180 IFK$=CHR$(10) THENIFI(1)+(SE-1)=DL THEN1430 ELSEIFDL<=SE THEN1430 ELSE1320
1190 IFK$=CHR$(94) THENIFI(1)=1THEN1430 ELSE1350
1200 IFK$="M" THEN3290
1210 IFK$="N" THEN1590
1220 IFK$="X" THENI(1)=1:GOTO1660
1230 IFDL=0 THEN1300
1240 IFK$="S" THENIO=2:GOSUB2480:GOSUB200:GOTO1050
1250 IFK$="O" THENGOSUB2260:I(1)=1:GOTO1050
1260 IFK$="P" THENGOSUB2710:I(1)=1:GOTO1050
1270 FORX=1TO SE:IFK$=I$(X) THENI=I(X)
1280 NEXTX:IFI=0 THEN1300 ELSEIFI>EN THEN1300
1290 IFDR$(I)<>"" THEN1450
1300 SOUND1,1:GOTO1110
1310 '--- SCROLL DR UP&DOWN
1320 I(1)=I(1)+(SE-1)
1330 IF(I(1)+(SE-1))>DL THENI(1)=DL-(SE-1):IFI(1)<1 THENI(1)=1
1340 GOTO1070
1350 I(1)=I(1)-(SE-1):IFI(1)<1 THENI(1)=1
1360 GOTO1070
1370 '--- SCROLL DR SIDEWAYS
1380 I(0)=I(0)+1:GOTO1400
1390 I(0)=I(0)-1:IFI(0)<1 THENI(0)=1
1400 GOSUB260:GOSUB240:PP=72:FORX=I(1)TO (I(1)+(SE-1)):PRINT@PP,STRING$(23,CHR$(143));
1410 IFX<(EN+1) THENPX=PP:GOSUB1020
1420 PP=PP+32:NEXTX
1430 SOUND200,1:GOTO1110
1440 '--- DR ITEM UPDATE
1450 SOUND200,1:GOSUB890:GOSUB600
1460 GOSUB100
1470 IF K>0 AND K<NE+1 THENGOSUB800:GOTO1450
1480 IFK$="M" THENGOSUB200:GOTO3290
1490 IFK$="R" THENGOSUB200:GOTO1050
1500 IFK$="D" THEN680
1510 IFK$="C" THENGOSUB280:IFK>0 AND K<NE+1 THEN1470 ELSEIFK$="R" THEN1490 ELSEIFK$="X" THEN1550 ELSE1450
1520 IFK$="N" THEN1590
1530 IFK$="P" THENGOSUB200:GOSUB2710:GOTO1050
1540 IFK$="S" THENIO=2:GOSUB2480:GOSUB200:GOTO1050
1550 IFK$="X" THENI(1)=1:GOSUB200:IFIE$="e" THEN1660 ELSE1050
1560 IFK$="O" THENGOSUB2260:GOTO1050
1570 SOUND1,1:GOTO1460
1580 '--- NEW DR
1590 DL=DL+1:IFDL>EN THENDL=EN:GOSUB1000:GOSUB200:GOTO1050
1600 I=DL:I(1)=I-(SE-1):IFI(1)<1 THENI(1)=1
1610 DR$(I)="*** NEW ***":DA(I)=0:DB(I,0)=0:DY(I)=0:DF(I)=1:DB$(I)="I":GOSUB890:GOSUB600:K$="C":GOTO1510
1620 '--- SCROLL CR MONEY
1630 FORY=I(0)TO I(0)+2:IFCR(X,Y)<>0 THENPRINT@PX,USINGN8$;CR(X,Y);
1640 PX=PX+8:NEXTY:RETURN
1650 '--- CR DISPLAY
1660 IE$="i":GOSUB830:GOSUB260:GOSUB240
1670 IFCL=0 THENPRINT@163,"enter"BB$;"income"BB$"items";:PRINT@229,"hit"BB$"n"BB$"to"BB$"begin";:GOTO1730
1680 PP=64:PX=PP:IX=1
1690 FORX=I(1) TO (I(1)+(SE-1)):PRINT@PP+0,STRING$(32,CHR$(143));
1700 IFX<(IN+1) THEN1710 ELSE1720
1710 PX=PP:PRINT@PP+0,IX$(IX);:PX=PX+1:PRINT@PX,USINGA6$;CR$(X);:PX=PX+7:GOSUB1630
1720 I(IX)=X:IX=IX+1:PP=PP+32:NEXTX:SOUND200,1
1730 I=0:GOSUB100
1740 IFK$=CHR$(09) THENIFI(0)=NE-2 THEN2060 ELSE2010
1750 IFK$=CHR$(08) THENIFI(0)=1 THEN2060 ELSE2020
1760 IFK$=CHR$(10) THENIFI(1)+(SE-1)=CL THEN2060 ELSEIFCL<=SE THEN2060 ELSE1950
1770 IFK$=CHR$(21) THENIFI(0)=1 THEN2060 ELSEI(0)=1:GOTO2030
1780 IFK$=CHR$(93) THENIFI(0)=NE-2 THEN2060 ELSEI(0)=NE-2:GOTO2030
1790 IFK$=CHR$(95) THENIFI(1)=1 THEN2060 ELSEI(1)=1:GOTO1680
1800 IFK$=CHR$(91) THENIFI(1)+(SE-1)=CL THEN2060 ELSEIFCL<=(SE-1) THEN2060 ELSEI(1)=CL:GOTO1960
1810 IFK$=CHR$(94) THENIFI(1)=1 THEN2060 ELSE1980
1820 IFK$="N" THEN2220
1830 IFK$="X" THENI(1)=1:GOTO1050
1840 IFK$="M" THEN3290
1850 IFCL=0 THEN1930
1860 IFK$="S" THENIO=2:GOSUB2480:GOSUB200:GOTO1660
1870 IFK$="O" THENGOSUB2260:GOTO1660
1880 IFK$="P" THENGOSUB2710:I(1)=1:GOTO1660
1890 IFK$="M" THEN3290
1900 FORX=1TO SE:IFK$=I$(X) THENI=I(X)
1910 NEXTX:IFI=0 THEN1930 ELSEIFI>CL THEN1930
1920 IFCR$(I)<>"" THEN2080 
1930 SOUND1,1:GOTO1730
1940 '--- SCROLL CR UP/DOWN
1950 I(1)=I(1)+(SE-1)
1960 IFI(1)+(SE-1)>CL THENI(1)=CL-(SE-1):IFI(1)<1 THENI(1)=1
1970 GOTO1680
1980 I(1)=I(1)-(SE-1):IF I(1)<1 THENI(1)=1
1990 GOTO1680
2000 '--- SCROLL CR SIDEWAYS
2010 I(0)=I(0)+1:GOTO2030
2020 I(0)=I(0)-1:IFI(0)<1 THENI(0)=1
2030 GOSUB260:GOSUB240:PP=72:FORX=I(1) TO (I(1)+(SE-1))
2040 PRINT@PP,STRING$(23,CHR$(143));:IFX<(IN+1) THENPX=PP:GOSUB1630
2050 PP=PP+32:NEXTX
2060 SOUND200,1:GOTO1730
2070 '--- CR ITEM UPDATE
2080 SOUND200,1:GOSUB890:GOSUB640
2090 GOSUB100
2100 IFK>0 ANDK<NE+1 THENGOSUB790:GOTO2080
2110 IFK$="M" THENGOSUB200:GOTO3290
2120 IFK$="R" THENGOSUB200:GOTO1660
2130 IFK$="D" THEN740
2140 IFK$="C" THENGOSUB280:IFK>0 AND K<NE+1 THEN2100 ELSEIFK$="R" THEN2120 ELSEIFK$="X" THEN2180 ELSE2080
2150 IFK$="N" THEN2220
2160 IFK$="S" THENIO=2:GOSUB2480:GOSUB200:GOTO1660
2170 IFK$="P" THENGOSUB200:GOSUB2710:GOTO1660
2180 IFK$="X" THENI(1)=1:GOSUB200:IFIE$="e" THEN1660 ELSE1050
2190 IFK$="O" THENGOSUB2260:GOTO1660
2200 SOUND1,1:GOTO2090
2210 '--- NEW CR
2220 CL=CL+1:IFCL>IN THENCL=IN:GOSUB1000:GOSUB200:GOTO1660
2230 I=CL:I(1)=CL-(SE-1):IFI(1)<1 THENI(1)=1
2240 CR$(I)="*** NEW ***":CA(I)=0:CB$(I)="I":CB(I,0)=0:CF(I)=1:CY(I)=0:GOSUB890:GOSUB640:K$="C":GOTO2140
2250 '--- OPEN NEW PERIOD
2260 CLS:SOUND200,1:PRINT@33,STRING$(30,CHR$(140)):I(0)=1:PRINTTAB(3)"SHIFT AND OPEN NEW PERIOD":PRINT TAB(1)STRING$(30,CHR$(131)):GOSUB2430
2270 PP=196:Y=0:FORX=1TO3:PRINT@PP,USINGA5$;PD$(X+Y);:PRINT@PP+9,USINGA5$;PD$(X+1+Y);:PRINT@PP+18,USINGA5$;PD$(X+2+Y);:PP=PP+64:Y=Y+2:NEXTX
2280 IFCL=0 OR DL=0 THENPRINT@421,"MUST ENTER AT LEAST 1          INCOME AND EXPENSE ITEM";:K$=INKEY$:IFK$="" THEN2280 ELSERETURN
2290 PRINT@460,"SURE?";:GOSUB60:IFK$<>"Y" THENGOSUB200:RETURN
2300 GOSUB2390:PRINT@384,"    OPEN PERIOD FOR: ";XX$;:GOSUB120:GOSUB180:PRINT@448,"        shifting periods";
2310 FORX=1TO DL:DY(X)=DY(X)+DR(X,1):IFDB$(X)="I" THENDB(X,0)=DB(X,0)+DR(X,1) ELSEDB(X,0)=DB(X,0)-DR(X,1)
2320 FORY=1TO NE-1:DR(X,Y)=DR(X,Y+1):NEXTY:DR(X,NE)=0:IFDF(X)=1 THENDR(X,NE)=DA(X):NEXTX:GOTO2340 ELSE2330
2330 Z1=0:FORZ=NE-(DF(X)-1) TO NE-1:Z1=Z1+DR(X,Z):NEXTZ:IFZ1>0 THENNEXTX ELSEDR(X,NE)=DA(X):NEXTX
2340 FORX=1TO CL:CY(X)=CY(X)+CR(X,1):IFCB$(X)="I" THENCB(X,0)=CB(X,0)+CR(X,1) ELSECB(X,0)=CB(X,0)-CR(X,1)
2350 FORY=1TO NE-1:CR(X,Y)=CR(X,Y+1):NEXTY:CR(X,NE)=0:IFCF(X)=1 THENCR(X,NE)=CA(X):NEXTX:GOTO2370
2360 Z1=0:FORZ=(NE-(CF(X)-1)) TO NE-1:Z1=Z1+CR(X,Z):NEXTZ:IFZ1>0 THENNEXTX ELSECR(X,NE)=CA(X):NEXTX
2370 FORY=1TO NE-1:PD$(Y)=PD$(Y+1):NEXTY
2380 PD$(NE)=XX$:H$="Y":GOSUB200:RETURN
2390 '--- GET DATE
2400 GOSUB180:GOSUB150:PRINT@448,;:LINEINPUT"  PERIOD MONTH (MM): ";XX$:IFXX$="" THEN2400 ELSEXM$=LEFT$(XX$,2)
2410 GOSUB180:GOSUB150:PRINT@448,;:LINEINPUT"    PERIOD DAY (DD): ";XX$:IFXX$="" THEN2410 ELSEXD$=LEFT$(XX$,2):XX$=XM$+"/"+XD$:RETURN
2420 '--- INIT DATES DISPLAY
2430 X$=STRING$(7,BB$):PP=163:Y=0:FORX=1TO3:PRINT@PP,X$;:PRINT@PP+1,"per";:PRINT@PP+09,X$;:PRINT@PP+10,"per";:PRINT@PP+18,X$;:PRINT@PP+19,"per";
2440 POKE(1024+PP+5),(I(0)+X+Y+47):POKE(1024+PP+14),(I(0)+X+Y+1+47):POKE(1024+PP+23),(I(0)+X+Y+2+47)
2450 PRINT@PP+32,CHR$(133);:PRINT@PP+32+6,CHR$(138);:PRINT@PP+32+09,CHR$(133);:PRINT@PP+32+15,CHR$(138);:PRINT@PP+32+18,CHR$(133);:PRINT@PP+32+24,CHR$(138);:PP=PP+64:Y=Y+2:NEXTX
2460 X$=STRING$(7,CHR$(131)):PRINT@PP,X$;:PRINT@PP+09,X$;:PRINT@PP+18,X$;:RETURN
2470 '--- I/O ROUTINES
2480 GOSUB130:SOUND200,1:IFIO=1 THENIO$="load" ELSEIO$="save"
2490 GOSUB180:PRINT@454,IO$+" file - ";PW$;:IFIO=2 THEN2590
2500 '--- LOAD FILE
2510 SOUND220,1:FL$="B"+PW$+".BUD":OPEN"I",#1,FL$:SOUND220,1:INPUT#1,DL,CL
2520 FORX=1TO DL:INPUT#1,DR$(X),DA(X),DB$(X),DB(X,0),DY(X),DF(X)
2530 FORY=1TO NE:INPUT#1,DR(X,Y):NEXTY,X
2540 FORX=1TO CL:INPUT#1,CR$(X),CA(X),CB$(X),CB(X,0),CY(X),CF(X)
2550 FORY=1TO NE:INPUT#1,CR(X,Y):NEXTY,X
2560 FORX=1TO NE:INPUT#1,PD$(X):NEXTX
2570 CLOSE#1:H$="Y":GOSUB120:GOSUB200:RETURN
2580 '--- SAVE FILE
2590 SOUND220,1:OPEN "O",#1,FL$:SOUND220,1:WRITE#1,DL,CL
2600 FORX=1TO DL:WRITE#1,DR$(X),DA(X),DB$(X),DB(X,0),DY(X),DF(X)
2610 FORY=1TO NE:WRITE#1,DR(X,Y):NEXTY,X
2620 FORX=1TO CL:WRITE#1,CR$(X),CA(X),CB$(X),CB(X,0),CY(X),CF(X)
2630 FORY=1TO NE:WRITE#1,CR(X,Y):NEXTY,X
2640 FORX=1TO NE:WRITE#1,PD$(X):NEXTX
2650 CLOSE#1:GOSUB120:RETURN
2660 '--- CREATE NEW FILE
2670 CLS:SOUND200,1:FL$="B"+PW$+".BUD":
2680 PRINT@33,;:PRINTTAB(1)STRING$(30,CHR$(156));:PRINT"      ENTER PERIOD DATES":PRINTTAB(1)STRING$(30,CHR$(147));:GOSUB2430
2690 PP=196:FORX=1TO NE:PRINT@418,"ENTER DATE FOR PERIOD";X;:GOSUB2400:PD$(X)=XX$:PRINT@PP,PD$(X);:PP=PP+9:IFX=3 ORX=6 THENPP=PP+37:NEXTX: ELSENEXTX
2695 GOSUB160:GOTO1660
2700 '--- PRINT ROUTINE
2710 GOSUB180:PRINT@449,"        TBLS:aMT   pYMTS/DUE   PRINT         bAL   rETURN";:GOSUB100
2720 IFK$="A" THENGOSUB2760:GOTO2790
2730 IFK$="P" THENGOSUB2760:GOTO3100
2740 IFK$="B" THENGOSUB2760:GOTO2890 ELSERETURN
2750 '--- SETUP PRINTER CODES
2760 GOSUB130:GOSUB180:PRINT@459,"printing";
2770 POKE150,18:Q0$=CHR$(27)+CHR$(23):Q1$=CHR$(27)+CHR$(66)+CHR$(1):Q2$=CHR$(27)+CHR$(66)+CHR$(0):Q3$=CHR$(27)+CHR$(31):Q4$=CHR$(27)+CHR$(32):PRINT#-2,Q0$;:RETURN
2780 '--- PRINT TABLE
2790 GOSUB3030:PRINT#-2,Q1$;Q3$;S0$;"FORCASTED BUDGET AMOUNTS FOR: ";PW$:LC=LC+1
2800 PRINT#-2:PRINT#-2,S0$;"INCOME AMOUNTS":LC=LC+2:GOSUB3010
2810 FORX=1TO CL:PRINT#-2,S2$;S2$;:PRINT#-2,USINGA8$;CR$(X);:PRINT#-2," ";:FORY=1TO NE:PRINT#-2,USINGN7$;CR(X,Y);:PRINT#-2,S2$;:NEXTY:PRINT#-2:LC=LC+1:GOSUB2990:NEXTX
2820 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S2$;S2$;"TOTAL    ";:FORX=1TO NE:PRINT#-2,USINGN7$;CT(X);:PRINT#-2,S2$;:NEXTX:PRINT#-2,Q2$;Q4$:LC=LC+1:GOSUB2990
2830 PRINT#-2:PRINT#-2,Q1$;Q3$;S0$;"EXPENSE AMOUNTS":LC=LC+2:GOSUB3010
2840 FORX=1TO DL:PRINT#-2,S2$;S2$;:PRINT#-2,USINGA8$;DR$(X);:PRINT#-2," ";:FORY=1TO NE:PRINT#-2,USINGN7$;DR(X,Y);:PRINT#-2,S2$;:NEXTY:PRINT#-2:LC=LC+1:GOSUB2990:NEXTX
2850 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S2$;S2$;"TOTAL    ";:FORX=1TO NE:PRINT#-2,USINGN7$;DT(X);:PRINT#-2,S2$;:NEXTX:PRINT#-2,Q2$;Q4$:LC=LC+1:GOSUB2990
2860 PRINT#-2:PRINT#-2,Q1$;Q3$;S0$;"BUDGET PERIOD BALANCE":LC=LC+2:GOSUB3010
2870 PRINT#-2,Q1$;Q3$;S2$;S2$;"BALANCE  ";:FORX=1TO NE:PRINT#-2,USINGN7$;BL(X);:PRINT#-2,S2$;:NEXTX:PRINT#-2,Q1$;Q3$:LC=LC+1:GOSUB2970:GOSUB120:RETURN
2880 '---- PRINT BALANCES
2890 GOSUB3030:PRINT#-2,Q1$;Q3$;S0$;"FORCASTED BUDGET BALANCES FOR: ";PW$:LC=LC+1
2900 PRINT#-2:PRINT#-2,S0$;"INCOME BALANCES":LC=LC+2:GOSUB3010
2910 FORX=1TO CL:PRINT#-2,S2$;S2$;:PRINT#-2,USINGA8$;CR$(X);:PRINT#-2,"";:FORY=1TO NE:IFCB$(X)="I" THENCB(X,Y)=CB(X,Y-1)+CR(X,Y) ELSECB(X,Y)=CB(X,Y-1)-CR(X,Y)
2920 PRINT#-2,USINGN9$;CB(X,Y);:PRINT#-2,"";:NEXTY:PRINT#-2:LC=LC+1:GOSUB2990:NEXTX
2930 PRINT#-2:PRINT#-2,Q1$;Q3$;S0$;"EXPENSE BALANCES";Q2$;Q4$:LC=LC+2:GOSUB3010
2940 FORX=1TO DL:PRINT#-2,S2$;S2$;:PRINT#-2,USINGA8$;DR$(X);:PRINT#-2,"";:FOR Y=1TO NE:IFDB$(X)="I" THENDB(X,Y)=DB(X,Y-1)+DR(X,Y) ELSEDB(X,Y)=DB(X,Y-1)-DR(X,Y)
2950 PRINT#-2,USINGN9$;DB(X,Y);:PRINT#-2,"";:NEXTY:PRINT#-2:LC=LC+1:GOSUB2990:NEXTX:GOSUB2970:GOSUB120:RETURN
2960 '--- END OF PAGE LINES
2970 FORW=LC TO 65:PRINT#-2:NEXTW:LC=0:RETURN
2980 '--- PAGE
2990 IFLC<60 THENRETURN ELSEGOSUB2970:FORW=1TO 5:PRINT#-2:NEXTW:LC=5:GOSUB3010:RETURN
3000 '--- PRINT DATE LINE
3010 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S2$;S2$;"PERIOD>    ";:FORW=1TO NE:PRINT#-2,PD$(W);S2$;S2$;:NEXTW:PRINT#-2:GOSUB3080:PRINT#-2,Q2$;Q4$;:LC=LC+1:RETURN
3020 '--- PRINT HEADING
3030 FORX=1TO5:PRINT#-2:NEXTX:PRINT#-2,Q1$;Q3$;S0$;"HOME BUDGET ANALYST";:PRINT#-2,Q2$;Q4$:LC=6:RETURN
3040 '--- PRINT TITLE LINE
3050 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S0$;S5$;"         CURRENT     YTD      CURRENT  REMAINING"
3060 PRINT#-2,S5$;S0$;"DESC      AMOUNT   BALANCE    BALANCE   BALANCE":GOSUB3080:PRINT#-2,Q2$;Q4$;:LC=LC+2:RETURN
3070 '--- PRINT DASH LINE
3080 PRINT#-2,S2$;S2$;STRING$(8,"-");:FORW=1TO NE:PRINT#-2,STRING$(9,"-");:NEXTW:PRINT#-2:LC=LC+1:RETURN
3090 '--- PRINT SINGLE PERIOD ROUTINE
3100 GOSUB3030:PRINT#-2,Q1$;Q3$;S0$;"PAYMENTS FOR: ";PW$;S2$;S5$;" BUDGET PERIOD: ";PD$(1):LC=LC+1
3110 PRINT#-2:PRINT#-2,S0$;"INCOME ITEMS":LC=LC+2:GOSUB3050:FORX=1TO CL:IFCR(X,1)=0 THEN3170
3120 PRINT#-2,S5$;S0$;:PRINT#-2,USINGA8$;CR$(X);:PRINT#-2," ";:PRINT#-2,USINGN7$;CR(X,1);:PRINT#-2,S2$;
3130 CY=CY(X)+CR(X,1):PRINT#-2,USINGN9$;CY;:PRINT#-2,S2$;:IFCB$(X)="I" THEN3150
3140 CB(X,1)=CB(X,0)-CR(X,1):PRINT#-2,S5$;S0$;:PRINT#-2,USINGN9$;CB(X,1);:GOTO3160
3150 CB(X,1)=CB(X,0)+CR(X,1):PRINT#-2,USINGN9$;CB(X,1);
3160 PRINT#-2,S5$;S0$;:PRINT#-2:LC=LC+1:GOSUB2990
3170 NEXTX
3180 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S5$;S0$;"TOTAL    ";:PRINT#-2,USINGN7$;CT(1);:PRINT#-2,S2$;:PRINT#-2,Q2$;Q4$:LC=LC+1:GOSUB2990
3190 PRINT#-2:PRINT#-2,Q1$;Q3$;S0$;"EXPENSE ITEMS":LC=LC+2:GOSUB3050
3200 FORX=1TO DL:IFDR(X,1)=0 THEN3260
3210 PRINT#-2,S5$;S0$;:PRINT#-2,USINGA8$;DR$(X);:PRINT#-2," ";:PRINT#-2,USINGN7$;DR(X,1);:PRINT#-2,S2$;
3220 DY=DY(X)+DR(X,1):PRINT#-2,USINGN9$;DY;:PRINT#-2,S2$;:IFDB$(X)="I" THEN3240
3230 DB(X,1)=DB(X,0)-DR(X,1):PRINT#-2,S0$;:PRINT#-2,USINGN9$;DB(X,1);:GOTO3250
3240 DB(X,1)=DB(X,0)+DR(X,1):PRINT#-2,USINGN9$;DB(X,1);
3250 PRINT#-2,S5$;S0$;:PRINT#-2:LC=LC+1:GOSUB2990
3260 NEXTX
3270 PRINT#-2,Q1$;Q3$;:GOSUB3080:PRINT#-2,S5$;S0$;"TOTAL    ";:PRINT#-2,USINGN7$;DT(1);:PRINT#-2,"    BUDGET BALANCE ";:PRINT#-2,USINGN7$;BL(1);:PRINT#-2,S2$;:LC=LC+1:GOSUB2990:GOSUB2970:GOSUB120:RETURN
3280 '--- MENU
3290 CLS:SOUND200,1:PRINT@64,"";:GOSUB3560
3300 PRINT@256,"   sAVE FILE     pRINT             oPEN NEW PER  rEAD NEW FILE     iNCOME DSPLY  eXPENSE DSPLY     qUIT";
3310 PRINT@419,"CURRENT FILE - ";PW$;
3320 GOSUB3650:IFK$="" THEN3320
3330 IFK$="S" THENIO=2:GOSUB2480:GOSUB200:GOTO3290
3340 IFK$="P" THENGOSUB2710:I(1)=1:GOTO3290
3350 IFK$="O" THENGOSUB2260:GOTO3290
3360 IFK$="R" THENRUN
3370 IFK$="I" THENI(1)=1:GOTO1660
3380 IFK$="E" THENI(1)=1:GOTO1050 
3390 IFK$="X" THENGOSUB130:SAVE"BUD.BAS":GOSUB120:GOTO3320
3400 IFK$="Q" THENGOSUB130:CLS:PRINT"BUDGET PROGRAM TERMINATED":END
3410 GOTO3320
3420 '--- PROGRAM INIT
3430 CLEAR1100:GOSUB120:EN=45:IN=15:NE=9:SE=9
3440 DIMDR$(EN),DA(EN),DB$(EN),DY(EN),DF(EN)
3450 DIMCR$(IN),CA(IN),CB$(IN),CY(IN),CF(IN)
3460 DIMDR(EN,NE),DB(EN,NE):DIMCR(IN,NE),CB(IN,NE)
3470 DIMPD$(NE),BL(NE),DT(NE),CT(NE)
3480 DIMI$(SE),I(SE),IX$(SE)
3490 I(0)=1:DL=0:CL=0
3500 FORX=1TO SE:READIX$(X),I$(X),I(X):NEXTX
3510 DATA a,A,1,b,B,2,c,C,3,d,D,4,e,E,5,f,F,6,g,G,7,h,H,8,i,I,9
3520 N1$="#":N7$="####.##":N8$="####.##-":N9$="#####.##-"
3530 A5$="%   %":A6$="%     %":A8$="%      %"
3540 BB$=CHR$(128):S2$="  ":S5$="     ":S0$="          ":GOSUB3560:GOTO3590
3550 '--- TITLE SCREEN
3560 SOUND200,1:CLS:PRINT@100,"home";BB$;"budget";BB$;"analyst";BB$;BB$:POKE(1024+121),52:POKE(1024+122),46:POKE(1024+123),48:PRINT
3570 PRINT"    BY: GLEN DUFUR":PRINT"    COPYRIGHT (C) 1989":RETURN
3580 '--- PASSWORD
3590 PRINT:PRINTTAB(4):LINEINPUT"PASSWORD: ";PW$:PW$=LEFT$(PW$,5)
3600 PRINT@324,"lOAD EXISTING BUDGET":PRINT@388,"cREATE NEW BUDGET":PRINT@456,"SELECT OPTION"
3610 GOSUB3650:IFK$="" THEN3610
3620 IFK$="C" THEN2670
3630 IFK$="L" THENIO=1:GOSUB2480:GOTO1660
3640 SOUND1,5:GOTO3610
3650 IFXX$=CHR$(145) THENXX$=CHR$(146):XY$=CHR$(148):XZ$=CHR$(150): ELSEXX$=CHR$(145):XY$=CHR$(152):XZ$=CHR$(153)
3660 PRINT@66,CHR$(138);:PRINTSTRING$(26,XX$);CHR$(133);:PRINT@130,CHR$(138);STRING$(26,XY$);CHR$(133);
3670 K$=INKEY$:PRINT@99,BB$;:PRINT@124,BB$;:RETURN
3680 '---PCLEAR ROUTINE
3690 PCLEAR1:GOTO3430
