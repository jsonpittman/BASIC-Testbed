0 ' COPYRIGHT 1989  FALSOFT,INC
10 PCLEAR4
20 INPUT"OUTPUT FILE NAME";OF$
30 INPUT"LEFTMOST X";XL
40 INPUT"RIGHTMOST X";XR
50 INPUT"TOPMOST Y";YT
60 INPUT"BOTTOMMOST Y";YB
70 INPUT"BEGINNING LINE NUMBER";K
80 LG=INT(XL/8):RG=INT(XR/8)
90 IF XR/8<>INT(XR/8) THEN ODD=1 ELSE ODD=0
100 M1=INT((XR-XL-1)/8):M2=YB-YT
110 DIM V(M1,M2),N(15)
120 PMODE4
130 SCREEN1,1
140 PCLS
150 COLOR 1
160 LINE (XL,YT)-(XR,YB),PSET,BF
170 X=XL:Y=YT:CO=1
180 A$=INKEY$:IF A$<>"" GOTO 220
190 PSET(X,Y)
200 A$=INKEY$:IF A$<>"" GOTO 220
210 PRESET(X,Y):GOTO 180
220 IF A$="P" THEN CO=1 ELSE IF A$="R" THEN CO=0
230 IF CO=1 THEN PSET(X,Y) ELSE PRESET(X,Y)
240 IF(A$=CHR$(9) AND X+1<=XR) THEN X=X+1
250 IF(A$=CHR$(8) AND X-1>=XL) THEN X=X-1
260 IF(A$=CHR$(10) AND Y+1<=YB) THEN Y=Y+1
270 IF(A$=CHR$(94) AND Y-1>=YT) THEN Y=Y-1
280 IF(A$="S") THEN 300
290 GOTO 180
300 OPEN "O",#1,OF$
310 PRINT #1,K;" DATA ";M1;",";M2;",";XL;",";XR;",";YT;",";YB;",";LG;",";RG
320 T=0:K=K+2
330 FOR I=0 TO M2
340 FOR J=LG TO RG
350 AD=&HE00+32*(YT+I)+J
360 N(T)=PEEK(AD)
380 T=T+1:IF T<=15 THEN GOTO 410
390 GOSUB 460
400 T=0:K=K+2
410 NEXT J
420 NEXT I
430 IFT<=15 THEN GOSUB 460
440 CLOSE #1
450 END
460 PRINT #1,K;" DATA ";N(0);",";N(1);",";N(2);",";N(3);",";N(4);",";N(5);",";N(6);",";N(7);",";N(8);",";N(9);",";N(10);",";N(11);",";N(12);",";N(13);",";N(14);",";N(15)
470 RETURN
