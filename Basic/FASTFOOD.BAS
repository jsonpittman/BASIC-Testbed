10 REM"ESTIMATING EXPENSES"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1988"
30 CLS5:IF CT=10 THEN 370
40 J=RND(4)+2:D=RND(-TIMER)
50 PRINT@3,"YOUR MENU";:IF CT=9 THEN PRINT@28,CT+1; ELSE PRINT@25,"#";CT+1;
60 LA=0:ZA=0:F=0:YE=1:AA=0:BB=0
70 FOR T=1056TO1087:POKET,243:NEXT:SOUND100,3
80 CH=120+RND(40):PRINT@98,"CHICKEN-$";:PRINTUSING"#.##";CH/100;
90 SA=70+RND(20):PRINT@162,"SALAD  -$";:PRINTUSING"#.##";SA/100;
100 SD=32+RND(20):PRINT@114,"SODA  -$";:PRINTUSING"#.##";SD/100;
110 CO=40+RND(20):PRINT@178,"COFFEE-$";:PRINTUSING"#.##";CO/100;
120 FOR T=1248 TO 1279
130 POKET,252:NEXTT:SOUND200,2
140 M=70+RND(30)*10:M=M/100
150 GOSUB 310:PRINT@288,"YOU WANT TO BUY";J;N$;"S."
160 PRINT@352,"IS $";
170 PRINTUSING"#.##";M;
180 PRINT" ENOUGH FOR THIS ? ";
190 Z$=INKEY$
200 IF Z$="Y" OR Z$="N" THEN 210 ELSE 190
210 PRINTZ$:CT=CT+1
220 IF Z$="Y"AND M*100>J*AA THEN 260
230 IF Z$="Y" AND M*100=J*AA THEN 260 
240 IF Z$="N" AND M*100<J*AA THEN 260
250 GOTO 280
260 RI=RI+1:PRINT@428,"CORRECT";
270 FOR T=200TO255STEP11:SOUNDT,1:NEXTT:GOTO 290
280 PRINT@428,"SORRY";:SOUND 10,3
290 PRINT@484,"PRESS <ENTER> TO GO ON ";
300 EN$=INKEY$:IF EN$=CHR$(13) THEN 30 ELSE 300
310 RN=RND(4)
320 IF RN=1THEN N$="CHICKEN":AA=CH
330 IF RN=2 THEN N$="SALAD":AA=SA
340 IF RN=3 THEN N$="SODA":AA=SD
350 IF RN=4 THEN N$="COFFEE":AA=CO
360 RETURN
370 CLS8:FOR T=1024TO1055:POKET,214:PLAY"L100;G":NEXT:PRINT@37,"HERE IS YOUR SCORECARD";
380 FOR T=1119TO1088STEP-1:POKET,214:PLAY"L100;A":NEXTT
390 PRINT@202,"SCORE = ";10*RI;"%";
400 FOR T=1344TO1375:POKET,214:PLAY"L100;A":NEXT T
410 PRINT@416," ":PRINT@448," ":PRINT@418,"DO YOU WANT TO PLAY AGAIN? ";
420 EN$=INKEY$
430 IF EN$="Y" THEN RUN ELSE IF EN$="N" THEN END ELSE 420
