1 '************************************
2 '**        The Post Office         **
3 '**    By Orman Cyril Beckles II   **
4 '**                                **
5 '**       (C) Copyright 1987       **
6 '**                                **
7 '************************************
8 '
9 '
10 GOTO 6730:'                           <-- DOES A PCLEAR 1      
11 RGB:CLEAR 2000:FILES 2,332:DIM S(949):TZ=5:ON ERR GOTO 7210
12 HI=65497:'                            <-- HI SPEED POKE
13 W=80    :'                            <-- DEFAULT WIDTH
14 TB=20   :'                            <-- DEFAULT TAB  
15 POKE 150,1:'                          <-- BAUD RATE 
16 EL=2:'                                <-- # OF LINES BETWEEN LABELS
17 PC=0:'                                <-- PRINT COUNTRY 0=NO 1=YES
18 DC=TB+13:'                            <-- DEFAULT COLUMN START
19 KK=1:'                                <-- KEY CLICK
20 WIDTH W:PALETTE 0,0:ON BRK GOTO 20:CLS1:ATTR 1,0:CLS:GOSUB 6100
25 LOCATE TB+6,6:PRINT"Number Of Free Records =";INT(FREE(0)*13)
30 GOSUB 6000:NF=LOF(1):LOCATE 0,20:PRINT:CLOSE #1:LOCATE 0,7:PRINTTAB(TB+6)"Number Of Used Records =";NF
35 LOCATE 0,12
40 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"A";:ATTR 1,0:PRINT">";:ATTR 3,0:PRINT" ADD    Address(s)";:ATTR 1,0:PRINT
45 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"D";:ATTR 1,0:PRINT"> ";:ATTR 3,0:PRINT"DELETE Address(s)";:ATTR 1,0:PRINT
50 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"E";:ATTR 1,0:PRINT">";:ATTR 3,0:PRINT" EDIT   Address(s)";:ATTR 1,0:PRINT
55 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"F";:ATTR 1,0:PRINT">";:ATTR 3,0:PRINT" FORM   Letter (s)";:ATTR 1,0:PRINT
60 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"P";:ATTR 1,0:PRINT">";:ATTR 3,0:PRINT" PRINT  Routine(s)";:ATTR 1,0:PRINT
65 PRINTTAB(TB+9)"<";:ATTR 3,0:PRINT"X";:ATTR 1,0:PRINT">";:ATTR 3,0:PRINT" EXIT FROM PROGRAM";:ATTR 1,0:PRINT
70 LOCATE 0,22:ATTR 3,2:PRINTTAB(TB+3)"By Orman Cyril Beckles II  (C) 1987":ATTR 1,0:PRINTCHR$(8)
75 GOSUB 7190
80 IF IK$="A" THEN 1000:' <-- "ADD" MODULE
85 IF IK$="D" THEN 2000:' <-- "DEL" MODULE
90 IF IK$="E" THEN 3000:' <-- "EDT" MODULE
95 IF IK$="F" THEN 4000:' <-- "FOM" MODULE
100 IF IK$="P" THEN 5000:' <-- "PRT" MODULE
105 IF IK$="X" THEN 6725:' <-- "XIT" 
110 GOTO 75
1000 ON BRK GOTO 12:'             <-- START OF ADD MODULE
1005 GOSUB 6100
1010 GOSUB 6500
1015 GOSUB 6000:GOSUB 6015
1020 IF AD(1)=1 AND LEN(V$(4))>1 AND LEN(V$(5))>1 THEN  AD(1)=0:FL(3)=1:GOSUB 7150:GOSUB 7110
1025 GOTO 1000
2000 ON BRK GOTO 12:FL(1)=1:'    <-- START OF DELETE MODULE
2005 GOSUB 6100:GOSUB 6200
2010 LOCATE 0,21
2015 PRINTTAB(TB+11)"PLEASE SELECT FIELD"
2020 PRINTTAB(TB+2)"-> Where deletetion can be found <-"
2025 GOSUB 7190
2030 A=VAL(IK$)
2035 GOSUB 6100
2040 PRINTTAB(TB+8)"ENTER STRING TO DELETE"
2045 PRINT
2050 PRINTTAB(TB+10)"->";:LINEINPUT SD$:GOSUB 6100
2055 GOSUB 6000
2060 FOR T=1 TO LOF(1)
2065 POKE (HI-1),0:GET #1,T:POKE HI,0
2070 S(T)=0
2075 GOSUB 6400
2080 IF FL(2)=1 THEN FL(2)=0:GOSUB 2100:GOTO 2085
2085 NEXT T
2090 POKE (HI-1),0:CLOSE #1:POKE HI,0
2095 GOTO 2190
2100 LOCATE 0,9
2105 PRINTTAB(TB)"       CODE:";DC$
2110 PRINTTAB(TB)"       NAME:";DN$
2115 PRINTTAB(TB)"    ADDRESS:";DA$
2120 PRINTTAB(TB)"       CITY:";DY$
2125 PRINTTAB(TB)"      STATE:";DS$
2130 PRINTTAB(TB)"        ZIP:";DZ$
2135 PRINTTAB(TB)"TELEPHONE #:";DT$
2140 PRINTTAB(TB)"   F,L NAME:";DF$
2145 PRINTTAB(TB)"    COUNTRY:";DO$
2150 IF FL(1)=1 THEN GOTO 2165
2155 S(T)=1
2160 RETURN
2165 GOSUB 7190
2170 IF IK$="Y" OR IK$="y" THEN 2155
2175 IF IK$="N" OR IK$="n" THEN 2160
2180 IF IK$="A" OR IK$="a" THEN FL(1)=0:GOTO 2150
2185 GOTO 2165
2190 'DOUBLE CHECK OF DELETES
2195 GOSUB 6100
2200 LOCATE 0,6:PRINTTAB(TB+14)" DELETIONS"
2205 CV=9:C=1
2210 GOSUB 6000
2215 FOR T=1 TO LOF(1)
2220 IF S(T)=1 THEN 2225 ELSE 2290
2225 POKE (HI-1),0:GET #1,T:POKE HI,0
2230 CV=CV+1
2235 IF CV>21 THEN CV=9
2240 LOCATE 0,7:PRINTTAB(TB+8)"MARKED FOR DELETIONS=";C:C=C+1:LOCATE 1,CV
2245 IF A=1  OR A=0 THEN PRINT TAB(TB+5)A$;" ";B$
2250 IF A=2 THEN PRINT TAB(TB+5) DN$;" ";DC$
2255 IF A=3 AND W=80 THEN PRINTTAB(18) DA$;" ";DN$ ELSE IF A=3 THEN PRINTTAB(2)C$;" "; DC$
2260 IF A=4 AND W=80 THEN PRINT DY$;" "; DN$ ELSE IF A=4 THEN PRINT DY$;" ";DC$
2265 IF A=5 THEN PRINT DS$;" ";DN$
2270 IF A=6 THEN PRINT DZ$;" ";DN$
2275 IF A=7  AND W=80 THEN PRINT DT$;" "; DN$ ELSE IF A=7 THEN PRINT DT$;" "; DC$
2280 IF A=8 AND W=80 THEN PRINT DF$;" "; DN$ ELSE IF A=8 THEN PRINT DF$;" "; DC$
2285 IF A=9 AND W=80 THEN PRINT DO$;" "; DN$ ELSE IF A=9 THEN PRINT DO$;" "; DC$
2290 NEXT T
2295 POKE(HI-1),0:CLOSE#1:POKE HI,0
2300 T=21-CV:FOR Y=1 TO T:LOCATE 1,CV+T:PRINT:NEXT Y:PRINTTAB(TB+4)"DO YOU REALY WANT TO ERASE THESE";
2305 GOSUB 7190
2310 IF IK$="Y" THEN 2325
2315 IF IK$="N" THEN STOP
2320 GOTO 2305
2325 GOSUB 6100:PRINTTAB(TB+8)"* Busy Deleting Records *":GOSUB 6000
2330 POKE (HI-1),0:OPEN "D",#2,"TEMP.FIL",166:POKE HI,1
2335 FIELD #2, 10 AS J$,25 AS K$,25 AS L$,25 AS M$,10 AS N$,7 AS O$,14 AS P$,25 AS Q$,25 AS R$
2340 FOR T=1 TO LOF(1)
2345 IF S(T)=1 THEN 2405
2350 F=F+1:POKE (HI-1),0:GET #1,T:POKE HI,0
2355 LSET J$=DC$
2360 LSET K$=DN$
2365 LSET L$=DA$
2370 LSET M$=DY$
2375 LSET N$=DS$
2380 LSET O$=DZ$
2385 LSET P$=DT$
2390 LSET Q$=DF$
2395 LSET R$=DO$
2400 POKE (HI-1),0:PUT #2,F:POKE HI,0
2405 S(T)=0:NEXT T
2410 CLOSE #1
2415 CLOSE #2
2420 POKE (HI-1),0:KILL"MAILER.FIL":POKE HI,0
2425 POKE (HI-1),0:RENAME "TEMP.FIL" TO "MAILER.FIL":POKE HI,0
2430 GOTO 12
3000 ON BRK GOTO 12:FL(2)=0:' EDIT MODULE
3005 GOSUB 6100:GOSUB 6200
3010 LOCATE 0,21
3015 PRINTTAB(TB+11)"PLEASE SELECT FIELD"
3020 PRINTTAB(TB+2)"-> Where edit string can be found <-"
3025 GOSUB 7190
3030 A=VAL(IK$)
3035 GOSUB 6100
3040 PRINTTAB(TB+10)"ENTER STRING TO EDIT"
3045 PRINT
3050 PRINTTAB(TB+12)"->";:LINEINPUT SD$:IF SD$="" THEN SD$=" "
3055 GOSUB 6000:'OUTPUT ROUTINE
3060 FOR NR=1 TO LOF(1)
3065 POKE (HI-1),0:GET #1,NR:POKE HI,0
3070 S(T)=0
3075 GOSUB 6400
3080 IF FL(2)=1 THEN FL(2)=0:GOTO 3105
3085 GOTO 3125
3090 POKE (HI-1),0:CLOSE #1:POKE HI,0
3095 GOTO 12
3100 GOTO 5280
3105 GOSUB 6100
3110 V$(1)=DC$:V$(2)=DN$:V$(3)=DA$:V$(4)=DY$:V$(5)=DS$:V$(6)=DZ$:V$(7)=DT$:V$(8)=DF$:V$(9)=DO$
3115 GOSUB 6500
3120 LSET DC$=V$(1):LSET DN$=V$(2):LSET DA$=V$(3):LSET DY$=V$(4):LSET DS$=V$(5):LSET DZ$=V$(6):LSET DT$=V$(7):LSET DF$=V$(8):LSET DO$=V$(9):PUT #1,NR
3125 NEXT NR
3130 GOTO 3090
4000 ON BRK GOTO 12:FL(1)=1:' <-- START OF FORM MODULE
4005 GOSUB 6100:GOSUB 6200
4010 LOCATE 0,19
4015 PRINTTAB(TB+11)"PLEASE SELECT FIELD"
4020 PRINTTAB(TB+2)" Where print-out string can be found "
4025 GOSUB 7190
4030 A=VAL(IK$)
4035 GOSUB 6100:LOCATE 0,7
4040 PRINTTAB(TB+10)"ENTER STRING TO PRINT"
4045 PRINT
4050 PRINTTAB(TB+12)"->";:LINEINPUT SD$:IF SD$="" THEN SD$=" "
4055 LOCATE 0,11:PRINTTAB(TB+8)"ENTER NAME OF LETTER FILE"
4060 PRINT:PRINTTAB(TB+12)"->";:LINEINPUT LF$
4065 IF LF$="" THEN 4055
4070 GOSUB 6000:'  <-- OUTPUT ROUTINE
4075 FOR T=1 TO LOF(1)
4080 POKE (HI-1),0:GET #1,T:POKE HI,0
4085 S(T)=0
4090 GOSUB 6400
4095 IF FL(2)=1 THEN FL(2)=0:GOSUB 4115:GOTO 4100
4100 NEXT T
4105 POKE (HI-1),0:CLOSE #1:POKE HI,0
4110 GOTO 4160
4115 LOCATE 0,7:GOSUB 6255
4120 IF FL(1)=1 THEN GOTO 4135
4125 S(T)=1
4130 RETURN
4135 GOSUB 7190
4140 IF IK$="Y" OR IK$="y" THEN 4125
4145 IF IK$="A" OR IK$="a" THEN FL(1)=0:GOTO 4120
4150 IF IK$="N" OR IK$="n" THEN 4130
4155 GOTO 4135
4160 '  <-- DOUBLE CHECK OF DELETES
4165 GOSUB 6100
4170 LOCATE 0,6:PRINTTAB(TB+13)" PRINT-OUT"
4175 CV=9:C=1
4180 GOSUB 6000
4185 FOR T=1 TO LOF(1)
4190 IF S(T)=1 THEN 4195 ELSE 4260
4195 POKE (HI-1),0:GET #1,T:POKE HI,0
4200 CV=CV+1
4205 IF CV>21 THEN CV=9
4210 LOCATE 0,7:PRINTTAB(TB+8)"MARKED FOR PRINTING=";C:C=C+1:LOCATE 1,CV
4215 IF A=1  OR A=0 THEN PRINT TAB(TB+5) DC$;" ";DN$
4220 IF A=2 THEN PRINT TAB(TB+5) DN$;" ";DC$
4225 IF A=3 AND W=80 THEN PRINTTAB(18) DA$;" "; DN$ ELSE IF A=3 THEN PRINTTAB(2)DA$;" ";DC$
4230 IF A=4 AND W=80 THEN PRINT DY$;" "; DN$ ELSE IF A=4 THEN PRINT DY$;" "; DC$
4235 IF A=5 THEN PRINT DS$;" ";DN$
4240 IF A=6 THEN PRINT DZ$;" ";DN$
4245 IF A=7  AND W=80 THEN PRINT DT$;" "; DN$ ELSE IF A=7 THEN PRINT DT$;" "; DC$
4250 IF A=8 AND W=80 THEN PRINT DF$;" "; DN$ ELSE IF A=8 THEN PRINT DF$;" "; DC$
4255 IF A=9 AND W=80 THEN PRINT DO$;" "; DN$ ELSE IF A=9 THEN PRINT DO$;" "; DC$
4260 NEXT T
4265 POKE (HI-1),0:CLOSE#1:POKE HI,0
4270 T=21-CV:FOR Y=1 TO T:LOCATE 1,CV+T:PRINT:NEXT Y:PRINTTAB(TB+0)"ARE YOU SURE YOU WISH TO PRINT THESE";
4275 GOSUB 7190
4280 IF IK$="Y" THEN 4295
4285 IF IK$="N" THEN 4000
4290 GOTO 4275
4295 CLS
4300 GOSUB 6100:PRINTTAB(TB+10)"Working..."
4305 GOSUB 6000
4310 FOR T=1 TO LOF(1)
4315 IF S(T)=1 THEN 4320 ELSE 4480
4320 POKE (HI-1),0:GET #1,T:POKE HI,0
4325 GOSUB 4345
4330 POKE HI-1,0:PRINT#-2,CHR$(12):POKE HI,0:'<-- EJECTS A PAGE
4335 NEXT T
4340 CLOSE #1:GOTO 12
4345 P=INSTR(DF$,","):RF$=LEFT$(DF$,P-1):FOR TF=P TO 25:IF MID$(DF$,TF,1)<>CHR$(32) THEN FC=FC+1:NEXT TF
4350 RL$=MID$(DF$,P+1,FC-1)
4355 L=LEN(RF$):R1$=RF$:GOSUB 4430:RF$=R1$:L=LEN(RL$):R1$=RL$:GOSUB 4430:RL$=R1$
4360 OPEN "I",#2,LF$
4365 IF EOF(2)=-1 THEN CLOSE #2:RETURN
4370 LINE INPUT #2,IL$
4375 P=INSTR(IL$,".FN."):IF P>0 THEN 4395
4380 P=INSTR(IL$,".LN."):IF P>0 THEN 4425
4385 POKE HI-1,0:PRINT #-2, IL$:POKE HI,0
4390 GOTO 4365
4395 ' FOUND .FN.
4400 MID$(IL$,P,4)="    "
4405 P1$=MID$(IL$,1,P-1)
4410 P2$=MID$(IL$,P+4,LEN(IL$))
4415 IL$=P1$+RF$+P2$
4420 GOTO 4375
4425 MID$(IL$,P,4)="    ":P1$=MID$(IL$,1,P-1):P2$=MID$(IL$,P+4,LEN(IL$)):IL$=P1$+RL$+P2$:GOTO 4380
4430 FOR U=1 TO LEN(R1$)
4435 C$=MID$(R1$,U,1)
4440 IF U=1 THEN 4465
4445 IF ASC(C$)<91 AND ASC(C$)>64 THEN C=ASC(C$)+32:C$=CHR$(C)
4450 MID$(R1$,U,1)=C$
4455 NEXT U
4460 RETURN
4465 'FIRST LETTER
4470 IF ASC(C$)>96 THEN C=ASC(C$)-32:C$=CHR$(C)
4475 GOTO 4450
4480 NEXT T
4485 POKE (HI-1),0:CLOSE #1:POKE HI,0
4490 GOTO 12
5000 ON BRK GOTO 12:FL(1)=1:' <-- START OF PRINT MODULE
5005 GOSUB 6100:GOSUB 6200
5010 LOCATE 0,19
5015 PRINTTAB(TB+11)"PLEASE SELECT FIELD"
5020 PRINTTAB(TB+2)" Where print-out string can be found "
5025 GOSUB 7190
5030 A=VAL(IK$)
5035 GOSUB 6100:LOCATE 0,7
5040 PRINTTAB(TB+10)"ENTER STRING TO PRINT"
5045 PRINT
5050 PRINTTAB(TB+12)"->";:LINEINPUT SD$:IF SD$="" THEN SD$=" "
5055 GOSUB 6000
5060 FOR T=1 TO LOF(1)
5065 POKE (HI-1),0:GET #1,T:POKE HI,0
5070 S(T)=0
5075 GOSUB 6400
5080 IF FL(2)=1 THEN FL(2)=0:GOSUB 5100:GOTO 5085
5085 NEXT T
5090 POKE (HI-1),0:CLOSE #1:POKE HI,0
5095 GOTO 5145
5100 LOCATE 0,7:GOSUB 6255
5105 IF FL(1)=1 THEN GOTO 5120
5110 S(T)=1
5115 RETURN
5120 GOSUB 7190
5125 IF IK$="Y" OR IK$="y" THEN 5110
5130 IF IK$="A" OR IK$="a" THEN FL(1)=0:GOTO 5105
5135 IF IK$="N" OR IK$="n" THEN 5115
5140 GOTO 5120
5145 'DOUBLE CHECK OF DELETES
5150 GOSUB 6100
5155 LOCATE 0,6:PRINTTAB(TB+13)" PRINT-OUT"
5160 CV=9:C=1
5165 GOSUB 6000
5170 FOR T=1 TO LOF(1)
5175 IF S(T)=1 THEN 5180 ELSE 5245
5180 POKE (HI-1),0:GET #1,T:POKE HI,0
5185 CV=CV+1
5190 IF CV>21 THEN CV=9
5195 LOCATE 0,7:PRINTTAB(TB+8)"MARKED FOR PRINTING=";C:C=C+1:LOCATE 1,CV
5200 IF A=1  OR A=0 THEN PRINT TAB(TB+5) DC$;" "; DN$
5205 IF A=2 THEN PRINT TAB(TB+5) DN$;" ";DC$
5210 IF A=3 AND W=80 THEN PRINTTAB(18) DA$;" ";DN$ ELSE IF A=3 THEN PRINTTAB(2) DA$;" "; DC$
5215 IF A=4 AND W=80 THEN PRINT DY$;" ";DN$ ELSE IF A=4 THEN PRINT DY$;" ";DC$
5220 IF A=5 THEN PRINT DS$;" ";DN$
5225 IF A=6 THEN PRINT DZ$;" ";DN$
5230 IF A=7 AND W=80 THEN PRINT DT$;" ";DN$ELSE IF A=7 THEN PRINT DT$;" ";DC$
5235 IF A=8 AND W=80 THEN PRINT DF$;" ";DN$ ELSE IF A=8 THEN PRINT DF$;" ";DC$
5240 IF A=9 AND W=80 THEN PRINT DO$;" ";DN$ ELSE IF A=9 THEN PRINT DO$;" ";DC$
5245 NEXT T
5250 POKE (HI-1),0:CLOSE#1:POKE HI,0
5255 T=21-CV:FOR Y=1 TO T:LOCATE 1,CV+T:PRINT:NEXT Y:PRINTTAB(TB+0)"ARE YOU SURE YOU WISH TO PRINT THESE"
5260 GOSUB 7190
5265 IF IK$="Y" THEN 5280
5270 IF IK$="N" THEN GOTO 5000
5275 GOTO 5260
5280 CLS
5285 PRINT"WORKING .";
5290 GOSUB 6000
5295 FOR T=1 TO LOF(1)
5300 IF S(T)=1 THEN 5305 ELSE 5365
5305 POKE (HI-1),0:GET #1,T
5310 F=0:TF=0:POKE (HI-1),0
5315 TF=TF+1:CC$=MID$(DY$,TF,1):IF CC$=CHR$(32) THEN F=F+1
5320 IF TF=25 OR F=2 THEN 5330
5325 GOTO 5315
5330 IF TF=25 THEN T$=DY$ :GOTO 5335 ELSE T$=LEFT$(DY$,TF)
5335 PRINT#-2,DN$
5340 PRINT#-2,DA$
5345 PRINT#-2,T$;", ";DS$
5350 PRINT#-2,DZ$
5355 IF PC=1 THEN EL=EL-1:PRINT#-2,DO$
5360 FOR TT=1 TO EL:PRINT#-2," ":NEXT TT
5365 NEXT T
5370 POKE (HI-1),0:CLOSE #1:POKE HI,0
5375 GOTO 12
5380 '
5385 '
5390 '-- END OF PRINT ROUTINE --
6000 '-- START OUTPUT ROUTINE --
6005 CLOSE#1:POKE (HI-1),0:LOCATE 0,20:ATTR 3,0,B:PRINTTAB(TB+6)"PLEASE WAIT - ACCESSING DISK";:ATTR 1,0:OPEN "D",#1,"MAILER.FIL:0",166
6010 FIELD #1, 10 AS DC$,25 AS DN$,25 AS DA$,25 AS DY$,10 AS DS$,7 AS DZ$,14 AS DT$,25 AS DF$,25 AS DO$:LOCATE 0,20:PRINT:RETURN
6015 LSET DC$=V$(1):LSET DN$=V$(2):LSET DA$=V$(3):LSET DY$=V$(4):LSET DS$=V$(5)
6020 LSET DZ$=V$(6):LSET DT$=V$(7):LSET DF$=V$(8):LSET DO$=V$(9)
6025 NR=LOF(1)+1
6030 POKE (HI-1),0:PUT #1,NR:POKE HI,0
6035 POKE (HI-1),0:CLOSE #1:POKE HI,0
6040 LOCATE 0,20:PRINT
6045 RETURN
6100 '-- HEADER SUBROUTINE --
6105 CLS:ATTR 1,0:PRINTSTRING$(40+TB+TB,"*"):PRINTTAB(TB+12);:ATTR 3,3:PRINT" The Post Office ";:ATTR 1,0:PRINT:PRINT:PRINTSTRING$(40+TB+TB,"*"):LOCATE 0,9:RETURN
6200 '-- HEADERS SUB-ROUTINE --
6205 LOCATE 0,7
6210 PRINTTAB(TB+10)"<1>. Code             ONLY"
6215 PRINTTAB(TB+10)"<2>. Name             ONLY"
6220 PRINTTAB(TB+10)"<3>. Address          ONLY"
6225 PRINTTAB(TB+10)"<4>. City             ONLY"
6230 PRINTTAB(TB+10)"<5>. State            ONLY"
6235 PRINTTAB(TB+10)"<6>. Zip              ONLY"
6240 PRINTTAB(TB+10)"<7>. Telephone        ONLY"
6245 PRINTTAB(TB+10)"<8>. First/Last       ONLY"
6250 PRINTTAB(TB+10)"<9>. Country          ONLY":PRINT:PRINTTAB(TB)"Default-> <0>. ANY OF THE ABOVE":RETURN
6255 'SUB ROUTINE 2
6260 PRINTTAB(TB+TZ)"       CODE:";DC$
6265 PRINTTAB(TB+TZ)"       NAME:";DN$
6270 PRINTTAB(TB+TZ)"    ADDRESS:";DA$
6275 PRINTTAB(TB+TZ)"       CITY:";DY$
6280 PRINTTAB(TB+TZ)"      STATE:";DS$
6285 PRINTTAB(TB+TZ)"        ZIP:";DZ$
6290 PRINTTAB(TB+TZ)"TELEPHONE #:";DT$
6295 PRINTTAB(TB+TZ)"   F,L NAME:";DF$
6300 PRINTTAB(TB+TZ)"    COUNTRY:";DO$
6305 RETURN
6400 '-- SEARCH SUB-ROUTINE -- 
6405 IF A=1 AND INSTR(DC$,SD$)>0 THEN FL(2)=1:RETURN 
6410 IF A=2 AND INSTR(DN$,SD$)>0 THEN FL(2)=1:RETURN 
6415 IF A=3 AND INSTR(DA$,SD$)>0 THEN FL(2)=1:RETURN
6420 IF A=4 AND INSTR(DY$,SD$)>0 THEN FL(2)=1:RETURN
6425 IF A=5 AND INSTR(DS$,SD$)>0 THEN FL(2)=1:RETURN
6430 IF A=6 AND INSTR(DZ$,SD$)>0 THEN FL(2)=1:RETURN
6435 IF A=7 AND INSTR(DT$,SD$)>0 THEN FL(2)=1:RETURN
6440 IF A=8 AND INSTR(DF$,SD$)>0 THEN FL(2)=1:RETURN
6445 IF A=9 AND INSTR(DO$,SD$)>0 THEN FL(2)=1:RETURN
6450 IF A=0 AND INSTR(DC$,SD$)>0 THEN FL(2)=1:RETURN
6455 IF A=0 AND INSTR(DN$,SD$)>0 THEN FL(2)=1:RETURN
6460 IF A=0 AND INSTR(DA$,SD$)>0 THEN FL(2)=1:RETURN
6465 IF A=0 AND INSTR(DY$,SD$)>0 THEN FL(2)=1:RETURN
6470 IF A=0 AND INSTR(DS$,SD$)>0 THEN FL(2)=1:RETURN
6475 IF A=0 AND INSTR(DZ$,SD$)>0 THEN FL(2)=1:RETURN
6480 IF A=0 AND INSTR(DT$,SD$)>0 THEN FL(2)=1:RETURN
6485 IF A=0 AND INSTR(DF$,SD$)>0 THEN FL(2)=1:RETURN
6490 IF A=0 AND INSTR(DO$,SD$)>0 THEN FL(2)=1:RETURN
6495 RETURN
6500 '-- PRINT AND INPUT SUBROUTINE --
6505 PRINTTAB(TB)"        CODE:";:ATTR 1,0:PRINTV$(1)
6510 PRINTTAB(TB)"        NAME:";:ATTR 1,0:PRINTV$(2)
6515 PRINTTAB(TB)"     ADDRESS:";:ATTR 1,0:PRINT V$(3)
6520 PRINTTAB(TB)"        CITY:";:ATTR 1,0:PRINT V$(4)
6525 PRINTTAB(TB)"       STATE:";:ATTR 1,0:PRINTV$(5)
6530 PRINTTAB(TB)"         ZIP:";:ATTR 1,0:PRINTV$(6)
6535 PRINTTAB(TB)" TELEPHONE #:";:ATTR 1,0:PRINTV$(7)
6540 PRINTTAB(TB)"    F,L NAME:";:ATTR 1,0:PRINTV$(8)
6545 PRINTTAB(TB)"     COUNTRY:";:ATTR 1,0:PRINTV$(9)
6550 LOCATE 0,9
6555 GU=0:R=9:C=DC:L=10:VN=1:GOSUB 7000:IF GU=1 THEN 6555
6560 GU=0:R=10:C=DC:L=25:VN=2:GOSUB 7000:IF GU=1 THEN 6555
6565 R=11:C=DC:L=25:VN=3:GOSUB 7000:IF GU=1 THEN 6560
6570 GU=0:R=12:C=DC:L=25:VN=4:GOSUB 7000:IF GU=1 THEN 6565
6575 GU=0:R=13:C=DC:L=10:VN=5:GOSUB 7000:IF GU=1 THEN 6570
6580 GU=0:FL(3)=1:GOSUB 7150:GOSUB 7040:R=14:C=DC:L=7:VNN=6:GOSUB 7000:IF GU=1 THEN 6575
6585 GU=0:R=15:C=DC:L=14:VN=7:GOSUB 7000:IF GU=1 THEN 6580
6590 GU=0:R=16:C=DC:L=25:VN=8:GOSUB 7000:IF GU=1 THEN 6585
6595 GU=0:R=17:C=DC:L=25:VN=9:GOSUB 7000:IF GU=1 THEN 6590
6600 RETURN
6605 '-- INPUT LIMIT SUB-ROUTINE --
6610 S$=""
6615 M=L+C
6620 LOCATE C,R
6625 P=C-1
6630 LOCATE P+1,R:GOSUB 7190
6635 IF IK$=CHR$(9) THEN 6630
6640 IF IK$="^" THEN UA=1:RETURN
6645 IF IK$=CHR$(8) AND  P=(C-1) THEN S$="":GOTO 6630 ELSE IF IK$=CHR$(8) THEN LOCATE P,R:ATTR 3,0:PRINT" ";:LOCATE P-1,R:P=P-1:S$=MID$(S$,1,LEN(S$)-1):GOTO 6630
6650 IF IK$=CHR$(13) OR IK$=CHR$(10)  THEN GOTO 6680
6655 IF P=M-1 THEN 6630
6660 P=P+1
6665 S$=S$+IK$:LOCATE P,R:ATTR 3,0:PRINT IK$;:ATTR 1,0
6670 IF P=M-1 THEN LOCATE P+1,R:PRINT"  ";:SOUND 1,1:GOTO 6630
6675 GOTO 6630
6680 IF S$<>"" THEN P=P+1:LOCATE P,R:PRINT
6685 RETURN
6690 GOSUB 7190
6695 GOSUB 6000
6700 DC$=CO$:LSET B$=NA$:LSET C$=AD$:LSET D$=CI$:LSET E$=ST$:LSET F$=ZI$:LSET G$=TE$:LSET H$=FL$:LSET I$=CN$
6705 NR=LOF(1)+1
6710 POKE (HI-1),0:PUT #1,NR:POKE HI,0
6715 POKE (HI-1),0:CLOSE #1:POKE HI,0
6720 RETURN
6725 POKE (HI-1),0:POKE 113,0:EXEC 40999:'<-- BREAK OUT FROM MAIN MENU
6730 PCLEAR 1:RUN 11
7000 '-- BEGINNING OF INPUT LOOP --
7005 LOCATE C,R:PRINTV$(VN):LOCATE C+L,R:PRINT:GOSUB 6605
7010 IF S$<>"" THEN V$(VN)=S$
7015 IF UA=1 THEN 7025
7020 RETURN
7025 UA=0
7030 IF S$<>"" THEN V$(VN)=S$
7035 GU=1:RETURN
7040 IF FL(3)=2 THEN FL(3)=0:RETURN:'<-- ZIP CODE LOOKUP MODULE
7050 POKE (HI-1),0:OPEN "D",#2,"ZIPCODES.FIL",42
7055 FIELD #2, 25 AS ZC$,10 AS ZS$,7 AS ZZ$:IF LOF(2)=0 THEN CLOSE #2:LOCATE 0,20:PRINT:GOTO 7095
7060 FOR T=1 TO LOF(2)
7065 POKE (HI-1),0:GET #2,T:POKE HI,0
7070 BS=25-LEN(T1$):T$=STRING$(BS," ")
7075 T1$=T1$+T$
7080 IF ZC$=T1$ AND INSTR(ZS$,T2$)>0 THEN V$(6)=ZZ$:POKE (HI-1),0:LOCATE 0,20:PRINT:CLOSE #2:POKE HI,0:RETURN
7085 NEXT T
7090 CLOSE #2
7095 V$(6)=""
7100 AD(1)=1
7105 RETURN
7110 '-- ADD ZIP CODE TO FILE --
7115 POKE HI-1,0:OPEN "D",#2,"ZIPCODES.FIL",42
7120 FIELD #2,25 AS ZC$,10 AS ZS$,7 AS ZZ$
7125 NR=LOF(2)+1
7130 LSET ZC$=T1$:LSET ZS$=T2$:LSET ZZ$=V$(6)
7135 PUT #2,NR
7140 CLOSE #2:POKE HI,0
7145 RETURN
7150 '
7155 IF FL(3)=1 AND LEN(V$(4))=0 OR LEN(V$(5))=0 THEN FL(3)=2:RETURN
7160 IF FL(3)=1 THEN CV$=V$(4):GOSUB 7165:T1$=CV$:CV$=V$(5):GOSUB 7165:T2$=CV$:FL(3)=0:RETURN
7165 FOR L=1 TO LEN(CV$)
7170 LX$=MID$(CV$,L,1):LX=ASC(LX$)
7175 IF LX>90 AND LX<123 THEN LX=LX-32:MID$(CV$,L,1)=CHR$(LX)
7180 NEXT L
7185 RETURN
7190 '-- INKEY STRING SUB-ROUTINE --
7195 IK$=INKEY$:IF IK$="" THEN 7195
7200 IF KK=1 THEN SOUND 200,1
7205 RETURN
7210 IF ERNO=26 AND ERLIN=4340 THEN CLS:GOSUB 6100:LOCATE 0,12:PRINTTAB(TB)"** LETTER FILE REQUESTED  NOT PRESENT **":LOCATE 0,20:PRINTTAB(TB+5)"** PRESS ANY KEY TO CONTINUE **":GOSUB 7190:GOTO 12
7215 IF ERNO=20 AND ERLIN=6005 THEN GOSUB 6100:LOCATE 0,12:PRINTTAB(TB+0)"** FILE: MAILER.FIL  HAS AN I/O ERROR **":LOCATE 0,20:PRINTTAB(TB+5)"** PRESS ANY KEY TO CONTINUE **";:GOSUB 7190:GOTO 12
7220 IF ERNO=20 AND ERLIN=12 THEN GOSUB 6100:LOCATE 0,12 :PRINTTAB(TB+0)"** IO ERROR ON DATA DISK PLEASE CHECK **":LOCATE 0,20:PRINTTAB(TB+5)"** PRESS ANY KEY TO CONTINUE **";:GOSUB 7190:GOTO 12
7225 IF ERNO=20 AND ERLIN=9545 THEN GOSUB 6100:LOCATE 0,12:PRINTTAB(TB)  "** IO ERROR IN FILE ZIPCODES  PLZ CHK **":LOCATE 0,20:PRINTTAB(TB+5)"** PRESS ANY KEY TO CONTINUE **";:GOSUB 7190:GOTO 12
7230 IF ERNO=26 AND ERLIN=4355 THEN GOSUB 6100:LOCATE 0,12:PRINTTAB(TB+2)"* NO LETTER FILE PRESENT ON DISK * ";:GOSUB 7190:GOTO 12
7231 IF ERNO=26 AND ERLIN=4360 THEN 20
7235 STOP
7240 A$=INKEY$:IF A$="" THEN 7240
