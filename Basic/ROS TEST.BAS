100 '     PROGRAM LISTING 1
200 '     ROS TEST PROGRAM
300 '     BY DENNIS H. WEIDE
400 '     (C) 1986
500 '     FOR TESTING THE ROSPPI CIRCUIT
600 '
700 '
800 CLS:POKE &HFF43,137:POKE &HFF47,137
900 POKE &HFF40,0:POKE &HFF41,0
1000 POKE &HFF44,0:POKE &HFF45,0
1100 FOR X=0 TO 7
1200 READ Y:PA(X)=Y:NEXT X
1300 FOR X=0 TO 7
1400 READ Y:PB(X)=Y:NEXT X
1500 FOR X=0 TO 7
1600 READ Y:PC(X)=Y:NEXT X
1700 PRINT
1800 '     SET PIN NUMBERS
1900 DATA 4,3,2,1,40,39,38,37,18,19,20,21,22,23,24,25,3,5,7,9,12,14,16,18
2000 CLS:PRINT:PRINT
2100 PRINTTAB(6)"ROSPPI TEST PROGRAM"
2200 PRINT:PRINTTAB(4)"1. TEST 8255 PPI'S "
2300 PRINT:PRINTTAB(4)"2. TEST 74LS240 BUFFERS"
2400 PRINT:PRINTTAB(4)"3. END CIRCUIT TEST"
2500 PRINT:PRINTTAB(4)"ENTER ONE OF THE ABOVE";
2600 INPUT Q
2700 ON Q GOTO 3000,3500,2900
2800 GOTO 2000
2900 CLS:END
3000 AD=&HFF40:IC=1:P=1:GOSUB 4300
3100 AD=&HFF41:IC=1:P=2:GOSUB 4300
3200 AD=&HFF44:IC=2:P=1:GOSUB 4300
3300 AD=&HFF45:IC=2:P=2:GOSUB 4300
3400 GOTO 2000
3500 IC=5:AD=&HFF40:GOSUB 6100
3600 IC=6:AD=&HFF41:GOSUB 6100
3700 IC=8:AD=&HFF44:GOSUB 6100
3800 IC=9:AD=&HFF45:GOSUB 6100
3900 GOTO 2000
4000 '
4100 '     8255 TEST SUBROUTINE
4200 '
4300 CLS:PRINT"SET LOGIC TESTER TO RED"
4400 FOR X=0 TO 7
4500 IF P=1 THEN 4600 ELSE IF P=2 THEN 4700
4600 PRINT@64,"ADDR="HEX$(AD)"   IC="IC"   PIN="PA(X):GOTO 4800
4700 PRINT@64,"ADDR="HEX$(AD)"   IC="IC"   PIN="PB(X)
4800 PRINT@128,"OUTPUT IS LOW (RED)"
4900 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
5000 POKE AD,2^X
5100 PRINT@128,"OUTPUT IS HIGH (GREEN)"
5200 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
5300 POKE AD,0
5400 PRINT@128,"OUTPUT IS LOW (RED)"
5500 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
5600 NEXT X
5700 RETURN
5800 '
5900 '     74LS240 TEST SUBROUTINE
6000 '
6100 CLS:PRINT"SET LOGIC TESTER TO GREEN"
6200 FOR X=0 TO 7
6300 PRINT@64,"ADDR="HEX$(AD)"   IC="IC"   PIN="PC(X)
6400 PRINT@128,"OUTPUT IS HIGH (GREEN)"
6500 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
6600 POKE AD,2^X
6700 PRINT@128,"OUTPUT IS LOW (RED)"
6800 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
6900 POKE AD,0
7000 PRINT@128,"OUTPUT IS HIGH (GREEN)"
7100 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Q
7200 NEXT X
7300 RETURN
