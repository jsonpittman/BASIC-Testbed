10 CLEAR 7000
20 PMODE0,1:PCLEAR 1
30 GOSUB 500:'INITIALIZE
40 GOSUB 8000:'HELP SCREEN
50 GOSUB 10000:'DISPLAY SCREEN
60 GOSUB 1000:'ENTER DATA
70 GOSUB 2000:'CHECK COMMAND
80 IF ER$<>"" THEN GOSUB 9500:GOTO 60:'ERROR
90 IF CN>9 THEN 160
100 GOSUB 2500:'VALID ROW AND DELIMITER?
110 IF ER$<>"" THEN GOSUB 9500:GOTO 60:'ERROR
120 GOSUB 3000:'VALID COLUMN?
130 IF ER$<>"" THEN GOSUB 9500:GOTO 60:'ERROR
140 IF CN<6 OR CN=8 OR CN=9 THEN GOSUB 3500:'IS CELL ON SCREEN?
150 IF ER$<>"" THEN GOSUB 9500:GOTO 60:'ERROR
160 IF CN=1 THEN GOSUB 4000:'ENTER DATA
170 IF CN=2 THEN GOSUB 4500:GOSUB 10000:'ENTER FUNCTION
180 IF CN=3 THEN GOSUB 12500:GOSUB 10000:'INCREMENT CELL
190 IF CN=4 THEN GOSUB 13000:GOSUB 10000:'DECREMENT CELL
200 IF CN=5 THEN GOSUB 9000:GOSUB 10000:'SPREAD FIGURES
210 IF CN=6 THEN GOSUB 5000:'VIEW FUNCTION
220 IF CN=7 THEN GOSUB 11000:GOSUB 10000:'MOVE WINDOW
230 IF CN=8 THEN GOSUB 5500:GOSUB 10000:'COPY ROWS
240 IF CN=9 THEN GOSUB 6000:GOSUB 10000:'COPY COLUMNS
250 IF CN=10 THEN GOSUB 6500:'PRINT SPREADSHEET
260 IF CN=11 THEN GOSUB 7500:'SAVE SPREADSHEET
270 IF CN=12 THEN GOSUB 8000:GOSUB 10000:'HELP SCREEN
280 IF CN=13 OR CN=14 THEN GOSUB 8500:GOSUB 10000:'CLR DATA/ALL
290 IF CN=15 THEN GOSUB 11500:GOSUB 10000:'UPDATE SPREADSHEET
300 IF CN=16 THEN GOSUB 16500:'VIEW FUNCTIONS ON SCREEN
310 IF CN<>17 THEN 60
320 PRINT@32,"QUIT. ARE YOU SURE(Y/N)";:INPUT YN$:IF YN$<>"Y" THEN 60
330 END
500 '*****INITIALIZATIONS*****
510 CUR$=CHR$(175):POKE 150,41
520 BD$=CHR$(150)
530 DC=1:DR=1
540 NM$="0123456789.":OP$="^*/-+()"
550 V1$="0123456789.^*/+-[]()"
560 FOR I=1 TO 6
570 READ P1(I),P2(I)
580 NEXT I
590 DATA 3,4,2,2,2,2,1,1,1,1,0,4
600 NC=17:DIM VC$(NC),V$(20)
610 FOR I=1 TO NC
620 READ VC$(I)
630 NEXT I
640 DATA DAT,F,I,D,SPREAD,V,J,CR,CC,P,S,H,ERASE,CLRD,U,VF,Q
650 CLS4
660 BN$=BD$+STRING$(22,32)+BD$
670 PRINT@32+4,STRING$(24,150);
680 PRINT@2*32+4,BD$+"     CHEAP SPREAD     "+BD$;
690 PRINT@3*32+4,BD$+"A SPREADSHEET PROGRAM "+BD$;
700 PRINT@4*32+4,BN$;
710 PRINT@5*32+4,BD$+"     BY BILL REED     "+BD$;
740 PRINT@6*32+4,STRING$(24,150);
750 PRINT@11*32+4,"LOAD OR CREATE (L/C)";:INPUT LC$
760 IF LC$="L" THEN GOSUB 7000:GOSUB 10000:GOTO 810
770 IF LC$<>"C" THEN 750
780 PRINT@13*32+4,"# OF ROWS(DEFAULT=40)";:INPUT MR:IF MR=0 THEN MR=40
790 PRINT@14*32+4,"# OF COLS(DEFAULT=14)";:INPUT MC:IF MC=0 THEN MC=14
800 DIM C$(MR,MC),FC$(MR,MC)
810 RETURN
1000 '*****ENTER COMMAND*******
1010 PRINT@0,STRING$(64,32);
1020 PRINT@0,">";
1030 S9=2:LN=30:GOSUB 1500:IP$=C9$:'INKEY$ INPUT
1040 RETURN
1500 '**INKEY$ INPUT ROUTINE***
1510 PS=S9:E9=S9+LN:C9$=""
1520 PRINT@PS,CUR$;
1530 C8$=INKEY$:IF C8$="" THEN 1530 ELSE C8=ASC(C8$)
1540 IF C8=8 OR C8=9 OR C8=13 OR C8=21 OR C8>31 THEN 1550 ELSE 1530
1550 IF C8<>13 THEN 1570
1560 PRINT@PS," ";:C9=VAL(C9$):RETURN
1570 IF C8<>8 THEN 1612
1580 IF PS=S9 THEN 1530
1590 IF LEN(C9$)>1 THEN C9$=LEFT$(C9$,LEN(C9$)-1) ELSE C9$=""
1600 PRINT@PS," ";:PS=PS-1
1610 GOTO 1520
1612 IF C8<>21 THEN 1620
1614 IF PS=S9 THEN 1530
1616 C9$="":PRINT@S9,STRING$(LN+1,32);:PS=S9:GOTO 1520
1620 IF C8<>9 THEN  1670
1630 IF PS=E9 THEN 1530
1640 PRINT@PS," ";:PS=PS+1
1650 C9$=C9$+" "
1660 GOTO 1520
1670 IF PS=E9 THEN 1530
1680 IF TY$="N" AND INSTR("0123456789.+-",C8$)=0 THEN 1530
1690 C9$=C9$+C8$
1700 PRINT@PS,C8$;:PS=PS+1
1710 GOTO 1520
2000 '******FIND COMMAND*******
2010 CM$=""
2020 'REMOVE BLANKS IN COMMAND
2030 FOR I=1 TO LEN(IP$)
2040 IF INSTR(NM$,MID$(IP$,I,1))>0 THEN 2070
2050 IF MID$(IP$,I,1)<>" " THEN CM$=CM$+MID$(IP$,I,1)
2060 NEXT I
2070 II=I
2080 FOR I=1 TO NC
2090 IF CM$=VC$(I) THEN 2110
2100 NEXT I
2110 IF I>NC THEN ER$=CM$+" COMMAND NOT FOUND"
2120 CN=I
2130 RETURN
2500 '**VALID ROW & DELIMITER?*
2510 O1$=""
2520 FOR I=II TO LEN(IP$)
2530 IF INSTR(NM$,MID$(IP$,I,1))>0 THEN O1$=O1$+MID$(IP$,I,1) ELSE 2550
2540 NEXT I
2550 II=I+1:O1=VAL(O1$)
2560 IF LEN(O1$)=0 THEN ER$="OPERAND 1 INVALID OR MISSING":GOTO 2600
2570 IF CN<9 AND O1>MR THEN ER$="MAX. NO. OF ROWS IS"+STR$(MR)
2580 IF CN=9 AND O1>MC THEN ER$="MAX. NO. OF COLS IS"+STR$(MC)
2590 IF MID$(IP$,I,1)<>"-" AND MID$(IP$,I,1)<>"," THEN ER$="DELIMITER MUST BE '-' OR ','"
2600 RETURN
3000 '******CHECK COLUMN******
3010 O2$=""
3020 FOR I=II TO LEN(IP$)
3030 IF INSTR(NM$,MID$(IP$,I,1))>0 THEN O2$=O2$+MID$(IP$,I,1) ELSE 3050
3040 NEXT I
3050 II=I:O2=VAL(O2$)
3060 IF LEN(O2$)=0 THEN ER$="OPERAND 2 INVALID OR MISSING":GOTO 3090
3070 IF CN<>8 AND O2>MC THEN ER$="MAX. NO. OF COLS IS"+STR$(MC)
3080 IF CN=8 AND O2>MR THEN ER$="MAX. NO. OF ROWS IS"+STR$(MR)
3090 RETURN
3500 '***IS CELL ON SCREEN?****
3510 IF CN<9 AND (O1>DR+13-1 OR O1<DR) THEN 3550
3520 IF CN=9 AND (O1>DC+3-1 OR O1<DC) THEN 3550
3530 IF CN<6 AND (O2>DC+3-1 OR O2<DC) THEN 3550
3540 GOTO 3560
3550 ER$="CELL(S) NOT ON SCREEN"
3560 RETURN
4000 '***ENTER DATA IN CELL****
4010 S9=96+(O1-DR+1)*2+(O1-DR)*30+(O2-DC)*10:LN=9:GOSUB 1500:C$(O1,O2)=C9$
4020 IF S9=502 THEN GOSUB 10000:'REDISPLAY IF SCREEN SCROLLS
4030 RETURN
4500 '*****ENTER FUNCTION*****
4510 S9=32:LN=30:GOSUB 1500:FC$(O1,O2)=C9$:IP$=C9$
4520 IF IP$="" THEN 4540
4525 IF LEFT$(IP$,1)="-" THEN FC$(O1,O2)="":ER$="ENTER NEG. NO. AS 0-NUMBER":GOSUB 9500:GOTO 4540
4530 IF LEFT$(IP$,1)="C" OR LEFT$(IP$,1)="R" THEN GOSUB 13500 ELSE GOSUB 14000
4540 RETURN
5000 '*****VIEW FUNCTION******
5010 IF FC$(O1,O2)<>"" THEN PRINT@32,FC$(O1,O2) ELSE PRINT@32,"NO FUNCTION DEFINED";
5020 GOSUB 12000:'PRESS ANY KEY
5030 RETURN
5500 '*******COPY ROWS********
5510 FOR I=1 TO MC
5520 C$(O2,I)=C$(O1,I)
5530 NEXT I
5540 RETURN
6000 '******COPY COLUMNS******
6010 FOR I=1 TO MR
6020 C$(I,O2)=C$(I,O1)
6030 NEXT I
6040 RETURN
6500 '****PRINT SPREADSHEET****
6510 PRINT@32,"STD PRINT OR NARROW(s/N)";:INPUT SZ$
6520 IF SZ$="N" THEN SZ$=CHR$(29) ELSE SZ$=CHR$(30)
6530 PRINT@32,STRING$(30,32);
6540 PRINT@32,"ENTER START,END ROW";:INPUT R1,R2
6550 PRINT@32,STRING$(30,32);
6560 PRINT@32,"ENTER START,END COL";:INPUT C1,C2
6570 IF SZ$=CHR$(30) AND C2-C1>8 THEN ER$="CAN ONLY PRINT 8 COLS STD SIZE":GOSUB 9500:GOTO 6660
6580 ER$="READY PRINTER AND PRESS ANY KEY":GOSUB 9500
6590 PRINT#-2,SZ$
6600 FOR I=R1 TO R2
6610 FOR J=C1 TO C2
6620 PRINT#-2,USING"%       %";C$(I,J);
6630 NEXT J
6640 PRINT#-2,CHR$(13);
6650 NEXT I
6660 PRINT#-2,CHR$(30)
6670 RETURN
7000 '****LOAD SPREADSHEET****
7010 PRINT@13*32+4,"FILENAME: ";:INPUT F$
7020 IF INSTR(F$,"/")=0 AND INSTR(F$,".")=0 THEN F$=F$+"/DAT"
7030 OPEN"I",#1,F$
7040 INPUT#1,MR,MC
7050 DIM C$(MR,MC),FC$(MR,MC)
7060 FOR I=1 TO MR
7070 FOR J=1 TO MC
7080 INPUT#1,C$(I,J)
7090 NEXT J,I
7100 FOR I=1 TO MR
7110 FOR J=1 TO MC
7120 INPUT#1,FC$(I,J)
7130 NEXT J,I
7140 CLOSE#1
7150 RETURN
7500 '****SAVE SPREADSHEET****
7510 PRINT@32,"NEW FILE/REPL EXISTING(N/R)";:INPUT NR$
7520 IF NR$="N" THEN PRINT@32,STRING$(30,32);:PRINT@32,"";:INPUT"FILENAME: ";F$ ELSE IF NR$<>"R" THEN 7510
7530 IF INSTR(F$,"/")=0 AND INSTR(F$,".")=0 THEN F$=F$+"/DAT"
7540 IF NR$="R" THEN KILL F$
7550 OPEN"O",#1,F$
7560 WRITE#1,MR,MC
7570 FOR I=1 TO MR
7580 FOR J=1 TO MC
7590 WRITE#1,C$(I,J)
7600 NEXT J,I
7610 FOR I=1 TO MR
7620 FOR J=1 TO MC
7630 WRITE#1,FC$(I,J)
7640 NEXT J,I
7650 CLOSE#1
7660 RETURN
8000 '******HELP SCREEN*******
8020 CLS:PRINT@10,"CHEAP SPREAD";
8030 PRINT@2*32+2,"DATx,y- ENTER DATA IN CELL";
8040 PRINT@3*32+2,"SPREADx,y-SPREAD AMT TO COLS";
8050 PRINT@4*32+2,"Fx,y  - ENTER FUNCTION IN CELL";
8060 PRINT@5*32+2,"Vx,y OR VF - VIEW FUNC";
8070 PRINT@6*32+2,"CCx,y - COPY COLUMN";
8080 PRINT@7*32+2,"CRx,y - COPY ROW";
8090 PRINT@8*32+2,"Ix,y  - INCR  Dx,y - DECR";
8100 PRINT@9*32+2,"Jx,y  - JUMP TO ROW,COL";
8110 PRINT@10*32+2,"ERASE - CLEAR EVERYTHING";
8120 PRINT@11*32+2,"CLRD  - CLEAR DATA ONLY";
8130 PRINT@12*32+2,"H - HELP       Q  - QUIT"
8140 PRINT@13*32+2,"U - UPDATE     S  - SAVE";
8150 PRINT@14*32+2,"P - PRINT      ";
8160 PRINT@15*32+9,"PRESS ANY KEY";
8170 GOSUB 12000
8180 RETURN
8500 '***CLEAR DATA/FUNCTION***
8510 IF CN=13 THEN PRINT@32,"CLEAR EVERYTHING(Y/N) ";
8520 IF CN=14 THEN PRINT@32,"CLEAR ALL DATA(Y/N) ";
8530 INPUT YN$:IF YN$<>"Y" THEN 8620
8540 PRINT@32,"ENTER START ROW TO CLEAR";:INPUT S2
8550 PRINT@32,"ENTER START COL TO CLEAR";:INPUT S3
8560 FOR I=S2 TO MR
8570 FOR J=S3 TO MC
8580 C$(I,J)=""
8590 IF CN=13 THEN FC$(I,J)=""
8600 NEXT J
8610 NEXT I
8620 RETURN
9000 '*****SPREAD FIGURES*****
9010 PRINT@32,"HOW MANY COLUMNS";:INPUT CL
9020 IF CL>MC-O2+1 THEN ER$="MAX NO. OF COLUMNS IS"+STR$(MC-O2+1):GOSUB 9500:GOTO 9070
9030 O3=(VAL(C$(O1,O2)))/CL:O3=INT(O3*100+.5)/100
9040 FOR J=O2 TO INT(O2+CL+.999)-1
9050 C$(O1,J)=STR$(O3)
9060 NEXT J
9070 RETURN
9500 '***PRINT ERROR MESSAGES**
9510 PRINT@32,ER$;
9520 ER$=""
9530 GOSUB 12000
9540 RETURN
10000 '*****DISPLAY SCREEN*****
10010 GOSUB 10500
10020 FOR I=1 TO 13
10030 FOR J=1 TO 3
10040 IF J+DC-1>MC THEN 10080
10050 IF I+DR-1>MR THEN 10090
10060 PRINT@96+(I-1)*30+I*2+(J-1)*10,C$(I+DR-1,J+DC-1);
10070 NEXT J
10080 NEXT I
10090 RETURN
10500 '***ROW & COL HEADERS***
10510 CLS
10520 FOR I=1 TO 3
10530 PRINT@66+(I-1)*10,DC+I-1;
10540 NEXT I
10550 FOR I=1088 TO 1119
10560 IF PEEK(I)>63 THEN POKE I,PEEK(I)-64
10570 NEXT I
10580 FOR I=1 TO 13
10590 PRINT@64+(I*32),RIGHT$(STR$(I+DR-1),2);
10600 NEXT I
10610 FOR I=1120 TO 1504 STEP 32
10620 IF PEEK(I)>63 THEN POKE I,PEEK(I)-64
10630 IF PEEK(I+1)>63 THEN POKE I+1,PEEK(I+1)-64
10640 NEXT I
10650 RETURN
11000 '*****MOVE WINDOW******
11010 DR=O1:DC=O2:RETURN
11500 '***UPDATE SPREADSHEET***
11510 PRINT@32,"UPDATING...";
11520 FOR C1=1 TO MC
11530 FOR R1=1 TO MR
11540 IF FC$(R1,C1)="" THEN 11560
11550 IP$=FC$(R1,C1):O1=R1:O2=C1:GOSUB 4530
11560 NEXT R1,C1
11570 RETURN
12000 '*****PRESS ANY KEY******
12010 I$=INKEY$:IF I$="" THEN 12010
12020 RETURN
12500 '*****INCREMENT CELL*****
12510 PRINT@32,"ENTER INCREMENT";:INPUT IX
12520 O3=VAL(C$(O1,O2))+IX
12530 C$(O1,O2)=STR$(O3)
12540 RETURN
13000 '*****DECREMENT CELL*****
13010 PRINT@32,"ENTER DECREMENT";:INPUT DX
13020 O3=VAL(C$(O1,O2))-DX
13030 C$(O1,O2)=STR$(O3)
13040 RETURN
13500 '**SUM ROWS OR COLUMNS**
13510 O3=O1:O4=O2
13520 II=2:GOSUB 2500
13530 IF LEFT$(IP$,1)="C" AND O1>MC THEN ER$="MAX. NO. OF COLS IS";STR$(MC) ELSE ER$=""
13540 IF ER$<>"" THEN GOSUB 9500:GOTO 13700
13550 GOSUB 3000
13560 IF LEFT$(IP$,1)="R" AND O2>MR THEN ER$="MAX. NO. OF ROWS IS";STR$(MR) ELSE ER$=""
13570 IF ER$<>"" THEN GOSUB 9500:GOTO 13700
13580 VL=0
13590 IF LEFT$(IP$,1)="C" THEN 13600 ELSE 13650
13600 '*****SUM COLUMNS*****
13610 FOR I=O1 TO O2
13620 VL=VL+VAL(C$(O3,I))
13630 NEXT I
13640 GOTO 13690
13650 '*****SUM ROWS********
13660 FOR I=O1 TO O2
13670 VL=VL+VAL(C$(I,O4))
13680 NEXT I
13690 C$(O3,O4)=STR$(VL)
13700 RETURN
14000 '**BREAK DOWN FUNCTION***
14010 VN=0:TS=0:SK$=""
14020 FOR I=1 TO LEN(IP$)
14030 I9$=MID$(IP$,I,1)
14040 IF I9$="[" THEN GOSUB 14500:GOTO 14070
14050 IF INSTR(NM$,I9$)>0 THEN GOSUB 15000:GOTO 14070
14060 IF INSTR(OP$,I9$)>0 THEN GOSUB 15500
14070 IF ER$<>"" THEN GOSUB 9500:GOTO 14120
14080 IF INSTR(V1$,I9$)=0 THEN ER$="INVALID CHARACTER":GOSUB 9500:GOTO 14120
14090 NEXT I
14100 IF TS>0 THEN GOSUB 15500
14110 GOSUB 16000:'EVAL FUNCT
14120 RETURN
14500 '*****EVALUATE CELL******
14510 O3$="":O4$=""
14520 I=I+1
14530 IF I>LEN(IP$) THEN 14610
14540 I9$=MID$(IP$,I,1)
14550 IF INSTR(NM$,I9$)>0 THEN O3$=O3$+I9$:I=I+1:GOTO 14530
14560 IF I9$<>"-" AND I9$<>"," THEN ER$="DELIMITER MUST BE '-' OR ','":GOTO 14660
14570 I=I+1
14580 IF I>LEN(IP$) THEN 14610
14590 I9$=MID$(IP$,I,1)
14600 IF INSTR(NM$,I9$)>0 THEN O4$=O4$+I9$:I=I+1:GOTO 14580
14610 IF I9$<>"]" THEN ER$="MISSING RIGHT BRACKET":GOTO 14660
14620 O3=VAL(O3$):O4=VAL(O4$)
14630 IF O3>MR THEN ER$="MAX. NO. OF ROWS IS"+STR$(MR)
14640 IF O4>MC THEN ER$="MAX. NO. OF COLS IS"+STR$(MC)
14650 IF ER$="" THEN VN=VN+1:V$(VN)=C$(O3,O4):IF V$(VN)="" THEN V$(VN)=" "
14660 RETURN
15000 '****EVALUATE CONSTANT***
15010 N9$=""
15020 IF I>LEN(IP$) THEN 15050
15030 I9$=MID$(IP$,I,1)
15040 IF INSTR(NM$,I9$)>0 THEN N9$=N9$+I9$:I=I+1:GOTO 15020
15050 VN=VN+1:V$(VN)=N9$:I=I-1
15060 RETURN
15500 '**PUSH/POP OPER STACK***
15510 IF I9$=")" THEN 15520 ELSE 15610
15520 IF TS=0 THEN ER$="MISSING LEFT PARENTHESIS":GOTO 15680
15530 TM$=MID$(SK$,TS,1)
15540 IF TM$="(" THEN 15550 ELSE 15570
15550 TS=TS-1:IF TS=0 THEN SK$="" ELSE SK$=LEFT$(SK$,TS)
15560 GOTO 15680
15570 VN=VN+1:V$(VN)=TM$
15580 TS=TS-1:IF TS=0 THEN SK$="" ELSE SK$=LEFT$(SK$,TS):GOTO15520
15600 '***PUT ON STACK?*******
15610 IF TS=0 THEN 15670
15620 TM$=MID$(SK$,TS,1)
15630 IF P1(INSTR(OP$,TM$))<P2(INSTR(OP$,I9$)) THEN 15670
15640 VN=VN+1:V$(VN)=TM$
15650 TS=TS-1:IF TS=0 THEN SK$="" ELSE SK$=LEFT$(SK$,TS)
15660 GOTO 15610
15670 TS=TS+1:SK$=SK$+I9$
15680 RETURN
16000 '****EVAL IN POSTFIX****
16010 I=1
16020 IF VN=1 THEN 16170
16030 TM$=LEFT$(V$(I),1)
16040 IF INSTR(OP$,TM$)=0 THEN I=I+1:GOTO 16030
16050 IF VAL(V$(I))<>0 THEN I=I+1:GOTO 16030:'NUMBER
16060 IF TM$="^" THEN V$(I-2)=STR$(VAL(V$(I-2))^VAL(V$(I-1)))
16070 IF TM$="*" THEN V$(I-2)=STR$(VAL(V$(I-2))*VAL(V$(I-1)))
16080 IF TM$="/" THEN IF VAL(V$(I-1))<>0 THEN V$(I-2)=STR$(VAL(V$(I-2))/VAL(V$(I-1))) ELSE V$(I-2)="0"
16090 IF TM$="+" THEN V$(I-2)=STR$(VAL(V$(I-2))+VAL(V$(I-1)))
16100 IF TM$="-" THEN V$(I-2)=STR$(VAL(V$(I-2))-VAL(V$(I-1)))
16110 IF I+1>VN THEN 16170
16120 FOR J=I+1 TO VN
16130 V$(J-2)=V$(J)
16140 NEXT J
16150 VN=VN-2
16160 GOTO 16010
16170 IF LEN(V$(1))>9 THEN V$(1)=LEFT$(V$(1),9)
16180 C$(O1,O2)=V$(1)
16190 RETURN
16500 '**VIEW SCREEN OF FUNC***
16510 GOSUB 10500
16520 FOR I=1 TO 13
16530 FOR J=1 TO 3
16540 IF I+DR-1>MR THEN 16580
16550 XX$=FC$(I+DR-1,J+DC-1):IF LEN(XX$)>9 THEN XX$=LEFT$(XX$,9)
16560 PRINT@96+(I-1)*30+I*2+(J-1)*10,XX$;
16570 NEXT J
16580 NEXT I
16590 RETURN
