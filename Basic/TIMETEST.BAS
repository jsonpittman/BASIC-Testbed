0 ' COPYRIGHT 1989  FALSOFT,INC
10 '**********TIME TEST*******
11 '******M.CROOKS-4/30/84******
12 '********PLEASANTON,CA*******
13 '
14 'THIS PROGRAM CREATES AND
15 'SENDS TO THE PRINTER
16 'RANDOMLY GENERATED PROBLEMS
17 'TESTING ON THE MULTIPLICA-
18 'TION OR DIVISION TABLES OR
19 'ON A MIXTURE OF BOTH.
20 '
21 'SEVENTY-FIVE PROBLEMS ARE
22 'SENT TO THE PRINTER,
23 'ALIGNED SO THAT THERE ARE
24 'THREE VERTICAL COLUMNS OF
25 'TWENTY-FIVE PROBLEMS.
27 '****************************
28 '          START
29 '****************************
30 CLEAR 700:DIM E$(75)
40 K=0:A=0:X=0:N=0:Y=0
50 CLS:FORI=1TO3:PRINT:NEXT
60 PRINTTAB(11)"TIME TEST",,,
70 PRINT"  THIS PROGRAM TESTS ONLY THE"
80 PRINT"  MULTIPLICATION AND DIVISION"
90 PRINT"  TABLES ONE THROUGH TWELVE.",,,
100 PRINT:INPUT"  PRESS<ENTER>TO CONTINUE";W$
110 CLS:PRINT:PRINTTAB(6)"WHAT KIND OF TEST?","   >>>SELECT DIGIT 1 OR 2<<<"
120 PRINT@32*5+3," 1-MULT. OR DIV.
130 PRINT@32*6+3," 2-REVIEW TEST (MIXTURE OF":PRINT@32*7+7," MULT. & DIV. PROBLEMS)"
140 PRINT:INPUT"   WHICH ONE<TYPE 1 OR 2>";X
150 IFX<1 OR X>2THENGOTO140
160 GOTO550
170 IF X=2THENGOTO340
180 IF A<>0 THEN GO TO 300
190 '***************************
200 '          TEST
210 '***************************
220 CLS:PRINT:PRINT:PRINT"  ON WHAT TABLE DO YOU WISH","  TO TEST?"
230 INPUT"      >>>TYPE 1-12<<<";A
240 IF A<1OR A>12THENGOTO220
250 CLS:PRINT:PRINT TAB(4)"WHAT ARE PROBLEMS TO BE?","   >>>SELECT DIGIT 1 OR 2<<<"
260 PRINT@32*4+5," 1-MULTIPLICATION"
270 PRINT@32*5+5," 2-DIVISION"
280 PRINT:INPUT"   WHICH ONE<TYPE 1 OR 2>";Y
290 IF Y<1 OR  Y>2 THEN GO TO 280
300 IF Y=1 THEN 390 ELSE 470
310 '***************************
320 '         REVIEW
330 '***************************
340 Y=RND(2)
350 IF Y=1 THEN GO TO 390 ELSE 470
360 '***************************
370 '      MULTIPLICATION
380 '***************************
390 IF A=0 THEN AA=RND(12)
400 IF A<>0 THEN AA=A
410 A$=STR$(AA):B$=" X":C$=STR$(RND(12)):D$="="
420 E$(K)=A$+B$+C$+D$
430 GOTO570
440 '***************************
450 '        DIVISION
460 '***************************
470 IF A=0 THEN AA=RND(12)
480 IF A<>0THEN AA=A
490 A$=STR$(AA*RND(12)):B$=" /":C$=STR$(AA):D$="="
500 E$(K)=A$+B$+C$+D$
510 GO TO 570
520 '***************************
530 '      END OF DATA
540 '***************************
550 FOR K=1 TO 75:IF K=2 THEN CLS:PRINT@103,"CREATING PROBLEMS":PRINT@230,"10 SECONDS, PLEASE!"
560 GOTO170
570 NEXT
580 '***************************
590 '      PRINT OUT
600 '***************************
610 CLS:PRINT:PRINT" PLEASE POSITION PAPER AT START"
620 PRINT" OF SHEET & TURN ON PRINTER.",,," WHEN YOU ARE READY TO PRINT"," OUT THE COPIES OF TESTS-"
630 PRINT:INPUT"    PRESS<ENTER>TO BEGIN";W$
640 CLS:PRINT@203,"PRINTING"
650 FOR N=1 TO 75 STEP 3
660 PRINT#-2,E$(N);TAB(30),E$(N+1);TAB(60);E$(N+2)
670 PRINT#-2:NEXT N
680 CLS:PRINT@103,"PRINTING COMPLETE",,,,,"   DO YOU WANT TO-"
690 PRINT@32*8+6," 1-CREATE MORE TESTS"
700 PRINT@32*9+6," 2-END"
710 PRINT:INPUT"   WHICH ONE<TYPE 1 OR 2>";X
720 IF X<1 OR X>2 THEN GO TO 710
730 IF X=1 GO TO 40
740 CLS:PRINT@203,"THE END";:
750 GOTO750
