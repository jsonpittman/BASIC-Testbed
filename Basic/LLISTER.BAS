1 UNLOAD:POKE65497,0:CLEAR7000:POKE150,18:WIDTH40:PALETTERGB:Q=57:DIMA$(67),PL$(114):ST$=STRING$(35,32)
2 CLS:LOCATE7,2:ATTR4,5:PRINT"32 / 40 TWO COLUMN LLISTER";:ATTR1,0:LOCATE8,4:ATTR3,2:PRINT"FOR THE COLOR COMPUTER 3";:ATTR1,0:LOCATE10,6:ATTR3,3:PRINT"BY:GEORGE QUELLHORST";:ATTR1,0:LOCATE38,8:ATTR0,0
3 FORT=1TO8:LOCATE2,T:ATTR3,3,B:PRINT"*";:ATTR1,0:NEXT:FORT=0TO8:LOCATE37,T:ATTR3,3,B:PRINT"*";:ATTR1,0:NEXT:ATTR1,0:L$=STRING$(36,42):LOCATE2,0:ATTR3,3,B:PRINTL$;:ATTR1,0:LOCATE2,8:ATTR3,3,B:PRINTL$;:ATTR1,0:LOCATE2,9
4 L$=STRING$(20,45):ATTR0,0:LOCATE10,11:PRINT"YOU HAVE TWO OPTIONS":LOCATE10,12:PRINTL$:LOCATE7,14:PRINT"1. GENERATE A 32 OR 40":LOCATE10,15:PRINT"TWO COLUMN LLISTING"
5 LOCATE7,18:PRINT"2. PRINT A PREVIOUSLY":LOCATE10,19:PRINT"GENERATED LLISTING":LOCATE10,20:PRINT"SAVED ON YOUR DISK":LOCATE8,23:PRINT"PRESS EITHER <1> OR <2>";:ATTR1,0:LOCATE38,22:POKE65496,0
6 ATTR0,0:IN$=INKEY$:IN=INSTR(1,"12",IN$):IFIN=0ORIN$=""THEN6
7 ON IN GOTO 8,102
8 CLS:LOCATE3,0:ATTR3,2:PRINT"32/40 COLUMN LLISTER FOR COCO III";:ATTR1,0:LOCATE38,0:ATTR0,0
9 LOCATE1,3:PRINT"PROGRAMS NAME......:"
10 LOCATE1,5:PRINT"LLISTINGS HEADER...:"
11 LOCATE1,7:PRINT"LLISTINGS FILE NAME:"
12 LOCATE1,9:PRINT"FIRST PAGE NUMBER..:":LOCATE1,11:PRINT"DISK OR PRINTER....:"
13 LOCATE1,13:PRINT"32 OR 40 LLISTING..:":LOCATE1,15:PRINT"10 OR 12 CPI.......:"
14 S$=STRING$(39,61):LOCATE0,17:PRINTS$;:FORT=18TO23:LOCATE0,T:PRINT"=";:NEXT:FORT=18TO23:LOCATE38,T:PRINT"=";:NEXT:LOCATE0,23:PRINTS$;
15 LOCATE2,19:ATTR3,2:PRINT"PROGRAM MUST BE SAVED IN ASCII CODE";:ATTR1,0:LOCATE7,21:ATTR3,2:PRINT"EXTENTION /BAS IS ASSUMED";:ATTR1,0
16 ATTR0,0:LPOKE442671,170:LPOKE442670,95:LOCATE21,3:LINEINPUT"> ";DN$:DN$=DN$+"/BAS":IFDN$="/BAS"THEN16
17 IFLEN(DN$)>12THENLOCATE21,3:PRINT" ":GOSUB81:GOSUB83:GOTO16ELSE18
18 OPEN"D",#1,DN$,1:IFLOF(1)=0THENGOSUB81:GOSUB139:LOCATE6,20:ATTR0,0,B:PRINT"FILE DOES NOT EXIST TRY AGAIN";:ATTR0,0:LOCATE21,3:PRINT" ":CLOSE#1:KILLDN$:GOTO16ELSELOCATE23,3:PRINTDN$:LOCATE36,3:ATTR3,3,B:PRINT"OPEN";:ATTR1,0:LOCATE3,1
19 GOSUB81:LOCATE3,19:ATTR3,2:PRINT"TITLE FOR YOUR LISTING MAXIMUM 16";:ATTR1,0:LOCATE4,21:ATTR3,2:PRINT"PRESS ENTER IF NO TITLE DESIRED";:ATTR1,0:LOCATE21,5
20 ATTR0,0:LPOKE442847,170:LPOKE442846,95:LOCATE21,5:LINEINPUT"> ";HE$:ATTR1,0
21 ATTR0,0:IFLEN(HE$)>16THEN22ELSE23
22 LOCATE21,5:PRINTSTRING$(17,32);:LOCATE0,6:PRINTSTRING$(10,32);:GOSUB81:GOSUB82:ATTR1,0:GOTO20
23 LE=LEN(HE$):IFHE$=""THENOT=1:LOCATE23,5:PRINT"NO HEADER";:ATTR1,0:LOCATE35,7
24 GOSUB81:LOCATE6,18:ATTR3,2:PRINT"FILENAME FOR YOUR LLISTING";:ATTR1,0:LOCATE2,20:ATTR3,3,B:PRINT"WHEN LISTING TO PRINTER PRESS ENTER";:ATTR0,0:LOCATE7,22:ATTR3,2:PRINT"EXTENTION /DAT IS ASSUMED";:ATTR1,0
25 ATTR0,0:LPOKE442991,170:LPOKE442990,95:LOCATE21,7:LINEINPUT"> ";NM$
26 IFLEN(NM$)>8THENLOCATE21,7:PRINT" ":GOSUB81:GOSUB83:GOTO25ELSE27
27 IFNM$=""THENNM$="N":LOCATE23,7:PRINT"PRINTER";:L$="P":ATTR1,0:LOCATE38,7:ELSENM$=NM$+"/DAT"
28 GOSUB81:LOCATE4,18:ATTR3,2:PRINT"ENTER THE STARTING PAGE NUMBER";:ATTR1,0:LOCATE3,20:ATTR3,2:PRINT"OF YOUR 32 OR 4O COLUMN LLISTING";:ATTR1,0:LOCATE1,22:ATTR3,2:PRINT"2 DIGITS MAX. ENTER STARTS AT PAGE 1";:ATTR1,0
29 LPOKE443139,170:LPOKE443138,95:LOCATE21,9:ATTR0,0:LINEINPUT"> ";PG$:IFPG$=""THENPG$="1":LOCATE23,9:PRINT"1"
30 IFLEN(PG$)>3THENLOCATE21,9:PRINT" ":GOSUB81:GOSUB82:GOTO29ELSEPG=VAL(PG$)
31 IFL$="P"THENU=-2:LOCATE21,11:PRINT"> PRINTER";:GOTO34ELSEGOSUB81:LOCATE10,19:ATTR3,2:PRINT"ENTER <D> FOR DISK";:ATTR1,0:LOCATE9,21:ATTR3,2:PRINT"ENTER <P> FOR PRINTER";:ATTR1,0
32 LPOKE443297,170:LPOKE443296,95:LOCATE21,11:ATTR0,0:LINEINPUT"> ";L$:IFL$=""THEN32
33 A1=INSTR(1,"PD",L$):IFA1=1THENU=-2ELSEIFA1=2THENU=2ELSEGOSUB139:LOCATE21,11:PRINT" ":GOTO32
34 GOSUB81:LOCATE13,20:ATTR3,2:PRINT"ENTER 32 OR 40";:ATTR1,0
35 LPOKE443459,170:LPOKE443458,95:LOCATE21,13:ATTR0,0:LINEINPUT"> ";W$
36 W=VAL(W$):IFW=32ORW=40THEN37ELSEGOSUB139:LOCATE21,13:ATTR0,0:PRINT" ":GOTO35
37 GOSUB81:LOCATE14,18:ATTR3,2:PRINT"PLEASE NOTE";:ATTR1,0:LOCATE5,20:ATTR3,2:PRINT"40 COLUMNS CAN ONLY BE LISTED";:ATTR1,0:LOCATE9,22:ATTR3,2:PRINT"IN THE 12 C.P.I. MODE";:ATTR1,0
38 LPOKE443619,170:LPOKE443618,95:LOCATE21,15:ATTR0,0:LINEINPUT"> ";X$:X=VAL(X$)
39 IFX=10ORX=12THEN40ELSEGOSUB139:LOCATE21,15:PRINT" ":GOTO38
40 IFW=40ANDX=10THENGOSUB139:LOCATE21,15:PRINT" ":GOTO38
41 AL=0:CN=1:CO=(8*X):PN=INT((CO-LE)/2):A$(0)=STR$(X)+W$+"*"+NM$
42 CLS:IF L$="P"THEN GOSUB84
43 CLS:LOCATE10,6:PRINT"INSERT PROGRAM DISK":LOCATE12,8:PRINT"AND PRESS ENTER":LOCATE18,10:LINEINPUT">";A$
44 BL$=STRING$(W,32):OPEN "O",#2,NM$
45 FIELD#1,1 AS RD$:LF=LOF(1)
46 CLS:LOCATE9,1:ATTR3,2,B:PRINT"N O W    L O A D I N G";:ATTR0,0:LOCATE8,4:PRINT"LOADING WILL BE FINISHED":LOCATE7,6:PRINT"BEFORE THIS NUMBER REACHES":LOCATE17,9:PRINT"< 114 >":ATTR1,0:LOCATE38,9
47 PG$=STR$(PG):IFVAL(PG$)<10THENPG$="00"+RIGHT$(PG$,1)ELSEPG$="0"+RIGHT$(PG$,2)
48 LOCATE10,16:ATTR0,0:PRINT"NOW LOADING PAGE NUMBER":LOCATE17,19:PRINT"< ";PG$;:LOCATE23,19:PRINT">":ATTR1,0:LOCATE38,9
49 FORF=1TO114:PL$(F)=BL$
50 FORF1=1TOW:CN=CN+1:IFCN>=LF THENST=1:GOTO58
51 GET#1,CN:IFRD$=CHR$(13)THEN53
52 MID$(PL$(F),F1,1)=RD$:NEXTF1
53 IF PL$(F)<>STRING$(W,32)ANDPL$(F-1)=STRING$(W,32)THENPL$(F-1)=PL$(F):PL$(F)=BL$:CN=CN-INSTR(1,PL$(F-1),CHR$(13)):GOTO50
54 F$=STR$(F):F$=RIGHT$(F$,LEN(F$)-1):IFVAL(F$)<10THENF$="0"+RIGHT$(F$,1)
55 IFVAL(F$)<100THENF$="0"+RIGHT$(F$,2)
56 ATTR0,0:A$=LEFT$(F$,1):B$=MID$(F$,2,1):C$=RIGHT$(F$,1)
57 S=443362:LPOKES,60:LPOKE(S+4),ASC(A$):LPOKE(S+6),ASC(B$):LPOKE(S+8),ASC(C$):LPOKE(S+12),62:NEXTF
58 CLS:GOSUB61
59 IFST=1THEN101
60 OT=1:GOTO 46
61 CLS:IFU=-2THENLOCATE7,6:ATTR0,0:PRINT"POSITION PAPER IN PRINTER":LOCATE11,8:PRINT"AND PRESS <ENTER>":GOSUB139:LOCATE18,10:LINEINPUT">";I$
62 IFAL=0 ANDU=2 THEN PRINT#2,A$(0) ELSE IF U=2 THEN PRINT#2
63 IF OT=1 THENFORT=1TO5:PRINT#U:NEXT:GOTO65
64 FORT=1TO2:PRINT#U:NEXT:PRINT#U,STRING$(PN,32)+HE$:PRINT#U,STRING$(PN,32)+STRING$(LE,61):PRINT#U
65 IFF<114ANDST=1THENQ=INT(((F)/2)+.5):Z=1
66 IFL$="D" OR U=2 THEN 68
67 CLS:LOCATE14,10:ATTR3,2,B:PRINT"NOW PRINTING";:ATTR1,0:LOCATE35,10:ATTR0,0:GOTO69
68 CLS:LOCATE11,10:ATTR3,2,B:PRINT"NOW SAVING TO DISK";:ATTR1,0:LOCATE35,10:ATTR0,0
69 FORG=1TOQ
70 IFW=40THENA$="    "+PL$(G)+"       "+PL$(G+Q):GOTO72
71 IF CO=96 THEN A$="          "+PL$(G)+"           "+PL$(G+Q) ELSE IF CO=80 THEN A$="    "+PL$(G)+"       "+PL$(G+Q)
72 PRINT#U,A$:NEXTG
73 PRINT#U
74 IF Z=1 THEN 78
75 IF CO=80 THEN PRINT#U,STRING$(70,32);:PRINT#U,"Page";PG ELSE IF CO=96 THEN PRINT#U,STRING$(86,32);:PRINT#U,"Page";PG
76 PRINT#U:PRINT#U
77 IFST=1THEN101ELSEPG=PG+1:AL=1:RETURN
78 W=(63-(Q+6)):IF W=0 THEN75
79 RN=INT((CO-40)/2):PRINT#U,STRING$(RN,32);STRING$(40,42)
80 W=(W-2):IFW=<0THEN75ELSEPRINT#U,STRING$(W,13):GOTO75
81 ATTR1,0:FORT=18TO22:LOCATE1,T:PRINTST$;:NEXTT:RETURN
82 LOCATE1,20:ATTR0,0,B:PRINT"YOU HAVE ENTERED TOO MANY CHARACTERS";:ATTR1,0:LOCATE11,2:GOSUB139:RETURN
83 LOCATE2,20:ATTR0,0,B:PRINT"FILENAME CAN'T EXCEED 8 CHARACTERS";:ATTR1,0:GOSUB139:RETURN
84 CLS:LOCATE10,0:PRINT"PRINT OPTION SELECT":LOCATE10,1:PRINTSTRING$(19,"=")
85 LOCATE0,3:PRINT"1. STANDARD / NORMAL ..........<10 CPI>":LOCATE0,5:PRINT"2. STANDARD / ITALIC ..........<10 CPI>"
86 LOCATE0,7:PRINT"3. STANDARD / N.L.Q............<10 CPI>":LOCATE0,9:PRINT"4. STANDARD / N.L.Q. / ITALIC..<10 CPI>"
87 LOCATE0,13:PRINT"5. ELITE / NORMAL..............<12 CPI>":LOCATE0,15:PRINT"6. ELITE / ITALIC..............<12 CPI>"
88 LOCATE0,17:PRINT"7. ELITE / N.L.Q...............<12 CPI":LOCATE0,19:PRINT"8. ELITE / NORMAL / ITALIC.....<12 CPI>"
89 LOCATE10,21:ATTR3,2,B:PRINT"[PRESS KEY DESIRED]";:ATTR1,0:LOCATE39,22
90 IN$=INKEY$:ATTR0,0:IN=INSTR(1,"12345678",IN$):IFIN=0ORIN$=""THEN90
91 IFW=40ANDIN<=4THENGOSUB139:LOCATE0,23:ATTR3,2:PRINT"40 COLUMNS CAN ONLY BE LISTED IN 12 CPI";:ATTR1,0:LOCATE39,23:GOTO90
92 ON IN GOTO 93,94,95,96,97,98,99,100
93 PRINT#-2,CHR$(27);CHR$(19);:RETURN
94 PRINT#-2,CHR$(27);CHR$(19);CHR$(27);CHR$(66);CHR$(1);:RETURN
95 PRINT#-2,CHR$(27);CHR$(18);:RETURN
96 PRINT#-2,CHR$(27);CHR$(18);CHR$(27);CHR$(66);CHR$(1);:RETURN
97 PRINT#-2,CHR$(27);CHR$(23);:RETURN
98 PRINT#-2,CHR$(27);CHR$(23);CHR$(27);CHR$(66);CHR$(1);:RETURN
99 PRINT#-2,CHR$(27);CHR$(29);:RETURN
100 PRINT#-2,CHR$(27);CHR$(29);CHR$(27);CHR$(66);CHR$(1);:RETURN
101 CLS:UNLOAD:GOSUB139:LOCATE6,10:ATTR3,2,B:PRINT"DONE. RETURNING TO MAIN MENU";:ATTR1,0:LOCATE35,10:FORT=1TO2000:NEXTT:IFU=-2THENKILL"N/DAT":RUNELSERUN
102 CLS:LOCATE11,0:ATTR3,2:PRINT"D I R E C T O R Y :";:ATTR1,0:LOCATE38,0:PRINT:PRINT:FORD=3TO11:DSKI$ 0,17,D,E$,F$:FORG=1TO8:IFG<5THENI=(G-1)*32ELSEI=(G-5)*32
103 IFG=5THENE$=F$
104 PL$=MID$(E$,I+1,8)
105 IFASC(LEFT$(PL$,1))=255THEN108
106 IFASC(LEFT$(PL$,1))=0THENNEXTG,D
107 H$=MID$(E$,I+9,3):PL$(R)=PL$+"/"+H$:R=R+1:NEXTG,D
108 O=2:LOCATE0,O:FORD=1TOR-1
109 ATTR0,0:IFD/3<>INT(D/3)THENPRINTPL$(D);"  ";ELSEPRINTPL$(D);:O=O+1
110 NEXTD:HSTAT Z$,Z1,Z2,Y:IFZ2=0THENY=Y+1ELSEY=Y+2
111 LOCATE10,Y:PRINTSTRING$(20,42):Y1=Y+2:Y2=Y1+1:Y3=Y2+1:LOCATE0,Y1:PRINT"LOAD A FILE, ENTER FILE NAME":LOCATE0,Y2:PRINT"PRESS <ENTER> TO ABORT.";
112 LOCATE24,Y3:ATTR3,2:PRINT"/DAT IS ASSUMED";:ATTR1,0:LOCATE0,Y3:ATTR0,0:LINEINPUT"ENTER FILENAME:";F$
113 IFLEN(F$)>8THENLOCATE0,Y3:PRINT" ":GOTO112ELSEIFF$=""THEN8
114 GOSUB140
115 OPEN "I",#1,F$+".DAT":V=0
116 LINE INPUT #1,A$:A$(V)=A$:V=V+1
117 M$=MID$(A$(0),6,1):IFM$="*"THENM=1
118 IFEOF(1)=-1 AND M<>1 THEN 121
119 IFEOF(1)<>-1THEN124
120 IF V=67 AND M<>1 THEN121ELSE124
121 GJ=1:CLS:GOSUB139:LOCATE6,10:ATTR3,3:PRINT"THIS IS NOT A STANDARD FILE";:ATTR1,0:LOCATE4,12:ATTR3,3:PRINT"PRINT IT ANYWAY ? <Y>ES OR <N>0";:ATTR1,0:LOCATE38,12:ATTR0,0
122 IN$=INKEY$:IN=INSTR(1,"YN",IN$):IFIN=0ORIN$=""THEN122ELSEIFIN=1THENGOSUB84ELSEIFIN=2THEN1
123 IFGJ=1THEN133
124 IFV2=0 ANDV=67THENGOSUB126ELSEIFV=67 ANDV2=1 THENGOSUB135ELSEIFV=67 ANDV2=2 THENGOSUB133
125 GOTO116
126 X$=MID$(A$(0),2,2):W$=MID$(A$(0),4,2):N=INSTR(1,A$(0),"/"):NM$=MID$(A$(0),7,(N-1)):W=VAL(W$):X=VAL(X$):GOSUB139:CLS:LOCATE8,0:ATTR3,2:PRINT"PRINT A FILE FROM DISK";:ATTR1,0:LOCATE38,0
127 LOCATE3,3:ATTR0,0:PRINT"THE FOLLOWING FILE HAS BEEN LOADED":LOCATE7,5:PRINT"INTO THE COMPUTERS MEMORY":LOCATE14,9:ATTR3,2:PRINTNM$;:ATTR1,0:LOCATE38,7
128 LOCATE7,11:ATTR3,2:PRINT"IT IS A ";:PRINTW$;:PRINT" COLUMN LISTING";:ATTR1,0:LOCATE38,10:IFW=40THEN129ELSE130
129 LOCATE8,15:ATTR4,5:PRINT"YOU MUST USE 12 C.P.I.";:ATTR1,0:LOCATE10,17:ATTR4,5:PRINT"TO PRINT THIS FILE";:ATTR1,0:LOCATE38,17:GOTO131
130 LOCATE10,15:ATTR4,5:PRINT"IT IS FORMATTED FOR";:ATTR1,0:LOCATE9,17:ATTR4,5:PRINT"PRINTOUT AT";X;:PRINT"C.P.I.";:ATTR1,0:LOCATE38,17
131 ATTR0,0:LOCATE1,22:PRINT"PRESS ANY KEY TO SELECT PRINTER CODES":ATTR1,0:LOCATE38,22
132 ATTR0,0:IFINKEY$=""THEN 132 ELSE GOSUB 84
133 CLS:LOCATE7,5:PRINT"POSITION PAPER IN PRINTER":LOCATE13,7:PRINT"PRESS <ENTER>";:LOCATE6,9:PRINT"TO PRINT THE ENTIRE LISTING";:LOCATE14,17:PRINT"<SPACEBAR>";:LOCATE5,19:PRINT"FOR A PAUSE AFTER EACH SHEET";:GOSUB139:ATTR1,0:LOCATE38,19
134 ATTR0,0:ID$=INKEY$:IFID$=CHR$(13)THENV2=1ELSEIFID$=" "THENV2=2ELSEIFID$=""THEN134ELSE135
135 CLS:LOCATE14,10:ATTR3,2,B:PRINT"NOW PRINTING";:ATTR1,0:LOCATE35,10:ATTR0,0
136 ATTR0,0:U=U+1:V=0:V4=1:FORT=1TO66:PRINT#-2,A$(T):NEXTT:IFEOF(1)=-1THEN137ELSEGOSUB140:RETURN
137 UNLOAD:CLS:LOCATE10,10:ATTR3,2:PRINT"PRINTING IS FINISHED";:ATTR1,0:LOCATE6,20:ATTR0,0,B:PRINT"PRESS ANY KEY FOR MAIN MENU";:ATTR1,0:LOCATE38,22:GOSUB139
138 ATTR0,0:IFINKEY$=""THEN138ELSERUN
139 FORS=1TO10:SOUND180,1:NEXT:RETURN
140 CLS:LOCATE5,10:ATTR3,2,B:PRINT"NOW LOADING PAGE";:PRINT(U+1);:PRINT"INTO MEMORY";:ATTR1,0:LOCATE38,10:ATTR0,0:RETURN
