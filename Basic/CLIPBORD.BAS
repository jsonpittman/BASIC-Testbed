1 'COCO 3 CLIPBORD
2 'BY MICHAEL GOCHOCO
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 PALETTE RGB:WIDTH 80:ATTR 3,2:CLS
20 CLEAR 5000:DIM A$(24)
30 A$(1)="******************************** COMPUTER CLIPBOARD ****************************"
40 FOR A=2 TO 23
50 A$(A)="                                                                                "
60 NEXT A
70 FOR A=1 TO 24
80 PRINT A$(A);
90 NEXT A
100 A=2:B=1
110 LOCATE B,A-1
120 B$=INKEY$
130 IF B$="" THEN 120
140 GOTO 220
150 LOCATE B,A-1
160 MID$(A$(A),B+1,1)=B$
170 PRINT B$;
180 B=B+1
190 GOSUB 310
200 LOCATE 0,A-1:PRINT A$(A);:LOCATE B,A-1
210 GOTO 110
220 ON BRK GOTO 360
230 IF B$=CHR$(12) THEN 110
240 IF B$=CHR$(95) THEN 370
250 IF B$=CHR$(94) THEN A=A-1
260 IF B$=CHR$(10) THEN A=A+1
270 IF B$=CHR$(8) THEN B=B-1
280 IF B$=CHR$(9) THEN B=B+1
290 IF B$=CHR$(94) OR B$=CHR$(10) OR B$=CHR$(8) OR B$=CHR$(9) THEN GOSUB 310 ELSE 150
300 GOTO 110
310 IF A-1<1 THEN A=2
320 IF A-1>22 THEN A=23
330 IF B<1 THEN B=1
340 IF B>78 THEN B=78
350 RETURN
360 END
370 CLS
380 LOCATE 23,12
390 PRINT "CLIPBOARD MENU"
400 PRINT TAB(25);"V-RETURN TO SCREEN"
410 PRINT TAB(25);"P-PRINT SCREEN"
420 PRINT TAB(25);"N-NEW SCREEN"
430 PRINT TAB(25);"B-SELECT BAUD"
440 PRINT TAB(25);"E-END PROGRAM"
450 PRINT:PRINT TAB(25);"SELECT OPTION";
460 INPUT O$
470 IF O$="V" THEN 530
480 IF O$="P" THEN 600
490 IF O$="N" THEN 540
500 IF O$="B" THEN GOTO 560
510 IF O$="E" THEN END
520 GOTO 370
530 CLS:GOTO 70
540 CLS:CLEAR:GOTO 20
550 INPUT "POKE 150,";B
560 CLS:LOCATE 25,8
570 INPUT "POKE 150,";B
580 POKE 150,B
590 GOTO 370
600 FOR A=1 TO 24
610 PRINT #-2,A$(A)
620 NEXT A
630 GOTO 370
