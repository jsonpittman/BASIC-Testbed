0 ' COPYRIGHT 1989  FALSOFT,INC
10 CLEAR 2000:DIMA$(72),D(72)
20 A=0:K=0:F=0
30 FORX=3 TO 11
40 DSKI$0,17,X,N1$,N2$
50 T$=N1$+LEFT$(N2$,127)
90 FOR N=0TO7
100 NAM$(N)=MID$(T$,N*32+1,8)
110 EXT$(N)=MID$(T$,N*32+9,3)
120 C(N)=ASC(MID$(T$,32*N+12,1))
130 NEXT N
140 FOR N=0TO7
150 IF C(N)<>2 AND C(N)<>0 OR LEFT$(NAM$(N),1)=CHR$(0) THEN 180
160 A$(A)=NAM$(N)+"/"+EXT$(N):D(A)=C(N)
170 A=A+1
180 NEXT N
190 NEXT X
200 DATA 32,64,96,128,160,192,224,256,288,320,352,384,416,448,11
210 CLS
220 FORB=0TOA-1
230 IF K=0 THEN READ P
240 IFP=11THENRESTORE:READP:PRINT@480,"enter 0 for more";:F=1:GOSUB320
250 PRINT@(14*K+P),B+1;LEFT$(A$(B),8)
260 IF K=>1 THEN K=0ELSEK=K+1
270 NEXT B
280 IFF=1THENPRINT@480,"enter 0 to see previous screen";:RESTORE:K=0
290 GOSUB 320
300 GOTO 220
310 END
320 PRINT@0,"":PRINT@0,"ENTER FILE NO.";:INPUTR
330 IFR=99 THEN PRINT@480,"";:END ELSE IFR<0ORR>A THEN320
340 IFR=0ANDF=0THEN320:ELSEIFR=0THENCLS:K=0:F=1:RETURN
350 IF D(R-1)=0 THEN LOADA$(R-1),R ELSE PRINT@0,"can't load ml file--try again";:FOR D=1TO2500:NEXTD:GOTO320
