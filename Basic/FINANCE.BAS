100 ' FINANCIAL TIME VALUE CONVERSIONS
105 CLEAR 200,&H4000+16127  'SAVE SPACE FOR TEXT DUMP CODE
107 POKE 150,18  'SET PRINTER TO 2400
108 ' TEXT DUMP ROUTINE
110 DATA &H86,&HFE,&HB7,0,&H6F,&H8E,4,0,&H5F,&H5C,&HA6,&H80,&H81,&H60
115 DATA &H2B,2,&H84,&HBF,&HAD,&H9F,&HA0,2,&HC1,&H20,&H2B,&HEF,&H86,&H0D
120 DATA &HAD,&H9F,&HA0,2,&H8C,6,0,&H2B,&HE3,&H7F,0,&H6F,&H39
125 FOR I=&H4000+16128 TO &H4000+16128+40
130 READ A:POKE I,A
135 NEXT I
140 DEFUSR0=&H7F00
142 CLS:PRINT"ARE YOU USING A PRINTER?(Y OR N)";
143 Q$=INKEY$:IF Q$="" GOTO 143
145 IF Q$="Y" THEN QQ=1 ELSE QQ=0
147 PRINT
150 IF QQ=1 THEN PRINT "TYPE P TO PRINT RESULTS AFTER A CONVERSION, THEN TYPE Y OR N AS":PRINT"PROMPT REQUESTED.":PRINT:PRINT"HIT ANY KEY TO CONTINUE"
170 IF QQ=1 THEN A$=INKEY$:IF A$=""GOTO 170
205 F1$="$$#######.##":P$="PRESENT VALUE IS: ":U$="UNIFORM VALUE IS: ":F$="FUTURE VALUE IS: "
210 CLS
230 PRINT@5,"FINANCIAL CONVERSIONS"
240 PRINT@70,"BY DALE TINKLEPAUGH"
250 PRINT@131,"SELECT CONVERSION RESULT"
260 PRINT@197,"1 PRESENT VALUE"
270 PRINT@229,"2 UNIFORM SERIES VALUE"
280 PRINT@261,"3 FUTURE VALUE"
290 PRINT@293,"4 INTEREST RATE"
300 PRINT@385,"PRESS A NUMBER BETWEEN 1 AND 4"
310 A$=INKEY$:IF A$=""GOTO 310
320 CASE=INSTR(1,"1234",A$)
330 ON CASE GOSUB 1000,2000,3000,4000
340 GOTO 210
1000 'MENU FOR PRESENT VALUE
1010 CLS
1020 PRINT@64,"WHICH WOULD YOU LIKE TO CONVERT"
1030 PRINT@96,"TO A PRESENT VALUE?"
1040 PRINT@165,"1 UNIFORM SERIES"
1050 PRINT@197,"2 GRADIENT SERIES"
1060 PRINT@229,"3 PROPORTIONAL SERIES"
1070 PRINT@261,"4 FUTURE SUM"
1080 PRINT@293,"5 RETURN TO MAIN MENU"
1090 PRINT@385,"PRESS A NUMBER BETWEEN 1 AND 5"
1100 A$=INKEY$:IF A$=""GOTO 1100
1110 IF A$="5" THEN RETURN
1120 CASE=INSTR(1,"1234",A$)
1130 ON CASE GOSUB 1200,1400,1600,1800
1140 GOTO 1000
1200 'PRESENT VALUE OF UNIFORM SERIES
1210 CLS
1215 PRINT"PRESENT VALUE OF UNIFORM SERIES":PRINT
1220 INPUT"NUMBER OF PERIODS";N
1230 INPUT "INTEREST RATE/PERIOD(%)";IP
1240 I=IP/100
1250 INPUT "PERIODIC AMOUNT";A
1260 Q=(1+I)^N
1270 PA=(Q-1)/(I*Q)
1280 P=PA*A
1285 PRINT P$
1290 PRINT USING F1$;P
1300 PRINT"DO ANOTHER? (Y OR N)";
1305 A$=INKEY$:IF A$=""GOTO 1305
1307 IF A$="P" AND QQ=1 THEN A=USR0(0)
1310 IF A$="Y" THEN GOTO 1210 ELSE IF A$="N" THEN RETURN ELSE GOTO 1305
1400 'PRESENT VALUE OF GRADIENT SERIES
1405 CLS
1407 PRINT"PRESENT VALUE OF GRADIENT":PRINT"SERIES":PRINT
1410 INPUT"NUMBER OF PERIODS";N
1415 INPUT "INTEREST RATE/PERIOD(%)";IP
1420 I=IP/100
1425 INPUT"PERIODIC INCREASE ($)";G
1430 Q=(1+I)^N
1435 A=(Q-1)/(I*Q)
1440 B=N/Q
1445 R=(A-B)/I
1450 P=R*G
1455 PRINT P$:PRINTUSING F1$;P
1460 PRINT"DO ANOTHER? (Y OR N)";
1462 A$=INKEY$:IF A$="" THEN GOTO 1462
1463 IF A$="P" AND QQ=1 THEN A=USR0(0)
1464 IF A$="Y" AND QQ=1 THEN 1405 ELSE IF A$="N" THEN RETURN ELSE GOTO 1462
1465 RETURN
1600 'PRESENT VALUE OF PROPORTIONAL SERIES
1605 CLS
1607 PRINT"PRESENT VALUE OF PROPORTIONAL":PRINT"SERIES":PRINT
1610 INPUT "NUMBER OF PERIODS";N
1615 INPUT"INTEREST RATE/PERIOD(%)";IP
1620 I=IP/100
1625 INPUT "RATE OF INCREASE(%)";RP
1630 R=RP/100
1635 INPUT"FIRST PERIOD AMOUNT";C
1640 Q=I+1
1645 IF R=I THEN 1705 ELSE IF R<I THEN 1675 ELSE 1650
1650 ' R>I
1655 X=((1+R)/Q)-1
1660 FA=(((X+1)^N)-1)/X
1665 PC=FA/Q
1670 GOTO 1715
1675 ' R<I
1680 X=(Q/(1+R))-1
1685 QX=(1+X)^N
1690 PA=(QX-1)/(X*QX)
1695 PC=PA/(1+R)
1700 GOTO 1715
1705 ' R=I
1710 PC=N/Q
1715 ' FINAL COMPUTATION
1720 P=C*PC
1725 PRINT P$:PRINTUSING F1$;P
1727 PRINT"DO ANOTHER? (Y OR N)";
1730 A$=INKEY$:IF A$=""THEN GOTO 1730
1732 IF A$="P" AND QQ=1 THEN A=USR0(0)
1734 IF A$="Y" THEN GOTO 1605 ELSE IF A$="N" THEN RETURN ELSE GOTO 1730
1735 RETURN
1800 'PRESENT VALUE OF FUTURE SUM
1805 CLS
1807 PRINT"PRESENT VALUE OF FUTURE SUM":PRINT
1810 INPUT "NUMBER OF PERIODS";N
1815 INPUT"INTEREST RATE/PERIOD (%)";IP
1820 I=IP/100
1825 INPUT"FUTURE SUM";F
1830 PF=1/(1+I)^N
1835 P=F*PF
1840 PRINT P$:PRINTUSING F1$;P
1842 PRINT"DO ANOTHER? (Y OR N)";
1845 A$=INKEY$:IF A$="" THEN GOTO 1845
1847 IF A$="P" AND QQ=1 THEN A=USR0(0)
1849 IF A$="Y" THEN GOTO 1805 ELSE IFA$="N" THEN RETURN ELSE GOTO 1845
2000 'MENU FOR UNIFORM SERIES VALUE
2010 CLS
2020 PRINT@64,"WHICH WOULD YOU LIKE TO CONVERT"
2030 PRINT@96,"TO A UNIFORM SERIES VALUE?"
2040 PRINT@165,"1 PRESENT SUM (LOAN)"
2050 PRINT@197,"2 GRADIENT SERIES"
2060 PRINT@229,"3 FUTURE SUM"
2070 PRINT@261,"4 RETURN TO MAIN MENU"
2080 PRINT@353,"PRESS A NUMBER BETWEEN 1 AND 4"
2090 A$=INKEY$:IF A$=""GOTO 2090
2100 IF A$="4" THEN RETURN ELSE CASE=INSTR(1,"123",A$)
2110 ON CASE GOSUB 2200,2600,2800
2120 GOTO 2000
2200 'UNIFORM SERIES VALUE OF PRESENT SUM
2205 CLS
2207 PRINT"UNIFORM SERIES VALUE OF PRESENT":PRINT"SUM":PRINT
2210 INPUT"NUMBER OF PERIODS";N
2215 INPUT "INTEREST RATE/PERIOD (%)";IP
2220 I=IP/100
2225 INPUT"PRESENT SUM";P
2230 Q=(1+I)^N
2235 R=I*Q/(Q-1)
2240 A=R*P
2245 PRINT U$:PRINTUSING F1$;A
2247 PRINT"DO ANOTHER (Y OR N)?"
2250 A$=INKEY$:IF A$="" THEN GOTO 2250
2252 IF A$="P" AND QQ=1 THEN A=USR0(0)
2255 IF A$="Y" THEN GOTO 2250 ELSE RETURN
2600 'UNIFORM SERIES VALUE OF GRADIENT SERIES
2605 CLS
2607 PRINT"UNIFORM SERIES VALUE OF GRADIENT":PRINT"SERIES":PRINT
2610 INPUT "NUMBER OF PERIODS";N
2615 INPUT"INTEREST RATE/PERIOD (%)";IP
2620 I=IP/100
2625 INPUT"PERIODIC INCREASE";G
2630 AG=(1/I)-N/(((1+I)^N)-1)
2635 A=G*AG
2640 PRINT U$:PRINTUSING F1$;A
2645 PRINT"DO ANOTHER (Y OR N)?";
2650 A$=INKEY$:IF A$="" GOTO 2650
2652 IF A$="P" AND QQ=1 THEN A=USR0(0)
2655 IF A$="Y" THEN GOTO 2605 ELSE IF A$="N" THEN RETURN ELSE GOTO 2650
2800 'UNIFORM SERIES VALUE OF FUTURE SUM
2805 CLS
2807 PRINT"UNIFORM SERIES VALUE OF FUTURE":PRINT"SUM":PRINT
2810 INPUT"NUMBER OF PERIODS";N
2815 INPUT"INTEREST RATE/PERIOD (%)";IP
2820 I=IP/100
2825 INPUT"FUTURE SUM";F
2830 AF=I/(((1+I)^N)-1)
2835 A=AF*F
2840 PRINT U$:PRINTUSING F1$;A
2845 PRINT"DO ANOTHER (Y OR N)?";
2850 A$=INKEY$:IF A$="" GOTO 2850
2852 IF A$="P"AND QQ=1 THEN A=USR0(0)
2855 IF A$="Y" THEN GOTO 2805 ELSE IF A$="N" THEN RETURN ELSE GOTO 2850
3000 'MENU FOR FUTURE VALUE
3005 CLS
3010 PRINT@64,"WHICH WOULD YOU LIKE TO CONVERT"
3015 PRINT@96,"TO A FUTURE VALUE?"
3020 PRINT@165,"1 PRESENT SUM"
3025 PRINT@197,"2 UNIFORM SERIES"
3030 PRINT@229,"3 GRADIENT SERIES"
3035 PRINT@261,"4 RETURN TO MAIN MENU"
3040 PRINT@353,"PRESS A NUMBER BETWEEN 1 AND 4"
3045 A$=INKEY$:IF A$=""GOTO 3045
3050 IF A$="4" THEN RETURN ELSE CASE=INSTR(1,"123",A$)
3055 ON CASE GOSUB 3200,3400,3600
3060 GOTO 3000
3200 'FUTURE VALUE OF PRESENT SUM
3205 CLS
3207 PRINT"FUTURE VALUE OF PRESENT SUM":PRINT
3210 INPUT"NUMBER OF PERIODS";N
3215 INPUT"INTEREST RATE/PERIOD";IP
3220 I=IP/100
3225 INPUT"PRESENT SUM";P
3230 FP=(1+I)^N
3235 F=FP*P
3240 PRINT F$:PRINTUSING F1$;F
3245 PRINT"DO ANOTHER (Y OR N)?";
3250 A$=INKEY$:IF A$="" GOTO 3250
3252 IF A$="P" AND QQ=1 THEN A=USR0(0)
3255 IF A$="Y" THEN GOTO 3205 ELSE IF A$="N" THEN RETURN ELSE GOTO 3250
3400 'FUTURE VALUE OF UNIFORM SERIES
3405 CLS
3407 PRINT"FUTURE VALUE OF UNIFORM SERIES":PRINT
3410 INPUT"NUMBER OF PERIODS";N
3415 INPUT"INTEREST RATE/PERIOD (%)";IP
3420 I=IP/100
3425 INPUT"PERIODIC AMOUNT";A
3430 FA=(((1+I)^N)-1)/I
3435 F=FA*A
3440 PRINT F$:PRINTUSING F1$;F
3445 PRINT"DO ANOTHER (Y OR N)?";
3450 A$=INKEY$:IF A$="" GOTO 3450
3452 IF A$="P" AND QQ=1 THEN A=USR0(0)
3455 IF A$="Y"THEN GOTO 3405 ELSE IF A$="N" THEN RETURN ELSE GOTO 3450
3600 'FUTURE VALUE OF GRADIENT SERIES
3605 CLS
3607 PRINT"FUTURE VALUE OF GRADIENT SERIES":PRINT
3610 INPUT"NUMBER OF PERIODS";N
3615 INPUT"INTEREST RATE/PERIOD (%)";IP
3620 I=IP/100
3625 INPUT"PERIODIC INCREASE";G
3630 FG=(((((1+I)^N)-1)/I)-N)/I
3635 F=FG*G
3640 PRINT F$:PRINTUSING F1$;F
3645 PRINT"DO ANOTHER (Y OR N)?";
3650 A$=INKEY$:IF A$=""GOTO 3650
3652 IF A$="P" AND QQ=1 THEN A=USR0(0)
3655 IF A$="Y" THEN GOTO 3605 ELSE IF A$="N" THEN RETURN ELSE GOTO 3650
4000 'MENU FOR INTEREST RATE CONVERSIONS
4010 CLS
4020 PRINT@64,"WOULD YOU LIKE TO CONVERT:"
4030 PRINT@165,"1 NOMINAL ANNUAL RATE TO"
4040 PRINT@197,"  EFFECTIVE ANNUAL RATE"
4050 PRINT@229,"2 EFFECTIVE ANNUAL RATE TO"
4060 PRINT@261,"  NOMINAL ANNUAL RATE"
4070 PRINT@293,"3 RETURN TO MAIN MENU"
4080 PRINT@385,"PRESS A NUMBER BETWEEN 1 AND 3"
4090 A$=INKEY$:IF A$=""GOTO 4090
4100 IF A$="3" THEN RETURN ELSE CASE=INSTR(1,"12",A$)
4110 ON CASE GOSUB 4300,4600
4120 GOTO 4000
4300 'NOMINAL TO EFFECTIVE
4310 CLS
4315 PRINT"NOMINAL TO EFFECTIVE":PRINT
4320 PRINT"NUMBER OF COMPOUNDING":PRINT"PERIODS/YEAR"
4340 INPUT"(FOR CONTINUOUS INPUT 0)";M
4350 INPUT"NOMINAL ANNUAL RATE";IP
4355 I=IP/100
4360 IF M=0 THEN 4380
4370 IA=(1+I/M)^M-1:GOTO 4390
4380 IA=EXP(I)-1
4390 PRINT"EFFECTIVE ANNUAL RATE IS:":PRINTIA*100;"%"
4400 PRINT"DO ANOTHER (Y OR N)?";
4410 A$=INKEY$:IF A$="" GOTO 4410
4415 IF A$="P" AND QQ=1 THEN A=USR0(0)
4420 IF A$="Y" THEN GOTO 4310 ELSE IF A$="N" THEN RETURN ELSE GOTO 4410
4600 'EFFECTIVE TO NOMINAL
4610 CLS
4615 PRINT"EFFECTIVE TO NOMINAL":PRINT
4620 PRINT"NUMBER OF COMPOUNDING":PRINT"PERIODS/YEAR"
4630 INPUT"(FOR CONTINUOUS, INPUT 0)";M
4640 INPUT"EFFECTIVE ANNUAL RATE";IP
4650 IA=IP/100
4660 IF M=0 THEN GOTO 4690
4670 I=(1+IA)^(1/M)-1
4680 NI=I*M:GOTO 4700
4690 NI=LOG(1+IA)
4700 PRINT"NOMINAL ANNUAL RATE IS:":PRINTNI*100;"%"
4710 PRINT"DO ANOTHER (Y OR N)?";
4720 A$=INKEY$:IF A$=""GOTO 4720
4725 IF A$="P" AND QQ=1 THEN A=USR0(0)
4730 IF A$="Y"THEN GOTO 4610 ELSE IF A$="N" THEN RETURN ELSE GOTO 4720
