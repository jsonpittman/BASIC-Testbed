0 'COPYRIGHT 1990 FALSOFT, INC.
10 'MUSIC CATALOGUE
20 'BY GIANCARLO PILLOT
30 'MADE APRIL, 1990
40 'VERSION 1.00
50 CLEAR 1000
60 DIM T$(100),A$(100),F$(100),TY$(100)
70 POKE 150,18 'REM SET BAUD RATE HERE, CURRENTLY AT 2400
80 CLS 1
90 PRINT "THE MUSIC CATALOGUE"
100 PRINT "BY GIANCARLO PILLOT"
110 PRINT STRING$(32,131);
120 PRINT
130 PRINT "PLEASE CHOOSE AN OPTION:"
140 PRINT TAB(3);"(1) LOAD A FILE"
150 PRINT TAB(3);"(2) SAVE A FILE"
160 PRINT TAB(3);"(3) EDIT CURRENT FILE"
170 PRINT TAB(3);"(4) GET A DISK DIRECTORY"
180 A$=INKEY$
190 IF A$<"1" OR A$>"4" THEN 180
200 G=VAL(A$)
210 ON G GOTO 240,450,640,1860
220 STOP
230 'THE FOLLOWING ROUTINE IS   FOR LOADING FILES
240 CLS
250 PRINT@64,"A FILENAME IS 8 CHARACTERS OR"
260 PRINT " LESS AND CONTAINS NO PERIODS"
270 PRINT " (.) OR SLASHES (/)"
280 PRINT 
290 PRINT "ENTER <^> TO GO BACK"
300 PRINT@0,"NAME OF FILE TO LOAD";
310 INPUT FILE$
320 IF LEN(FILE$)>8 OR LEN(FILE$)<1 OR INSTR(FILE$,"/") OR INSTR(FILE$,".") THEN SOUND 100,1:GOTO 300
330 IF FILE$="^" THEN 80
340 X=1
350 SIZE=0
360 OPEN "I",#1,FILE$+".CAS"
370 IF EOF(1)=-1 THEN 410
380 INPUT #1,T$(X),A$(X),F$(X),TY$(X)
390 X=X+1
400 GOTO 370
410 SIZE=X-1
420 CLOSE #1
430 GOTO 80
440 'THE FOLLOWING ROUTINE IS   FOR SAVING FILES
450 CLS
460 PRINT@64,"A FILENAME IS 8 CHARACTERS OR"
470 PRINT " LESS AND CONTAINS NO PERIODS"
480 PRINT" (.) OR SLASHES (/)"
490 PRINT
500 PRINT "ENTER <^> TO GO BACK"
510 PRINT@0,"NAME OF FILE TO SAVE";
520 INPUT FILE$
530 IF LEN(FILE$)>8 OR LEN(FILE$)<1 OR INSTR(FILE$,"/") OR INSTR(FILE$,".") THEN SOUND 100,1:GOTO 510
540 IF FILE$="^" THEN 80
550 OPEN "O",#1,FILE$+".CAS"
560 FOR X=1 TO SIZE
570 WRITE#1,T$(X)
580 WRITE#1,A$(X)
590 WRITE#1,F$(X)
600 WRITE#1,TY$(X)
610 NEXT X
620 CLOSE #1
630 GOTO 80
640 CUR=1
650 'DISPLAY/ EDIT MODE
660 CLS
670 PRINT "RECORD #";CUR
680 PRINT"ARTIST:"
690 PRINT A$(CUR)
700 PRINT"TITLE:"
710 PRINTT$(CUR)
720 PRINT "FORMAT OF RECORDING:"
730 PRINT F$(CUR)
740 PRINT "TYPE OF MUSIC:"
750 PRINT TY$(CUR)
760 PRINT
770 PRINT " aDD  sORT  dELETE  pRINT  qUIT"
780 PRINT "UP AND DOWN ARROWS TO VIEW"
790 IF SIZE=100 THEN PRINT:PRINT"FILE FULL"
800 IF SIZE=0 THEN PRINT:PRINT "FILE EMPTY"
810 A$=INKEY$
820 IF A$="^" AND CUR<SIZE THEN CUR=CUR+1:GOTO 660
830 IF A$=CHR$(10) AND CUR>1 THEN CUR=CUR-1:GOTO 660
840 IF A$="A" AND SIZE<100 THEN 910
850 IF A$="S" THEN 1050
860 IF A$="D" THEN 1380
870 IF A$="P" AND SIZE>0 THEN 1520
880 IF A$="Q" THEN 80
890 GOTO 810
900 'ADD SELECTION
910 CLS
920 PRINT "TYPE 31 CHARACTERS OR LESS IN    RESPONSE"
930 LINEINPUT "ARTIST: ";A$(SIZE+1)
940 A$(SIZE+1)=LEFT$(A$(SIZE+1),31)
950 LINEINPUT "TITLE: ";T$(SIZE+1)
960 T$(SIZE+1)=LEFT$(T$(SIZE+1),31)
970 LINE INPUT "FORMAT OF RECORDING: ";F$(SIZE+1)
980 F$=LEFT$(F$,31)
990 LINE INPUT "TYPE OF MUSIC: ";TY$(SIZE+1)
1000 TY$(SIZE+1)=LEFT$(TY$(SIZE+1),31)
1010 SIZE=SIZE+1
1020 CUR=SIZE
1030 GOTO 660
1040 'SORTING BY SINGLE BUBBLE
1050 CLS
1060 PRINT "WOULD YOU LIKE TO SORT BY:"
1070 PRINT TAB(3);"(1) ARTIST"
1080 PRINT TAB(3);"(2) TITLE"
1090 A$=INKEY$
1100 IF A$="1" THEN 1130
1110 IF A$="2" THEN 1250
1120 GOTO 1090
1130 PRINT:PRINT "SORTING..."
1140 F=0
1150 FOR X=1 TO SIZE-1
1160 IF A$(X)<=A$(X+1) THEN 1220
1170 T$=T$(X):T$(X)=T$(X+1):T$(X+1)=T$
1180 T$=A$(X):A$(X)=A$(X+1):A$(X+1)=T$
1190 T$=F$(X):F$(X)=F$(X+1):F$(X+1)=T$
1200 T$=TY$(X):TY$(X)=TY$(X+1):TY$(X+1)=T$
1210 F=1
1220 NEXT X
1230 IF F=1 THEN 1140
1240 GOTO 660
1250 PRINT:PRINT "SORTING..."
1260 F=0
1270 FOR X=1 TO SIZE-1
1280 IF T$(X)<=T$(X+1) THEN 1340
1290 T$=T$(X):T$(X)=T$(X+1):T$(X+1)=T$
1300 T$=A$(X):A$(X)=A$(X+1):A$(X+1)=T$
1310 T$=F$(X):F$(X)=F$(X+1):F$(X+1)=T$
1320 T$=TY$(X):TY$(X)=TY$(X+1):TY$(X+1)=T$
1330 F=1
1340 NEXT X
1350 IF F=1 THEN 1260
1360 GOTO 660
1370 'DELETE A SELECTION
1380 IF SIZE<1 THEN 660
1390 PRINT:PRINT"ARE YOU SURE? (Y/N)"
1400 A$=INKEY$:IF A$="" THEN 1400
1410 IF A$="N" THEN 660
1420 FOR X=CUR TO SIZE-1
1430 T$(X)=T$(X+1)
1440 A$(X)=A$(X+1)
1450 F$(X)=F$(X+1)
1460 TY$(X)=TY$(X+1)
1470 NEXT X
1480 SIZE=SIZE-1
1490 IF CUR>SIZE THEN CUR=SIZE
1500 GOTO 660
1510 'PRINTING..
1520 CLS
1530 PRINT "DO YOU WANT TO PRINT WITH"
1540 PRINT " HEADINGS (Y/N)"
1550 A$=INKEY$
1560 IF A$="" THEN 1550
1570 IF A$="N" THEN 1730
1580 PRINT "PRESS ANY KEY OR <^> TO ABORT"
1590 A$=INKEY$:IF A$="" THEN 1590
1600 IF A$="^" THEN 660
1610 FOR X=1 TO SIZE
1620 PRINT#-2
1630 PRINT#-2,"ARTIST:"
1640 PRINT#-2,A$(X)
1650 PRINT#-2,"TITLE:"
1660 PRINT#-2,T$(X)
1670 PRINT#-2,"FORMAT:"
1680 PRINT#-2,F$(X)
1690 PRINT#-2,"TYPE OF MUSIC:"
1700 PRINT#-2,TY$(X)
1710 NEXT X
1720 GOTO660
1730 PRINT"PRESS ANY KEY OR <^> TO ABORT"
1740 A$=INKEY$
1750 IF A$="" THEN 1740
1760 IF A$="^" THEN 660
1770 FOR X=1 TO SIZE
1780 PRINT#-2
1790 PRINT#-2,A$(X)
1800 PRINT#-2,T$(X)
1810 PRINT#-2,F$(X)
1820 PRINT#-2,TY$(X)
1830 NEXT X
1840 GOTO 660
1850 'DIRECTORY
1860 CLS
1870 DIR
1880 PRINT FREE(0);"FREE GRANULES"
1890 LINE INPUT "PLEASE PRESS <ENTER>";L$
1900 GOTO 80
