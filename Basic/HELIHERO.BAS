10 'Helicopter Hero  (C) 1985    Phil Holsten  908 Peachwood Ct  Modesto, CA   95350
20 DATA66,10,102,2,134,2,138,4,166,2,170,2,198,8,230,6,262,6,296,4,330,2,362,2,394,4
30 PLAY"V20O1L255T255":CLS2:FORX=1TO32:A$=A$+CHR$(159):B$=B$+CHR$(143):NEXT:PRINT@480,B$;:PRINT@448,A$;:PRINT@491,"^ OUR HERO ^";:FORX=1TO13:READ Y,Z:FORW=1TO Z:PLAY"P5C":PRINT@Y+W,CHR$(191);:NEXTW,X
40 A$(1)="HELICOPTER HERO":A$(2)="BY PHIL HOLSTEN":A$(3)="COPYRIGHT  1985":W=176
50 FORX=1TO3:FORY=173TO191:PLAY"C":PRINT@Z,CHR$(159);:PRINT@Y,CHR$(191);
60 IF Y>176THENPRINT@Z,MID$(A$(X),Y-176,1);
70 Z=Y:NEXTY:PRINT@191,CHR$(159);:FORV=1TO700:NEXTV:FORY=173TO191:PLAY"A":PRINT@Z,CHR$(159);:PRINT@Y,CHR$(191);:Z=Y:NEXTY:PRINT@191,CHR$(159);:FORV=1TO400:NEXTV,X:FORX=31TO1STEP-2:PLAY"P20;1":PRINT@480,A$;:PLAY"V=X;2":NEXT
80 CLS3
90 PRINT@199,"SPEED UP POKE (Y/N)";:I$=INKEY$:IF I$=""THEN90
100 IF I$<>"Y"THEN130
110 PRINT@293,"DOUBLE OR TRIPLE  (2/3)";:I$=INKEY$:IF I$=""THEN110
120 IF I$="3"THENPOKE65497,0ELSEPOKE65495,0
130 PRINT@361,;:INPUT"LEVEL (2-20)";LL:IF LL>20THEN LL=20
140 IF LL<2THEN LL=2
150 S=0:T=1:CV=1:R=0:I=0:J=0:PLAY"O1T255V20L255":P$(1)="R2D3NR2U8NR2D2NR2L2D2L2U6NL2NR2":P$(2)="L2D3NL2U8NL2D2NL2R2D2R2U6NL2NR2":R$="R4U6L2D1BD3D1L4NU1L2D1":S$="D1L2D7R2U7R2D7R2U7R2ND7U3R1E1R1E1"
160 GOSUB850
170 PMODE3,1:PCLS3:K=0:A=14:B=18:P=1:C=A:D=B
180 COLOR2,3:IF LL=20THEN LINE(4,4)-(248,164),PSET,BF:GOTO230ELSE FORY=4TO160STEP28:FORX=4TO236STEP28:LINE(X,Y)-(X+20,Y+20),PSET,BF:NEXTX,Y:FORY=25TO160STEP28:FORX=4TO236STEP28:IFRND(LL)=1THEN200
190 LINE(X,Y)-(X+20,Y+8),PSET,BF
200 NEXTX,Y:FORX=26TO236STEP28:FORY=4TO160STEP28:IFRND(LL)=1THEN220
210 LINE(X,Y)-(X+8,Y+20),PSET,BF
220 NEXTY,X
230 LINE(38,178)-(246,182),PSET,BF:IF S>0THEN COLOR4,3:LINE(244-S,178)-(248,182),PSET,BF
240 DRAW"C2BM4,182U2NR4U2R4BR4D4R4U4BR4NR4D2NR4D2R4BR4NR4U4":IF JOYSTK(0)>5THEN PRINT@227,"move joystick to the left";
250 L=9:FORX=14TO238STEP28:Z=((X-14)/28)+1:IFRND(3)=1THEN P(Z,0)=0:L=L-1:GOTO290
260 Y=(RND(6)*28)-4:DRAW"C1BM=X;,=Y;"+R$
270 IFPPOINT(X,Y+1)=2THENDRAW"C2BM=X;,=Y;"+R$:Y=Y+2:DRAW"C1BM=X;,=Y;"+R$:GOTO270
280 P(Z,1)=X:P(Z,2)=Y:P(Z,0)=1
290 NEXTX:IFRND(3)=1THENGS=0:GOTO320
300 GS=1:M=(RND(7)+1)*28+4:N=(RND(4)+1)*28+4:IFPPOINT(M+6,N+20)=1THEN300
310 COLOR3,3:LINE(M,N)-(M+20,N+20),PSET,BF:X=M+6:Y=N+8:DRAW"C2BM=X;,=Y;"+S$
320 SCREEN1,0
330 DRAW"C2BM=C;,=D;"+P$(Q):DRAW"C4BM=A;,=B;"+P$(P)
340 S=S+.2:IF S>0THENLINE(248-S,178)-(248-S,182),PSET
350 IF S>209THEN780
360 C=A:D=B:Q=P:PLAY"V8;1"
370 IFINKEY$="@"THENONP GOSUB570,630:PP=PP+1:IF PP=4THEN PP=0:GOSUB730
380 IFJOYSTK(1)=0THEN T=T-1:IF T<0THEN T=0:GOTO420ELSESOUND150,4:TIMER=0:TT=1
390 IF TT=0THEN420
400 IFB>154ORB<14ORA<12ORA>238ORTT=1ANDTIMER>500THENTT=0:GOTO420
410 PLAY"O4V25;12":CIRCLE(A,B-3),10,3:CIRCLE(A,B-3),10,2:CIRCLE(A,B-2),10,2:PLAY"O1V10"
420 O=INT(JOYSTK(0)/14)
430 IFPEEK(343)=247THENIFPPOINT(A-6,B+3)=3ORPPOINT(A-6,B-5)=3ORPPOINT(A-2,B-7)=3THENP=2:GOTO760ELSEPOKE343,255:A=A-O*2:P=2:GOTO450
440 IFPEEK(344)=247THENIFPPOINT(A+6,B+3)=3ORPPOINT(A+6,B-5)=3ORPPOINT(A+2,B-7)=3THENP=1:GOTO760ELSEPOKE344,255:A=A+O*2:P=1:GOTO450
450 IFPEEK(345)=247THENIFPPOINT(A-4,B-8)=3ORPPOINT(A+4,B-8)=3THEN760ELSEPOKE345,255:B=B-O:GOTO470
460 ONP GOSUB480,510:B=B+O:IF K=L THEN CV=CV+1:LL=LL-2:IF LL<2THEN LL=2:GOTO160:ELSE160
470 GOTO330
480 IFPPOINT(A+4,B+4)=3ORPPOINT(A-2,B+1)=3ORPPOINT(A-4,B-6)=3THEN760
490 IFPPOINT(A+4,B+4)=1THEN540
500 RETURN
510 IFPPOINT(A-4,B+4)=3ORPPOINT(A+2,B+1)=3ORPPOINT(A+4,B-6)=3THEN760
520 IFPPOINT(A-4,B+4)=1THEN540
530 RETURN
540 FORX=1TO9:IF P(X,0)=0THEN560
550 IF ABS(P(X,1)-A)<=14AND ABS(P(X,2)-B)<=14THEN P(X,0)=0:J=J+1:K=K+1:SOUND200,1:DRAW"C2BM=P(X,1);,=P(X,2);"+R$:X=9
560 NEXTX:RETURN
570 E=A+4:F=B-4
580 PSET(G,H,2):PSET(E,F,4):G=E:H=F
590 E=E+2:IFPPOINT(E+2,F)<>2THEN600ELSE580
600 IF PPOINT(E+2,F)<>3OR E>244THEN PSET(G,H,2):RETURN
610 IF GS=1AND E<M+26AND E>M-4AND F>N-1AND F<N+21THEN690
620 GOTO680
630 E=A-4:F=B-4
640 PSET(G,H,2):PSET(E,F,4):G=E:H=F
650 E=E-2:IFPPOINT(E-2,F)<>2THEN660ELSE640
660 IF PPOINT(E-2,F)<>3OR E<16THENPSET(G,H,2):RETURN
670 IF GS=1AND E>M-4AND E<M+26AND F>N-1AND F<N+21THEN690
680 FORX=1TO11STEP2:CIRCLE(E,F),X,4:PLAY"V25;=X;":NEXTX:FORX=1TO11STEP2:CIRCLE(E,F),X,2:NEXTX:FORX=0TO2STEP2:LINE(X,X/2)-(250+X,167+X/2),PRESET,B:NEXT:RETURN
690 PSET(G,H,2):GS=0:X=M+10:Y=N+10:COLOR4,3:PLAY"V15":FORZ=0TO10STEP2:LINE(X-Z,Y-Z)-(X+Z,Y+Z),PSET,B:PLAY"V-EEFFFFGGGG":NEXT:COLOR2,3:FORZ=0TO10STEP1:LINE(X-Z,Y-Z)-(X+Z,Y+Z),PSET,B:NEXT
700 IF RND(5)=3THEN T=T+1:FORX=1TO5:PLAY"O5L100;1;2;3;4;5;6;5;4;3;2;":NEXT:PLAY"O1L255"
710 S=S-50-(RND(25)*2):IF S<0THENLINE(38,178)-(246,182),PSET,BF:RETURN
720 LINE(38,178)-(248-S,182),PSET,BF:RETURN
730 FORZ=1TO9:IF P(Z,0)=0THEN750
740 X=P(Z,1):Y=P(Z,2):DRAW"C1BM=X;,=Y;"+R$
750 NEXTZ:RETURN
760 DRAW"C2BM=C;,=D;"+P$(Q):DRAW"C4BM=A;,=B;"+P$(P):PLAY"V25":R=R+1:I=I+(L-K):FORX=11TO1STEP-2
770 PLAY"V-;9;8;7;6;":CIRCLE(A,B-2),X,2:PLAY"4;3;2;1;":NEXTX:IF R=3THEN820ELSE CV=CV+1:GOTO160
780 PLAY"V31":I=I+(L-K)
790 DRAW"C2BM=A;,=D;"+P$(Q):DRAW"C4BM=A;,=B;"+P$(P):Q=P:D=B:B=B+BB:BB=BB+.5:IF RND(2)=1THEN P=3-P
800 PLAY"V-DC":IF B>170THEN B=B-BB:GOTO810ELSE790
810 PLAY"V31O1":FORX=1TO40:PSET(RND(7)*2+A-8,RND(15)+B-10,RND(2)):PLAY"CC":NEXT:FORX=1TO500:NEXT
820 CLSRND(9)-1:FORX=1TO20:PLAY"V31O5L255;1;2;4;3;5":PRINT@RND(400)," GAME OVER ";:NEXTX
830 GOSUB850:PRINT@484,;:INPUT"another mission (Y/N)";J$
840 IF J$="Y"THEN CLS3:GOTO130ELSE CLS:IF I$="3"THEN POKE65496,0:END ELSE POKE65494,0:END
850 CLS4:PRINT@68,"CAVERNS  ";CV;:PRINT@100,"LEVEL  ";LL;:PRINT@196,"HEROES LEFT  ";3-R;:PRINT@260,"SHIELDS LEFT  ";T;:PRINT@356,"MEN SAVED  ";J;:PRINT@388,"MEN LEFT BEHIND  ";I;:IF I>0THENPRINT@420,"SAVE %  ";INT((J/(J+I))*100);
860 RETURN
