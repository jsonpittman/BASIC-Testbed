1 'I-SPIN
2 'BY JOSEPH PENDELL
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
30 RES=64
40 NUMPOINTS=6
50 DIM R(NUMPOINTS)
60 DIM TH(NUMPOINTS)
70 DIM X(RES,NUMPOINTS)
80 DIM Y(RES,NUMPOINTS)
90 PI=2*ATN(1E10)
100 MOD=5
110 'GET COORDINATES AND MAKE POLAR
120 FOR I=1 TO NUMPOINTS
130 READ X,Y
140 R(I)=MOD*SQR(X*X+Y*Y)
150 IF X<>0 THEN 210
160 'SPECIAL CASE ANGLES
170 IF Y=0 THEN TH(I)=0
180 IF Y>0 THEN TH(I)=PI/2
190 IF Y<0 THEN TH(I)=3*PI/2
200 GOTO 250
210 TH(I)=ATN(Y/X)
220 'ACCOUNT FOR DIFFERENT QUADRANTS
230 IF X<0 THEN TH(I)=TH(I)+PI
240 IF X>0 AND Y<0 THEN TH(I)=TH(I)+2*PI
250 NEXTI
260 'X,Y COORDINATES FOR ROTATING FIGURE
270 DATA -10,15,10,15,-10,-15,10,-15,0,15,0,-15
280 CLS
290 PRINT@0,"SETTING UP GRAPHICS";
300 INC=2*PI/RES
310 BASE=0
320 'CALCULATE POINTS FOR ROTATED FIGURE
330 FOR DANGLE=0 TO 2*PI STEP INC
340 FOR I=1 TO NUMPOINTS
350 X(BASE,I)=128+R(I)*COS(TH(I)+DANGLE)
360 Y(BASE,I)=96-R(I)*SIN(TH(I)+DANGLE)
370 PRINT".";
380 NEXT I
390 BASE=BASE+1
400 NEXT DANGLE
410 'START THE SHOW!
420 PMODE 1,1
430 PCLS
440 SCREEN 1,0
450 PMODE 1,3
460 FOR J=1 TO 4
470 FOR I=0 TO RES-1 STEP J
480 PCLS
490 'CONNECT POINTS
500 LINE(X(I,1),Y(I,1))-(X(I,2),Y(I,2)),PSET
510 LINE(X(I,3),Y(I,3))-(X(I,4),Y(I,4)),PSET
520 LINE(X(I,5),Y(I,5))-(X(I,6),Y(I,6)),PSET
530 PCOPY 3 TO 1
540 PCOPY 4 TO 2
550 NEXT I
560 NEXT J
570 GOTO 460
