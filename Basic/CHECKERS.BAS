0 'THE CHECKERS CHAMP COPYRIGHT     (C)  1988 BY MARC CAMPBELL    ' COPYRIGHT 1989  FALSOFT,INC
1 WIDTH32:CLS0:POKE41598,32:POKE41599,32:X=RND(-TIMER):X=0:POKE140,0:POKE&HFFD9,0:POKE&HF015,&H21:HBUFF1,14:HBUFF2,269:E$="O1L16C":B$="O5L32C":DIMP(31,1):DIMA(11):DIMB(11):DIMC(11):DIMD(11):DIMBL(31):ONERRGOTO227
2 RGB:PALETTE0,0:PALETTE9,35:PALETTE10,53:PALETTE11,52:PALETTE12,0:PALETTE13,39:PALETTE14,57:PALETTE15,19
3 X=0:HCOLOR,8:HSCREEN2
4 DATAO3L12G,L6O4C,L12O3G,L6O4C,L12O3G,L3.O4C,L12O3G,L12O4C,L16D,L12E,L16D,C,L6E,L12C,L6D,L3CP6,L6C,L8E,E,L4E,L6C,L8E,E,L4E,L6C,L8E,E,L6E,L10D#,D,L3C#,O3G,O4L8E,E,L6E,L12C,L6D,C,O3C,O2C
5 HCOLOR4:FORQ=0TO304STEP20:HLINE(Q,0)-(Q+20,15),PSET,B:HLINE(Q,105)-(Q+20,120),PSET,B:IFX=0THENHPAINT(Q+1,1),3,4:HPAINT(Q+1,106),12,4:X=1ELSEHPAINT(Q+1,106),3,4:HPAINT(Q+1,1),12,4:X=0
6 NEXT:FORQ=15TO90STEP15:HLINE(0,Q)-(20,Q+15),PSET,B:HLINE(300,Q)-(320,Q+15),PSET,B:IFX=1THENHPAINT(1,Q+1),3,4:HPAINT(301,Q+1),12,4:X=0ELSEHPAINT(301,Q+1),3,4:HPAINT(1,Q+1),12,4:X=1
7 NEXT:HCOLOR1:HPRINT(5,3),"T H E":HPRINT(26,9),"C H A M P":HCOLOR4:HDRAW"BM50,31R20D10L20G5D10F5R20D10L25H10U20E10R5;BM70,31D40R10U15R10D15R10U40L10D15L10U15L10;BM100,31D40R30U10L20U8R10U4L10U8R20U10L30;BM145,31R20D10L20G5D10F5R20D10L25H10U20E10R5"
8 HDRAW"BM165,31D40R10U10F10R10H15U10E15L10G10U10L10;BM195,31D40R30U10L20U8R10U4L10U8R20U10L30;BM225,31D40R10U10F10R10H15U5R10E5U10H5L40BR25BD5R10F3D4G3L10U10;BM255,36E5R25D10L15G5R15F5D15G5L25U10R15E5L15H5U5"
9 FORQ=48TO273STEP+30:IFX=0THENHPAINT(Q,35),3,4:X=1ELSEHPAINT(Q,35),12,4:X=0
10 NEXT:HCOLOR5:HPRINT(4,11),"(C) MCMLXXXVIII by Marc Campbell"
11 POKE&HFFD8,0:FORQ=1TO40:READA$:PLAYA$:IFINKEY$<>""THENQ=40:NEXTELSEIFX=0THENPALETTE12,36:PALETTE3,0:X=1:NEXTELSEPALETTE12,0:PALETTE3,36:X=0:NEXT:EXEC43345
12 EX=1:EY=22:POKE&HFFD9,0:PALETTE0,0:PALETTE3,36:PALETTE12,0:HCOLOR11:HPRINT(5,16),"PRESS CLEAR TO RESTART OPTIONS":HCOLOR4:HPRINT(0,18),"Do you want to load a saved game?  (Y/N)"
13 GOSUB226:PLAYB$:HPRINT(0,18),STRING$(40,32):IFA$="Y"ORA$="y"THEN14ELSE18
14 HCOLOR4:HPRINT(3,18),"Load from (1) disk or (2) cassette"
15 GOSUB226:IFA$<>"1"ANDA$<>"2"THENPLAYE$:GOTO15ELSEPLAYB$:IFA$="1"THEND=1ELSED=-1
16 X=3:Y=20:GOSUB156:HPRINT(5,22),"Press any key to load the game":EXEC44539:POKE&HFFD8,0:OPEN"I",#D,F$
17 FORX=1TOEOF(D):INPUT#D,NG(0),NG(1):FORQ=0TO11:INPUT#D,A(Q),B(Q),C(Q),D(Q):NEXTQ:INPUT#D,TN,NP,IT,GM,J(0),J(1):NEXTX:CLOSE#D:POKE&HFFD9,0:GOTO38
18 HCOLOR1:HPRINT(0,18),"  Do you want to install scores? (Y/N)"
19 GOSUB226:PLAYB$:IFA$="Y"ORA$="y"THEN20ELSE24
20 HCOLOR4:HPRINT(2,18)," Load from (1) disk or (2) cassette "
21 GOSUB226:IFA$<>"1"ANDA$<>"2"THENPLAYE$:GOTO21ELSEPLAYB$:IFA$="1"THEND=1ELSED=-1
22 X=3:Y=20:GOSUB156:HPRINT(4,22),"Press any key to load the scores":EXEC44539:POKE&HFFD8,0:OPEN"I",#D,F$
23 FORX=1TOEOF(D):INPUT#D,NG(0),NG(1):NEXTX:CLOSE#D:POKE&HFFD9,0:GOSUB222
24 HCOLOR13:HPRINT(2,18),"Press 1 for Player One vs CoCo Champ":HPRINT(2,20),"Press 2 for Player One vs Player Two"
25 GOSUB226:IFA$<>"1"ANDA$<>"2"THENPLAYE$:GOTO25ELSEPLAYB$:IFA$="1"THENNP=1ELSENP=2
26 IFNP=2THEN28ELSEHCOLOR10:GOSUB222:HPRINT(4,18),"How smart is the Checkers Champ?":HPRINT(11,20),"1    DULL-WITTED":HPRINT(11,21),"2  RUN-OF-THE-MILL":HPRINT(11,22),"3    DEVESTATING"
27 GOSUB226:IFA$<>"1"ANDA$<>"2"ANDA$<>"3"THENPLAYE$:GOTO27ELSEPLAYB$:IFA$="1"THENIT=1ELSEIFA$="2"THENIT=2ELSEIFA$="3"THENIT=3
28 GOSUB222:HCOLOR14:HPRINT(4,18),"Press 1 to play classic checkers":HPRINT(4,20),"Press 2 to play all king version"
29 GOSUB226:IFA$<>"1"ANDA$<>"2"THENPLAYE$:GOTO29ELSEPLAYB$:IFA$="1"THENGM=1ELSEGM=2
30 GOSUB222:HCOLOR15:HPRINT(4,18),"Player 1: RED     RIGHT JOYSTICK"
31 IFNP=1THENHPRINT(4,19),"Player 2: BLACK   COLOR COMPUTER"
32 IFNP=2THENHPRINT(4,19),"Player 2: BLACK    LEFT JOYSTICK"
33 HPRINT(3,20),STRING$(34,32):HPRINT(3,21),"Who goes first? (1) RED  (2) BLACK":HPRINT(19,22),"(3) COMPUTER PICKS"
34 GOSUB226:IFA$<>"1"ANDA$<>"2"ANDA$<>"3"THENPLAYE$:GOTO34ELSEPLAYB$:IFA$="1"THENTN=0ELSEIFA$="2"THENTN=1ELSEIFA$="3"THENTN=RND(2)-1
35 J(0)=0:J(1)=0:FORQ=0TO11:A(Q)=Q:B(Q)=Q+20
36 IFGM=2THENC(Q)=2:D(Q)=2ELSEC(Q)=0:D(Q)=0
37 NEXT
38 EX=1:EY=0:POKE&HF015,&HAA:HCOLOR,8:HSCREEN2
39 HCOLOR5:HLINE(2,0)-(319,7),PSET,BF:HCOLOR2:HLINE(1,1)-(1,8),PSET:HLINE-(318,8),PSET:HLINE(0,2)-(0,9),PSET:HLINE-(317,9),PSET
40 HCOLOR9:HLINE(64,183)-(256,191),PSET,BF:HCOLOR10,8:HPRINT(9,23),"OOPS  STAT  SAVE  QUIT":FORQ=112TO208STEP48:HLINE(Q,183)-(Q,191),PRESET:NEXT
41 HCOLOR11:HLINE(2,16)-(39,66),PSET,BF:HCOLOR7:HLINE(1,17)-(1,67),PSET:HLINE-(38,67),PSET:HLINE(0,18)-(0,68),PSET:HLINE-(37,68),PSET:HCOLOR8:HLINE(4,18)-(37,28),PSET,BF:HCOLOR3:HLINE(4,43)-(37,53),PSET,BF
42 HCOLOR4:HLINE(280,150)-(318,160),PSET:HLINE-(280,170),PSET:HLINE-(280,150),PSET:HPAINT(281,152),4,4:HCOLOR11:HLINE(280,171)-(280,185),PSET
43 HCOLOR7:X=LEN(STR$(NG(1))):HPRINT(1,4),STRING$(4-X,48)+RIGHT$(STR$(NG(1)),X-1):X=LEN(STR$(NG(0))):HPRINT(1,7),STRING$(4-X,48)+RIGHT$(STR$(NG(0)),X-1)
44 POKE&HF015,&H21:PALETTE0,27:HCOLOR4:X=0:Z=0:FORR=16TO156STEP20
45 FORC=48TO244STEP28
46 HLINE(C,R)-(C+28,R+20),PSET,B:IFX=0THENHPAINT(C+1,R+1),3,4:X=1ELSEX=0
47 NEXTC:IFX=0THENX=1ELSEX=0
48 NEXTR
49 X=76:W=244:Y=16:FORQ=0TO31:BL(Q)=0:P(Q,0)=X:P(Q,1)=Y:X=X+56:IFX>W THENY=Y+20:IFZ=0THENX=48:W=216:Z=1ELSEZ=0:X=76:W=244
50 NEXT
51 FORQ=0TO11:IFA(Q)=-2THEN52ELSEBL(A(Q))=2:W=P(A(Q),0)+14:X=P(A(Q),1)+10:HCIRCLE(W,X),10:IFC(Q)=2THENHDRAW"BM"+STR$(W+2)+","+STR$(X+2)+"C1L8U6F4E2F2E4D6L8C4":HPAINT(W+2,X+1),1,1
52 IFB(Q)=-2THEN53ELSEBL(B(Q))=1:Y=P(B(Q),0)+14:Z=P(B(Q),1)+10:HCIRCLE(Y,Z),10:HPAINT(Y,Z),3,4:IFD(Q)=2THENHDRAW"BM"+STR$(Y+2)+","+STR$(Z+2)+"C1L8U6F4E2F2E4D6L8C4":HPAINT(Y+2,Z+1),1,1
53 NEXT
54 GOSUB129:GOSUB89:GOTO163
55 HPRINT(1,0),"Select your piece"+STRING$(23,32):GOSUB83
56 IFBUTTON(TN*2)=1THENGOSUB135:GOTO57ELSE54
57 FORQ=0TO31:IFX>P(Q,0)ANDX<P(Q,0)+28ANDY>P(Q,1)ANDY<P(Q,1)+20THEN58ELSENEXT:PLAYE$:GOTO54
58 P=Q:IFTN=1THEN59ELSEFORQ=0TO11:IFB(Q)=P THENN=Q:GOTO60ELSENEXT:PLAYE$:GOTO54
59 FORQ=0TO11:IFA(Q)=P THENN=Q:GOTO60ELSENEXT:PLAYE$:GOTO54
60 GOSUB89:HPRINT(1,0),"Make your move"+STRING$(26,32):GOSUB83
61 IFBUTTON(TN*2)=1THENGOSUB135:GOTO62ELSE60
62 FORQ=0TO31:IFX>P(Q,0)ANDX<P(Q,0)+28ANDY>P(Q,1)ANDY<P(Q,1)+20ANDBL(Q)=0THEN63ELSENEXT:PLAYE$:GOTO54
63 M=Q
64 IFTN=1 ANDC(N)<>2 THEN71
65 IFJ>0THEN68
66 IF(P AND4)=4 THENIFP-M=5 ANDINT(P/4)<>P/4 ORP-M=4THEN77
67 IFP-M=3 AND(P AND4)=0ORP-M=4THEN77
68 IFP-M=9 AND(P AND4)<>((P-4)AND4) ANDINT(P/4)<>P/4 THEN90
69 IFP-M=7THEN90
70 IFTN=0AND D(N)<>2 THENPLAYE$:GOTO54
71 IFJ>0THEN74
72 IF(M AND4)=4 THENIFM-P=5 ANDINT(M/4)<>M/4 ORM-P=4THEN77
73 IFM-P=3 AND(M AND4)=0ORM-P=4THEN77
74 IFM-P=9 AND(M AND4)<>((M-4)AND4) ANDINT(M/4)<>M/4 THEN90
75 IFM-P=7THEN90
76 PLAYE$:GOTO54
77 HGET(P(P,0)+1,P(P,1)+1)-(P(P,0)+27,P(P,1)+19),2:HCOLOR8:HLINE(P(P,0)+1,P(P,1)+1)-(P(P,0)+27,P(P,1)+19),PSET,BF:HPUT(P(M,0)+1,P(M,1)+1)-(P(M,0)+27,P(M,1)+19),2,PSET:HCOLOR4:EXEC43345
78 IFJ(TN)=12THEN146
79 IFTN=0ANDM>-1ANDM<4ANDD(N)<>2THEND(N)=2:GOSUB151
80 IFTN=1ANDM>27ANDM<32ANDC(N)<>2THENC(N)=2:GOSUB151
81 IFTN=0THENB(N)=M:BL(P)=0:BL(M)=1:IFJ>0THEN104ELSETN=1:GOTO54
82 IFTN=1THENA(N)=M:BL(P)=0:BL(M)=2:IFJ>0THEN104ELSETN=0:GOTO54
83 QX=JOYSTK(0):QX=JOYSTK(1):X=50+JOYSTK(TN*2)*3.5:Y=18+JOYSTK(TN*2+1)*2.5:X=INT(X):Y=INT(Y)
84 IFY=175 THENY=185:IFX>253THENX=253ELSEIFX<64THENX=64
85 IFY>145ANDX=270THENF=1:HCOLOR4,8:HLINE(280,150)-(318,160),PRESET:HLINE-(280,170),PRESET:HSET(280,170,11):FORQ=1TO100:NEXT:HLINE(280,150)-(318,160),PSET:HLINE-(280,170),PSET:GOTO88
86 F=0:HGET(X,Y)-(X+4,Y+4),1:HDRAW"C1BM"+STR$(X)+","+STR$(Y)+"NR2ND2F4C4":FORQ=1TO50:NEXT:HPUT(X,Y)-(X+4,Y+4),1,PSET
87 HPUT(X,Y)-(X+4,Y+4),1,PSET
88 RETURN
89 IFTN=0THENHCOLOR3:RETURNELSEHCOLOR8:RETURN
90 J=1:FORQ=0TO11
91 IFTN=0THENOP=A(Q)ELSEIFTN=1THENOP=B(Q)
92 IFTN=1 ANDC(N)<>2 THEN98
93 IFP-M=7 AND(P AND4)=4 AND P-4=OP THENX=OP:GOSUB154:GOTO103
94 IFP-M=7 AND(P AND4)=0 AND P-3=OP THENX=OP:GOSUB154:GOTO103
95 IFP-M=9 AND(P AND4)=4 ANDP-5=OP THENX=OP:GOSUB154:GOTO103
96 IFP-M=9 AND(P AND4)=0 ANDP-4=OP THENX=OP:GOSUB154:GOTO103
97 IFTN=0 ANDD(N)<>2 THEN102
98 IFM-P=7 AND(M AND4)=4 AND M-4=OP THENX=OP:GOSUB154:GOTO103
99 IFM-P=7 AND(M AND4)=0 AND M-3=OP THENX=OP:GOSUB154:GOTO103
100 IFM-P=9 AND(M AND4)=4 ANDM-5=OP THENX=OP:GOSUB154:GOTO103
101 IFM-P=9 AND(M AND4)=0 ANDM-4=OP THENX=OP:GOSUB154:GOTO103
102 NEXT:J=0:PLAYE$:GOTO54
103 J(TN)=J(TN)+1:HCOLOR8:HLINE(P(X,0)+1,P(X,1)+1)-(P(X,0)+27,P(X,1)+19),PSET,BF:GOTO77
104 P=M:FORQX=0TO3:PP(QX)=-10:PJ(QX)=-10:NEXT:IFTN=1AND C(N)<>2 THEN108
105 IF(P AND4)=4 THENPP(0)=P-4:IF((P-5)AND4)=((P-4)AND4)THENPP(1)=P-5
106 IF(P AND4)=0 THENPP(1)=P-4:IFINT((P-3)/4)<>(P-3)/4THENPP(0)=P-3
107 IFTN=0AND D(N)<>2 THEN111
108 IF(M AND4)=4 THENPP(2)=M+4:IF((M+3)AND4)=((M+4)AND4)THENPP(3)=M+3
109 IF(M AND4)=0 THENPP(3)=M+4:IFINT((M+5)/4)<>(M+5)/4THENPP(2)=M+5
110 IFTN=1 AND C(N)<>2 THEN114
111 IF(PP(0)AND4)=4 THENPJ(0)=PP(0)-4:IF((PP(1)-5)AND4)<>(PP(1)AND4)THENPJ(1)=PP(1)-5
112 IF(PP(0)AND4)=0 THENPJ(1)=PP(1)-4:IF((PP(0)-3)AND4)<>(PP(0)AND4)THENPJ(0)=PP(0)-3
113 IFTN=0AND D(N)<>2 THEN116
114 IF(PP(2) AND4)=4 THENPJ(2)=PP(2)+4:IF((PP(3)+3)AND4)=((PP(3)+4)AND4)THENPJ(3)=PP(3)+3
115 IF(PP(2) AND4)=0 THENPJ(3)=PP(3)+4:IFINT((PP(2)+5)/4)<>(PP(2)+5)/4THENPJ(2)=PP(2)+5
116 GOSUB131
117 FORQ=0TO3:PB(Q)=0:NEXT:FORQX=0TO3
118 IFPJ(QX)<0THENPB(QX)=1ELSEIFBL(PJ(QX))>0THENPB(QX)=1
119 NEXTQX
120 IFTH=1THENRETURN
121 IF PB(0)=0 OR PB(1)=0 OR PB(2)=0 OR PB(3)=0 THEN122ELSE54
122 FORQX=0TO3:IFPP(QX)=-1THEN126
123 IFTN=0THENIFBL(PP(QX))=2 AND PB(QX)=0 THEN127
124 IFTN=1THENIFBL(PP(QX))=10 THENBL(PP(QX))=1
125 IFTN=1THENIFBL(PP(QX))=1 AND PB(QX)=0 THEN127
126 NEXTQX:GOTO54
127 GOSUB89:IFTH=2THEN219ELSEHPRINT(1,0),"Continue your jump"+STRING$(21,32)
128 GOSUB83:IFBUTTON(TN*2)=1THENGOSUB135:GOTO62ELSE128
129 IFJ=0THENRETURNELSEIFTN=0THENTN=1ELSETN=0
130 J=0:RETURN
131 FORQX=0TO3:IFPJ(QX)<0 ORPJ(QX)>31 THENPJ(QX)=-1
132 NEXT
133 FORQX=0TO3:IFPP(QX)<0OR PP(QX)>31THENPP(QX)=-1
134 NEXT:RETURN
135 PLAYB$:IFY<185ANDF=0THENRETURN
136 IFF=0THEN138ELSEF=0:GOSUB89:HPRINT(1,0),"Are you sure you want to surrender?":EXEC44539:A$=INKEY$:PLAYB$:IFA$<>"Y"ANDA$<>"y"THEN54
137 IFTN=0THENTN=1:GOTO146ELSETN=0:GOTO146
138 IFX>63ANDX<112THEN54
139 IFX>111ANDX<160THENHCOLOR2:M$="Red:"+STR$(12-J(1))+"  Black:"+STR$(12-J(0)):M$=M$+STRING$(39-LEN(M$),32):HPRINT(1,0),M$:GOSUB145:GOTO54
140 IFX>207ANDX<256THENHCOLOR2:HPRINT(1,0),"Are you sure you want to quit? (Y/N)"+STRING$(3,32):EXEC44539:A$=INKEY$:PLAYB$:IFA$="Y"ORA$="y"THENGOSUB235:POKE&HFFD8,0:CLS:RGB:HSCREEN0:POKE41598,3:POKE41599,3:ENDELSE54
141 IFX>159ANDX<208THENIFJ>0THEN54ELSEHCOLOR2:HPRINT(1,0),"Save to (1) disk or (2) cassette?"+STRING$(6,32)
142 EXEC44539:A$=INKEY$:IFA$=CHR$(12)THEN54ELSEIFA$<>"1"ANDA$<>"2"THENPLAYE$:GOTO142ELSEPLAYB$:IFA$="1"THEND=1ELSED=-1
143 X=1:Y=0:GOSUB156:IFGR=1THENRETURNELSEHPRINT(1,0),"Press any key to save your game"+STRING$(8,32):EXEC44539
144 POKE&HFFD8,0:OPEN"O",#D,F$:PRINT#D,NG(0);NG(1);:FORQ=0TO11:PRINT#D,A(Q);B(Q);C(Q);D(Q);:NEXT:PRINT#D,TN;NP;IT;GM;J(0);J(1);:CLOSE#D:POKE&HFFD9,0:GOTO54
145 IFBUTTON(TN*2)=0THEN145ELSEPLAYB$:RETURN
146 HCOLOR2:IFTN=0THENHPRINT(1,0),"Red is the winner"+STRING$(22,32):NG(0)=NG(0)+1:IFNG(0)>999THENNG(0)=0
147 IFTN=1THENHPRINT(1,0),"Black is the winner"+STRING$(20,32):NG(1)=NG(1)+1:IFNG(1)>999THENNG(1)=0
148 POKE&HFFD8,0:FORQ=255TO0STEP-1:POKE140,Q:EXEC43345:NEXT:PLAY"L3O3CGL16FEL12DO4L3CO3GL16FEL12DO4L3CO3GL16FEFL3DO2P6DP6L2.CL12P12CC":POKE&HFFD9,0
149 HPRINT(1,0),"Do you want to play again? (Y/N)"+STRING$(7,32)
150 EXEC44539:A$=INKEY$:PLAYB$:IFA$="Y"ORA$="y"THENHSCREEN2:GOTO12ELSEGOSUB235:POKE&HFFD8,0:CLS:RGB:HSCREEN0:POKE41598,3:POKE41599,3:END
151 J=0:HCOLOR2:HPRINT(1,0),"Greetings, Your Highness":HDRAW"BM"+STR$(P(M,0)+16)+","+STR$(P(M,1)+12)+"C1L8U6F4E2F2E4D6L8":HPAINT(P(M,0)+16,P(M,1)+11),1,1
152 POKE&HFFD8,0:PLAY"L12O3CEGL6O4CL24CCO3L6GL24GGL12EGEL6CP6L12CEGL6O4CL24CCO3L12ECGL6CP6L12O3CEGO4L6CO3L12GO4L6C":POKE&HFFD9,0
153 RETURN
154 IFOP=A(Q)THENA(Q)=-2:BL(OP)=0:RETURN
155 IFOP=B(Q)THENB(Q)=-2:BL(OP)=0:RETURN
156 HPRINT(X,Y),"What is the filename? "+STRING$(18,32):F$="":F=0:HPRINT(22+X,Y),CHR$(127)
157 EXEC44539:A$=INKEY$:IFA$=CHR$(8)THENF=F-1:IFF<0THENPLAYE$:F=0:GOTO157ELSEF$=LEFT$(F$,F):HPRINT(22+X+F,Y),CHR$(127)+" ":GOTO157
158 IFA$="/"ORA$="."THENPLAYE$:GOTO157
159 IFA$=CHR$(13)THENHPRINT(22+X,Y),F$+" ":PLAYB$:RETURN
160 IFASC(A$)<32THENPLAYE$:GOTO157
161 F$=F$+A$:F=F+1:IFF=9THENPLAYE$:F$=LEFT$(F$,8):F=8:GOTO157
162 HPRINT(21+X+F,Y),A$+CHR$(127):GOTO157
163 IFTN=0THENTH=0:GOTO55ELSEIFNP=2THEN55ELSEGOSUB89:HPRINT(1,0),"Thinking..."+STRING$(28,32):TH=1:MP=3584:J=0
164 FORMX=0TO11:IFA(MX)=-2THEN170ELSEM=A(MX):N=MX
165 GOSUB104
166 FORMY=0TO3:IFPP(MY)=-1THEN167ELSEIFBL(PP(MY))<>1THENPB(MY)=1
167 NEXTMY
168 FORMW=0TO3:IFPB(MW)=1THEN169ELSEXX=MW:PR=0:GOSUB224:POKEMP,MX:POKEMP+1,PP(MW):POKEMP+2,PJ(MW):GOSUB197:POKEMP+3,PR:MP=MP+4
169 NEXTMW
170 NEXTMX
171 IFMP=3584THEN179ELSEXW=PEEK(3584):XX=PEEK(3585):XY=PEEK(3586):XZ=PEEK(3587):FORMX=3584TO MP-1STEP4
172 PC=PEEK(MX):PP=PEEK(MX+1):PJ=PEEK(MX+2):PR=PEEK(MX+3)
173 IFPR>XZ THENXW=PC:XX=PP:XY=PJ:XZ=PR
174 IFPR=XZ THENIFRND(10)<RND(10)THENXW=PC:XX=PP:XY=PJ:XZ=PR
175 NEXTMX
176 P=A(XW):M=XY:N=XW:FORMZ=0TO11:IFB(MZ)=XX THEN177ELSENEXT
177 X=B(MZ):B(MZ)=-2:BL(X)=0:MZ=11:TH=2:J=1:GOTO103
178 FORMZ=0TO3:IFPB(MZ)=0 ANDPJ(MZ)>-1 THENXY=PJ(MZ):XX=PP(MZ):GOTO176ELSENEXT
179 BK=6656:POKEBK,255:MX=0
180 IFA(MX)=-2THEN188ELSEM=A(MX):N=MX
181 GOSUB104:GOSUB228
182 IFIT=1THEN186ELSEIFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(2))=0 AND BL(PA(1))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(1)THENPOKEBK,PA(2):BK=BK+1ELSENEXTQ
183 Q=12:IFPA(0)>-1 ANDPA(3)>-1 THENIFBL(PA(3))=0 ANDBL(PA(0))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(0)THENPOKEBK,PA(3):BK=BK+1ELSENEXTQ
184 Q=12:IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(1))=0 AND BL(PA(2))=1 THENPOKEBK,PA(1):BK=BK+1
185 IFPA(0)>-1 AND PA(3)>-1 THENIFBL(PA(0))=0 AND BL(PA(3))=1 THENPOKEBK,PA(0):BK=BK+1
186 FORMW=0TO3:IFPP(MW)=-1THEN187ELSEIFBL(PP(MW))>0THEN187ELSEPOKEMP,MX:POKEMP+1,PP(MW):PR=4:GOSUB204:POKEMP+2,PR:MP=MP+3
187 NEXTMW
188 MX=MX+1:IFMX<12THEN180
189 IFIT<3THEN191ELSEFORML=3585TO MP-1STEP3:FORQW=6656TO BK-1:IFPEEK(ML)=PEEK(QW)THENPOKEML+1,PEEK(ML+1)+1
190 NEXTQW,ML
191 IFMP=3584THEN137ELSEXW=PEEK(3584):XX=PEEK(3585):XY=PEEK(3586):FORMX=3584TO MP-1STEP3
192 PC=PEEK(MX):PP=PEEK(MX+1):PR=PEEK(MX+2)
193 IFPR>XY THENXW=PC:XX=PP:XY=PR
194 IFPR=XY THENIFRND(10)>RND(10)THENXW=PC:XX=PP:XY=PR
195 NEXTMX
196 P=A(XW):M=XX:N=XW:GOTO77
197 M=PJ(XX):N=MX:GOSUB104
198 FORMY=0TO3:IFPP(MY)=-1THEN199ELSEIFBL(PP(MY))<>1THENPB(MY)=1
199 NEXTMY
200 FORMY=0TO3:IFPB(MY)=0THEN201ELSE203
201 IFC(N)=2THENBL(PP(MY))=10
202 XX=MY:PR=PR+1:GOTO197
203 NEXTMY:RETURN
204 IFIT=1THEN208ELSEM=A(MX):P=M:GOSUB228:IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(2))=0 AND BL(PA(1))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(1)THENPR=PR+1ELSENEXTQ
205 Q=12:IFPA(0)>-1 ANDPA(3)>-1 THENIFBL(PA(3))=0 ANDBL(PA(0))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(0)THENPR=PR+1ELSENEXTQ
206 Q=12:IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(1))=0 AND BL(PA(2))=1 THENPR=PR+1
207 IFPA(0)>-1 AND PA(3)>-1 THENIFBL(PA(0))=0 AND BL(PA(3))=1 THENPR=PR+1
208 IFPP(MW)>27ANDPP(MW)<32ANDC(N)<>2 THENPR=PR+1
209 M=PP(MW):P=M:GOSUB228
210 IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(2))=2 ANDA(MX)=PA(2)AND BL(PA(1))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(2)THENPR=PR-1ELSENEXTQ
211 Q=12:IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(2))=0 ANDBL(PA(1))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(2)THENPR=PR-1ELSENEXTQ
212 Q=12:IFPA(0)>-1 ANDPA(3)>-1 THENIFBL(PA(3))=2 ANDA(MX)=PA(3)ANDBL(PA(0))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(3)THENPR=PR-1ELSENEXTQ
213 Q=12:IFPA(0)>-1 ANDPA(3)>-1 THENIFBL(PA(3))=0 ANDBL(PA(0))=1 THENFORQ=0TO11:IFD(Q)=2ANDB(Q)=PA(3)THENPR=PR-1ELSENEXTQ
214 Q=12:IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(1))=2 ANDA(MX)=PA(1) AND BL(PA(2))=1 THENPR=PR-1
215 IFPA(1)>-1 ANDPA(2)>-1 THENIFBL(PA(1))=0 ANDBL(PA(2))=1 THENPR=PR-1
216 IFPA(0)>-1 AND PA(3)>-1 THENIFBL(PA(0))=2 AND A(MX)=PA(0)AND BL(PA(3))=1 THENPR=PR-1
217 IFPA(0)>-1 ANDPA(3)>-1 THENIFBL(PA(0))=0 AND BL(PA(3))=1 THENPR=PR-1
218 RETURN
219 FORMY=0TO3:IFPP(MY)=-1THEN220ELSEIFBL(PP(MY))<>1THENPB(MY)=1
220 NEXTMY
221 GOTO178
222 FORQ=18TO22:HPRINT(0,Q),STRING$(40,32):NEXTQ:RETURN
223 RETURN
224 IFC(N)=2THENBL(PP(MW))=10:PR=PR+1
225 RETURN
226 EXEC44539:A$=INKEY$:IFA$=CHR$(12)THENGOSUB222:GOTO12ELSERETURN
227 POKE&HFFD9,0:PLAYE$:HPRINT(EX,EY),"E R R O R    Press any key to continue  ":EXEC44539:HPRINT(EX,EY),STRING$(40,32):IFEY=0THENX=160:GOTO141ELSE156
228 FORQ=0TO3:PA(Q)=-10:NEXT:IF(P AND4)=4 THENPA(0)=P-4:IF((P-5)AND4)=((P-4)AND4)THENPA(1)=P-5
229 IF(P AND4)=0 THENPA(1)=P-4:IFINT((P-3)/4)<>(P-3)/4THENPA(0)=P-3
230 IF(M AND4)=4 THENPA(2)=M+4:IF((M+3)AND4)=((M+4)AND4)THENPA(3)=M+3
231 IF(M AND4)=0 THENPA(3)=M+4:IFINT((M+5)/4)<>(M+5)/4THENPA(2)=M+5
232 FORQ=0TO3:IFPA(Q)<0ORPA(Q)>31THENPA(Q)=-1
233 NEXTQ
234 RETURN
235 GR=1:HPRINT(1,0),"Do you want to save the score? (Y/N)"+STRING$(3,32):EXEC44539:A$=INKEY$:PLAYB$:IFA$<>"Y"ANDA$<>"y"THEN237
236 X=160:GOSUB141:GR=0:HPRINT(1,0),"Press any key to save the score":EXEC44539:POKE&HFFD8,0:OPEN"O",#D,F$:PRINT#D,NG(0);NG(1);:CLOSE#D:POKE&HFFD9,0
237 HCLS8:HCOLOR0:PALETTE0,18:FORQ=191TO0STEP-1:HLINE(289,Q)-(30,Q),PSET:NEXT:RETURN
