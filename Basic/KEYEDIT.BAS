0 'COPYRIGHT 1989, FALSOFT INC.
10 POKE150,41'SET PORT FOR 1200 BAUD
11 'KEYEDIT V.1
12 'WRITTEN BY MIKE DALENE
13 'MARCH 14,1988
20 REM ROM TO RAM TRANSFER
30 FORX=32000 TO32025:READA:POKEX,A:NEXTX:EXEC 32000
40 DATA 26,80,142,120,0,236,132
50 DATA 183,255,223,237,129,183
60 DATA 255,222,140,254,255,37
70 DATA 241,183,255,223,28,175,57
80 REM REDEFINE SOME OF THE KEYS AS LISTED BELOW IN DATA STATEMENTS NEXT TO NEW VALUE
90 DATA[UP],[SH] [UP],[DN],[SH] [DN],[LF],[SH] [LF],[RT],[SH] [RT]
100 DATA [SPC],[SH] [SPC],0,[SH] 0,[ENTER],[SH] [ENTER],[CLR],[SH] [CLR],[BRK],[SH] [BRK],@,[SH] @
110 DIMA$(20),X(20)
120 FORC=1TO20:READA$(C):X(C)=PEEK(41581+C):NEXTC:CLS:PRINT"I AM DONE ENTERING THE DATA.":FORG=1TO25:NEXT:GOSUB 170
130 'JUMP TO PARTS OF PROGRAM
140 IFA$="6" THEN510
150 ON VAL(A$) GOSUB 270,410,435,525,640
160 GOSUB 170:GOTO140
170 REM DISPLAY MENU
180 CLS:PRINTTAB(8);"MENU OF CHOICES"
190 PRINT"(1) EDIT KEYCODES"
200 PRINT"(2) PRINT NEW CODES"
210 PRINT"(3) SAVE NEW CODES TO DISK"
220 PRINT"(4) LOAD FILES"
230 PRINT"(5) PLACE DATA IN MEMORY"
240 PRINT"(6) EXIT PROGRAM"
250 A$=INKEY$:IF A$<"1" OR A$>"6" THEN 250
260 RETURN
270 CLS:PRINT"CONTROL KEYCODE EDITOR ENABLED"
280 A=1
285 A$=INKEY$
290 IFA$=CHR$(PEEK(41582)) THEN A=A-1
295 IFA$=CHR$(PEEK(41585)) THENA=20
297 IFA$=CHR$(PEEK(41583)) THENA=1
300 IFA$=CHR$(PEEK(41584)) THENA=A+1
310 IFA<1THENA=1
320 IFA>20 THENA=20
330 IFA$=CHR$(PEEK(41594)) THEN 350
340 PRINT@32,A;A$(A);X(A):GOTO285
350 PRINT"CODE IS NOW ";A$(A);X(A):INPUT"ENTER NEW CODE";X(A):PRINT"YOUR LINE NOW READS "A$(A);X(A):PRINT"DO YOU WISH TO CHANGE MORE Y/N""
360 A$=INKEY$:IFA$=""THEN 360
370 IFA$="Y" THEN270
380 IFA$="N"THENRETURN
390 GOTO360
400 'PRINT OUT CODES
410 CLS:PRINTTAB(9);"PRINTING FILE":PRINT#-2,CHR$(27);CHR$(53);CHR$(1)
420 FORC=1TO20:PRINT#-2,C;TAB(3);A$(C);TAB(20);X(C):NEXT:RETURN
430 'FILE SAVE
435 CLS:PRINTTAB(11);"FILE SAVE"
440 INPUT"ENTER YOUR FILES NAME";F$:IFLEN(F$)>8 THEN PRINT"YOUR FILENAME IS TOO BIG, IT CAN ONLY CONTAIN UP TO 8 LETTERS":GOTO 440
450 INPUT"DO YOU WANT A (B)INARY OR (T)EXT FILE";B$
460 IFB$="B" THEN490
470 IFB$="T" THEN 500
480 GOTO450
490 SAVEMF$+".BIN",41582,41601,&HB4F4:RETURN
500 OPEN"O",#1,F$+".DAT":FORC=1TO20:WRITE#1,A$(C),X(C):NEXTC:CLOSE#1:RETURN
510 END
520 REM LOAD DATA FROM TAPE
525 CLS:PRINTTAB(11);"FILE LOAD"
530 INPUT"FILENAME";F$:IF LEN(F$)>8 THEN PRINT"TOO BIG! ONLY 8 CHARACTERS OR LESS ALLOWED!":GOTO530
540 INPUT"(B)INARY OR (T)EXT";T$
550 IFT$="B"THEN580
560 IFT$="T" THEN 590
570 GOTO 540
580 LOADMF$+".BIN":RETURN
590 OPEN"I",#1,F$+".DAT"
600 FORC=1TO20
610 INPUT#1,A$(C),X(C)
620 NEXT:CLOSE#1:RETURN
630 REM PUT DATA IN KEYBOARD MATRIX
640 CLS:PRINT"LOADING NEW DATA":FORC=1TO20:POKE41581+C,X(C):PRINT"-";::NEXTC:PRINT"DONE!!!":FORTT=1TO200:NEXTTT:RETURN
