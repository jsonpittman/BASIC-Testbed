100 '     FINDADDR PROGRAM
200 '     BY DENNIS H. WEIDE
300 '
400 '     THIS PROGRAM WILL READ
500 '     THE START, END AND EXEC
600 '     ADDRESSES OF AN ML PROGRAM
700 '     FROM DISK AND PRINT THE
800 '     RESULTS ON THE SCREEN
900 '
1000 '
1100 CLS
1200 LINEINPUT"ENTER FILENAME > ";F$
1300 CLS
1400 '
1500 '   OPEN FILE TO READ
1600 '   A BYTE AT A TIME
1700 '
1800 OPEN "D",#1,F$,1
1900 FIELD #1,1 AS A$
2000 '
2100 '   READ FIRST BYTE TO
2200 '   SEE IF FILE IS AN
2300 '   ML BINARY FILE
2400 '
2500 GET #1,1
2600 IF ASC(A$)>0 THEN 7000
2700 '
2800 '   READ BYTES 4 AND 5
2900 '   AND CALCULATE THE
3000 '   START ADDRESS
3100 '
3200 GET #1,4
3300 SA=ASC(A$)*256
3400 GET #1,5
3500 SA=SA+ASC(A$)
3600 '
3700 '   READ BYTES 2 AND 3 AND
3800 '   CALCULATE PROGRAM SIZE
3900 '
4000 GET #1,2
4100 PS=ASC(A$)*256
4200 GET #1,3
4300 PS=PS+ASC(A$)
4400 '
4500 '   CALCULATE END ADDRESS
4600 '
4700 EA=SA+PS-1
4800 '
4900 '   LOCATE LAST TWO BYTES
5000 '   OF FILE AND CALCULATE
5100 '   EXECUTE ADDRESS
5200 '
5300 GET #1,PS+9
5400 EX=ASC(A$)*256
5500 GET #1,PS+10
5600 EX=EX+ASC(A$)
5700 CLOSE#1
5800 '
5900 '   PRINT RESULTS TO SCREEN
6000 '
6100 PRINT F$
6200 PRINT
6300 PRINT "FUNCTION         DEC     HEX"
6400 PRINT "-----------------------------"
6500 PRINT "START ADDRESS   ";SA;"  ";HEX$(SA)
6600 PRINT "END ADDRESS     ";EA;"  ";HEX$(EA)
6700 PRINT "EXEC ADDRESS    ";EX;"  ";HEX$(EX)
6800 PRINT "TOTAL BYTES     ";EA-SA+1;"   ";HEX$(EA-SA+1)
6900 END
7000 PRINT F$:PRINT:PRINT "NOT A BINARY FILE"
