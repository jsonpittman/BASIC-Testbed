0 CLS:PRINT@201,"DESKTOP LOW":PRINT@262,"BY H. ALLEN CURTIS":PRINT@296,"COPYRIGHT 1987":FORI=0TO500:NEXT
5 CLEAR5000:W=256
10 K$=K$+"C":A1=PEEK(VARPTR(K$)+2):A2=PEEK(VARPTR(K$)+3):IFA2<2THENA2=254:A1=A1-1:GOTO25ELSEA2=A2-2:GOTO25GOTO25
15 GOSUB440:CLS:PRINT@200,"FILENAME: ";:LINEINPUTF$:SAVEMF$+"/LR",&HE00,&H2600,&HAC73:RETURN
20 GOSUB440:CLS:PRINT@200,"FILENAME: ";:LINEINPUTF$:LOADMF$+"/LR":RETURN
25 L2=1:P=176:D=15:S=8:DIMF$(84),M(84),R(500),S(100),F(500),G(200):GOSUB565:F$(0)=D$(7):M(0)=D(7):PMODE4:PCLS1:COLOR0,1:C2=1
30 GET(0,0)-(1,D),S
35 GET(18,20)-(113,170),R,G:GET(18,20)-(113,170),F,G:K$="1":GOTO310
40 PCLS:T=V:L=U
45 IF(Q=0 OR Z=U) AND L+4>W-1THENL=U:IFT<P THENT=T+D+1
50 IFQ=1 AND Z<>U AND L+4>W-1THENGOSUB175
55 GET(L,T)-(L+1,T+D),S:LINE(L,T)-(L+1,T+D),PSET,BF
60 POKE&H23,A1:POKE&H24,A2
65 K$=INKEY$:IFK$=""THEN65
70 K=ASC(K$):IFK>64 AND K<91THENN=K-64:B=N:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
75 IFK>96 AND K<123THENN=K-96:B=N+26:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
80 IFK>47 AND K<58THENN=K-47:B=N+52:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
85 IFK>32 AND K<48THENN=K-32:B=N+62:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
90 IFK>57 AND K<65THENN=K-57:B=N+77:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
95 IFK=32THENZ=L+S:IFL+8<W THENLINE(L,T)-(L+7,T+D),PRESET,BF:L=L+S:GOTO45ELSELINE(L,T)-(L+1,T+D),PRESET,BF:L=U:T=T+1+D:GOTO45
100 IFK=13THENPUT(L,T)-(L+1,T+D),S:L=U
105 IFK=13 AND T<P THENT=T+1+D:GOTO45
110 IFK=8 AND L>1THENLINE(L,T)-(L+1,T+D),PRESET,BF:L=L-2:GOTO45
115 IFK=93THENB=0:LINE(L,T)-(L+1,T+D),PRESET,BF:GOTO160
120 IFK=94THENPUT(L,T)-(L+1,T+D),S:L=U:IFT>D THENT=T-1-D:GOTO45
125 IFK=91THENZ=U:PUT(L,T)-(L+1,T+D),S:DRAW"BM"+STR$(L)+","+STR$(T+1+INT(.75*D))+"R3":L=L+4:IFL>W-5THENL=L-4
130 IFK=9 AND L+4<W THENPUT(L,T)-(L+1,T+D),S:L=L+4:GOTO45
135 IFK=92THEN40
140 IFK=12THENPUT(L,T)-(L+1,T+D),S:GOTO200
145 IFK=10THENPUT(L,T)-(L+1,T+D),S:IFT3=0THENT3=1:L=T1 ELSEIFT3=1THENT3=0:L=T2
150 IFK=21THENPUT(U,T)-(W-1,T+D),R,PSET:L=U:GOTO45
155 GOTO45
160 IFL+M(B)<W THENGOSUB195:L=L+2+M(B):IFL>W-1THENL2=L-W+1:L=W-1:GOTO45ELSE45
165 IFQ=0 OR Z=U THENL=U:IFT<P THENT=T+D+1:GOTO70ELSE70
170 GOSUB175:GOTO70
175 Z1=2*INT(.5*Z):Z=Z1
180 GET(Z,T)-(L,T+D),G,G:PUT(Z,T)-(L,T+D),R,PSET:L1=L-Z:L=U:Z=U:IFT<P THENT=T+D+1
185 IFL+L1<0THENL1=L1+2:GOTO185
190 PUT(L,T)-(L+L1,T+D),G,PSET:L=L+L1+L2:L=2*INT(.5*L+.5):L2=0:RETURN
195 DRAW"BM"+STR$(L)+","+STR$(T)+F$(B):RETURN
200 CLS:PRINT@40,"B: BACK TO SCREEN":PRINT@72,"C: COLOR SWAP":PRINT@104,"D: DESIGN SELECT":PRINT@136,"F: FONT SELECT":PRINT@168,"I: INPUT":PRINT@200,"K: KEYS LISTED":PRINT@232,"M: MARGIN SPECIFY":PRINT@264,"O: OUTPUT
205 PRINT@296,"S: SWITCH COLOR SET":PRINT@328,"T: TAB SET":PRINT@360,"W: WORDWRAP SET":PRINT@392,"X: EXIT DESKTOP":POKE&H23,A1:POKE&24,A2:POKE&HFFD6,0
210 K$=INKEY$:IFK$=""THEN210
215 IFK$="D" OR K$="d"THENGOSUB615:GOTO200
220 IFK$="F" OR K$="f"THEN285
225 IFK$="C" OR K$="c"THENC3=C2:C2=C1:C1=C3:COLORC1,C2:GOTO200
230 IFK$="I" OR K$="i"THENGOSUB20:GOTO200
235 IFK$="K" OR K$="k"THENGOSUB540:GOTO200
240 IFK$="O" OR K$="o"THENGOSUB15:GOTO200
245 IFK$="M" OR K$="m"THENGOSUB455:GOTO200
250 IFK$="B" OR K$="b"THEN280
255 IFK$="T" OR K$="t"THENGOSUB525:GOTO200
260 IFK$="X" OR K$="x"THENEND
265 IFK$="S" OR K$="s"THENC=ABS(C-1):GOTO200
270 IFK$="W" OR K$="w"THENGOSUB500:GOTO200
275 SOUND60,5:SOUND60,3:GOTO200
280 SCREEN1,C:GOTO340
285 GOSUB345
290 POKE&H23,A1:POKE&H24,A2
295 K$=INKEY$:IFK$="" THEN295
300 IFK$<"1" OR K$>"2" THENSOUND60,10:GOTO295
305 GOSUB440
310 GOSUB450
315 OPEN"I",#1,"FONT"+K$
320 FORI=1TO84:LINEINPUT#1,F$(I):NEXT
325 FORI=1TO84:INPUT#1,M(I):NEXT
330 INPUT#1,D,S:CLOSE#1
335 PUT(18,20)-(113,170),F,PSET
340 SCREEN1,C:GOSUB495:GET(L,T)-(L+1,T+D),S:POKE&HFFD7,0:GOTO45
345 SCREEN1,C:GET(18,20)-(113,170),F,G:PUT(18,20)-(113,170),R,PSET:DRAW"BM25,28NGD6NL2RNR2U6BM32,29RDLBD3RDL
350 DRAW"BM50,24G3ERE2R4GNL3G4D5EU4BR2D5G4UH2LG2ER3FERE2URUE2NFG2U4E4F2DH2DFBM64,29G2ND4LD4NHFRE2NU5RU4FBM72,29G2RD4NHFNEU4NU2E3ND6FNFD6E2BM83,25G3LR3NR2NUD8NE2H2RU5
355 DRAW"BM24,43NGNDR3ND2FDGL2NG2DG2R5ULBM32,44RDLBD3RDL
360 DRAW"BM49,43R6NDNGL5D3NR3D3NLR2HU4BM59,45ND3GD2FR3NU3EU2HL2BM66,45D4RU4R3D4RU3BM76,43ND5G2NR4FD2FRE
365 DRAW"BM24,62NGNDR3ND5FDGNL2FDGL3ULBM32,63RDLBD3RDL
370 DRAW"BM50,56R5BR2DNLNRDRNRDRE2BL7L5DLNGBR5G2ND8GD8BDBL2L2DR2DFNDRURE2U9NE3RD3ND4REFBM64,61G2ND6LD5LF2RE2NU7RU6FBM72,61NG2D3E3DRFLD7NE2HNU6BL2GNU5LNHU7BM83,57G2RD11NE2H2RU8L2R5
375 DRAW"BM26,81G3DE4D6NLRNRU2NRNL4U4BM32,82RDLBD3RDL
380 DRAW"BM46,78R11D2RHL2BL6D6R3D2ENRHL3D6GU13LBM62,82G3D2F3R5E3U2H3L4G3D2F3R3E3U2H2BM72,82R3GNLD7RU5NU2E3R2DRD7LNUE2U4BM88,80DLGR7NR5G5UNE2FDRDRDR4E2
385 DRAW"BM23,98NR5D2EDR3ND3FD2GL3ULBM32,99RDLBD3RDL
390 DRAW"BM48,96NR6D8RU4NU3R3BM56,99ND4GD3FR4NU5EU3HL3BM64,99D5RU5R4D5U4BM74,96D7FRHU5NL3NR2U2
395 DRAW"BM25,110NR2G2D3FR3NU2EUHL3ND2UEBM32,111RDLBD3RDL
400 DRAW"BM48,109R7NDL6D8NLR2HU3NU3R3NUDBM58,112ND4GD3FR4NU4EU3HL3BM66,112RD5NLR2HU3ER3D5NLR2HU3BM79,109D8REBL3U4NR3NL2U2
405 DRAW"BM23,124NDNFR5D2HDG2D2RU2EBM32,125RDLBD3RDL
410 DRAW"BM52,123R4NFL4G2D2NR3D4BM60,125G2D2F2R2E2U2H2LBM68,125ND6FER2F2D4BM78,123DND7GR4
415 DRAW"BM24,136ND5GDFGDFR3NU5EUHNL2EUHL2BM32,137RDLBD3RDL
420 DRAW"BM50,136NGR3NR4D4L2NGR3NU3NR2D2HD2GL3BM62,138G2DED2R3E2UGU2L2BM70,138NGD4RUNU3E3RD4RNEU3BM81,136D6R2NEL2HU4GR4
425 DRAW"BM24,149ND2GDFR3DG2NLRE2U3HND2L2BM32,150RDLBD3RDLBM50,148NG3NR7ND3FD3NR4L3NGR3ND7LD8GL3BM60,148G3NRF2NU3R3E2NU2LU3L2BM68,148G2RD3RU2NU2E3RD5RNE2U4BM79,147NG2D2NR2ND3LD3FR2E2
430 DRAW"BM24,163ND5GD4FR3U5G2DE3ND4HL2BM32,164RDLBD3RDLBM50,164NR3D2NR2D3BM56,166GDFREUHBM61,166D2NDE2FD2BM68,164D2NLNRD2FE
435 RETURN
440 POKE&H23,A1-1:POKE&H24,A2:RETURN
445 POKE&H23,A1-2:POKE&H24,A2:RETURN
450 POKE&H23,A1-3:POKE&H24,A2:RETURN
455 GOSUB440:CLS:PRINT@129,"ENTER TOP MARGIN (0 - 10): ";:LINEINPUTV$
460 PRINT@193,"ENTER LEFT MARGIN (0-200):     ":PRINT@220,"";
465 GOSUB445:LINEINPUTU$:V=VAL(V$):U=VAL(U$):Z=U
470 IFV<0THENV=0
475 IFV>10THENV=10
480 IFU>W-50THENSOUND60,3:PRINT@262,"LEFT MARGIN TOO BIG":PRINT"   RELATIVE TO RIGHT MARGIN!":PRINT@330,"TRY AGAIN.":GOTO460
485 IFU<0THENU=0
490 IFH=3 AND U>400THENU=400
495 P=V+(D+1)*(-1+INT((192-V)/(D+1))):L=U:T=V:RETURN
500 CLS:PRINT@194,"VALUE 0 MEANS NO WRAP-AROUND.   ANY OTHER VALUE SPECIFIES       THE RIGHT MARGIN."
505 GOSUB440:PRINT@290,"ENTER VALUE (50 TO 256):     ";:PRINT@316,"";:LINEINPUTW$:IFVAL(W$)=0THENQ=0:RETURN
510 Q=1:W=VAL(W$):IFW<U+50THENSOUND60,3:PRINT@356,"RIGHT MARGIN TOO SMALL          RELATIVE TO LEFT MARGIN.":PRINT@425,"TRY AGAIN.":GOTO505
515 IFW>256THENW=256
520 RETURN
525 GOSUB440:CLS:PRINT@130,"ENTER 1ST TAB VALUE: ";:LINEINPUTT$:T1=VAL(T$):IFT1>256THENT1=256
530 GOSUB440:PRINT@194,"ENTER 2ND TAB VALUE: ";:LINEINPUTT$:T2=VAL(T$):IFT2>256THENT2=256
535 RETURN
540 CLS:PRINT@33,"RIGHT ARROW: MOVE CURSOR RIGHT":PRINT@65,"LEFT ARROW: BACKSPACE":PRINT@97,"UP ARROW: CARRIAGE RETURN &":PRINT@139,"MOVE UP 1 LINE":PRINT@161,"DOWN ARROW: TAB"
545 PRINT@193,"SHIFT RIGHT ARROW: DRAW DESIGN":PRINT@225,"SHIFT LEFT ARROW: CLEAR LINE":PRINT@257,"SHIFT DOWN ARROW: UNDERLINE":PRINT@289,"SHIFT CLEAR: CLEAR SCREEN
550 PRINT@321,"ENTER: CARRIAGE RETURN &":PRINT@363,"MOVE DOWN 1 LINE":PRINT@385,"SHIFT 0: UPPER/LOWER CASE
555 PRINT@481,"PRESS SPACE TO RETURN TO MENU";
560 K$=INKEY$:IFK$=""THEN560ELSERETURN
565 D(1)=14:D$(1)="D15BR5U15BR5D15BR5U15
570 D(2)=14:D$(2)="R15BD5L15BD5R15BD5L15
575 D(3)=14:D$(3)="R15D15L15U14BR2BDR11D11L11U10BR2BDR7D7L7U6BR2BDR3D3L3U2
580 D(4)=14:D$(4)="BR3G2NUDLD5NF7EDF6NE6RE6NHU5LU2LNL3HL2G2NRNH2DH3DL
585 D(5)=14:D$(5)="BR7BDG3RNE2DG3RNE3DG3RNE3R6ND2R7H3LNF2ENH3RH3UNHRH2
590 D(6)=14:D$(6)="R5G5NU4BD2E7F7G7H7BD2D5R5NH4BR4R5U5NG4BU4U5L5NF4BL2BD3G4F4E4H4BD3GFE
595 D(7)=62:D$(7)="BR31BD4L3GL3NU2D2GNU4GD2GD3R5NFUL2GL2GLGNDL8GLNG2BR8D2LGL2NG2BR4ENFGD3NR2DL2GLBR5FRFR5ER2ER5FR2FR5ERE2R2FRLHL2UNL2U4LNGR2FR2NF2L2H2U2R6FRF2BL10U3L2NDHLHL5NGUR2BR3U2HU2HNU4HU3NUGL2HL3BD13U3LND3U2HUH2RGFDFD
600 D(8)=15:D$(8)="BR5R5FL7ND6GD8HNUD2LGDR7D2RU2R7UH2DNU3HU8LD5
605 D$(9)="D15
610 RETURN
615 CLS:PRINT@12,"DESIGNS":PRINT@65,"1: VERTICAL STRIPES":PRINT@97,"2: HORIZONTAL STRIPES":PRINT@129,"3: CONCENTRIC SQUARES":PRINT@161,"4: HEART":PRINT@193,"5: XMAS TREE"
620 PRINT@225,"6: DIAMONDS & TRIANGLES":PRINT@257,"7: COCO CAT":PRINT@289,"8: BELL":PRINT@321,"9: VERTICAL BAR
625 POKE&H23,A1:POKE&H24,A2
630 K$=INKEY$:IFK$=""THEN630
635 IFK$<"0" OR K$>"9"THENSOUND60,5:RETURN
640 K=VAL(K$):F$(0)=D$(K):M(0)=D(K):RETURN
