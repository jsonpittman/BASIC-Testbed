10 REM"PRESCHOOL COUNTING CARS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1986"
30 BB=0
40 FOR B= 1 TO 9
50 CLS0:PRINT@96,STRING$(64,223);
60 B$=CHR$(165)+CHR$(172)+CHR$(174)+CHR$(172)+CHR$(170)
70 PRINT@27-BB,B$;
80 C$=CHR$(145)+CHR$(175)+CHR$(175)+CHR$(175)+CHR$(175)+CHR$(175)+CHR$(175)
90 PRINT@57-BB,C$;
100 D$=CHR$(252)+CHR$(128)+CHR$(128)+CHR$(252)
110 PRINT@91-BB,D$;
120 BB=BB+3:SOUND40,2
130 NEXTB
140 CLS0
150 A=RND(4)
160 FOR T= 1 TO A
170 PRINT@130+M,B$;
180 PRINT@160+M,C$;
190 PRINT@194+M,D$;
200 M=M+7:SOUND 200,2
210 NEXT T
220 M=0
230 B=RND(4)
240 FOR T= 1 TO B
250 PRINT@258+M,B$;
260 PRINT@288+M,C$;
270 PRINT@322+M,D$;
280 M=M+7:SOUND 200,2
290 NEXT T
300 PRINT@10,STRING$(11,255);
310 PRINT@74,STRING$(11,255);
320 PRINT@42,CHR$(255);:PRINT@52,CHR$(255);
330 PRINT@47,"?";
340 EN$=INKEY$
350 IF EN$="1" THEN J=1:J$=" ONE "
360 IF EN$="2" THEN J=2:J$=" TWO "
370 IF EN$="3" THEN J=3:J$="THREE"
380 IF EN$="4" THEN J=4:J$="FOUR "
390 IF EN$="5" THEN J=5:J$="FIVE "
400 IF EN$="6" THEN J=6:J$=" SIX "
410 IF EN$="7" THEN J=7:J$="SEVEN"
420 IF EN$="8" THEN J=8:J$="EIGHT"
430 IF EN$<>"1" AND EN$<>"2" AND EN$<>"3" AND EN$<>"4" AND EN$<>"5" AND EN$<>"6" AND EN$<>"7" AND EN$<>"8" THEN 340
440 PRINT@47,EN$;
445 PRINT@461,J$;
450 C=VAL(EN$)
460 IF C=A+B THEN FOR T=1 TO 3:PLAY"03;L30CEGGGFEDC":NEXT T:GOTO 480
470 IF C<>A+B THEN 340
480 AN$=INKEY$
490 IF AN$=CHR$(13) THEN RUN ELSE IF AN$="E" THEN 500 ELSE 480
500 CLS:END
