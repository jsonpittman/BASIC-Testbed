0 ' COPYRIGHT 1989  FALSOFT,INC
10 DIMX(1026),Y(1026),A(1026)
20 IX=0
30 X(1)=128:Y(1)=10:X(2)=188:Y(2)=114:X(3)=68:Y(3)=114:X(4)=128:Y(4)=10:IT=4:PT=4:A(1)=2:A(2)=3:A(3)=4:A(4)=0:MT=7
40 PMODE4,1:SCREEN1,1:PCLS0:COLOR1,0:LP=1:CC=0
50 X=X(LP):Y=Y(LP)
60 IFCC=0 THENDRAW"BM=X;,=Y;":CC=1
70 LINE-(X,Y),PSET
80 LP=A(LP):IFLP=0 THEN 90 ELSE 50
90 IFIX=IT THEN 200
100 GOSUB110:GOTO40
110 LP=1
120 LQ=A(LP)
130 XK=(X(LQ)-X(LP))/3:YK=(Y(LQ)-Y(LP))/3
140 X(MT)=X(LP)+XK:X(MT+2)=X(LP)+XK+XK:Y(MT)=Y(LP)+YK:Y(MT+2)=Y(LP)+YK+YK
150 X(MT+1)=(X(LP)+X(LQ))/2+(Y(LQ)-Y(LP))/3.4641
160 Y(MT+1)=(Y(LP)+Y(LQ))/2-(X(LQ)-X(LP))/3.4641
170 A(LP)=MT:A(MT)=MT+1:A(MT+1)=MT+2:A(MT+2)=LQ:MT=MT+3
180 LP=LQ:LQ=A(LQ):IFLQ>0 THEN 120
190 IX=IX+1:RETURN
200 IFINKEY$=""THEN200ELSE20
