0 ' COPYRIGHT 1989  FALSOFT,INC
1 REM INNKEEP c1989 L.JONES
2 REM COLOR BASIC,4K
10 A$=CHR$(128):B$=CHR$(133):C$=CHR$(138):D$=CHR$(131):E$=CHR$(140):F$=CHR$(32):CLS:FOR A=23TO407STEP32:PRINT@A,C$:NEXT:FOR A=416TO439:PRINT@A,D$:NEXT
15 FOR A=96TO224STEP64:PRINT@A,E$;E$,"     ";E$;E$;:PRINT@A+32,C$,"     ";C$;:NEXT:PRINT@152,"H:"
20 FOR A=67TO291STEP32:PRINT@A,B$,"    ";C$;:NEXT:FOR A=N TO17:PRINT@35+A,E$;:PRINT@340-A,D$;:NEXT:PRINT@24,"P:":PRINT@88,"M:":M=N:IF P>H THEN H=P:PRINT@154,MID$(STR$(H),2,6);
25 P=N:PRINT@22,C$;:PRINT@53,D$;A$;:PRINT@85,E$;B$;:PRINT@321,A$;:PRINT@352,C$;A$;D$;:PRINT@384,E$;A$;E$;
30 PRINT@448,"READY":FOR A=1TO999:NEXT:PRINT@448,F$
35 D=51
40 R=RND(20):PRINT@D,E$;:IF D=36THEN S=1ELSEIF D=51THEN S=-1
45 K$=INKEY$:IF K$=F$THEN70ELSEIF K$=CHR$(8)THEN V=-1ELSEIF K$=CHR$(9)THEN V=1
50 T=T+V:IF T<355THEN T=355ELSEIF T>370THEN T=370
55 PRINT@T,D$;A$;A$;F$;:D=D+S:PRINT@D,"*";:IF R>1THEN40ELSE S=32:PRINT@D-32,F$;:IF D=T-31OR D=T-30THENGOSUB65ELSEIF D<320THEN45ELSE M=M+1:PRINT@90,M:SOUND170,9:SOUND159,3:SOUND170,3:SOUND159,9:SOUND147,13
60 PRINT@D,D$;:PRINT@D-288,E$;:IF M<3THEN35ELSE G$=INKEY$:PRINT@462,"GAME  OVER":IF G$="N"THEN20ELSEIF G$="Y"THEN75ELSE60
65 P=P+10:SOUND239,1:SOUND244,1:SOUND243,1:PRINT@26,MID$(STR$(P),2,6);:RETURN
70 PRINT@455,"PAUSE":P$=INKEY$:PRINT@455,F$:IF P$=K$THEN50ELSEIF P$<>"E"THEN70
75 PRINT@462,"       END"
