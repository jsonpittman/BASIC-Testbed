10 ' *** TAKETEST ADMINISTERS THE SESSION
20 FILES 2,570:PCLEAR1:CLEAR 10000:XB=400 ' *** XB INITIALIZES TIME DELAY SETTING IN LINE 1560
30 DIM W$(3,50),R(10),S$(10),D$(10),C$(10),B$(3),W(50),TP$(2)
40 TP$(1)="test":TP$(2)="practice"
50 CLS5:PRINT@261,"--------";:PRINT@192,"enter your name:";:PRINT@261,"";:LINEINPUT"";RN$
60 CLS5:PRINT@232,"GETTING WORDS";:PRINT@288+(13-LEN(RN$))/2,"one moment, please ";RN$;:A$="1 MOMENT PLEEZ"+RN$:GOSUB1430:POKE65494,0
70 OPEN"D",#1,"HELP/SCN",17
80 FIELD#1,12AS A$,2AS B$,3AS C$
90 F$=STRING$(8," ")+"/TST":GET#1,1:F=INSTR(A$,"/"):MID$(F$,1,F-1)=LEFT$(A$,F-1) ' *** GETS NAME OF TEST FILE
100 YR$=MID$(C$,1,1):MO$=MID$(C$,2,1):DA$=MID$(C$,3,1) ' *** GETS DATE INFO
110 TP=ASC(MID$(B$,2,1)) ' *** TP --> TEST OR PRACTICE
120 CLOSE#1
130 ' *** CHANGE FILE NAME TO EXECUTE
140 GOSUB1000:PRINT@192,"SELECTED TEST IS: ";F$;:PRINT@268,"FOR ";TP$(TP);:PRINT@352,"change file?";
150 FOR Y=1TO300 ' *** SHORT INTERRUPT-LIKE DELAY
160 Y$=INKEY$:IF Y$="Y"THEN170ELSE NEXT Y:GOTO190
170 GOSUB1000:PRINT@17,"--------";:PRINT@49,"/TST ASSUMED";:PRINT@0,"";:LINEINPUT"enter file name: ";F$:F$=F$+"/TST" ' *** CHANGES FILE NAME
180 PRINT@330,"TEST OR PRACTICE?";:PRINT@367,"<1> test";:PRINT@399,"<2> practice";:GOSUB1570:TP=VAL(Y$):IF TP<1OR TP>2THEN SOUND120,3:GOTO180
190 ' *** LOAD SPELLING WORDS FROM F$
200 J=0:OPEN"D",#1,F$,51
210 FIELD#1,20 AS B$(1),20 AS B$(2),11 AS B$(3)
220 E=LOF(1)
230 FOR I=1 TO E
240 GET#1,I:W$(1,I)=B$(1):W$(2,I)=B$(2):W$(3,I)=B$(3):NEXT I
250 CLOSE#1
260 ' *** DISPLAY NOTE TO EXPLAIN PROCEDURE TO ASK COCO FOR WORD REPEAT
270 CLS4:SOUND200,1:FOR X9=4TO12:PRINT@32*X9+8,STRING$(16," ");:NEXT X9
280 PRINT@171,"n  o  t  e";
290 PRINT@232,"YOU MAY ASK TO";:PRINT@264,"HAVE ANY WORD";:PRINT@296,"REPRONOUNCED AT";:PRINT@328,"ANYTIME ....";:PRINT@393,"JUST PRESS <^>";
300 XB=1200:GOSUB1560
310 ' *** COMMENCE SESSION
320 UNLOAD:GOSUB 1000:A$="O K WE WILL START NOW":GOSUB1440:XB=1000:GOSUB1560
330 EN=E:GOSUB1190:A$="THIS TEST HAS "+E$+" WORDSS   GET READY":GOSUB1430:GOSUB1000
340 Z$="score = ### out of ### = ###.# %"
350 POKE65495,0:K=0:S=0:T=0:P=0:KK=0:GOSUB1020 ' *** SPEEDUP POKE AND INITIALIZE VARIABLES
360 FOR I=1 TO E
370 C1=C1+C:C=0 ' *** C1 --> TOTAL NO. REPEAT REQUESTS THIS SESSION ... C --> NO. REPEAT REQUESTS PREVIOUS WORD (IF ANY), SO MUST BE RESET TO 0
380 ' *** SHORTEN WORD OF BLANK SPACES AT END, IF ANY
390 FOR X9=20TO1STEP-1:IF MID$(W$(1,I),X9,1)<>" "THEN W$(1,I)=LEFT$(W$(1,I),X9)ELSE NEXT X9
400 ' *** SAME FOR PRONUNCIATION
410 FOR X9=20TO1STEP-1:IF MID$(W$(2,I),X9,1)<>" "THEN W$(2,I)=LEFT$(W$(2,I),X9)ELSE NEXT X9
420 ' *** LL --> COUNT OF WORDS TO REDO (IF PRACTICE)
430 II=I:LL=0:J=1:C$=""
440 ' *** IF ANY REPEATS EXIST (I.E.- KK<>0), THEN 1 IN 10 CHANCE, ONE OF THEM WILL BE ASKED HERE (NO CREDIT BASIS), UNLESS A GRADED EXAM
450 IF KK=0THEN460ELSE R=RND(99):IF R>89THEN R=RND(K):I=R(R):LL=1
460 B$="":CLS ' *** RESET INPUT STRING VARIABLE (B$) TO NULL
470 PRINT USING Z$;S,T,P;
480 PRINT@163,C$; ' *** C$ IS SET TO LAST INCORRECT SPELLING OF THIS WORD (IF ANY)
490 GOSUB1560:A$="SPELL":GOSUB1450:A$=W$(2,I):GOSUB1420
500 SOUND100,1
510 PRINT@256,"ANSWER: ";B$;:Y$=INKEY$ ' *** PRINTS CURRENT ENTRY FOR THE ANSWER AND CLEARS KEYBOARD
520 Y$=INKEY$:IF Y$=""THEN520ELSE Y=ASC(Y$) ' *** WAIT ON KEYSTROKE
530 IF Y>64 AND Y<91THEN B$=B$+Y$:SOUND240,1:GOTO510ELSE IF Y$=CHR$(8)THEN570ELSE IF Y$=CHR$(94)THEN C=C+1:GOSUB1450:GOTO510ELSE IF Y$=CHR$(13)THEN580ELSE SOUND100,1:GOTO510
540 ' *** CHR$(8) --> BACKSPACE (LEFT ARROW)
550 ' *** CHR$(94) --> REPEAT PRONUNCIATION (UP ARROW)
560 ' *** C --> NO. OF REPEAT REQUESTS THIS WORD
570 B=LEN(B$):IF B<1THEN500ELSE B$=LEFT$(B$,B-1):PRINT@256,STRING$(32," "):GOTO500
580 B=LEN(B$):IF LEFT$(B$,1)=" "THEN B$=RIGHT$(B$,B-1):GOTO 580 ' *** DELETES BLANKS FROM START OF WORD, IF ANY
590 B=LEN(B$):IF RIGHT$(B$,1)=" "THEN B$=LEFT$(B$,B-1):GOTO590 ' *** DELETES ANY BLANKS FROM END OF ANSWER
600 IF B$=W$(1,I) THEN C$="":GOSUB720:GOTO620 ' *** BRANCH IF CORRECT ANSWER
610 GOSUB770:IF TP=1THEN650ELSE IF LL=1THEN620ELSE J=J+1:IFJ=4THENGOSUB830ELSE460
620 IF JJ=2THEN GOSUB1560:RETURN ' *** JJ --> THIS IS A WORD REPEATED DUE TO PREVIOUS FAILURE IN THREE TRIES
630 IF LL=1THEN II=II-1 ' *** LL=1 --> WORD WAS BEING REPEATED BY RANDOM SELECTION,SO NOW MUST RESET 'I' TO ONE LESS TO GET WORD THAT WOULD HAVE BEEN STATED
640 IF TP<>1THEN I=II
650 NEXT I:IF TP=1THEN910 ' *** DO NOT REPEATY WORDS IF THIS IS A GRADED EXAM
660 IF KK<=0THEN910 ' *** NO REPEATS LEFT SINCE K<=0
670 IF KK=1THEN A$="JUST 1 WORD LEFT"ELSE A$="SEEMS WE HAVE A FEW WORDS LEFT":GOSUB1430:LL=1
680 FOR X=1 TO K ' *** K --> NO. WORDS TO REPEAT AFTER FIRST PASS
690 CLS:I=R(X):IF I=0THEN910
700 LL=1:JJ=2:GOSUB460:NEXT X
710 GOSUB1560:GOTO910
720 D=RND(8):A$=C$(D):GOSUB1430 ' *** RIGHT ANSWER - GIVE CONGRATULATIONS
730 IF LL=0 AND J=1THEN740ELSE750 ' *** IF REPEAT WORD, DO NOT CHANGE SCORE
740 T=T+1:S=S+1:P=S/T*100:RETURN
750 A$="U FINE ULLY GOT THAET 1 RIGHT":GOSUB1430
760 KK=KK-1:R(K)=0:RETURN
770 D=RND(8):C$=B$+" was wrong!":IF TP<>1THEN A$="RONG THATSS"+STR$(J):GOSUB1430
780 A$=D$(D):GOSUB1430 ' *** STATE CHIDING PHRASE
790 IF LL=1AND TP<>1THEN820 ' *** IF REPEAT WORD DO NOT CHANGE SCORE, BUT EXECUTE REPEAT PROCEDURE
800 IF J=1THEN T=T+1 ' *** T --> NO. OF ORIGINAL WORDS ASKED SO FAR
810 P=S/T*100:RETURN ' *** P --> % ... S --> NO. CORRECT (FIRST TRY ONLY)
820 A$="TRY AGGIN LAITER":GOSUB1430:RETURN
830 K=K+1:R(K)=I:KK=KK+1 ' *** IF 3 WRONG TRIES, SPELL OUT AND SAVE FOR LATER REPEAT IN R(K)
840 A$="HERE IS THE ANNSER":XB=2800:GOSUB1450
850 B$=" ":A$=W$(2,I)+"  IS SPELLED  ":GOSUB1430:GOSUB1560
860 CLS2:X8=LEN(W$(1,I)):PRINT@193,STRING$(31," ");
870 FOR X9=1 TO X8:A$=MID$(W$(1,I),X9,1):B$=B$+" "+A$:PRINT@192,B$;:XB=800:GOSUB1450:NEXT X9:X9=400 ' *** VERBALLY SPELLS OUT WORD WITH TIMED DELAY AFTER EACH LETTER
880 PRINT@293,W$(1,I);
890 A$=W$(2,I)+"  NOW REMEMBER THAET":GOSUB1430 ' *** REPRONOUNCES WORD AFTER SPELLING OUT VERBALLY
900 B$="":RETURN
910 POKE65494,0:GOSUB1000 ' *** TEST IS COMPLETE -->SUMMARIZE RESULTS
920 A$="CONGRATULATIONS":PRINT@392,A$;:GOSUB1430
930 A$="U COMPLEETED THEE TEST":GOSUB1430
940 PP=FIX(P+.5):PRINT@448,USING Z$;S,T,P
950 EN=S:GOSUB1190:S$=E$
960 EN=T:GOSUB1190:T$=E$
970 EN=PP:GOSUB1190:PP$=E$
980 A$="YOUR SCORE IS "+S$+"  UV A POSSIBLE "+T$+" 4 A FI NUL GRADE UV "+PP$+" PERCENT":GOSUB1430
990 END
1000 CLS:SOUND200,1:RETURN ' *** CLS WITH BEEP
1010 ' *** CONGRATULATORY REMARKS
1020 C$(1)="OUT STANDEENG"
1030 C$(2)="VERY GOOD  KEEP GOEENG"
1040 C$(3)="YOUR ABOUT REDDEE 4 THE NEXT LEFVEL NOW"
1050 C$(4)="SEEMS LIKE U HAFVE THAT 1 DOWN NOW"
1060 C$(5)="WAY 2 GO  FAR OUT EARTHLEENG"
1070 C$(6)="DARTH VAIDER ONLY WISHEZ HE COULD SPELL THAET WELL"
1080 C$(7)="WILL MIRRAKULLS NEFVER SEESS"
1090 C$(8)="NOW THATSS THE WAY 2 DO THIS GAME"
1100 ' *** CHIDING REMARKS
1110 D$(1)="HOO R U TRYEENG 2 KID THIS IZ SERIOUS BIZZENESS"
1120 D$(2)="I CAN C WE R GOEENG 2 HAFV 2 WORK ON THIS 1"
1130 D$(3)="ARNT U ABOUT REDDY 2 TRY 4 REEL"
1140 D$(4)="NOPE  TRY AGGIN"
1150 D$(5)="KEENG UV THE SPELLING BEEZ YOUR NOT"
1160 D$(6)="I WOE NNT COUNT THAET 1 IF U GIFV ME 5 BUCKSS"
1170 D$(8)="I CAN DO BETTER THAN THAET WITHOUT PRO GRAMMING"
1180 RETURN
1190 ' *** ROUTINE TO PRONOUNCE NUMBERS > 9 PROPERLY
1200 ED=FIX(EN/10):ER=FIX(EN-10*ED+.5)
1210 IF ER=0THEN ER$=""ELSE ER$=STR$(ER)
1220 IF ER=0AND ED=1THEN E$="TEN":RETURN
1230 ON ED GOTO1250,1320,1330,1340,1350,1360,1370,1380,1390,1400
1240 ED$="":GOTO1410
1250 ON ER GOTO 1260,1270,1280,1290,1300,1310,1310,1310,1310,
1260 E$="ELEVEN":RETURN
1270 E$="TWELVE":RETURN
1280 E$="THIRTEEN":RETURN
1290 E$="FOURTEEN":RETURN
1300 E$="FIFTEEN":RETURN
1310 E$=ER$+"TEEN":RETURN
1320 ED$="TWENTY":GOTO1410
1330 ED$="THIRTY":GOTO1410
1340 ED$="FORTY":GOTO1410
1350 ED$="FIFTY":GOTO1410
1360 ED$="SIXTY":GOTO1410
1370 ED$="SEVENTY":GOTO1410
1380 ED$="EIGHTY":GOTO1410
1390 ED$="NINETY":GOTO1410
1400 E$="1 HUN DRED":RETURN
1410 E$=ED$+" "+ER$:RETURN
1420 XB=99*LEN(A$):GOTO1450 ' *** VARIABLE DELAY FOR STATED WORD
1430 XB=60*LEN(A$):GOTO1450 ' *** VARIABLE TIME DELAY
1440 XB=1000 ' *** MEDIUM DELAY
1450 POKE65494,0 ' *** SPEECH SUBROUTINE FROM TANDY'S SS/C MANUAL
1460 X=&HFF00:Y=&HFF7E
1470 IF A$=""THEN A$=" "
1480 POKE X+1,52:POKE X+3,63
1490 POKE X+35,60
1500 POKE 65407,34 ' *** TOGGLE MULTI-PAK SLOT TO SS/C
1510 FOR M=1 TO LEN(A$)
1520 IF PEEK(Y) AND 128=0THEN 1520
1530 POKE Y,ASC(MID$(A$,M,1)):NEXT M
1540 IF PEEK(Y) AND 128=0THEN 1540
1550 POKE Y,13:POKE65407,51:POKE65495,0
1560 FOR X9=1TO XB:NEXT X9:XB=400:RETURN ' *** PROGRAMMED DELAY
1570 Y$=INKEY$ ' *** STROBE KEYBOARD
1580 Y$=INKEY$:IF Y$=""THEN1580ELSE RETURN
0 ' COPYRIGHT 1989  FALSOFT,INC
10 '*** TAKETEST ADMINISTERS THE SESSION
20 FILES 2,570:PCLEAR1:CLEAR 10000:XB=400
30 DIM W$(3,50),R(10),S$(10),D$(10),C$(10),B$(3),W(50),TP$(2)
40 TP$(1)="TEST":TP$(2)="PRACTICE"
45 Z$=" % %  ##  %              %"
50 CLS5:PRINT@32*8+5,"--------";:PRINT@32*6,"enter your record name:";:PRINT@32*8+5,"";:LINEINPUT"";RN$
60 CLS5:PRINT@32*7+8,"GETTING WORDS";:PRINT@288+(13-LEN(RN$))/2,"one moment, please, ";RN$;:A$="1 MOMENT PLEEZ"+RN$:GOSUB1430:POKE65494,0
70 OPEN"D",#1,"HELP/SCN",17
80 FIELD#1,12 AS A$,2 AS B$,3 AS C$
90 F$=STRING$(8," ")+"/TST":GET#1,1:F=INSTR(A$,"."):MID$(F$,1,F-1)=LEFT$(A$,F-1)
100 YR$=MID$(C$,1,1):MO$=MID$(C$,2,1):DA$=MID$(C$,3,1)
110 TP=ASC(MID$(B$,2,1))
120 CLOSE#1
190 '*** LOAD SPELLING WORDS FROM F$
200 J=0:OPEN"D",#1,F$,51
210 FIELD#1,20 AS B$(1),20 AS B$(2),11 AS B$(3)
220 E=LOF(1)
230 FOR I=1 TO E
240 GET#1,I:W$(1,I)=B$(1):W$(2,I)=B$(2):W$(3,I)=B$(3):NEXT I
250 CLOSE#1
260 '*** DISPLAY EXPLANATORY NOTE
270 CLS4:SOUND200,1:FOR X9=4TO12:PRINT@32*X9+8,STRING$(16," ");:NEXT X9
280 PRINT@32*6+11,"n  o  t  e";
290 PRINT@32*8+8,"YOU MAY ASK TO";:PRINT@32*9+8,"HAVE ANY WORD";:PRINT@32*10+8,"REPRONOUNCED AT";:PRINT@32*11+8,"ANYTIME ....";:PRINT@32*12+8,"PRESS <^>";
300 XB=2200:GOSUB1560
310 '*** COMMENCE SESSION
320 UNLOAD:GOSUB 1000:A$="O K WE WILL START NOW":GOSUB1440
330 EN=E:GOSUB1200:A$="THIS TEST HAS "+E$+" WORDSS   GET READY":GOSUB1430:GOSUB1000
340 Z$="SCORE = ### OUT OF ### = ###.# %"
350 POKE65495,0:K=0:S=0:T=0:P=0:KK=0:GOSUB1020
360 FOR I=1 TO E
390 FOR X9=1TO20:IF MID$(W$(1,I),X9,1)=" "THEN W$(1,I)=LEFT$(W$(1,I),X9-1)ELSE NEXT X9
430 II=I:LL=0:J=1:C$=""
440 'IF ANY REPEATS EXIST, THEN 1 IN 10 CHANCE, ONE OF THEM WILL BE ASKED HERE (NO CREDIT BASIS)
450 IF KK=0 THEN 460 ELSE R=RND(99):IF R>89 THEN R=RND(K):I=R(R):LL=1
460 B$="":CLS
470 PRINT USING Z$;S,T,P;
480 PRINT@32*5+3,C$;
490 GOSUB1560:A$="SPELL        "+W$(2,I):GOSUB1430
500 SOUND200,1
505 PRINT@32*8,STRING$(32," ");
510 PRINT@32*8,"ANSWER: ";B$;:GOSUB1570:Y=ASC(Y$)
530 IF Y>64 AND Y<91THEN B$=B$+Y$:SOUND240,1:GOTO510ELSE IF Y$=CHR$(8)THEN B=LEN(B$):B$=LEFT$(B$,B-1):GOTO505ELSE IF Y$=CHR$(94)THEN C=C+1:GOSUB1450:GOTO510ELSE IF Y$=CHR$(13)THEN580ELSE SOUND100,1:GOTO510
580 B=LEN(B$):IF LEFT$(B$,1)=" "THEN B$=RIGHT$(B$,B-1):GOTO 580
590 B=LEN(B$):IF RIGHT$(B$,1)=" "THEN B$=LEFT$(B$,B-1):GOTO590
600 IF B$=W$(1,I) THEN C$="":GOSUB720:GOTO615
610 GOSUB770:IF TP=1THEN640ELSE IF LL=1THEN620ELSE J=J+1:IFJ=4THENGOSUB830ELSE460
615 IF C>0THEN MID$(W$(3,I),3,1)=CHR$(C)
620 IF JJ=2THEN GOSUB1560:RETURN
630 IF LL=1THEN II=II-1
640 IF TP<>1 THEN I=II
650 NEXT I:IF TP-1THEN910
660 IF KK<=0THEN910
670 IF KK=1THEN A$="JUST 1 WORD LEFT"ELSE A$="SEEMS WE HAVE A FEW WORDS LEFT":GOSUB1430:LL=1
680 FOR X=1 TO K
690 CLS:I=R(X):IF I=0THEN910
700 LL=1:JJ=2:GOSUB460:NEXT X
710 GOSUB1560:GOTO910
720 D=RND(8):A$=C$(D):GOSUB1430 'RIGHT ANSWER
730 IF LL=0 AND J=1THEN740ELSE750
740 MID$(W$(3,I),1,1)=CHR$(1):T=T+1:S=S+1:P=S/T*100:RETURN
750 A$="U FINE ULLY GOT THAET 1 RIGHT":GOSUB1430
760 KK=KK-1:R(K)=0:RETURN
770 D=RND(8):C$=B$+" was wrong!":IF TP<>1THEN A$="RONG THATSS"+STR$(J):GOSUB1430
780 A$=D$(D):GOSUB1430
790 IF LL=1 AND TP<>1 THEN820
800 IF J=1THEN T=T+1
810 P=S/T*100:RETURN
820 A$="TRY AGGIN LAITER":GOSUB1430:RETURN
830 K=K+1:R(K)=I:KK=KK+1
840 A$="HERE IS THE ANNSER":XB=2800:GOSUB1450
850 B$=" ":A$=W$(2,I)+"  IS SPELLED  ":GOSUB1430:GOSUB1560
860 CLS(2):X8=LEN(W$(1,I)):PRINT@32*6+1,STRING$(31," ");
870 FOR X7=1 TO X8:A$=MID$(W$(1,I),X7,1):B$=B$+" "+A$:PRINT@32*6,B$;:XB=800:GOSUB1450:NEXT X7
880 PRINT@32*9+5,W$(1,I);
890 A$=W$(2,I)+"  NOW REMEMBER THAET":GOSUB1430
900 B$="":RETURN
910 POKE65494,0:GOSUB1140
920 CLS:A$="CONGRATULATIONS":PRINT@32*6+8,A$;:GOSUB1430
930 A$="U COMPLEETED THEE TEST":GOSUB1430
940 PP=INT(P):PRINT@32*14,USING Z$;S,T,P
950 EN=S:GOSUB1190:S$=E$
960 EN=T:GOSUB1190:T$=E$
970 EN=PP:GOSUB1190:PP$=E$
980 A$="YOUR SCORE IS "+S$+"  UV A POSSIBLE "+T$+" 4 A FINAL GRADE UV "+PP$+" PERCENT":GOSUB1410
990 GOTO 2000
1000 CLS:SOUND200,1:RETURN
1010 'REMARKS
1020 C$(1)="OUT STANDEENG"
1030 C$(2)="VERY GOOD  KEEP GOEENG"
1040 C$(3)="YOUR ABOUT REDDEE 4 THE NEXT LEFVEL NOW"
1050 C$(4)="SEEMS LIKE U HAFVE THAT 1 DOWN NOW"
1060 C$(5)="WAY 2 GO  FAR OUT EARTHLEENG"
1070 C$(6)="DARTH VAIDER ONLY WISHEZ HE COULD SPELL THAET WELL"
1080 C$(7)="WILL MIRRAKULLS NEFVER SEESS"
1090 C$(8)="NOW THATSS THE WAY 2 DO THIS GAME"
1100 '*** CHIDING REMARKS
1110 D$(1)="HOO R U TRYEENG 2 KID THIS IZ SERIOUS BIZZENESS"
1120 D$(2)="I CAN C WE R GOEENG 2 HAFV 2 WORK ON THIS 1"
1130 D$(3)="ARNT U ABOUT REDDY 2 TRY 4 REEL"
1140 D$(4)="NOPE  TRY AGGIN"
1150 D$(5)="KEENG UV THE SPELLING BEEZ YOUR NOT"
1160 D$(6)="I WOE NNT COUNT THAET 1 IF U GIFV ME 5 BUCKSS"
1170 D$(8)="I CAN DO BETTER THAN THAET WITHOUT PRO GRAMMING"
1180 RETURN
1190 '*** SAY #'S ROUTINE
1200 ED=FIX(EN/10):ER=FIX(EN-10*ED+.5)
1210 IF ER=0THEN ER$=""ELSE ER$=STR$(ER)
1220 IF ER=0 AND ED=1THEN E$="TEN":RETURN
1230 ON ED GOTO 1250,1320,1330,1340,1350,1360,1370,1380,1390,1400
1240 ED$="":GOTO1410
1250 ON ER GOTO 1260,1270,1280,1290,1300,1310,1310,1310,1310
1260 E$="ELEVEN":RETURN
1270 E$="TWELVE":RETURN
1280 E$="THIRTEEN":RETURN
1290 E$="FOURTEEN":RETURN
1300 E$="FIFTEEN":RETURN
1310 E$=ER$+"TEEN":RETURN
1320 ED$="TWENTY":GOTO1410
1330 ED$="THIRTY":GOTO1410
1340 ED$="FORTY":GOTO1410
1350 ED$="FIFTY":GOTO1410
1360 ED$="SIXTY":GOTO1410
1370 ED$="SEVENTY":GOTO1410
1380 ED$="EIGHTY":GOTO1410
1390 ED$="NINETY":GTO1243
1400 E$="1 HUN DRED":RETURN
1410 E$=ED$+" "+ER$:RETURN
1420 XB=99*LEN(A$):GOTO1450
1430 XB=60*LEN(A$):GOTO1450
1440 XB=1000 'MEDIUM DELAY
1450 POKE65494,0 ' *** SPEECH S/R
1460 X=&HFF00:Y=&HFF7E
1470 IF A$=""THEN A$=" "
1480 POKE X+1,52:POKE X+3,63
1490 POKE X+35,60
1500 POKE 65407,34
1510 FOR M=1 TO LEN(A$)
1520 IF PEEK(Y) AND 128=0THEN 1520
1530 POKE Y,ASC(MID$(A$,M,1)):NEXT M
1540 IF PEEK(Y) AND 128=0THEN 1540
1550 POKE Y,13:POKE65407,51:POKE65495,0
1560 FOR X9=1TO XB:NEXT X9:XB=400:RETURN 'PROGRAMMED DELAY
1570 Y$=INKEY$
1580 Y$=INKEY$:IF Y$=""THEN1580ELSE RETURN
2000 POKE65495,0 '*** TO SORT
2010 FOR I=1 TO E-1
2020 FOR J=1 TO E-1
2030 IF W$(1,J)<W$(1,J+1)THEN2070
2040 W1$=W$(1,J):W2$=W$(2,J):W3$=W$(3,J)
2050 W$(1,J)=W$(1,J+1):W$(2,J)=W$(2,J+1):W$(3,J)=W$(3,J+1)
2060 W$(1,J+1)=W1$:W$(2,J+1)=W2$:W$(3,J+1)=W3$
2070 NEXT J
2080 NEXT I
2090 POKE65494,0
2100 '*** UPDATE DIC'Y DATA
2110 OPEN"D",#1,"WORDLIST/DAT",51
2120 FIELD#1,51 AS B$
2130 FOR I=1 TO E
2140 NL=256*ASC(MID$(W$(3,I),8,1))+ASC(MID$(W$(3,I),9,1))
2150 GET#1,NL:BB$=B$
2160 NI=256*ASC(MID$(BB$,50,1))+ASC(MID$(BB$,51,1))
2170 GET#1,NI:BB$=B$
2180 NR=ASC(MID$(BB$,41,1)):NB=ASC(MID$(BB$,42,1)):RP=ASC(MID$(BB$,43,1))
2190 RN=ASC(MID$(W$(3,I),1,1)):BN=ASC(MID$(W$(3,I),2,1)):PR=ASC(MID$(W$(3,I),3,1))
2200 MID$(BB$,41,1)=CHR$(NR+RN):MID$(BB$,42,1)=CHR$(NB+BN):MID$(BB$,43,1)=CHR$(RP+PR)
2210 LSET B$=BB$:PUT#1,NI
2220 NEXT I
2300 UNLOAD:GOSUB1000:PRINT@32*8+6,"TRANSFERRING TO mainmenu";:RUN"MAINMENU/BAS"
