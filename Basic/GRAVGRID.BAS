0 GOTO 2
1 GOTO 5
2 PCLEAR 8
3 GOTO 1
5 '----------------------------'  '     GRAVITATIONAL GRID     '  '          VR: 3.1           '  '  BY: PATRICK D. GRENGS II  '  '      DECEMBER , 1985       '  '  RV: JULY 27, 1986         '  '----------------------------'
10 CLEAR200:PLAY"L255"
15 E$=STRING$(32,128)
16 PI=ATN(1E+9)
20 DIM X(50),Y(50),D(50),V(50),    G(50),XT(256),YT(192),N(1)
25 GOSUB900
30 IF H$="Y" THEN 45
32 R1=XS/4*XM:IF XM=1 THEN         S1=-3*R1 ELSE S1=0
34 R2=YS/4*YM:IF YM=1 THEN         S2=-3*R2 ELSE S2=0
45 P$="O5V15CAFDB"
50 PMODE4,1:PCLS:SCREEN1,1
52 X=128:Y=96:TG=0
55 IF JOYSTK(0)<9 AND X>7 THEN     X=X-8
56 IF JOYSTK(0)>54 AND X<248THEN   X=X+8
57 IF JOYSTK(1)<9 AND Y>7 THEN     Y=Y-8
58 IF JOYSTK(1)>54 AND Y<184THEN   Y=Y+8
60 IF(PEEK(343)AND8)=0 AND X>1     THEN X=X-2
61 IF(PEEK(344)AND8)=0 AND X<254   THEN X=X+2
62 IF(PEEK(341)AND8)=0 AND Y>1     THEN Y=Y-2
63 IF(PEEK(342)AND8)=0 AND Y<190   THEN Y=Y+2
65 PUT(X,Y)-(X+1,Y+1),N,NOT:PLAY   "L255":PUT(X,Y)-(X+1,Y+1),N,    NOT
67 IFPEEK(345)<>255 AND TG=0THEN   TG=TG+1:A=X:B=Y:PUT(A,B)-(A+1   ,B+1),N,NOT:PLAYP$:GOTO 55
68 I$=INKEY$:IF(I$="U" OR I$="D"   ) AND TG=1 THEN C=C+1:X(C)=A:   Y(C)=B:D(C)=SQR((A-X)^2+(B-Y)   ^2)*MM:PUT(A,B)-(A+1,B+1),N,    NOT:CIRCLE(A,B),D(C):PLAYP$:    G(C)=1:IF I$="U" THEN V(C)=-1   :GOTO52 ELSE V(C)=1:GOTO52
69 IF(I$="R" OR I$="L") AND TG=1   THEN C=C+1:X(C)=A:Y(C)=B:D(C)   =SQR((A-X)^2+(B-Y)^2):PUT(A,B   )-(A+1,B+1),N,NOT:CIRCLE(A,B)   ,D(C):PLAYP$:G(C)=2:IF I$="R"   THEN V(C)=1:GOTO52 ELSE V(C)=   -1:GOTO52
70 IF I$<>"" THEN IF ASC(I$)=13    THEN GP=C:GOTO100
71 GOTO55
100 PCLS
110 IF H$<>"Y" THEN FOR PM=1 TO     7 STEP 2:PMODE2,PM:SCREEN1,1    :PCLS:J=0:CC=0
115 FOR K=S2-YS*1 TO 192+YS STEP    YS:J=J+1:X=S1-XS:Y=K:ON G       GOSUB500,510:XT(J)=X:YT(J)=Y    :NEXT
120 FOR X1=S1 TO 256+XS STEP XS:    Z=0:CC=CC+1:IFCC=2 THEN CC=0
121 C=CC
125 X=X1:Y=S2-YS:ON G GOSUB500,     510:XT=X:YT=Y
130 FOR Y1=S2 TO 192+YS STEP YS:    C=C+1:Z=Z+1
135 X=X1:Y=Y1:ON G GOSUB500,510:    PSET(X,Y,5):IF CH>1 THEN        LINE(XT(Z),YT(Z))-(XT,YT),      PSET:LINE-(X,Y),PSET:LINE-      (XT(Z+1),YT(Z+1)),PSET:LINE-    (XT(Z),YT(Z)),PSET
140 IF CH=3  AND  C/2=INT(C/2)      THEN ON PT GOSUB170,171
142 XT(Z)=XT:YT(Z)=YT:XT=X:YT=Y
145 NEXT Y1:XT(Z+1)=X:YT(Z+1)=Y:    NEXT X1
150 S1=S1+R1:S2=S2+R2
160 IF H$<>"Y" THEN NEXT PM:        GOTO190
165 GOTO999
170 PAINT((XT(Z)+XT+X+XT(Z+1))/4    ,(YT(Z)+YT+Y+YT(Z+1))/4),1,1    :RETURN
171 D=0:D1=X:D2=XT:GOSUB290:        D2=XT(Z):GOSUB290:D2=XT(Z+1)    :GOSUB290:D1=XT:GOSUB290:       D1=XT(Z):GOSUB290:D2=XT:        GOSUB290
172 D1=Y:D2=YT:GOSUB290:D2=YT(Z)    :GOSUB290:D2=YT(Z+1):GOSUB      290:D1=YT:GOSUB290:D1=YT(Z):    GOSUB290:D2=YT:GOSUB290
175 D=D+.0001
177 X3=(XT(Z+1)-XT(Z))/D:           Y3=(YT(Z+1)-YT(Z))/D:           X4=(X-XT)/D:Y4=(Y-YT)/D
179 A1=XT(Z):B1=YT(Z):A2=XT:        B2=YT
180 FOR I=0 TO D:LINE(A1,B1)-       (A2,B2),PSET:A1=A1+X3:B1=B1+    Y3:A2=A2+X4:B2=B2+Y4:NEXT:      RETURN
190 'DISPLAY FRAMES
192 X$=INKEY$:T=10
194 FOR P=1 TO 7 STEP 2:PMODE2,P    :SCREEN1,1:I$=INKEY$:IF I$<>    ""THEN T=ASC(I$)*4-256
196 FORI=1 TO T:NEXTI,P:GOTO194
199 FOR P=1 TO 7 STEP 2:PMODE2,P    :SCREEN1,1:NEXT:GOTO199
200 'LINEAR GRAVITY
220 IF D<D(I) THEN DF=(D(I)-D)/     D(I):A=A+V(I)*(X(I)-X)*DF:      B=B+V(I)*(Y(I)-Y)*DF
230 RETURN
250 'NORMAL GRAVITY
270 IF D<D(I) THEN DF=(D(I)-D)/     D(I):DX=(X(I)-X):A=A+V(I)*      (DX-DX*SIN(PI*D/D(I))):DY=(Y    (I)-Y):B=B+V(I)*(DY-DY*SIN      (PI*D/D(I)))
280 RETURN
290 'RETURN MAX
295 E=ABS(D1-D2):IF E>D THEN D=E
296 RETURN
300 'LINEAR TWIST
340 IF D<D(I) THEN AG=AN*V(I)*      (D(I)-D)/D(I):X4=X-X(I):Y4=     Y-Y(I):X=X4*COS(AG)-Y4*SIN      (AG)+X(I):Y=X4*SIN(AG)+Y4*      COS(AG)+Y(I):A=A+X5-X:          B=B+Y5-Y
350 RETURN
370 'NORMAL TWIST
410 IF D<D(I) THEN AG=AN*V(I)*      (1-COS(PI*(D(I)-D)/D(I))):      X4=X-X(I):Y4=Y-Y(I):X=X4*       COS(AG)-Y4*SIN(AG)+X(I):Y=      X4*SIN(AG)+Y4*COS(AG)+Y(I)
420 RETURN
500 A=0:B=0:FOR I=1 TO GP:D=SQR(    (X-X(I))*(X-X(I))+(Y-Y(I))*     (Y-Y(I))):ON G(I) GOSUB 200,    300:GOTO520
510 A=0:B=0:FOR I=1 TO GP:D=SQR(    (X-X(I))*(X-X(I))+(Y-Y(I))*     (Y-Y(I))):ON G(I) GOSUB 250,    370
520 NEXT I:X=X+A:Y=Y+B:IF X<0       THEN X=0 ELSE IF X>255 THEN     X=255
525 IFY<0 THEN Y=0 ELSE IF Y>191    THEN Y=191
530 RETURN
900 CLS0
910 PRINT@4,"-=< GRAVITY INFLUXOR >=-";
915 PRINT@96,"HIGH RESOLUTION (Y/N)";:INPUT H$
920 PRINT@128,"ENTER X LINEAR STEP:";:INPUT XS:IF XS<1 OR XS>64     THEN PRINT@128,E$;:GOTO920
930 PRINT@160,"ENTER Y LINEAR STEP:";:INPUT YS:IF YS<1 OR YS>64     THEN PRINT@160,E$;:GOTO930
950 PRINT@192,"GRAVITY TYPE: 1>LINEAR  2>NORMAL";:INPUT G:IF G<1    OR G>2 THEN PRINT@192,E$;E$;    :GOTO950
955 PRINT@224,"<1> POINTS <2> GRID <3> CHECKERS";:INPUT CH
957 IF CH=3   THEN PRINT@256,       "FILLING: 1>PAINT 2>LINEAR";    :INPUT PT:IF PT<1 OR PT>2       THEN PRINT@256,E$:GOTO957
959 IF H$="Y" THEN 970
960 PRINT@288,"HORZ. DIR. 1-LEFT 2-RIGHT";:INPUT XM:IF XM<1 OR      XM>2 THEN PRINT@288,E$:GOTO     960
962 XM=XM*2-3
965 PRINT@320,"VERT. DIR. 1-UP 2-DOWN";:INPUT YM:IF YM<1 OR YM>2    THEN PRINT@320,E$;:GOTO965
967 YM=YM*2-3
970 PRINT@352,"ENTER MAGNITUDE:"    ;:INPUT MM:IF MM=0 THEN MM=1
980 PRINT@384,"ENTER MAX. ANG. TWIST:";:INPUT AN
985 AN=AN/90*PI
990 RETURN
999 SCREEN1,1:GOTO999
