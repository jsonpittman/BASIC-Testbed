0 ' COPYRIGHT 1989  FALSOFT,INC
10 'DMP-130 PRINTER FONT AND STYLE SETTER BY EDWARD JONES
20 WIDTH32:POKE150,18:CLS:PALETTE13,0:PALETTE12,37:PALETTE8,32:PALETTE9,48
30 PRINT@160,STRING$(32,243);:PRINT
40 PRINT" DMP-130 CHARACTER STYLE SETTER":PRINT"
50 PRINTSTRING$(32,243);
60 FORX=1TO800:NEXT
70 PRINT@481,"TURN ON PRINTER & PRESS A KEY";
80 A$=INKEY$:IFA$=""THEN80
90 CLS:PRINT" <A> NORMAL TEXT       <R> QUIT"
100 PRINT" <B> COMPRESSED TEXT";:PRINT@55,STRING$(8,156)
110 PRINT" <C> CONDENCED TEXT"
120 PRINT" <D> PROPORTIONALLY SPACED TEXT
130 PRINT" <E> CORRESPONDENCE NORMAL TEXT"
140 PRINT" <F> CORRESPONDENCE COMPRESSED"
150 PRINT" <G> START ITALICS-----<H> STOP"
160 PRINT" <I> START SUBSCRIPT---<K> STOP"
170 PRINT" <J> START SUPERSCRIPT-<K> STOP"
180 PRINT" <L> MICROFONT TEXT"
190 PRINT" <M> START ELONGATE----<N> STOP"
200 PRINT" <O> START BOLD--------<P> STOP"
210 PRINT" <Q> UNDERLINE---------<S> STOP"
220 PALETTE8,0:PALETTE9,48
230 PRINTSTRING$(32,243);
240 PRINT@481,"SELECT LETTERS TO SET PRINTER";
250 C$=INKEY$:IFC$=""THEN250
260 IFC$=" "THENCLS:PRINT@161,"PRINTER FONT AND STYLE IS SET":PRINT:LINEINPUT"WANT TO PRINT A TEST? <Y/N>;:T$
270 IFC$="A"THENPRINT@4,CHR$(216);:PRINT@292," ";:PRINT@36," ";:PRINT@68," ";:PRINT@132," ";:PRINT@164," ";:GOSUB520:GOTO240
280 IFC$="B"THENPRINT@36,CHR$(216);:PRINT@292," ";:PRINT@4," ";:PRINT@68," ";:PRINT@132," ";:PRINT@164," ";:GOSUB530:GOTO240
290 IFC$="C"THENPRINT@68,CHR$(216);:PRINT@292," ";:PRINT@4," ";:PRINT@36," ";:PRINT@132," ";:PRINT@164," ";:GOSUB540:GOTO240
300 IFC$="D"THENPRINT@100,CHR$(216);:GOSUB550:GOTO240
310 IFC$="E"THENPRINT@132,CHR$(216);:PRINT@292," ";:PRINT@4," ";:PRINT@36," ";:PRINT@68," ";:PRINT@164," ";:GOSUB570:GOTO240
320 IFC$="F"THENPRINT@164,CHR$(216);:PRINT@292," ";:PRINT@4," ";:PRINT@36," ";:PRINT@68," ";:PRINT@132," ";:GOSUB560:GOTO240
330 IFC$="G"THENPRINT@196,CHR$(216);:PRINT@218," ";:GOSUB580:GOTO240
340 IFC$="H"THENPRINT@218,CHR$(216);:PRINT@196," ";:GOSUB590:GOTO240
350 IFC$="I"THENPRINT@228,CHR$(216);:PRINT@260," ";:PRINT@250," ";:PRINT@282," ";:GOSUB600:GOTO240
360 IFC$="J"THENPRINT@260,CHR$(216);:PRINT@228," ";:PRINT@250," ";:PRINT@282," ";:GOSUB610:GOTO240
370 IFC$="K"THENPRINT@250,CHR$(216);:PRINT@282,CHR$(216);:PRINT@260," ";:PRINT@228," ";:GOSUB620:GOTO240
380 IFC$="L"THENPRINT@292,CHR$(216);:PRINT@4," ";:PRINT@36," ";:PRINT@68," ";:PRINT@132," ";:PRINT@164," ";:GOSUB630:GOTO240
390 IFC$="M"THENPRINT@324,CHR$(216);:PRINT@346," ";:GOSUB640:GOTO240
400 IFC$="N"THENPRINT@346,CHR$(216);:PRINT@324," ";:GOSUB650:GOTO240
410 IFC$="O"THENPRINT@356,CHR$(216);:PRINT@378," ";:GOSUB660:GOTO240
420 IFC$="P"THENPRINT@378,CHR$(216);:PRINT@356," ";:GOSUB670:GOTO240
430 IFC$="Q"THENPRINT@388,CHR$(216);:PRINT@410," ";:GOSUB680:GOTO240
440 IFC$="S"THENPRINT@410,CHR$(216);:PRINT@388," ";:GOSUB690:GOTO240
445 IFC$<"A"ORC$>"R"THEN240
450 IFC$="R"THENCLS:PRINT@161," PRINTER FONT AND STYLE IS SET":FORX=1TO800:NEXT:SOUND5,1:PRINT:PRINT
460 PRINT"   WANT TO PRINT A TEST? <Y/N>"
470 T$=INKEY$:IF T$=""THEN470
480 IF T$="N"THEN CLS:END
490 IF T$="Y"THEN700
500 GOTO470
520 PRINT#-2,CHR$(27);CHR$(19);:RETURN' NORMAL (10 CPI)
530 PRINT#-2,CHR$(27);CHR$(23);:RETURN' COMPRESSED (12 CPI)
540 PRINT#-2,CHR$(27);CHR$(20);:RETURN' CONDENCED (17 CPI)
550 PRINT#-2,CHR$(27);CHR$(17);:RETURN' PROPORTIONALLY SPACED
560 PRINT#-2,CHR$(27);CHR$(29);:RETURN' CORRESPONDENCE COMPRESSED (12 CPI)
570 PRINT#-2,CHR$(27);CHR$(18);:RETURN' CORRENPONDENCE NORMAL (10 CPI)
580 PRINT#-2,CHR$(27);CHR$(66);CHR$(1);:RETURN' START ITALICS
590 PRINT#-2,CHR$(27);CHR$(66);CHR$(0);:RETURN' STOP ITALICS
600 PRINT#-2,CHR$(27);CHR$(83);CHR$(1);:RETURN' START SUBSCRIPT
610 PRINT#-2,CHR$(27);CHR$(83);CHR$(0);:RETURN' START SUPERSCRIPT
620 PRINT#-2,CHR$(27);CHR$(88);:RETURN' STOP SUPER/SUBSCRIPT
630 PRINT#-2,CHR$(27);CHR$(77);:RETURN' MICROFONT
640 PRINT#-2,CHR$(27);CHR$(14);:RETURN' START ELONGATE
650 PRINT#-2,CHR$(27);CHR$(15);:RETURN' STOP ELONGATE
660 PRINT#-2,CHR$(27);CHR$(31);:RETURN' START BOLD
670 PRINT#-2,CHR$(27);CHR$(32);:RETURN' STOP BOLD
680 PRINT#-2,CHR$(15);:RETURN'UNDERLINE
690 PRINT#-2,CHR$(14);:RETURN' STOP UNDERLINE
700 CLS:PRINT" ENTER TEST MESSAGE":PRINTSTRING$(32,243);:PRINT
710 LINEINPUT TM$
720 PRINT#-2,TM$
730 PRINT@481,"PRINT THE MESSAGE AGAIN? <Y/N>";
740 R$=INKEY$:IFR$=""THENGOTO740
750 IF R$="Y"THEN720
760 IF R$="N"THEN780
770 GOTO740
780 PRINT@481,"CHANGE THE PRINT CODES?  <Y/N>";
790 PC$=INKEY$:IFPC$=""THEN790
800 IF PC$="Y"THENCLS:GOTO90
810 IF PC$="N"THENCLS:END
820 GOTO790
