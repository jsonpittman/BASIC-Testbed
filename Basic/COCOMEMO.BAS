0 ' COPYRIGHT 1989 FALSOFT, INC
1 DATA99,111,99,111,109,101,109,111,32,40,99,41,49,57,56,55,32,98,121,32,100,97,118,101,32,108,101,98,108,97,110,99
5 C=0:CLS:RESTORE:FORA=1024TO1055:READ B:IFB>96THENPRINT@C,CHR$(B);:C=C+1:NEXTELSEPOKEA,B:C=C+1:NEXT
10 CLEAR10000
20 DIM S$(315),A$(315),NF$(70)
25 POKE282,1
30 PRINT@65,"MENU":PRINT
40 PRINT" <1> DISPLAY A FILE":PRINT" <2> CREATE A FILE":PRINT" <3> MODIFY A FILE":PRINT" <4> DELETE A FILE":PRINT" <5> PRINT OUT A FILE":PRINT" <6> SEE DIRECTORY":PRINT" <7> CHECK FREE DISK SPACE":PRINT" <8> COPY A FILE":PRINT" <9> QUIT"
50 PRINT" SELECT (1-9) "
60 POKE65494,0
70 QE$=INKEY$
80 QE$=INKEY$:IF QE$=""THEN80
81 IFQE$<"1"ORQE$>"9"THEN80
82 FORA=1120+(VAL(QE$)*32)TO1120+(VAL(QE$)*32)+31:POKE A,PEEK(A)-64:NEXT
90 IFQE$="1"THEN160
100 IFQE$="2"THEN390
110 IFQE$="3"THEN6000
120 IFQE$="4"THEN1040
130 IFQE$="5"THEN1160
140 IFQE$="6"THEN7000
141 IFQE$="7"THEN1230
142 IFQE$="8"THEN9000
143 IFQE$="9"THEN1270
150 GOTO80
160 PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";Q1$
170 IFQ1$=""THENRUN10
180 IFLEN(Q1$)>8ORLEN(Q1$)<1THEN190ELSE GOTO200
190 GOTO160
200 Q1$=Q1$+STRING$(8-LEN(Q1$),32):PRINT@448," SEARCHING"
210 GOSUB5000
215 POKE&HFF40,0:POKE&HFF40,1
220 FORD=1TO DF:IF NF$(D)=Q1$ THEN260ELSENEXTD
230 PRINT@448," NON-EXISTENT FILE":FORD=1TO750:NEXT:RR=FREE(0):RUN
260 PRINT@448," FOUND":PRINT" LOADING";:FORD=1TO1000:NEXT:CLS
270 A$="":OPEN"D",#2,Q1$+".MEM",35
280 E=1
290 IF E>LOF(2)THEN350
300 GET #2,E
310 INPUT#2,A$
311 IFLEN(A$)=32THENPRINTA$;:GOTO330ELSEPRINTA$:GOTO330
320 PRINTA$
330 E=E+1
340 GOTO290
350 CLOSE#1
360 PRINT"HIT ANY KEY CONTINUE":QE$=INKEY$
370 QE$=INKEY$:IFQE$=""THEN370
380 RUN
390 REM
400 FL=0:PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";F1$
410 IFF1$=""THENRUN10
420 IFLEN(F1$)>8ORLEN(F1$)<1THEN430ELSEGOTO440
430 GOTO400
440 F1$=F1$+STRING$(8-LEN(F1$),32):PRINT" SEARCHING":GOSUB5000
450 FORD=1TO DF:IFNF$(D)=F1$ THEN500ELSENEXT
490 PRINT@448," READY.":GOTO510
500 PRINT@448," FOUND":PRINT" DO YOU WISH TO OVERWRITE IT?";
501 QE$=INKEY$
502 QE$=INKEY$:IFQE$=""THEN502
503 IFQE$="N"THENPRINT@480,STRING$(30,32);:PRINT@448,STRING$(30,32);:GOTO390
504 IFQE$="Y"THENPRINT@480,STRING$(30,32);:PRINT@448," READY.":KILL F1$+".MEM":GOTO510
505 GOTO502
510 POKE282,0:FORPP=1024TO1535:POKEPP,32:NEXTPP
520 RR=FREE(0):U=22:PRINT@12,"filename";:FORPP=LEN(F1$)TO1STEP-1:PRINT@U,CHR$(ASC(RIGHT$(F1$,PP))+32);:U=U+1:NEXTPP:POKE1044,58:U=1085:FORQQ=2TOLEN(STR$(RR)):S=VAL(MID$(STR$(RR),QQ,1)):P=S+48:POKE U,P:U=U+1:NEXTQQ:PRINT@44,"free disk space";:POKE1072,32
525 FORPP=1024TO1065:IFPEEK(PP)=64THENPOKE PP,32:NEXTPP ELSENEXTPP
530 POKE1077,32:POKE1083,58:PRINT@76,"line";:POKE1105,35:PRINT@96,"select command";:POKE1126,32:PRINT@128:PRINT@160:POKE1249,49:POKE1281,50:POKE1313,51:FORPP=1250TO1314STEP32:POKEPP,45:NEXTPP:PRINT@228,"save";
540 PRINT@260,"delete a line";:PRINT@292,"terminate file";:POKE1325,32
550 PRINT@321,"SPACEBAR";:PRINT@331,"enter text";:POKE1353,45:POKE1360,32:PRINT@128,"";:POKE1290,32:POKE1292,32:IF FL=1THEN570ELSE560
560 OPEN"D",#1,F1$+".MEM",35:T=1
570 PRINT@160:PRINT@128,"> ";S$(T)
580 U=1107:FORPP=2TOLEN(STR$(T)):S=VAL(MID$(STR$(T),PP,1))+48:POKEU,S:POKEU+1,32:U=U+1:NEXTPP
590 GOSUB770
600 E$=""
610 PRINT@96,"enter text";:POKE1125,32:POKE1130,32:POKE1131,32:POKE1132,32:POKE1133,32:PRINT@128:PRINT@160:PRINT@128,"";:E$="":GOSUB10000
620 REM
630 S$(T)=E$
640 T=T+1:IFT<G THEN650ELSE G=T:GOTO650
650 PRINT@128,"> "+S$(T)
660 GOTO580
670 CLOSE#1:KILL F1$+".MEM":OPEN"D",#1,F1$+".MEM",35
675 IFG=0THEN750ELSE Y=1:Z=1
680 IF S$(Y)="PSUNIA"THEN750
690 IF S$(Y)=""THEN730
700 WRITE #1,S$(Y)
710 PUT #1,Z
720 Z=Z+1
730 Y=Y+1
740 GOTO680
750 CLOSE #1
760 RUN
770 M$=INKEY$:PRINT@96,"select";:PRINT@103,"command";:POKE1126,32:
780 M$=INKEY$:IFM$=""THEN780
790 IFM$="1"THEN860
800 IFM$="2"THEN890
810 IFM$="3"THEN940
820 IFM$=" "THENRETURN
830 IFM$="^"THEN970
840 IFM$=CHR$(10)THEN1010
850 GOTO780
860 FORP=1120TO1140:POKEP,32:NEXTP:PRINT@96,"saving";:FORP=1126TO1128:POKEP,46:NEXTP
870 S$(G+1)="PSUNIA"
880 GOTO670
890 REM
900 S$(T)=""
910 PRINT@160
920 PRINT@128,"> "
930 GOTO650
940 PRINT@96,"deleting file";:POKE1128,32:FORP=1133TO1135:POKEP,46:NEXTP
950 CLOSE#1:KILL F1$+"/MEM"
960 RUN
970 IFT=1THEN580
980 T=T-1:PRINT@128,"":PRINT@160,""
990 PRINT@128,"> "+S$(T)
1000 GOTO580
1010 T=T+1:PRINT@128,"":PRINT@160,""
1020 PRINT@128,"> "+S$(T)
1030 GOTO580
1040 PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";Q$
1050 IFQ$=""THENRUN10
1060 IFLEN(Q$)>8ORLEN(Q$)<1THEN1040ELSE1070
1070 PRINT" SEARCHING":Q$=Q$+STRING$(8-LEN(Q$),32):GOSUB5000
1080 FORD=1TO DF:IF NF$(D)=Q$ THEN1130ELSE NEXTD
1120 PRINT@448," NON-EXISTENT FILE":FORD=1TO1000:NEXT:RUN
1130 PRINT@448," FOUND":PRINT" DELETING";
1140 KILL Q$+".MEM":RUN
1160 GOTO8000
1170 OPEN"D",#1,F$+".MEM",35
1174 CLS
1175 IFPEEK(65314)<>5THEN1179
1176 IFPEEK(65314)<>5THEN1178
1177 PRINT@32," PLEASE GET PRINTER READY":GOTO1176
1178 REM
1179 REM
1180 FOR CD=1TO LOF(1):GET#1,CD:INPUT#1,A$(CD):NEXT
1190 CLS:LINEINPUT"ENTER APPROPRIATE TITLE         > ";B$:CLS:PRINT:PRINTTAB(12);"PRINTING"
1191 DV=INT(LOF(1)/2)+1:CLOSE#1:PRINT#-2,TAB(40-LEN(B$)/2);CHR$(15);B$CHR$(14);:PRINT#-2:FOR G1=1TO DV:PRINT#-2,TAB(5);A$(G1);TAB(42);A$(DV+G1):NEXT:PRINT#-2,CHR$(7);:FORA=1TO50:NEXTA:PRINT#-2,CHR$(7)
1200 RUN
1210 QE$=INKEY$:IFQE$=""THEN1210
1220 RUN
1230 CLS:PRINT" FREE DISK SPACE:"
1240 RR=FREE(0):PRINT"IN GRANULES:"RR:PRINT"IN BYTES:"RR*2304:PRINT"ROOM FOR APPROXIMATELY"RR"FILES.";:PRINT"HIT ANY KEY TO CONTINUE":QE$=INKEY$
1250 QE$=INKEY$:IFQE$=""THEN1250
1260 RUN
1270 POKE113,0:EXEC40999
5000 DF=0:FOR XX=3TO11:DSKI$0,17,XX,Y$,Z$:FOR X1=1TO128STEP32:GOSUB5010:NEXT X1:Y$=Z$:FOR X1=1TO128STEP32:GOSUB5010:NEXT X1,XX
5002 RETURN
5010 LL$=MID$(Y$,X1,14):IFASC(LL$)=255ORASC(LL$)=0THENRETURNELSE5020
5020 IFMID$(LL$,9,3)<>"MEM"THENRETURNELSE5021
5021 DF=DF+1:NF$(DF)=LEFT$(LL$,8):RETURN
6000 FL=1:PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";F1$
6010 IFF1$=""THENRUN10
6020 IFLEN(F1$)<1ORLEN(F1$)>8THEN6000
6030 F1$=F1$+STRING$(8-LEN(F1$),32):PRINT@448," SEARCHING":GOSUB5000
6040 FORD=1TO DF:IF NF$(D)=F1$ THEN6060ELSENEXT
6050 PRINT@448," NON-EXISTENT FILE":FORD=1TO750:NEXT:RUN
6060 PRINT@448," FOUND":PRINT@480," LOADING";
6070 OPEN"D",#1,F1$+".MEM",35:T=LOF(1)
6075 IFLOF(1)<1THENG=1:T=1:PRINT@448,"":PRINT@480,"                              ";:PRINT@448," READY.":GOTO510
6080 FORD=1TO LOF(1):GET#1,D:INPUT#1,S$(D):NEXTD:G=LOF(1):PRINT@448,"":PRINT@480,"            ";:PRINT@448," READY.":GOTO510
7000 CLS:PRINT:PRINT" READING DIRECTORY":PRINT@32,"";:GOSUB5000
7010 FORD=1TO DF STEP3:PRINTTAB(1);NF$(D);TAB(12);NF$(D+1);TAB(23);NF$(D+2):NEXTD
7020 PRINT" HIT ANY KEY TO CONTINUE";
7030 A$=INKEY$
7040 A$=INKEY$:IFA$=""THEN7040
7050 RUN
8000 PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";F$
8010 IFF$=""THENRUN10
8020 IFLEN(F$)>8ORLEN(F$)<1THEN8000
8030 F$=F$+STRING$(8-LEN(F$),32):PRINT@448," SEARCHING":GOSUB5000
8040 FORD=1TO DF:IF NF$(D)=F$ THEN8060ELSE NEXTD
8050 PRINT@448," NON-EXISTENT FILE":FORD=1TO1000:NEXT:RUN
8060 PRINT@448," FOUND":PRINT" LOADING";:FORD=1TO750:NEXT:GOTO1170
9000 PRINT@416,"":PRINT@416,"";:LINEINPUT" FILENAME: ";F$
9001 IFF$=""THENRUN10ELSEIFLEN(F$)<1ORLEN(F$)>8THEN9000
9002 PRINT@448," SEARCHING":F$=F$+STRING$(8-LEN(F$),32):GOSUB5000
9003 FORD=1TO DF:IFNF$(D)=F$THEN9010ELSENEXTD
9004 PRINT@448," NON-EXISTENT FILE":FORD=1TO1000:NEXTD:RUN
9010 PRINT@448," FOUND":PRINT@480," LOADING";:OPEN"D",#1,F$+".MEM",35
9015 IFLOF(1)<1THEN9500
9020 E=LOF(1):FORD=1TO LOF(1):GET#1,D:INPUT#1,A$(D):NEXT:CLOSE#1
9030 PRINT@448," INSERT TARGET DISK AND":PRINT@480," STRIKE ANY KEY >";:SOUND100,3:EXEC41329
9040 PRINT@448," SEARCHING":PRINT@480,"                         ";:GOSUB5000
9050 FORD=1TO DF:IFNF$(D)=F$ THEN9100ELSE NEXTD
9060 PRINT@480,"                              ";:PRINT@448," COPYING":OPEN"D",#1,F$+".MEM",35
9070 FORD=1TO E:WRITE#1,A$(D):PUT#1,D:NEXTD:CLOSE#1
9080 RUN
9100 PRINT@448," FILE FOUND":PRINT@480," DO YOU WISH TO OVERWRITE IT?";
9105 QE$=INKEY$
9106 QE$=INKEY$:IFQE$=""THEN9106
9107 IFQE$="N"THENRUN
9108 IFQE$="Y"THEN KILL F$+".MEM":GOTO9060ELSE9109
9109 GOTO9106
9500 PRINT@480,"          ";:PRINT@448," NULL FILE":CLOSE#1:FORD=1TO1000:NEXTD:RUN
10000 PRINTCHR$(134);:FORQE=1TO50:QE$=INKEY$:IFQE$<>""THEN10002ELSENEXTQE
10001 PRINTCHR$(8);CHR$(137);:FORQE=1TO50:QE$=INKEY$:IFQE$<>""THEN10002ELSENEXT:PRINTCHR$(8);:GOTO10000
10002 IFASC(QE$)=8THEN10010ELSEIFASC(QE$)=21THEN10015ELSE10003
10003 IFASC(QE$)=13THENPRINT@160,"":PRINT@128,"":RETURNELSEIFASC(QE$)<32ORASC(QE$)>127ORASC(QE$)=34THENSOUND200,1:GOTO10001ELSE10004
10004 IFLEN(E$)=>32THENSOUND147,1:GOTO10001ELSE10005
10005 E$=E$+QE$:PRINTCHR$(8);QE$;:GOTO10000
10010 IFLEN(E$)=<0THENSOUND147,1:GOTO10001
10011 PRINTCHR$(8);CHR$(8);:E$=LEFT$(E$,LEN(E$)-1):GOTO10000
10015 IFLEN(E$)<=0THENSOUND147,1:GOTO10001ELSE E$="":PRINT@160,"":PRINT@128,"":PRINT@128,"";:GOTO10000
