0 'PIXTEXT4
1 'WRITTEN BY KEIRAN KENNY
2 'COPYRIGHT (C) AUGUST 1991
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
10 CLS:CLEAR500
20 IFPEEK(&H15F)=196THENPX=1
30 IFPEEK(&HFFFE)*256+PEEK(&HFFFF)=&H8C1B THENSP=65497:SL=65496ELSESP=65495:SL=65494
40 GOTO450
50 PMODE4,1:COLOR0,1
60 PRINT@224,"IS PICTURE IN MEMORY? Y/N"
70 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN70
80 IFK$="Y"THEN110
90 CLS:PRINT@224,"LOAD PIXNAME:";:LINEINPUTPX$:IFPX THENLOADMPX$:GOTO110
100 CLOADMPX$
110 POKESP,0
120 INPUT"ENTER SCALE:";SC:IFSC<8THENSC=4
130 SCREEN1,1
140 H=0:V=7*SC/4
150 IFINKEY$=CHR$(13)THEN280
160 IFPEEK(341)=247THENV=V-8*SC/4
170 IFPEEK(342)=247THENV=V+8*SC/4
180 IFPEEK(343)=247THENH=H-6*SC/4
190 IFPEEK(344)=247THENH=H+6*SC/4
200 IFV<7*SC/4THENV=7*SC/4
210 IFV>191THENV=191
220 IFH<0THENH=0
230 IFH>249THENH=249
240 DR=PPOINT(H,V)
250 IFDR=0THENPSET(H,V,1)ELSEPSET(H,V)
260 FORDL=1TO200:NEXT:PSET(H,V,DR)
270 GOTO150
280 X=H:Y=V
290 BT=X
300 K$=INKEY$:IFK$=""THEN300
310 IFK$=CHR$(12)THENH=0:V=Y+8*SC/4:GOTO150
320 IFK$=CHR$(9)THENCOLOR5:LINE(X,Y+2)-(X+5*(SC/4),Y-6*(SC/4)),PSET,BF:X=X+6*(SC/4):COLOR0:GOTO300
330 IFX>248ANDK$<>CHR$(8)ANDK$<>CHR$(12)ANDK$<>CHR$(13)THEN300
340 IFX=BT ANDK$=CHR$(8)THEN300ELSEIFK$=CHR$(8)THEN350ELSEIFK$=CHR$(13)THEN370ELSE360
350 COLOR5:LINE(X-6*(SC/4),Y-6*SC/4)-(X,Y+2),PSET,BF:X=X-6*SC/4:COLOR0:GOTO300
360 COLOR0:DRAW"S=SC;BM"+STR$(X)+","+STR$(Y)+L$(ASC(K$)):X=X+6*SC/4:GOTO300
370 CLS:POKESL,0:PRINT@131,"1. SAVE PIX",,TAB(3)"2. REPLAY",,TAB(3)"3. LOAD ANOTHER",TAB(3)"4. END":PRINT:PRINTTAB(6)"PRESS 1 - 4)"
380 K$=INKEY$:IFK$<"1"ORK$>"4"THEN380
390 ONVAL(K$) GOTO400,110,90,440
400 CLS:PRINT@224,"SAVE PIXNAME:";:LINEINPUTPX$
410 IFPX THENSAVEMPX$,3584,9727,40999:GOTO430
420 CSAVEMPX$,1536,7679,40999
430 GOTO370
440 CLS:END
450 DIMA(32),B(110),L$(90)
460 L$(48)="U4R3D4NL3":L$(49)="R2LU4NGBRBD4":L$(50)="BU4R3D2L3D2R3":L$(51)="R3U2NL2U2NL3BD4":L$(52)="BUNU3R2NUNDRBD":L$(53)="R3U2L3U2R3BD4":L$(54)="NR3U4D2R3D2":L$(55)="BU4R3DG3BR3":L$(56)="BRHEHERFGNLFGNLBR":L$(57)="BR3U4L3D2R3D2"
470 L$(65)="U3ERFDNL3D2":L$(66)="U4R2FGNL2FGNL2BR":L$(67)="BRHU2ERFBD2GNLBR":L$(68)="U4R2FD2GNL2BR":L$(69)="NR3U2NR2U2R3BD4":L$(70)="U2NR2U2R3BD4":L$(71)="BRHU2ER3BD2NLDGNL2BR":L$(72)="U4BR3D2NL3D2":L$(73)="R2LU4NLRBD4":L$(74)="BRNHREU3BD4"
480 L$(75)="U4D2RNE2F2":L$(76)="NU4R3":L$(77)="U4FRED4":L$(78)="U4F3U3D4":L$(79)="BRHU2ERFD2GNLBR":L$(80)="U4R2FGNL2BRBD2":L$(81)="BRHU2ER2FDG2ENFGNL2BR2":L$(82)="U4R2FGL2RF2":L$(83)="R2EHLHER2BD4":L$(84)="BR2U4NL2R2BD4":L$(85)="BRHU3BR4D3GNL2BR"
490 L$(86)="BR2H2U2BR4D2NG2BD2":L$(87)="NU4ERFNU4":L$(88)="E4BL4F4":L$(89)="BR2U2NH2E2BD4":L$(90)="NR4E4L4BF4"
500 L$(33)="UBU2NU2BD2":L$(36)="R3EHL2HER3L2NUD5UBR2":L$(37)="NE4BU3UBR4BD3D":L$(39)="BU3UBD4":L$(40)="BRHU2EBD4":L$(41)="EU2HBRBD4":L$(42)="BR2U2NG2NL2NH2NU2NE2NR2F2":L$(43)="BR2U4D2NL2R2BD2":L$(44)="NGNU":L$(45)="BU2R4BD2"
510 L$(46)="NU":L$(47)="E4BD4":L$(58)="UBU2UBD4":L$(59)="NGUBU2UBD4":L$(61)="BUNR4BU2R4BD3":L$(63)="NRBU2R3U2L3BD4BR3"
520 GOTO50
