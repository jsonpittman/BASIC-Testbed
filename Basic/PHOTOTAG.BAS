10 '**************
20 '* PHOTO TAG  *
30 '*  UTILITY   *
40 '*    BY      *
50 '*D.A.TUROWSKI*
60 '* AUGUST '86 *
70 '**************
75 CLEAR5000
80 CLS(0):FOR X=3 TO 61:SET(X,5,8):SET(X,25,8):NEXT X:FOR Y=5 TO 25:SET(3,Y,8):SET(61,Y,8):NEXT Y
90 FOR Y=2 TO 4:SET(54,Y,4):SET(58,Y,4):NEXT Y:FOR X=54 TO 58:SET(X,2,4):NEXT X
100 FOR X=10 TO 50:SET(X,7,2):SET(X,20,2):NEXT X
110 FOR Y=7 TO 20:SET(10,Y,2):SET(50,Y,2):NEXT Y
120 FOR X=7 TO 9:SET(X,8,6):NEXT X
130 GOTO 1000
200 REM
210 CLS(RND(8)):GOSUB 2000:PRINT@32*8,"DO YOU NEED INSTRUCTIONS ABOUT  <photo-tagger>";:INPUT R$:IF LEFT$(R$,1)="Y" THEN 3000 ELSE 220
220 CLS:GOSUB 2000:PRINT@32*3,"1)TURN ON PRINTER":SOUND 200,3:FORXX=1TO1000:NEXT XX:IF PEEK(65314)/2<>INT(PEEK(65314)/2) THEN PRINT@32*11+6,"printer is not on line!!!":PRINT@32*12,"turn printer on at this time!!!":FORXX=1TO1000:NEXT XX:GOTO220
230 CLS:GOSUB 2000:PRINT@32*5,"2)LINE UP LABELS IN PRINTER WITH  PRINT HEAD AT THIS TIME":SOUND 230,2
240 PRINT@32*10,"   PRESS ANY KEY TO CONTINUE":EXEC44539
250 PLAY"O3;V31;L10;A;V16;A;V8;A;V3;A;V1;L20;A"
255 CLS(RND(8)):GOSUB 2000
257 PRINT@32*14,"[press / to keep last entry!!!!]";
260 PRINT@32*7,"date of picture";: LINE INPUT A$:IF LEN(A$)>29 THEN PRINT "LINE TOO LONG, PLEASE RE-ENTER":GOTO 260
261 IF A$="/" THEN A$=E$:GOTO 270
262 E$=A$
270 PRINT"location of picture":LINE INPUT B$:IF LEN(B$)>29 THEN PRINT "LINE TOO LONG, PLEASE RE-ENTER":GOTO 270
271 IF B$="/" THEN B$=F$:GOTO 280
272 F$=B$
280 PRINT"people,places,things in picture":LINE INPUT C$: IF LEN(C$)>29 THEN PRINT"LINE TOO LONG, PLEASE RE-ENTER":GOTO 280
281 IF C$="/" THEN C$=G$:GOTO 290
282 G$=C$
290 PRINT"further information":LINE INPUT D$: IF LEN(D$)>29 THEN PRINT"LINE TOO LONG, PLEASE RE-ENTER":GOTO 290
291 IF D$="/" THEN D$=H$:GOTO 300
292 H$=D$
300 CLS(RND(8)):GOSUB 2000:PRINT@32*4,"is this correct?":PRINTSTRING$(32,"-");:PRINT@32*6,A$:PRINTB$:PRINTC$:PRINTD$:PRINT STRING$(32,"-");:SCREEN 0,1
310 R$=INKEY$:IF R$="" THEN 310
315 IF R$="Y" OR R$="y" THEN 320 
316 IF R$="N" OR R$="n" THEN 255
317 GOTO 310
320 REM ROUTINE TO PRINT ON THE     LABELS
325 PRINT@32*14+8,"stand by-printing!!";
330 PRINT#-2,A$
340 PRINT#-2,B$
350 PRINT#-2,C$
360 PRINT#-2,D$
370 PRINT#-2:PRINT#-2
380 PRINT@32*14,"press <C> TO CONTINUE,<E> TO END";:LINE INPUT R$:IF R$="C" THEN 255 ELSE IF R$="E" THEN END ELSE 380
1000 REM ROUTINE FOR TITLE SCREEN
1010 A$=CHR$(34)+"photo"+CHR$(128)+"tagger"+CHR$(34):L=5:GOSUB1060
1020 A$="by":GOSUB1070
1030 A$="d.a.turowski":GOSUB1070
1040 A$="august--1986":GOSUB1070
1050 GOTO 1180
1060 REM CLS(0)
1070 M=LEN(A$)
1080 FOR X=1TOM
1090 PRINT@32*L+15-M/2+X,MID$(A$,X,1);
1100 SOUND100,1
1110 NEXT X
1120 L=L+1
1130 SOUND 200,1
1140 FOR S=1TO80:NEXTS
1150 RETURN
1160 'ROUTINE BY JOHN D. BOYLE       FROM RAINBOW MAGAZINE 2/85
1170 'PROGRAM LISTING 2
1180 SCREEN 0,1:FOR XX=1 TO 500::NEXT XX:FOR YY=1TO3:PLAY"O3;V31;L10;A;V16;A;V8;A;V3;A;V1;L20;A":NEXT YY
1190 GOTO 200
2000 PRINT@10,"photo"+CHR$(128)+"tagger";:PRINT@32*2+8,"by"+CHR$(128)+"d.a.turowski";:RETURN
3000 CLS:PRINT:PRINT"photo-tagger  IS A UTILITY TO   HELP YOU KEEP TRACK OF YOUR     PHOTOS. IT WILL PRODUCE ON A    MAILING LABEL THE INFORMATION   THAT YOU USUALLY FORGET TO WRITEON THE BACK OF THE PHOTO SUCH ASTHE DATE, PEOPLE OR PLACES IN"
3010 PRINT"THE PHOTO, AND ALSO ANY OTHER   GENERAL INFORMATION YOU MAY WANTTO REMEMBER!"
3015 PRINT@32*15+5,"HIT ANY KEY TO CONTINUE";:EXEC44539
3020 CLS:PRINT:PRINT"photo-tagger IS SELF-PROMPTING  AND BY USING IT AFTER YOU       GET A NEW BATCH OF PICTURES, YOUAND YOUR COMPUTER CAN QUICKLY   PRODUCE LABELS TO PLACE ON THE  BACK OF EACH PICTURE."
3025 PRINT"ALSO, IF YOU ARE TYPING THE SAMEINFORMATION REPEATEDLY FOR A SETOF PICTURES, FOR EXAMPLE, THE   SAME DATE, BY PRESSING THE '/'  KEY, photo-tagger WILL DEFAULT  TO THE LAST USED ENTRY. ENJOY!!"
3030 PRINT@32*15,"press <enter> to begin photo-tag";:EXEC 44539:GOTO 220
