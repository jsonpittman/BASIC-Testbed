0 ' COPYRIGHT 1989 FALSOFT, INC
2 ' ############################
4 ' #                          #
6 ' # GALISAT 3                #
8 ' #                          #
10 '# COCO 3 VER. BY DAVID     #
12 '# FRANCIS. DEFAULT PLOT:   #
14 '# EAST=RIGHT, WEST=LEFT.   #
16 '# CHG. L TO -1 IN LINE     #
18 '# 6002 TO FLIP.            #
20 '#                          #
22 '############################
24 PALETTE8,63:WIDTH40:CLS3:PRINTTAB(13)"* GALISAT  3 *":LOCATE13,2:PRINT"CoCo 3 Version":LOCATE12,3:PRINT"by David Francis":LOCATE1,5:PRINT"Plots positions of Jupiter's Galilean   satellites
26 ON BRK GOTO4000
28 GOSUB6000 ' INITIALIZE
30 GOSUB2000 ' GET DATE
32 POKE65497,0
34 GOSUB5000 ' SETUP PLOT SCREEN
35 GOSUB1000 ' MAIN LOOP
36 ' ===========================
38 ' MAIN PROGRAM LOOP
40 ' ===========================
1000 DL=INT((N-INT(N))*20+.5)/20:MT=(358.476+.9856003*N)*P:MJ=(225.328+.0830853*N)*P:JJ=221.647+.9025179*N
1002 VT=1.92*SIN(MT)+.02*SIN(2*MT):VJ=5.55*SIN(MJ)+.17*SIN(2*MJ): K=(JJ+VT-VJ)*P:DT=SQR(28.07-10.406*COS(K))
1004 Z=SIN(K)/DT:I=ATN(Z/SQR(1-Z*Z)):I=I/P:F=(N-DT/173):F1=I-VJ
1006 U1=84.5506+203.405863*F+F1:U2=41.5015+101.2916323*F+F1:U3=109.9770+50.2345169*F+F1:U4=176.3586+21.4879802*F+F1
1008 X1=5.906*SIN(U1*P+PI):X1=139+L*INT(X1*3.5+.5):X2=9.397*SIN(U2*P+PI):X2=139+L*INT(X2*3.5+.5)
1010 X3=14.989*SIN(U3*P+PI):X3=139+L*INT(X3*3.5+.5):X4=26.364*SIN(U4*P+PI):X4=139+L*INT(X4*3.5+.5)
1012 IFINKEY$=CHR$(12) THENHSCREEN0:POKE65496,0:RUN
1014 HSET(X1,V1,4):HSET(X2,V1,4):HSET(X3,V1,4):HSET(X4,V1,4)
1016 HSET(X1,V):HSET(X2,V):HSET(X3,V):HSET(X4,V):FORII=1 TO 1000:NEXT
1018 HSET(X1,V1,2):HSET(X2,V1,2):HSET(X3,V1,2):HSET(X4,V1,2):HLINE(137,180)-(142,180),PSET:V=V+1:N=N+.05:IFV=170 THEN1020 ELSE1000
1020 IFINKEY$=CHR$(12) THENPOKE65496,0:RUN ELSE1020
1022 ' =========================
1024 ' GET DATE & CONVERT IT TO
1026 ' JULIAN DAY NO.
1028 ' =========================
2000 LOCATE1,10:INPUT" Enter YEAR (YYYY)";Y:INPUT"  Enter MONTH  (MM)";M:INPUT"  Enter DAY    (DD)";D
2002 GOSUB3000 ' DATE VALID?
2004 D=INT(D):G=1
2006 D1=INT(D):F=D-D1-.5
2008 J=-INT(7*(INT((M+9)/12)+Y)/4)
2010 IF G=0 THEN2018
2012 S=SGN(M-9):A=ABS(M-9)
2014 J1=INT(Y+S*INT(A/7))
2016 J1=-INT((INT(J1/100)+1)*3/4)
2018 J=J+INT(275*M/9)+D1+G*J1
2020 J=J+1721027+2*G+367*Y
2022 IF F>=0 THEN2026
2024 F=F+1:J=J-1
2026 N=J-2415020+F:DATE$=MONTH$(M)+STR$(D)+",":DAY=D
2028 RETURN
2030 ' =========================
2032 ' CHECK FOR VALID DATE
2034 ' =========================
3000 IFY<0 OR Y>99999 THENPRINT:PRINT" YEAR must be 0 - 99999.":GOTO3008
3002 IFD<1 OR D>31 THENPRINT:PRINT" DAY is invalid.":GOTO3008
3004 IF M<1 OR M>12 THENPRINT:PRINT" MONTH is invalid.":GOTO3008
3006 RETURN
3008 PRINT:PRINT" Press [ENTER] to re-enter data.
3010 I$=INKEY$
3012 I$=INKEY$:IFI$="" THEN3012
3014 IFI$<>CHR$(13) THEN3012
3016 POKE65496,0:RUN
3018 ' =========================
3020 ' END
3022 ' =========================
4000 RGB:WIDTH32:POKE65496,0:END
4002 ' =========================
4004 ' SETUP PLOT SCREEN
4006 ' =========================
5000 HSCREEN2:HCOLOR4:HLINE(250,0)-(320,191),PSET,BF
5002 HLINE(136,0)-(136,170),PSET:HLINE(142,0)-(142,170),PSET
5004 FORZZ=2 TO 170 STEP20:HLINE(0,ZZ)-(15,ZZ),PSET:HLINE(235,ZZ)-(250,ZZ),PSET:NEXT:FORZZ=0 TO 23STEP5:HPRINT(1,ZZ),DAY:DAY=DAY+2:IFDAY>30 THEN5006 ELSENEXT
5006 HDRAW"BM139,180BU2R2F1D2G1L3H1U2E1":HPAINT(139,180):HLINE(0,170)-(250,170),PSET:HPRINT(0,23),L$:HPRINT(27,23),R$
5008 HCOLOR2:HPRINT(32,1),"Plot":HPRINT(32,2),"begins":HPRINT(32,3),"on":HPRINT(32,4),MONTH$(M):HPRINT(31,5),STR$(D)+",":HPRINT(31,6),STR$(Y)+"."
5010 HPRINT(32,9),"Dashes":HPRINT(32,10),"indicate":HPRINT(32,11),"midnight":HPRINT(32,12),"EST on":HPRINT(32,13),"dates":HPRINT(32,14),"shown.
5012 HPRINT(32,18),"[CLEAR]=":HPRINT(32,19),"New Plot":HPRINT(32,21),"[BREAK]=":HPRINT(32,22),"End":HCOLOR4:RETURN
5014 ' =========================
5016 ' INITIALIZE
5018 ' =========================
6000 PALETTE0,9:DIMMONTH$(12):FORQ=1 TO 12:READMONTH$(Q):NEXT:PI=3.14159265:P=PI/180:V=0:V1=180
6002 L=1
6004 IFL=1 THENL$="WEST":R$="EAST" ELSEL$="EAST":R$="WEST
6006 RETURN
6008 ' =========================
6010 ' DATA FOR MONTHS
6012 ' =========================
7000 DATA January,February,March,April,May,June,July,August,Sept.,October,November,December
