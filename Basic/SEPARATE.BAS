0 ' COPYRIGHT 1989  FALSOFT,INC
100 CLEAR 500:SS$=CHR$(127)
105 CLS:PRINT "- ENTER, COMBINE, AND SEPARATE -"
110 P$="ENTER A DECIMAL NUMBER":VT$="D"
120 HV=999999:LV=1:SL=64:GOSUB 9020
124 NN$=LEFT$(VA$,LEN(VA$)-3)+RIGHT$(VA$,2)
130 P$="ENTER YOUR FIRST NAME":VT$="A"
140 HV=32:SL=128:GOSUB 9020
145 NN$=NN$+SS$+VA$+SS$
150 P$="NOW YOUR LAST NAME, DON'T BE SHY":VT$="A"
160 HV=32:SL=192:GOSUB 9020
165 NN$=NN$+VA$
167 PRINT:PRINT "---- THE ENTIRE RECORD IS ----"
170 PRINT NN$
180 I= INSTR(1,NN$,SS$):J= INSTR(I+1,NN$,SS$)
185 PRINT:PRINT "---- THE PARTS ARE -----------"
190 NB$=LEFT$(NN$,I-3)+"."+MID$(NN$,I-2,2)
200 PRINT "NUMBER ------>";NB$
230 PRINT "FIRST NAME -->";MID$(NN$,I+1,J-I-1)
240 PRINT "LAST NAME --->";RIGHT$(NN$,LEN(NN$)-J)
250 PRINT:PRINT "TOUCH 'ENTER' TO CONTINUE";:INPUT CT$:GOTO 105
9020 PRINT @SL,P$:PRINT @SL+32," ":PRINT @SL+32,"";
9026 LINE INPUT "==> ";VA$ 'INPUT THE VARIABLE
9028 LA= LEN(VA$):IF VT$= "A" GOTO 9044
9030 VV= VAL(VA$):IF VT$= "D" GOTO 9038
9034 IF INT(VV) <> VV GOTO 9020 ELSE GOTO 9042
9038 IF LA > 9 OR LA < 3 GOTO 9020
9040 IF MID$(VA$,LA-2,1) <> "." GOTO 9020
9042 IF VV < LV OR VV > HV GOTO 9020
9044 IF LA > HV GOTO 9020 ELSE RETURN
