1 REM**************************
2 REM* LIFE SKILLS SIMULATION *
3 REM*    CHECKBOOK SKILLS    *
4 REM*    BY FRED B.SCERBO    *
5 REM*     60 HARDING AVE     *
6 REM*  NORTH ADAMS,MA 01247  *
7 REM*   COPYRIGHT (C) 1990   *
8 REM**************************
9 CLS0
10 FORI=1TO32:PRINTCHR$(204);:NEXT
15 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
20 FORI=1TO32:PRINTCHR$(195);:NEXT
25 DATA93,88,96,93,88,84,94,92,90,93,92,93,,,126,124,122,125,,112,126,116,126,116,126,16,116,126,,117,124,125
30 DATA85,,,85,,,90,80,88,85,,84,,,122,,120,117,16,118,16,,122,,122,,16,122,,117,,116
35 DATA85,,,85,,,91,90,96,85,83,82,,,75,67,66,69,70,16,,,74,,74,,16,74,,69,67,67
40 DATA85,,,85,,,90,88,,85,80,,,,32,,74,69,68,66,,,74,,74,,16,74,,,,69
45 DATA85,,90,85,,,90,,,85,,81,,16,42,,42,37,,36,34,,42,,42,37,16,42,37,37,,37
50 DATA87,83,90,87,82,81,91,80,,87,83,87,,,43,35,42,39,34,32,43,33,43,33,43,39,33,43,39,37,35,39
55 PRINT@293," COMPUTER  SIMULATION ";
60 PRINT@325,"   CHECKBOOK SKILLS   ";
65 PRINT@357,"   BY FRED B.SCERBO   ";
70 PRINT@389,"  COPYRIGHT (C) 1990  ";
75 PRINT@453,"  SKILL LEVEL  (1-9)  ";
80 X$=INKEY$:IFX$=""THEN80
85 W=VAL(X$):IFW=0THEN80
90 PRINT@453,"  ALLOW HINTS? (Y/N)  ";
95 X$=INKEY$:IFX$="Y"THEN110
100 IFX$="N"THEN115
105 GOTO95
110 DL=1:GOTO120
115 DL=0
120 CLS0
125 Z=500
130 DIMB$(10):FORI=1TO10:READB$(I):NEXT:X=RND(-TIMER)
135 CLS0:Q=RND(10)
140 FD=0
145 X=INT(RND(Z))*W*2:A=X/100
150 Y=INT(RND(Z))*W*2:B=Y/100
155 CL=INT(RND(15)/2)
160 N$="##.##"
165 CLS0:PD=PD+1:PR=1
170 GOSUB460:PRINT" YOU HAVE A BALANCE OF ";:PRINTUSING"$##.##";A:PRINT" IN YOUR CHECKING ACCOUNT."
175 PRINT" THE ";B$(Q);" YOU WANT COST";:IF W>1THENPRINT" A"
180 IF W=1THENPRINT"S A"
185 PRINT" TOTAL OF ";:PRINTUSING"$##.##";B;:PRINT". DO YOU HAVE"
190 PRINT" ENOUGH MONEY TO BUY THE NEW"
195 PRINT" ";B$(Q);
200 PRINT" ?  (Y)ES OR (N)O"
205 GOSUB460
210 X$=INKEY$:IFX$=CHR$(3)THEN210
215 IFX$="N"THEN250
220 IFX$="Y"THEN235
225 IFX$="@"THEN465
230 GOTO210
235 IF A>B THEN PRINT" CORRECT! YOU HAVE ENOUGH.":CR=CR+1
240 IF B>A THEN PRINT" SORRY! YOU NEED MORE MONEY.":WR=WR+1
245 GOTO260
250 IF A>B THEN PRINT" SORRY! YOU HAVE ENOUGH.":WR=WR+1
255 IF B>A THEN PRINT" CORRECT! YOU NEED MORE MONEY.":CR=CR+1
260 GOSUB460
265 IF B>A THEN 305
270 PRINT" HOW MUCH MONEY WILL YOU HAVE":PRINT" LEFT?";
275 IFDL=0THEN285
280 PRINT" (";:PRINTUSINGN$;A;:PRINT"-";:PRINTUSINGN$;B;:PRINT")";
285 PRINT"=> $ ";
290 GOSUB340
295 IF INT(C*100000+.5)=INT((A-B)*100000+.5)THEN400
300 IF INT(C*100000+.5)<>INT((A-B)*100000+.5)THEN420
305 PRINT" HOW MUCH MORE MONEY DO YOU":PRINT" NEED? ";
310 IF DL=0THEN320
315 PRINT"(";:PRINTUSINGN$;B;:PRINT"-";:PRINTUSINGN$;A;:PRINT")";
320 PRINT"=> $ ";
325 GOSUB340
330 IF INT(C*100000+.5)=ABS(INT((B-A)*100000+.5))THEN410
335 IF INT(C*100000+.5)<>ABS(INT((B-A)*100000+.5))THEN430
340 Y$=""
345 X$=INKEY$:IFX$=""THEN345
350 IFX$=CHR$(13)THEN385
355 IFX$=CHR$(8)THEN375
360 IFX$="@"THEN465
365 IF ASC(X$)<13THEN345
370 Y$=Y$+X$:PRINTX$;:GOTO345
375 WW=LEN(Y$):IFWW=0THEN340
380 PRINTX$;:Y$=LEFT$(Y$,WW-1):GOTO345
385 C=VAL(Y$):IFLEN(Y$)=0 AND X$=CHR$(13)THEN340
390 PRINT:GOSUB460
395 RETURN
400 PRINT" CORRECT! YOU HAVE ";:PRINTUSING"$##.##";C;:PRINT" LEFT.":CR=CR+1
405 GOTO435
410 PRINT" CORRECT! YOU NEED ";:PRINTUSING"$##.##";C:CR=CR+1
415 NM=1:GOTO435
420 PRINT" WRONG! YOU HAVE ";:PRINTUSING"$##.##";A-B;:PRINT" LEFT.":WR=WR+1
425 GOTO435
430 PRINT" WRONG! YOU NEED ";:PRINTUSING"##.##";B-A:WR=WR+1:NM=1
435 GOSUB460:PRINT" PRESS <<<ENTER>>> TO CONTINUE.";:POKE1535,96:FORI=1TO300:NEXTI
440 FD=1
445 X$=INKEY$:IFX$=CHR$(13)THEN545
450 IFX$="@"THEN465
455 GOTO445
460 SQ=(143+(CL*16)):FORQS=1TO32:PRINTCHR$(SQ );:NEXTQS:RETURN
465 CLS:PRINT:PRINT:PRINT
470 PRINTTAB(3)"PROBLEMS COMPLETED  = "PD:PRINT
475 PRINTTAB(3)"CORRECT RESPONSES   = "CR:PRINT
480 PRINTTAB(3)"INCORRECT RESPONSES = "WR:PRINT
485 TR=CR+WR:IFTR=0THENTR=1
490 SC=INT(CR/TR*100)
495 PRINTTAB(3)"YOUR TOTAL SCORE    = "SC"%":PRINT
500 PRINTTAB(3)"ANOTHER TRY (Y/N) OR (C) ?";
505 X$=INKEY$:IFX$=""THEN505
510 IFX$="Y"THEN RUN
515 IFX$="N"THEN CLS:END
520 IFX$="C"THEN 530
525 GOTO505
530 PD=PD-1:IF FD=0 AND PR=1 THEN 165
535 IF PR=2THEN560
540 IF FD=1 THEN 135
545 IF NM=0THEN A=A-B:GOTO150
550 IF A-B<=0THEN A=A ELSE A=A-B
555 Y=INT(RND(Z))*W*2:B=(Y/100)+2
560 CLS0:PD=PD+1:PR=2
565 GOSUB460:PRINT" YOU HAVE A BALANCE OF ";:PRINTUSING"$##.##";A:PRINT" IN YOUR CHECKING ACCOUNT."
570 PRINT" YOU HAVE JUST RECEIVED A CHECK"
575 PRINT" FOR ";:PRINTUSING"$##.##";B;:PRINT". HOW MUCH"
580 PRINT" WILL YOUR NEW BALANCE BE ?"
585 GOSUB460
590 PRINT" RECORD YOUR NEW BALANCE HERE.":IF DL=0THEN600
595 PRINT" (";:PRINTUSINGN$;A;:PRINT"+";:PRINTUSINGN$;B;:PRINT")";
600 PRINT"=> $ ";
605 GOSUB340
610 IF INT(C*100000+.5)=ABS(INT((A+A)*100000+.5))THEN620
615 IF INT(C*100000+.5)<>ABS(INT((A+B)*100000+.5))THEN630
620 PRINT" CORRECT! YOU HAVE ";:PRINTUSING"##.##";C:CR=CR+1
625 GOTO635
630 PRINT" WRONG! YOU HAVE ";:PRINTUSING"##.##";A+B:WR=WR+1
635 GOSUB460:PRINT" PRESS <<<ENTER>>> TO CONTINUE. ";:FORI=1TO300:NEXTI
640 NM=0:FD=0
645 X$=INKEY$:IFX$=CHR$(13)THEN660
650 IFX$="@"THEN465
655 GOTO645
660 CLS0:Q=RND(10)
665 FD=0
670 A=A+B
675 Y=INT(RND(Z))*W*2:B=Y/100
680 GOTO150
685 DATA SHIRTS,CANDIES,SOCKS,DISKS,BOOKS,PADS,PENS,CARPETS,SUNGLASSES,TAPES
