0 ' COPYRIGHT 1989  FALSOFT,INC
10 REM NICKOLODEON 4.0
20 REM COPYRIGHT (C) 1983,1988
30 REM BILL BUDENHOLZER
40 REM****SET UP-1
50 PCLEAR1:CLS0:CLEAR1000:GOTO890
60 REM*****PLAY SONG
70 GOSUB660:FORI=1 TON-1:PRINT@136,B$;:PRINT@(144-INT(LEN(W$(I)))/2),W$(I);:PLAYS$(I):NEXT
80 PLAY"V31L2O2":FORI=1TO1000:NEXT:IFX=1 THENN=1:X=0:GOTO610ELSEGOTO610
90 CLS:PRINT@9,P$:PRINT@74,"EDIT MENU":PRINT@135,"1) EDIT ABSOLUTE MODE":PRINT@167,"2) EDIT KEYBOARD MODE":PRINT@199,"3) INSTRUCTIONS":GOSUB630:IFS$="M" THEN610ELSES=VAL(S$):IFS<1 ORS>3 THEN90ELSE ONS GOTO110,320,740
100 REM*****ABSOLUTE EDITOR
110 CLS:PRINT@9,P$:PRINT
120 F=0:PRINTN;:INPUTS$,W$:IFS$="DEL" THENF=1ELSEIFS$="INS" THENF=2ELSEIFS$="CHK" THENF=3ELSEIFS$="END" THENS$(N)="":W$(N)="end":GOTO90
130 IFS$="NEW" THENF=4ELSEIFF THEN140ELSEPLAYS$:S$(N)=S$:W$(N)=W$:N=N+1:GOTO120
140 ONF GOSUB160,240,280,300:GOTO120
150 REM*****DELETE
160 PRINT:INPUT"ENTER LINE #";LN:IFLN=0 THENRETURNELSEGOSUB210:GOSUB180:RETURN
170 REM*****SEARCH 'L' & 'O'
180 L=2:O=2:FORI=1 TON-1:E=INSTR(S$(I),"L"):IFE THENIFINSTR(S$(I),"1") THENL=VAL(MID$(S$(I),E+2,2))ELSEL=VAL(MID$(S$(I),E+2,1))
190 E=INSTR(S$(I),"O"):IFE THENO=VAL(MID$(S$(I),E+2,1))
200 NEXTI:RETURN
210 FORI=LN TON:S$(I)=S$(I+1):W$(I)=W$(I+1):NEXTI:N=N-1:RETURN
220 ONF GOSUB250,160,280,300,420,380,490,460,470:IFIN THEN340ELSE330
230 REM*****INSERT
240 PRINT:INPUT"ENTER LINE #";LN:IFLN=0 THENRETURNELSEFORI=N+1 TOLN STEP-1:S$(I)=S$(I-1):W$(I)=W$(I-1):NEXT:N=N+1:INPUT"NOTE, WORD";S$(LN),W$(LN):RETURN
250 IN=1:PRINT:INPUT"ENTER LINE #";LN:IFLN=0 THENRETURN
260 FORI=N+1 TOLN STEP-1:S$(I)=S$(I-1):W$(I)=W$(I-1):NEXT:N=N+1:TN=N:N=LN:GOSUB340:GOSUB180:IN=0:N=TN:RETURN
270 REM*****CHECK
280 PRINT:PLAY"V31L2O2T2":FORI=1 TON-1:PRINTI;:PRINTS$(I),W$(I):PLAYS$(I):NEXT:RETURN
290 REM*****NEW SONG
300 PRINT:PLAY"L2O2T2":N=1:O=2:L=2:S$(1)="":W$(1)="":RETURN
310 REM*****KEYBOARD EDITOR
320 CLS:POKE282,0
330 GOSUB340:GOTO330
340 F=0:PRINTN;"L"L;"O"O;"?";:GOSUB510:IFA$="L" THENF=6ELSEIFA$="O" THENF=5ELSEIFA$="I" THENF=1ELSEIFA$="D" THENF=2ELSEIFA$="C" THENF=3ELSEIFA$="N" THENF=4ELSEIFA$="E" THENF=7ELSEIFA$="P" THENF=8ELSEIFA$="T" THENF=9
350 IFF THEN220ELSEIFA$<"a" THENPRINT:RETURNELSES$=N$(K(ASC(A$)-96)):IFS$="" THENPRINT:RETURNELSES$(N)=T$+L$+O$+S$:PRINTCHR$(8);S$(N);:PLAYS$(N):L$="":O$="":T$=""
360 INPUT" WORD";W$(N):N=N+1:RETURN
370 REM***** 'L'
380 PRINTCHR$(8);"L";:GOSUB510:IFA$="+" THENIFL=1 THENL=2ELSEL=L*2
390 IFA$="=" THENIFL>1 THENL=L/2ELSEL=1
400 IFL>16 THENL=16
410 L$="L"+STR$(L):PRINTL:RETURN
420 PRINTCHR$(8);"O";:GOSUB510:IFA$="+" THENO=O+1:IFO>5 THENO=5
430 IFA$="=" THENO=O-1:IFO<1 THENO=1
440 O$="O"+STR$(O):PRINTO:RETURN
450 REM*****PAUSE
460 PRINTCHR$(8);"P";L:E$="P"+STR$(L):PLAYE$:S$(N-1)=S$(N-1)+E$:E$="":RETURN
470 PRINT:INPUT"INPUT NEW TEMPO";T$:IFT$="" THENRETURNELSET$="T"+T$:RETURN
480 REM*****END
490 POKE282,255:S$(N)="":W$(N)="end":GOTO90
500 REM*****INKEY$
510 A$=INKEY$:IFA$="" THEN510ELSERETURN
520 REM*****SAVE SONG
530 CLS:PRINT@9,P$:PRINT:PRINT"  ENTER SONG NAME";:INPUTSN$:CLS:PRINT@9,P$:PRINT:PRINT"  SAVING SONG":OPEN"O",#1,SN$:FORI=1 TON:WRITE#1,S$(I),W$(I):NEXT
540 CLOSE#1:GOTO610
550 REM*****LOAD SONG
560 N=1:CLS:PRINT@9,P$:PRINT:PRINT"  ENTER SONG NAME";:INPUTSN$:CLS:PRINT@9,P$:PRINT:PRINT"  LOADING SONG"
570 OPEN"I",#1,SN$
580 IFEOF(1) THEN590ELSEINPUT#1,S$(N),W$(N):PRINTW$(N);:N=N+1:GOTO580
590 CLOSE#1:GOTO610
600 REM*****MAIN MENU
610 CLS:PRINT@9,P$:PRINT@74,"MAIN MENU":PRINT@135,"1) EDIT SONG":PRINT@167,"2) SAVE SONG":PRINT@199,"3) LOAD SONG":PRINT@231,"4) PLAY SONG":PRINT@263,"5) RETURN TO BASIC":GOSUB640:S=VAL(S$):IFS<1 ORS>5 THEN610ELSE ONS GOTO90,530,560,70,930
620 REM*****SELECT
630 PRINT@418,"PRESS <M> TO RETURN TO MAIN     MENU"
640 PRINT@482,"SELECTION?";:GOSUB510:S$=A$:RETURN
650 REM*****DRAW PIANO
660 CLS0:FORI=7TO56:SET(I,4,1):NEXTI:FORI=8TO55:SET(I,14,1):NEXTI:FORI=8TO55:SET(I,15,1):NEXTI:FORI=8TO55:SET(I,24,1):SET(I,25,1):NEXTI:FORI=4TO26:SET(8,I,1):NEXTI:FORI=4TO26:SET(55,I,1):NEXTI:FORI=16TO26:SET(9,I,1):NEXTI
665 FOR I=18TO25:SET(10,I,1):NEXTI
670 FORI=18TO25:SET(53,I,1):NEXTI:FORI=16TO26:SET(54,I,1):NEXTI:FORI=14TO49:SET(I,6,1):NEXTI:FORI=6TO11:SET(49,I,1):NEXTI:FORI=49TO14STEP-1:SET(I,11,1):NEXTI:FORI=11TO6STEP-1:SET(14,I,1):NEXTI:FORI=10TO53:SET(I,16,5):NEXTI:J=10:K=14:GOSUB690
680 J=22:K=J+4:GOSUB690:J=34:K=J+4:GOSUB690:J=46:K=J+4:GOSUB690:SET(55,15,1):FORI=29TO33STEP2:SET(I,26,2):NEXTI:PRINT@138,"nickelodeon";:FORI=1TO1000:NEXTI:PLAY"V31L2O2T2":RETURN
690 FORI=J TOK STEP2:RESET(I,15):NEXTI
700 J=J+7:K=K+5
710 FORI=J TOK STEP2:RESET(I,15):NEXTI
720 RETURN
730 REM*****INSTRUCTIONS
740 CLS:PRINT@9,P$:PRINT:PRINTTAB(9)"ABSOLUTE MODE":PRINT:PRINT"ABSOLUTE MODE ALLOWS YOU TO     ENTER A SONG USING EXTENDED     BASIC'S PLAY STRING COMMANDS.   THE EDITOR PROMPTS WITH THE CUR-RENT NOTE NUMBER, THEN ASKS FOR"
750 PRINT"THE PLAY STRING AND THE WORD TO BE DISPLAYED ON THE PIANO. ENTERTHE INFORMATION USING THE FOL-  LOWING  FORMAT:":PRINT"  string,word<ENTER>":GOSUB870:PRINT"FOR APPEARENCE SAKE ENTER THE   WORD IN LOWER CASE MODE (REVERSEVIDEO)."
760 PRINT"SEE EXAMPLE BELOW.":PRINT:PRINT"1 ? L 8A,word<ENTER>":PRINT"2 ? O 3BP8,word<ENTER>":PRINT:PRINT"** NOTE THE SPACE FOLLOWING THE 'L' AND 'O' IS NECESSARY FOR THEEDITOR TO WORK PROPERLY. **"
770 GOSUB870:PRINT"THE EDITOR ALSO ACCEPTS THE FOL-LOWING COMMANDS:":PRINT
780 PRINT"  INS  ALLOWS INSERTION OF NOTE":PRINT"  DEL  DELETES NOTE":PRINT"  CHK  CHECKS SONG":PRINT"  NEW  RESETS NOTE COUNTER":PRINT"  END  RETURNS TO MENU":GOSUB870
790 BL$=CHR$(133)+STRING$(15," ")+CHR$(138):PRINT:PRINTTAB(9)"KEYBOARD MODE":PRINTTAB(6);STRING$(17,140):PRINTTAB(6);BL$:PRINTTAB(6)CHR$(133)"  w e   t y u  "CHR$(138):PRINTTAB(6)CHR$(133)" a s d f g h j "CHR$(138):PRINTTAB(6)BL$
800 PRINTTAB(6)STRING$(17,131):PRINT:PRINT"KEYBOARD MODE ALLOWS YOU TO USE THE KEYBOARD AS PIANO KEYBOARD. THE KEYS INDICATED ABOVE CORRES-POND TO THE KEYS ON A PIANO.":GOSUB870
810 PRINT:PRINT"PRESSING <A> GIVES A 'C' NOTE   PRESSING <W> GIVES A 'C#' NOTE.":PRINT"EDIT FUNCTIONS INCLUDE:":PRINT:PRINT"  <O>  CHANGES OCTAVE":PRINT"  <L>  CHANGES LENGTH":PRINT"  <I>  ALLOWS INSERT":PRINT"  <D>  ALLOWS DELETE"
820 PRINT"  <C>  CHECKS SONG":PRINT"  <N>  RESETS NOTE COUNTER":PRINT"  <P>  INSERTS A REST(PAUSE)":PRINT"  <E>  RETURNS TO MENU":PRINT"  <T>  ALLOWS TEMPO CHANGE":GOSUB870
830 PRINT:PRINT"USE <+> AND <-> AFTER <L> AND   <O> TO INCREASE OR DECREASE     LENGTH AND OCTAVE.":PRINT
840 PRINT" **  YOU MUST PRESS <SHIFT> AND THE KEY CODE, TO ACCESS ANY COM-MAND. **":PRINT:PRINT"INSERT AND DELETE ASK FOR A LINENUMBER. ENTER THE NUMBER OF THE LINE TO BE INSERTED OR DELETED.":GOSUB870
850 GOTO90
860 REM*****ENTER
870 PRINT@482,"PRESS <ENTER> TO CONTINUE";:INPUTX:CLS:PRINT@9,P$:RETURN
880 REM*****SET UP-2
890 DIMS$(100),W$(100),N$(12),K(25):PLAY"V31L2O2":B$=STRING$(16,128):P$="NICKELODEON":X=1:N=1:L=2:O=2:FORI=1TO12:READN$(I):NEXT:FORI=1TO25:READK(I):NEXT:DATAC,C#,D,D#,E,F,F#,G,G#,A,A#,B,1,0,0,5,4,6,8,10,0,12,0,0,0,0,0,0,0,0,3,7,11,0,2,0,9
900 FORN=1TO29:READS$(N),W$(N):NEXT:N=30:GOTO70
910 DATAL 8O 3A,put,B,a,A,noth,L 8B,er,L 16A,nic,L 8B,kel,AP8,in,F#,in,G,the,F#,nic,G,kel,L 16F#,o,L 8G,de,F#P8,on,E,all,F#,i,E,ev,F#,er,E,want,F#,from,E,you,F#,is,E,mu,D,sic,E,mu,D,sic,E,mu,D,sic,,end
920 REM*****END
930 CLEAR200:END
