10 REM SPELLDOWN GAME
20 REM (C) STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1987
30 CLS:P$=STRING$(32,255)
40 PRINT@8,"spelling    game";
50 PRINT@384,"TYPE A SPELLING WORD";
60 INPUT A$
70 A=LEN(A$):X=A
80 IF A>10 THEN PRINT@480,"THIS WORD IS TOO LONG";:FORT=1TO 2000:NEXT T:RUN
90 FOR T= 1 TO A
100 PRINT@34+D,MID$(A$,T,1)+" "+"-";
110 PLAY"O4L20AB"
120 D=D+32
130 NEXT T
140 T=0:D=0
150 FOR T=A TO 1 STEP-1
160 PRINT@40+D,"- "+MID$(A$,T,1);
170 D=D+32
180 PLAY"O2L20AB"
190 NEXT T
200 D=0:T=0
210 FOR T= 1 TO A
220 PRINT@47+D,"";:LINE INPUT C$(T)
230 D=D+32
240 NEXT T
250 PRINT P$;
260 FOR T= 1 TO A
270 EN$=" ":PRINT@384," ":PRINT@416," "
280 IF LEFT$(C$(T),1)<>MID$(A$,T,1) THEN PRINT@384,T;". ";C$(T)" DOESN'T START":PRINT"     WITH THE LETTER "MID$(A$,T,1):GOTO 330
290 IF RIGHT$(C$(T),1)<>MID$(A$,A,1) THEN PRINT@384,T;". ";C$(T)" DOESN'T END":PRINT"     WITH THE LETTER ";MID$(A$,A,1):GOTO 330
300 PRINT@384,T;". IS "C$(T)" CORRECT ? ";
310 EN$=INKEY$
320 IF EN$="Y" THEN R=R+1 ELSE IF EN$="N" THEN W=W+1 ELSE 310
330 PRINT EN$
340 AN$=INKEY$
350 IF AN$=CHR$(13) THEN 360 ELSE 340
360 A=A-1:NEXT T
370 PRINT@384,P$+P$;
380 PRINT@452,"YOUR SCORE IS";INT((R/X)*100+.5);"%"
390 PRINT@484,"PRESS 'A' OR 'E' ";
400 IN$=INKEY$
410 IF IN$="A" THEN RUN ELSE IF IN$="E" THEN 420 ELSE 400
420 CLS:END
