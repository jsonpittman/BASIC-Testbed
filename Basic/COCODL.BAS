10 '****************************
20 '**   COCO DATA LOGGER     **
30 '**   BY KENNETH R.BOONE   **
40 '**   RT. 6, BOX 336D      **
50 '**   RALEIGH, NC 27612    **
55 '**   COPYRIGHT MARCH 1991 **
60 '**   BY FALSOFT, INC.     **
65 '**   RAINBOW MAGAZINE     **
70 '****************************
80 'THIS PROGRAM USES THE JOYSTICK INPUT AND ACTS AS A TIME EVENT LOGGER.
90 CLS
100 INPUT "SELECT OUTPUT TYPE:             0=PRINT TIME ON.                1=PRINT TIME ON AND TIME OFF.   ";POFF
110 IF POFF=0 OR POFF=1 THEN GOTO 120 ELSE GOTO 100
120 CLS
130 INPUT "SELECT PRINTER ON (0)           OR PRINTER OFF    (2).";PRTTEST
140 IF PRTTEST=0 OR PRTTEST=2 THEN GOTO 150 ELSE GOTO 130
150 CLS
160 INPUT "ENTER TURN ON SETPOINT BETWEEN  1 AND 63";SPON
170 IF SPON > 0 AND SPON < 64 THEN GOTO 180 ELSE GOTO 160
180 CLS
190 INPUT "ENTER TURN OFF SETPOINT, WHICH  MUST BE LESS THAN TURN ON       SETPOINT";SOFF
200 IF SOFF < SPON AND SOFF >= 0 THEN GOTO 210 ELSE GOTO 190
210 CLS
220 INPUT "SET THE TIME. WHAT HOUR IS IT?    (1 TO 24)";HOUR
230 IF HOUR>=1 AND HOUR<=24 THEN GOTO 240 ELSE GOTO 220
240 CLS
250 INPUT "WHAT MINUTE IS IT";MINUTE
260 IF MINUTE>=0 AND MINUTE<=59 THEN GOTO 270 ELSE GOTO 250
270 CLS
280 INPUT "THE TIMER WILL START WHEN YOU   PRESS ENTER";X
290 CLS
300 DAY=0:COUNT=0:FLAG=0
310 GOTO 380
320 H=0:D=D+1
330 MINUTE=-1
340 IF HOUR=24 THEN GOTO 320        'IS DAY UP?
350 HOUR=HOUR+1
360 IF MINUTE=59 THEN GOTO 330      'IS HOUR UP?
370 MINUTE=MINUTE+1
380 FOR SECOND=0 TO 59 
390 PRINT @48,HOUR;":";MINUTE;":";SECOND
400 PRINT @80,"DAY ";DAY
410 PRINT @112,"COUNT";COUNT
420 IF TIMER>=60 THEN GOTO 430 ELSE GOTO 420   'IS SECOND UP?
430 TIMER=0      'RESET TIMER
440 IN=JOYSTK(0)  'JOYSTK 0 MUST    BE READ BEFORE 1
450 IN=JOYSTK(1)
460 PRINT @144,"INPUT";IN           'PRINT TIME ON ONLY?
470 PRINT @176,"ON SETPOINT ";SPON
480 PRINT @208,"OFF SETPOINT";SOFF
490 IF IN>SPON AND FLAG=0 THEN GOTO 610
500 IF IN>SPON AND FLAG=1 THEN GOTO 690
510 IF IN<SOFF AND FLAG=0 THEN GOTO 690
520 IF IN<SOFF AND FLAG=1 THEN FLAG=0
530 IF POFF=0 THEN GOTO 690
540 PRINT HOUR;":";MINUTE;":";SECOND;"   DAY";DAY
550 PRINT "COUNT";COUNT;"   OFF"
560 IF PRTTEST=2 THEN GOTO 690
570 PRTTEST=(PEEK(&HFF22) AND 1)
580 IF PRTTEST=1 THEN GOTO 690      'PRTTEST=1 PRINTER NOT READY
590 PRINT#-2,HOUR;":";MINUTE;":";SECOND;"   DAY";DAY;"   COUNT";COUNT;"   OFF"
600 GOTO 690
610 COUNT=COUNT+1
620 PRINT HOUR;":";MINUTE;":";SECOND;"   DAY";DAY
630 PRINT "COUNT";COUNT;"   ON"
640 IF PRTTEST=2 THEN GOTO 680
650 PRTTEST=(PEEK(&HFF22) AND 1)
660 IF PRTTEST=1 THEN GOTO 680
670 PRINT#-2,HOUR;":";MINUTE;":";SECOND;"   DAY";DAY;"    COUNT";COUNT;"   ON"
680 FLAG=1
690 NEXT SECOND
700 GOTO 360
