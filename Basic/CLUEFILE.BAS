1 '**********************
2 '*      cluefile      *
3 '*      (C) 1987      *
4 '*BY JAMES PH LAMONICA*
5 '*& MARY JEAN LAMONICA*
6 '*   EL PASO, TEXAS   *
7 '**********************
10 PMODE0:PCLEAR1:CLEAR12000:DIMD$(5,300)
20 CLS(3):PRINT@32,"THIS IS A PROGRAM TO CREATE THE WORD FILE FOR THE clueword GAME PLEASE PRESS ANY KEY TO BEGIN":EXEC44539
30 PRINT@224,"TO LOAD A PARTIALLY COMPLETED   FILE PRESS y, OR ELSE PRESS n"
40 I$=INKEY$
50 IFI$="Y"THENGOSUB500:GOTO90
60 IFI$="N"THENGOTO90
70 GOTO40
90 CLS(3):PRINT@32,"YOU MAY ENTER";300-DS;" SETS OF WORDS FOR clueword. ENTER THE NUMBER OF ADDITIONAL SETS YOU WISH ADD";:INPUTNS
95 IF NS=0 THENGOTO180
100 CLS(3):PRINT@64,"YOU WILL BE ASKED FOR FOUR CLUE WORDS AND THEN THE ANSWER WORD. PRESS RETURN AFTER EACH ENTRY.  PRESS A KEY WHEN READY TO BEGIN":EXEC44539
110 FORX=DS+1TODS+NS
115 CLS(6):PRINT"SET NUMBER ";X
120 FORY=1TO4
130 PRINT@Y*64,"ENTER clueword #";Y;:INPUTD$(Y,X)
132 IF D$(Y,X)="^"THENY=Y-2:GOTO140
140 NEXTY
150 PRINT@320,"ENTER THE ANSWER";:INPUTD$(5,X)
160 NEXTX
180 CLS(5):PRINT@96,"TO SAVE THE clueword FILE, PUT  IN A DISK AND PRESS A KEY PLEASE":EXEC44539
190 OPEN"O",#1,"CLUEDATA"
200 PRINT@200,"SAVING cluedata";
210 FOR X=1TODS+NS:FORY=1TO5:WRITE#1,D$(Y,X):NEXTY,X
220 CLOSE#1:GOTO20
500 CLS(4):PRINT@32,"TO LOAD A PARTIALLY COMPLETED   FILE, PUT IN THE PROPER DISK ANDPRESS A KEY PLEASE":EXEC44539
505 PRINT@264,"FINDING cluedata";
510 OPEN"I",#1,"CLUEDATA"
515 PRINT@264,"LOADING cluedata";
520 FOR X=1TO300
530 IF EOF(1)=-1THEN550
540 FORY=1TO5:INPUT#1,D$(Y,X):DS=X:NEXTY
545 NEXTX
550 CLOSE#1:RETURN
