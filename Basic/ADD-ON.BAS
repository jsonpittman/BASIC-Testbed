0 'COPYRIGHT 1990 FALSOFT, INC.
10 '
20 '
30 'ADD-ON WORD GAME
40 '(C) MARCH 28, 1989 BY
50 'JOEL MATHEW HEGBERG
60 '936 NORTH TWELFTH STREET
70 'DE KALB, ILLINOIS  60115
80 '
90 '
100 POKE65496,0:WIDTH32:CLS:PMODE0,1:PCLEAR2:PCLS:SCREEN0,0:HSCREEN0:CLEAR7600:DIMPL$(4),ZZ$(100),GB$(15,15),SC(4):HBUFF1,500:BD=0:POKE&HFF9A,BD
110 POKE65497,0:CLS:R=RND(-TIMER):HCOLOR4,2:HSCREEN2:HLINE(160,0)-(160,191),PSET:HPRINT(9,11),"CMP":HPRINT(29,11),"RGB"
120 IFBUTTON(0)<>0THEN120
130 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)=0THEN130
140 IFPX<160THENCMP:ELSERGB
150 '
160 'DATA FOR LETTER DISTRIBUTION
170 '
180 DATA A,8,B,3,C,3,D,3,E,11,F,3,G,2,H,3,I,8,J,2,K,1,L,5,M,2,N,5,O,7,P,3,Q,1,R,5,S,5,T,6,U,3,V,2,W,3,X,1,Y,2,Z,1
190 '
200 'DATA FOR LETTER VALUES
210 '
220 DATA 1,3,3,4,1,5,6,5,1,8,7,5,6,6,1,3,9,4,3,4,1,8,7,9,6,9,0
230 '
240 'DATA FOR STARTING WORDS
250 '
260 DATA 15,COMPUTER,PRINTER,TANDY,RAINBOW,DISKETTE,MONITOR,JOYSTICK,CARTRIDGE,KEYBOARD,KILOBYTES,CASSETTE,MOUSE,INTERFACE,BAUDRATE,BASIC
270 GOTO1140
280 POKE65497,0:HCOLOR8,0:HCLS:LN=0:A$="PLEASE WAIT...":GOSUB1340:RESTORE:FORX=1TO15:FORY=1TO15:GB$(X,Y)="":NEXTY,X:FORT=1TO100:ZZ$(T)="":NEXTT
290 FORT=1TO26:READ A$,A:FORB=1TO A:GOSUB1740:GOSUB1780
300 X=RND(100):IFZZ$(X)<>""THEN300
310 ZZ$(X)=A$:GOSUB1830:NEXTB,T:FORT=1TO100:IFZZ$(T)=""THEN ZZ$(T)="*":NEXTT:ELSENEXTT
320 IFINKEY$<>""THEN320
330 NP=1:FORT=1TO27:READA:POKE4000+T,A:NEXTT:HCLS:LN=0:A$="HOW MANY PLAYERS?":GOSUB1340:HPRINT(10,5),"1 PLAYER":HPRINT(30,5),"2 PLAYERS":HPRINT(10,10),"3 PLAYERS":HPRINT(30,10),"4 PLAYERS"
340 IFBUTTON(0)<>0THEN340
350 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)<>1THEN350
360 IFPY<40OR PY>87OR PX<80OR PX>311THENSOUND1,1:GOTO350
370 IFPY<48THEN390:ELSEIFPY>79THEN NP=NP+2:GOTO390
380 SOUND1,1:GOTO350
390 IFPX<152THEN410:ELSEIFPX>239THEN NP=NP+1:GOTO410
400 SOUND1,1:GOTO350
410 LP=100:HCLS:FORPL=1TO NP:SC(PL)=0:PL$(PL)=STRING$(9,32):GOSUB1550:NEXTPL
420 '
430 'SELECT A STARTING WORD.
440 '
450 READ A:B=RND(A):FORA=1TO B:READA$:NEXTA
460 '
470 'INITIALIZE GAME BOARD
480 '
490 FORX=1TO15:FORY=1TO15
500 IFRND(0)>.07THEN520
510 GB$(X,Y)=MID$(STR$(RND(4)),2,1)
520 NEXTY,X:FORT=1TOLEN(A$):GB$(T+4,8)=MID$(A$,T,1):NEXTT
530 '
540 'START THE GAME
550 '
560 FORPL=1TO NP
570 '
580 'PUT GAME BOARD ON SCREEN
590 '
600 ONBRKGOTO1110:HCOLOR8,0:HSCREEN2:POKE&HFF9A,BD:HLINE(95,22)-(215,143),PSET,B
610 FORT=103TO214STEP8:HLINE(T,23)-(T,143),PSET:NEXTT:FORT=31TO143STEP8:HLINE(95,T)-(215,T),PSET:NEXTT
620 FORX=1TO15:FORY=1TO15:IFGB$(X,Y)<"1"OR GB$(X,Y)>"4"THEN640
630 A=VAL(GB$(X,Y)):IFA=1THENHPAINT(X*8+93,Y*8+20),3,8:ELSEIFA=2THENHPAINT(X*8+93,Y*8+20),7,8:ELSEIFA=3THENHPAINT(X*8+93,Y*8+20),2,8:ELSEHPAINT(X*8+93,Y*8+20),5,8
640 HCOLOR4:IF(GB$(X,Y)>"@"AND GB$(X,Y)<="Z")OR GB$(X,Y)="*"THENHPRINT(X+11,Y+2),GB$(X,Y)
650 NEXTY,X
660 HCOLOR2:HLINE(80,168)-(227,191),PSET,B:GOSUB1700:HLINE(0,0)-(319,15),PRESET,BF:HLINE(0,144)-(319,160),PRESET,BF
670 HCOLOR8:HPRINT(6,0),"PLAYER #"+MID$(STR$(PL),2)+":"+STR$(SC(PL))
680 HPRINT(6,1),"ENTER YOUR WORD OR CLICK TO PASS.":W$=""
690 HCOLOR1:HLINE(LEN(W$)*8+96,159)-(LEN(W$)*8+101,159),PSET:FORT=0TO25:NEXTT:HLINE(LEN(W$)*8+96,159)-(LEN(W$)*8+101,159),PRESET:FORT=0TO25:NEXTT:IFBUTTON(0)=1THEN WS=0:GOTO1040
700 A$=INKEY$:IFA$=""THEN690:ELSEIFA$=CHR$(13)ANDLEN(W$)>1THEN760:ELSEIFA$=CHR$(8)THEN730
710 IFLEN(W$)>14THEN690:ELSEIFA$="*"THEN720:ELSEIFA$<"A"OR A$>"Z"THEN690
720 HCOLOR6:HPRINT(12+LEN(W$),19),A$:W$=W$+A$:GOTO690
730 IFW$=""THEN690
740 W$=LEFT$(W$,LEN(W$)-1)
750 HLINE(96,152)-(300,159),PRESET,BF:HCOLOR6:IFW$=""THEN690:ELSEHPRINT(12,19),W$:GOTO690
760 HLINE(0,8)-(319,15),PRESET,BF:HCOLOR8:HPRINT(6,1),"FIND START POSITION OF WORD."
770 PX=INT(JOYSTK(0)/4.25)+1:PY=INT(JOYSTK(1)/4.25)+1
780 HGET(PX*8+88,PY*8+16)-(PX*8+95,PY*8+23),1:HCOLOR6:HLINE(PX*8+88,PY*8+16)-(PX*8+95,PY*8+23),PSET,BF:FORT=0TO10:NEXTT
790 HPUT(PX*8+88,PY*8+16)-(PX*8+95,PY*8+23),1
800 IFINKEY$=CHR$(12)THEN660:ELSEIFBUTTON(0)<>1THEN770
810 HLINE(0,8)-(319,15),PRESET,BF:HCOLOR8:HPRINT(6,1),"WHICH DIRECTION? (USE ARROW KEYS)"
820 DX=0:DY=0:A$=INKEY$:IFA$=CHR$(12)THEN660:ELSEIFA$=CHR$(9)THENDX=1:ELSEIFA$=CHR$(10)THENDY=1:ELSE820
830 TP$=PL$(PL):UL=0:AL=0:LX=PX:LY=PY:LW=1
840 IFGB$(LX,LY)=MID$(W$,LW,1)THEN AL=1:GOTO880
850 IFGB$(LX,LY)>"@"AND GB$<="Z"THEN920
860 I=INSTR(TP$,MID$(W$,LW,1)):IFI=0THEN920
870 MID$(TP$,I,1)=" ":UL=1
880 LX=LX+DX:LY=LY+DY:LW=LW+1:IFLW>LEN(W$)THEN910
890 IFLX<1OR LX>15OR LY<1OR LY>15THEN970
900 GOTO840
910 IFUL=0OR AL=0THEN920:ELSE980
920 HLINE(0,8)-(319,15),PRESET,BF:HCOLOR3:HPRINT(6,1),"LETTERS DO NOT MATCH!!!"
930 IFINKEY$<>""THEN930
940 IFBUTTON(0)<>0THEN940
950 IFINKEY$=""AND BUTTON(0)=0THEN950
960 GOTO660
970 HLINE(0,8)-(319,15),PRESET,BF:HCOLOR3:HPRINT(6,1),"WORD TOO LONG!!!":GOTO930
980 LX=PX:LY=PY:DW=0:TW=0:WS=0:PL$(PL)=TP$
990 FORT=1TOLEN(W$):GB$(LX,LY)=MID$(W$,T,1):P=HPOINT(LX*8+93,LY*8+20):HCOLOR6:HPRINT(LX+11,LY+2),GB$(LX,LY):L=ASC(MID$(W$,T,1))-64:IFL=-22THEN L=27
1000 WS=WS+PEEK(4000+L):IFP=3THEN TW=TW+1:ELSEIFP=7THEN DW=DW+1:ELSEIFP=2THEN WS=WS+PEEK(4000+L)+PEEK(4000+L):ELSEIFP=5THEN WS=WS+PEEK(4000+L)
1010 LX=LX+DX:LY=LY+DY:NEXTT
1020 IFDW>0THEN WS=WS*2:DW=DW-1:GOTO1020
1030 IFTW>0THEN WS=WS*3:TW=TW-1:GOTO1030
1040 HCOLOR8:SC(PL)=SC(PL)+WS:HLINE(0,0)-(319,15),PRESET,BF:HPRINT(6,0),"PLAYER #"+MID$(STR$(PL),2)+":"+STR$(SC(PL))
1050 HPRINT(6,1),"SCORE FOR THIS WORD:"+STR$(WS)
1060 HPRINT(11,18),"CLICK TO CONTINUE."
1070 IFBUTTON(0)<>0THEN1070
1080 IFBUTTON(0)=0THEN1080
1090 NEXT PL:FORPL=1TO3:IFINSTR(PL$(PL)," ")<>0THENGOSUB1550
1100 NEXTPL:GOTO540
1110 HCOLOR8,0:HSCREEN2:POKE&HFF9A,BD:FORT=1TO NP:HPRINT(10,T*2),"PLAYER #"+MID$(STR$(T),2)+":"+STR$(SC(T)):NEXTT
1120 A$="CLICK BUTTON FOR MENU.":LN=23:GOSUB1340
1130 GOTO1310
1140 POKE65497,0:ONBRKGOTO1140:HCOLOR8,0:HSCREEN2:POKE&HFF9A,BD:HPRINT(1,0),"ABOUT ADD-ON  PLAY GAME  COLORS  BASIC"
1150 HLINE(0,8)-(319,8),PSET:HLINE(111,0)-(111,8),PSET:HLINE(199,0)-(199,8),PSET:HLINE(263,0)-(263,8),PSET
1160 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)=0THEN1160
1170 IFPY>8THENSOUND1,1:GOTO1160
1180 IFPX<111THEN1210:ELSEIFPX<199THEN280:ELSEIFPX<263THEN1350
1190 POKE65496,0:POKE113,0:EXEC(PEEK(&HFFFE)*256+PEEK(&HFFFF))
1200 NEW
1210 HCLS:A$="ADD-ON WORD GAME":LN=2:GOSUB1340:A$="(C) MARCH 29, 1989 BY":GOSUB1340:A$="JOEL MATHEW HEGBERG":GOSUB1340
1220 HPRINT(3,16),"ANY QUESTIONS ABOUT THIS PROGRAM MAY":HPRINT(3,17),"BE DIRECTED TO THE AUTHOR:":HPRINT(3,19),"Joel Mathew Hegberg":HPRINT(3,20),"936 North Twelfth Street":HPRINT(3,21),"De Kalb, Illinois  60115"
1230 LN=23:A$="CLICK TO CONTINUE.":GOSUB1340
1240 IFBUTTON(0)=1THEN1240
1250 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)=0THEN1250
1260 HCLS:A$="INDIVIDUAL LETTER SCORES":LN=1:GOSUB1340
1270 RESTORE:FORT=1TO26:READA$,A:NEXTT:FORT=1TO26
1280 READA:IFT/2<>INT(T/2)THENHPRINT(3,LN),CHR$(T+64)+"="+STR$(A)+" POINT(S)":ELSEHPRINT(23,LN),CHR$(T+64)+"="+STR$(A)+" POINT(S)":LN=LN+1
1290 NEXTT:HPRINT(13,LN),"*= 0 POINT(S)"
1300 A$="CLICK TO RETURN.":LN=23:GOSUB1340
1310 IFBUTTON(0)<>0THEN1310
1320 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)<>1THEN1320:ELSE1140
1330 GOTO1330
1340 HPRINT(20-INT(LEN(A$)/2),LN),A$:LN=LN+2:RETURN
1350 HCLS:A$="CLICK HERE TO RETURN":LN=0:GOSUB1340:HLINE(0,8)-(319,8),PSET
1360 HCOLOR8:FORT=0TO8:HLINE(150,20+T*12)-(170,30+T*12),PSET,B:NEXTT
1370 FORT=0TO8:HPAINT(160,21+T*12),T,8:NEXTT
1380 HPRINT(17,16),"BOARDER":HPRINT(19,17),"CMP":HPRINT(19,18),"RGB"
1390 GOSUB1740:GOSUB1780:GOSUB1830:IFBUTTON(0)=0THEN1390
1400 IFPY<12THEN1140
1410 IFPY>126THEN1460
1420 IFPX<150OR PX>170THENSOUND1,1:GOTO1390
1430 P=PEEK(&HFFB0+INT((PY-20)/12)):IFP>63THEN P=P-64
1440 P=P+1:IFP>63THEN P=0
1450 POKE&HFFB0+INT((PY-20)/12),P:GOTO1390
1460 IFPY>151THENSOUND1,1:GOTO1390:ELSEIFPY<136THEN1500
1470 IFPX<152OR PX>175THENSOUND1,1:GOTO1390
1480 IFPY<144THENCMP:BD=0:POKE&HFF9A,0:GOTO1390
1490 POKE&HFF9A,0:BD=0:RGB:GOTO1390
1500 IFPX<136OR PX>191THENSOUND1,1:GOTO1390
1510 BD=BD+1:IFBD>63THEN BD=0
1520 POKE&HFF9A,BD:GOTO1390
1530 GOTO1530
1540 '
1550 'USER LETTER SELECTION
1560 '
1570 IFLP<1THENRETURN:ELSEHCOLOR8,0:HSCREEN2:POKE&HFF9A,BD:HPRINT(9,0),"PLAYER #"+STR$(PL):HPRINT(9,1),"SELECT YOUR TILE(S).":HCOLOR2:HLINE(80,168)-(227,191),PSET,B:GOSUB1700
1580 FORX=0TO9:FORY=0TO9:IFZZ$(X*10+Y+1)<>""THEN HCOLOR4,0:HLINE(50+X*22,30+Y*13)-(50+X*22+15,30+Y*13+9),PSET,BF
1590 NEXTY,X
1600 IFLP<1THENRETURN:ELSEGOSUB1740:GOSUB1780:GOSUB1830
1610 IFBUTTON(0)<>1THEN1600
1620 IFPX<50OR PX>263OR PY<30OR PY>156THEN SOUND1,1:GOTO1600
1630 GX=INT((PX-50)/22):GY=INT((PY-30)/13):IFHPOINT(50+GX*22,30+GY*13)<>4THENSOUND1,1:GOTO1600
1640 HLINE(50+GX*22,30+GY*13)-(50+GX*22+15,30+GY*13+9),PRESET,BF
1650 IFBUTTON(0)<>0THEN1650
1660 LT$=ZZ$(GX*10+GY+1):ZZ$(GX*10+GY+1)="":FORT=1TO9:IFMID$(PL$(PL),T,1)=" "THENMID$(PL$(PL),T,1)=LT$:ELSENEXTT
1670 LP=LP-1:GOSUB1700:FORT=1TO9:IFMID$(PL$(PL),T,1)=" "THEN1600
1680 NEXTT:FORT=0TO800:NEXTT:RETURN
1690 END
1700 FORT=1TO9:IFMID$(PL$(PL),T,1)=" "THEN1720
1710 HCOLOR4:HLINE((T+4)*16+4,170)-((T+4)*16+16,189),PSET,BF:HCOLOR8:HPRINT(T*2+9,22),MID$(PL$(PL),T,1)
1720 NEXTT:RETURN
1730 '
1740 'GET POINTER LOCATION
1750 '
1760 PX=JOYSTK(0)*5:PY=JOYSTK(1)*3:RETURN
1770 '
1780 'PUT POINTER ON SCREEN.
1790 '
1800 HGET(PX,PY)-(PX+5,PY+5),1
1810 HCOLOR8:HLINE(PX,PY)-(PX+5,PY+5),PSET:HLINE(PX+4,PY)-(PX,PY),PSET:HLINE-(PX,PY+4),PSET:RETURN
1820 '
1830 'ERASE POINTER
1840 '
1850 HPUT(PX,PY)-(PX+5,PY+5),1:RETURN
