0 ' COPYRIGHT 1989  FALSOFT,INC
50 FILES 3,1000
100 CLEAR 750:DIM LI$(7):SS$=CHR$(127)
150 CLS0:PRINT @0,"--- MONEY MANAGER DATA BASE ---a
200 LI$(1)="1= ADD INFORMATION
203 LI$(2)="2= END SESSION
210 SL=128:NL=2:AD=0:GOSUB 9000
250 ON A GOTO 320,260
260 PRINT @385,STRING$(30,"*");
262 PRINT @417,"SESSION IS OVER - BYE FOR NOW ";
265 PRINT @449,STRING$(30,"*");
266 FOR I=1 TO 1800:NEXT I:CLS:END
320 CLS0:PRINT "--- ADDING INFO TO DATABASE ---b"
321 AD=1:LI$(1)="1= ENTER A CHECK
330 LI$(2)="2= RETURN TO THE FIRST MENU a
340 LI$(3)="3= END THIS SESSION":NL=3
345 SL=128:GOSUB 9000:ON A GOTO 370,150,260
370 CLS0:PRINT @0,"-----  CHECK INFORMATION ------c
375 GOSUB 380:GOSUB 400:GOSUB 406:GOSUB 410:GOSUB 414:GOTO 430
380 P$="1= ENTER THE MONTH:  1-12":LV=1:HV=12:SL=64
390 VT$="N":GOSUB 9020:MM$=RIGHT$("0"+VA$,2):RETURN
400 P$="2= ENTER THE DAY: 1-31":LV=1:HV=31:SL=128
402 VT$="N":GOSUB 9020:DD$=RIGHT$("0"+VA$,2):RETURN
406 P$="3= ENTER CHECK NUMBER: 1000-9999":LV=1000:HV=9999
408 SL=192:VT$="N":GOSUB 9020:CN$=VA$:RETURN
410 P$="4= ENTER THE AMOUNT: NNNNNN.NN":LV=1.00:HV=999999.99
412 VT$ ="D":SL=256:GOSUB 9020
413 AM$=LEFT$(VA$,LEN(VA$)-3)+RIGHT$(VA$,2):RETURN
414 P$="5= ENTER WHO CHECK WAS PAID TO":SL=320:VT$="A
420 HV=31:GOSUB 9020:CP$=VA$:RETURN
430 P$="6= ENTER WHAT CHECK WAS FOR":SL=384:VT$="A
440 HV=58:GOSUB 9020:CF$=VA$
470 PRINT @0,"---  FINAL O.K.  OR CHANGE ----d
480 PRINT @32,">TYPE 'Y' IF ALL ITEMS ARE O.K.<";
481 FOR I = 1 TO 220
482 A$=INKEY$:IF A$ <> "" GOTO 490 ELSE NEXT I
484 PRINT @32," OR TYPE LINE NUMBER TO CHANGE  ";:FOR I=1 TO 300
486 A$=INKEY$:IF A$ <> "" GOTO 490 ELSE NEXT I:GOTO 480
490 IF A$ = "Y" GOTO 520
500 A = VAL(A$):IF A > 0 AND A <7 GOTO 510 ELSE GOTO 470
510 ON A GOSUB 380,400,406,410,414,430:GOTO 480
520 CLS0:PRINT "------- ADDING THE CHECK ------e"
522 PRINT "     P L E A S E    W A I T":OK$= MM$+DD$+"C"+CN$
524 WR$ = OK$+"*"+AM$+SS$+CP$+SS$+CF$
526 GOSUB 9070:GOSUB 9100:CLS0
530 IF GE =1 THEN PRINT "----  CHECK WAS ADDED    ------f" ELSE PRINT "** DUPLICATE CHECK NOT ADDED **d
540 LI$(1)="1= ADD ANOTHER CHECK
550 LI$(2)="2= RETURN TO THE FIRST MENU a
557 LI$(3)="3= END THIS SESSION RIGHT NOW
560 SL=96:NL= 3:GOSUB 9000:ON A GOTO 370,150,260
9000 FOR I= 1 TO NL:PRINT @SL,LI$(I)
9001 SL= SL+32:NEXT I
9005 PRINT @32,"*   SELECT FROM THE FOLLOWING
9006 FOR I = 1 TO 200
9007 A$ = INKEY$:IF A$ <> "" GOTO 9012 ELSE NEXT I
9008 PRINT @32," *":FOR I = 1 TO 65
9009 A$ = INKEY$:IF A$ <> "" GOTO 9012 ELSE NEXT I
9010 GOTO 9005
9012 A = VAL(A$):IF A > 0 AND A < NL+1 THEN RETURN
9013 GOTO 9005
9020 PRINT @SL,P$ :PRINT @SL+32," " 'PROMPT
9024 PRINT @SL+32,""; 'REPOSITION
9026 LINE INPUT "> ";VA$ 'INPUT
9028 LA= LEN(VA$):IF VT$ = "D" GOTO 9044
9030 IF VT$ = "N" GOTO 9036
9032 IF LA > HV GOTO 9020
9034 RETURN 'VARIABLE IS IN RANGE
9036 VV= VAL(VA$):IF VV < LV OR VV > HV GOTO 9020
9038 IF VT$="D" GOTO 9034
9039 IF RIGHT$(VA$,1)< "0" OR RIGHT$(VA$,1)> "9" GOTO 9020
9040 IF VV <> INT(VV) GOTO 9020 ELSE GOTO 9034
9044 IF LA > 9 OR LA < 3 GOTO 9020
9046 IF MID$(VA$,LA-2,1) <> "." GOTO 9020
9048 GOTO 9036
9070 IF DD$ > "15" THEN DF$ = "15" ELSE DF$ = "01
9072 SG$= "M"+ MM$+ "D"+ DF$+ "/CHK"
9096 CLOSE:OPEN "I",#1,SG$:OPEN "O",#2,"WORK/CHK":RETURN
9100 IF EOF(1) = -1 GOTO 9130
9105 INPUT #1,LI$:IK$ = LEFT$(LI$,9)
9110 IF IK$< OK$ THEN WRITE #2,LI$ ELSE GOTO 9160
9120 GOTO 9100
9130 ON RA GOTO 9132:ON AD GOTO 9131,9134,9134
9131 WRITE #2,WR$
9132 RA=0:CLOSE:KILL SG$:COPY "WORK/CHK" TO SG$:GE=1:RETURN
9134 RA=0:GE=2:CLOSE:RETURN
9160 IF IK$ > OK$ GOTO 9200: 'INPUT >=OUTPUT
9162 ON AD GOTO 9134,9170,9170  'INPUT =OUTPT
9170 GE=1:RETURN 'CHANGE
9200 ON AD GOTO 9202,9220,9220: 'INPUT > OUTPUT
9202 ON RA GOTO 9210:WRITE #2,WR$:WRITE #2,LI$:RA=1:GOTO 9100
9210 WRITE #2,LI$:GOTO 9100
9220 ON RA GOTO 9210:GOTO 9134
