1 '****************************
2 '*     MICRO-DECISION       *
3 '*  COPYRIGHT  (C) 1984 BY  *
4 '* ROBERT ROGERS & RTR*SOFT *
5 '*  VERSION 1.0  JUNE 1984  *
6 '* FOR 4K COLOR BASIC COCOS *
7 '****************************
10 CLEAR10000:CLS
30 DIMA$(21),B(21),P(21),P$(21),T(21)
50 CLS:PRINTTAB(9)"MICRO DECISION":PRINT"ENTER THE ALTERNATIVES THAT ARE UNDER CONSIDERATION. <ENTER>    WILL TERMINATE ENTRY OF ITEMS.":PRINTSTRING$(32,143+32);:SOUND200,1
60 C=0
70 C=C+1
80 PRINT"ALTERNATIVE"C;:INPUTA$(C):IFA$(C)=""THENC=C-1:GOTO100
90 SOUND225,1:GOTO70
100 CLS:Z=0:FORT=1TO C:Z=Z+1:PRINTA$(T):IFZ>12 OR T=C THENINPUT"INCORRECT ITEM ";Z$:IFZ$=""THENZ=0:CLS:GOTO110:ELSEINPUT"CORRECTED: ";ZZ$:FORY=1TOC:IFZ$=A$(Y)THENA$(Y)=ZZ$:Z=0:CLS:GOTO100:ELSENEXTY:SOUND1,3:GOTO100
110 NEXTT
130 CLS:PRINT"     TESTING OF PREFERENCES
150 FORI=2TOC
160 FORJ=1TOI-1
180 PRINT"WHICH ALTERNATIVE DO YOU PREFER:"
190 PRINTSTRING$(32,CHR$(143+96))
200 PRINT@192,"<1> "A$(I)
210 PRINTTAB(5)"OR"
220 PRINT"<2> "A$(J)
230 PRINT:INPUT"TYPE CHOICE:";W:IFW<>1ANDW<>2THENCLS:GOTO180:ELSESOUND200,1:CLS
240 IFW=1THENT(I)=T(I)+1
250 IFW=2THENT(J)=T(J)+1
260 NEXTJ,I
280 FORN=1TOC:T(N)=T(N)+1:NEXTN
290 FORF=1TOC
300 L=0:V=0
310 FORN=1TOC
320 IFT(N)>V THEN L=N:V=T(N)
330 NEXTN
340 P(F)=L:H(P(F))=T(L)
350 T(L)=0
360 NEXTF
370 CLS:PRINT"HERE ARE THE ALTRNATIVES RANKED ON THE BASIS OF YOUR CHOICES..."
380 PRINTSTRING$(32,143+112);
390 PRINT"RANK    ALTERNATIVE
400 F=0:R=0:FORN=1TOC:F=F+1
410 IFF=12 AND C>12 THENPRINT"<ENTER> TO CONTINUE":EXEC44539:F=0
420 IFH(P(N))=H(P(N-1))THENPRINTR;"TIE";:ELSER=R+1:PRINTR;
425 PRINTTAB(9);A$(P(N))
430 NEXTN
440 PRINT"<ENTER> WHEN READY":EXEC44539
450 RUN
