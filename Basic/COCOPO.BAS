1 'COCO PO
2 'BY ROGER F. KRUEGER
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 'WITH THANKS TO WILLIAM BOSTON
7 'FOR HIS PROGRAM IN
8 'THE RAINBOW - AUG 82, PG 19.
20 'THIS PROGRAM IS FOR THE
21 'COCO 3 USING AN RGB MONITOR.
40 ' BY ROGER F. KRUEGER
60 ' 14 BANK STREET
80 ' DANSVILLE, NY  14437
85 ' (716) 335-5611
100 'MAY 11, 1987
120 'FILENAME: COCOPO
140 WIDTH80
160 PALETTE RGB:PALETTE 1,0:CLS 2:ATTR 5,1
180 CLEAR 2000
200 GOSUB 3040
220 CLS
240 LOCATE 0,0:PRINTSTRING$(80,"%")
260 LOCATE 0,1:PRINT"%%                                                                            %%"
280 LOCATE 0,2:PRINT"%%                    + + +  Purchase Order  + + +                            %%"
300 LOCATE 0,3:PRINT"%%                                                                            %%"
320 LOCATE 0,4:PRINTSTRING$(80,"%")
340 LOCATE 15,8:PRINT"1. Enter Data              2. Load Data"
360 LOCATE 15,10:PRINT"3. Hardcopy                4. Display
380 LOCATE 15,12:PRINT"5. Quit                     
400 LOCATE 28,15:PRINT"Select 1-5"
420 SOUND 200,1
440 GOSUB3000
460 IF VAL(K$)<1 OR VAL(K$)>5 THEN 220
480 ON VAL(K$) GOSUB 520,1220,1400,2200,2900
500 GOTO 220
520 CLS:T=0
540 LOCATE 0,0:LINEINPUT"P.O. # ";PP$:LOCATE 50,0:LINEINPUT"Date: ";D$
560 LOCATE 2,2:LINEINPUT"Company Name: ";A1$
580 LOCATE 2,3:LINEINPUT"Street: ";A2$
600 LOCATE 2,4:LINEINPUT"City, State, Zip: ";A3$
620 L1$="Qty":L2$="Description":L3$="Unit Price":L4$="Total Price"
640 LOCATE0,6
660 ATTR5,1,U:PRINTUSINGF$;L1$,L2$,L3$,L4$
680 ATTR5,1
700  N= 8
720 LOCATE 2,22:LINEINPUT"# of Entries ";Z$
740 FOR X=1 TO VAL(Z$):N=N+1
760 LOCATE0,N:LINEINPUTA$(Y):LOCATE6,N:LINEINPUTB$(Y):LOCATE50,N:LINEINPUTC$(Y):LOCATE63,N:PRINTUSING"$#####.##"; VAL(A$(Y))*VAL(C$(Y))
780 T=T+VAL(A$(Y))*VAL(C$(Y))
800 Y=Y+1
820 NEXT X
840 LOCATE 50,19:LINEINPUT"Shipping           ";SH$
860 LOCATE 25,20:LINEINPUT"Sales Tax (Y/N)? ";S$
880 IF S$="N" OR S$="n" THEN 940 ELSE 900
900 L=.07:LOCATE 50,20:PRINT"Sales Tax     ";:ATTR5,1,U:PRINTUSING"$#####.##";L*T
920 ATTR5,1
940 LOCATE 50,22:PRINT"Total         ";:PRINTUSING"$#####.##";(T*L)+T+VAL(SH$)+VAL(ST$)
960 LOCATE 25,22:LINEINPUT"Note (Y/N)? ";P$
980 IF P$="Y" OR P$="y" THEN 1000 ELSE 1020
1000 LOCATE0,23:LINEINPUT"Note ";PS$
1020 GOSUB2640
1040 CLS
1060 LOCATE15,12:LINEINPUT"  +  +  +  Save to Disk  (Y/N)?  +  +  +";SD$
1080 IF SD$="Y" OR SD$="y" THEN 1100 ELSE RETURN
1100 LOCATE 15,14:LINEINPUT"Filename: ";F1$
1120 OPEN "O",#1,F1$
1140 FOR X=0 TO VAL(Z$)-1
1160 WRITE#1,PP$,T,L,D$,A1$,A2$,A3$,Z$,A$(X),B$(X),C$(X),SH$,PS$,PY$
1180 NEXT X
1200 CLOSE:RETURN
1220 CLS
1240 SOUND 200,1:LOCATE 20,5:LINEINPUT"Filename: ";F1$
1260 OPEN "I",#1,F1$
1280 X=0
1300 IF EOF(1)=-1 GOTO 1380
1320 INPUT#1,PP$,T,L,D$,A1$,A2$,A3$,Z$,A$(X),B$(X),C$(X),SH$,PS$,PY$
1340 X=X+1
1360 GOTO 1300
1380 CLOSE:GOTO 2200
1400 CLS:T=0
1420 ATTR 5,1,B
1440 LOCATE 18,11:PRINT"Printing, please wait . . ."
1460 ATTR 5,1
1480 PRINT#-2,TAB(5)"Purchase Order #";PP$
1500 PRINT#-2:PRINT#-2
1520 PRINT#-2,TAB(50)D$
1540 PRINT#-2,TAB(5)A1$
1560 PRINT#-2,TAB(5)A2$
1580 PRINT#-2,TAB(5)A3$
1600 FOR X=1 TO 3:PRINT#-2:NEXT:PRINT#-2,TAB(5)"Dear Sirs:":FOR X=1 TO 2:PRINT#-2:NEXT
1620 PRINT#-2,TAB(5)"Please ship the items listed as soon as possible."
1640 PRINT#-2
1660 GOSUB 3040
1680 PRINT#-2,TAB(5)CHR$(15);:PRINT#-2,USINGF$;L1$,L2$,L3$,L4$
1700 PRINT#-2,CHR$(14)
1720 FOR X=0 TO VAL(Z$)-1
1740 PRINT#-2,USINGF2$;VAL(A$(X)),B$(X),VAL(C$(X)),VAL(A$(X))*VAL(C$(X))
1760 GOSUB2860
1780 NEXT X
1800 PRINT#-2:PRINT#-2
1820 PRINT#-2,TAB(40)"Shipping         ";:PRINT#-2,USING"          $####.##";VAL(SH$)
1840 IF L*T=0 THEN 1880
1860 PRINT#-2,TAB(40)"Sales Tax                  ";:PRINT#-2,CHR$(15);:PRINT#-2,USING"$####.##";L*T
1880 PRINT#-2,CHR$(14)
1900 PRINT#-2,TAB(40)"Total";:PRINT#-2,USING"                      $####.##";(T*L)+T+VAL(SH$)+VAL(ST$)
1920 PRINT#-2
1940 PRINT#-2,TAB(5)"Thank you."
1960 PRINT#-2
1980 IF PY$="C" OR PY$="c" THEN GOSUB 2740
2000 IF PY$="M" OR PY$="m" THEN GOSUB 2780
2020 IF PY$="V" OR PY$="v" THEN GOSUB 2820
2040 PRINT#-2:PRINT#-2
2060 PRINT#-2,TAB(50)"Your Name" 
2080 PRINT#-2,TAB(50)"Your Address"
2100 PRINT#-2,TAB(50)"Your City, State, Zip"
2120 PRINT#-2,TAB(50)"Your Phone #, if you wish."
2140 PRINT#-2
2150 IF PS$="" THEN 2180 
2160 PRINT#-2,TAB(5)"Note: ";PS$
2180 RETURN
2200 CLS:T=0
2220 LOCATE 5,0:PRINTPY$;:PRINT"                 Purchase Order #: "PP$
2240 LOCATE 50,0:PRINTD$
2260 LOCATE 5,2:PRINTA1$
2280 LOCATE 5,3:PRINTA2$
2300 LOCATE 5,4:PRINTA3$
2320 LOCATE 0,6:PRINTTAB(5);:ATTR5,1,U:PRINTUSINGF$;L1$,L2$,L3$,L4$
2340 ATTR5,1
2360 N=8
2380 FOR X=0 TO VAL(Z$)-1:N=N+1
2400 LOCATE0,N
2420 PRINTUSINGF2$;VAL(A$(X)),B$(X),VAL(C$(X)),VAL(A$(X))*VAL(C$(X))
2440 GOSUB 2860
2460 NEXT X
2480 LOCATE 50, 18:PRINT"Shipping";:PRINTUSING"         $####.##";VAL(SH$)
2500 IF L*T=0 THEN 2540
2520 L=.07:LOCATE 50,19:PRINT"Sales Tax        ";:ATTR5,1,U:PRINTUSING"$####.##";L*T
2540 ATTR5,1
2560 LOCATE 50,21:PRINT"Total       ";:PRINTUSING"     $####.##";(T*L)+T+VAL(SH$)+VAL(ST$)
2580 LOCATE 0,22:PRINT"R    Note: ";PS$
2600 GOSUB 3000
2620 IF K$="R" OR K$="r" OR K$="Y" OR K$="y" THEN 220 ELSE 2200
2640 CLS
2660 LOCATE 18,5:PRINT"+  +  +  Method of Payment  +  +  +"
2680 LOCATE 8,8:PRINT"(C) Check  (M)  MasterCard  (V)  VISA "
2700 LOCATE 9,10:LINEINPUT PY$
2720 RETURN
2740 PRINT#-2,TAB(5)"I am enclosing a check to cover the amount."
2760 RETURN
2780 PRINT#-2,TAB(5)"Marine Midland MasterCard  XXXX XXXX XXXX XXXX  Expires XX/XX"
2800 RETURN
2820 PRINT#-2,TAB(5)"BankOne VISA  XXXX XXXX XXXX XXXX  Expires XX/XX"
2840 RETURN
2860 T=T+VAL(A$(X))*VAL(C$(X))
2880 RETURN
2900 CLS:LOCATE 18,11:LINEINPUT"Are you sure (Y/N)?";K$
2905 SOUND 225,2
2910 IF K$="Y" OR K$="y" THEN 2920 ELSE RETURN
2920 PALETTERGB:CLS1:POKE113,0:EXEC40999
3000 K$=INKEY$:IF K$="" THEN 3000
3020 RETURN
3040 L1$="Qty":L2$="Description":L3$="Unit Price":L4$="Total Price"
3060 F$="%   %%                                       %   %          % %          %
3080 F2$="     ###   %                                        % $####.##     $####.##"
3100 RETURN
