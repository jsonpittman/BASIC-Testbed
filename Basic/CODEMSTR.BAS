0 ' COPYRIGHT 1989 FALSOFT,INC
10 CLS:HL=32:VL=8:FOR TM=0 TO 66 STEP 33:GOSUB 920:HL=HL-2:VL=VL-1:NEXT
20 PRINT@105,"CODEMASTER I";:X=1123:X1=25:GOSUB 900
30 PRINT@135,"BY RAY ONLEY 8/88";:X=1155:GOSUB 900
40 TM=297:HL=14:VL=3:GOSUB 920:PRINT@330,"LOADING DATA";
50 CLEAR5000:DIM W$(200),L$(48),W1$(37),C$(15),C1$(60)
60 FOR X=1 TO 47:READ L$(X):NEXT
70 FOR X=1 TO 36:READ W1$(X):NEXT
80 FOR X=1 TO15:READ C$(X):NEXT
90 FOR X=1 TO 7:READ W(1,X),W(2,X),W(3,X):NEXT
100 FORX=1TO 58:READ C1$(X):NEXT
110 FOR X=1 TO 7:READ T$(X):NEXT
120 T1=1:T2=3:P1=4:P2=28:P3=75:PLAY"O3":B$=CHR$(128):N=1:WPM=13:U=16:U2=2:T$="A"
130 PRINT@416,;:INPUT"KEYBOARD OR JOYSTICK (K OR J)";K$
140 IF K$="K" THEN KJ=1
150 'MAIN MENU
160 CLS:PRINTB$B$"command"B$"menu"B$B$
170 PRINT STRING$(16,239)
180 PRINT"  TAKE   TEST":PRINT"  CHANGE SPEED
190 PRINT"  CHANGE TEST":PRINT"  OPTIONS
200 TM=17:HL=15:VL=6:GOSUB 920
210 PRINT@51,"SPEED ="WPM;
220 PRINT@83," TEST MODE:";:PRINT@115,LEFT$(T$(N),11);
230 PRINT@147,"CONTROL:";:IF KJ=1 THEN PRINT"KEY"; ELSE PRINT"JOY";
240 J1=14:J3=1088:X1=15:GOSUB 1670
250 ON L+1 GOTO 970,1500,1750,1930
260 'GENERATE RND CHARACTORS
270 FOR Y=1 TO K:W$(Y)="     ":FOR X=1 TO 5
280 R=RND(90):IF R<44 THEN 280
290 IF R>57 AND R<65 THEN 280
300 MID$(W$(Y),X,1)=CHR$(R):NEXT X,Y
310 'TEXT-TO-CODE ROUTINE
320 TM=352:HL=32:VL=3:GOSUB 920:PRINT@385,"PRESS ANY KEY TO START TEST...";:EXEC 44539:GOSUB960
330 GOSUB 960:TM=228:HL=12:VL=3:GOSUB 920:PRINT@261,"TESTING...";
340 IF RT=0 THEN T$="A"
350 SF=(SS-ES)/K
360 OM=((SS+ES)/2)*.06
370 IF DL=1 THEN TM=352:HL=32:VL=3:GOSUB 920
380 IF F=3 THEN 1230
390 IF F=2 THEN U=SS*OM:U2=INT(50/U)
400 FOR V=1 TO K:IF DS=1 OR IS=1 THEN U=U-SF:U2=INT(50/U)
410 IF F=2 THEN PRINT@58,INT(U/OM);
420 IF RT=1 THEN T$=CHR$(64+RND(7))
430 PRINT@385,STRING$(29,32);
440 IF DL=1 THEN PRINT@385,LEFT$(W$(V),29);:IF LEN(W$(V))>29 THENF=7
450 FOR Y=1 TO LEN(W$(V))
460 IF Y>29 THEN O=Y-29 ELSEO=Y
470 IF DL=1 AND PEEK(O+1408)<64 THEN AN=64 ELSE AN=-64
480 IF SL=1 OR Y=1 THEN SL=0:GOTO 500
490 IF DL=1 THEN POKE O+1407,Q
500 IF DL=1 THEN Q=PEEK(O+1408):POKE O+1408,Q+AN
510 IF DL=1 AND Y=29 THEN PRINT@385,STRING$(29,32);:PRINT@385,MID$(W$(V),30,29);:SL=1
520 A$=INKEY$:IF A$=CHR$(12) THEN 160 
530 IF A$="P" THEN EXEC 44539
540 N$=MID$(W$(V),Y,1)
550 IF N$="" THEN 650
560 A=ASC(N$)
570 IF A>97 AND A<123 THEN A=A-32
580 IF A<45 OR A>90 THEN 650
590 N1$=L$(A-43)
600 FOR X=1 TO LEN(N1$)
610 N2$=MID$(N1$,X,1)
620 IF N2$="Z" THEN PLAY"L"+STR$(INT(U*T1))+T$:GOTO640
630 PLAY"L"+STR$(INT(U*T2))+T$
640 FORT=1TOP1*U2:NEXT T,X
650 IF F=1 THEN RETURN
660 FOR T=1 TO P2*U2:NEXT T,Y
670 IF F=3 THEN FOR T=1 TO 450:NEXT:RETURN
680 FOR T=1 TO P3*U2:NEXT T,V
690 IF F=2 THEN WPM=ES
700 PRINT@352,STRING$(100,32)
710 TM=353:HL=30:VL=3:GOSUB 920:PRINT@386,"PRESS ANY KEY FOR ANSWERS...";:EXEC 44539
720 IF N=3 OR N=7 THEN 1550
730 FOR LY=1 TO INT(K/15)-1
740 CLS:FOR X=1 TO 30:Z1=(LY-1)*30+X:PRINT STR$(Z1);") "W$(Z1),:NEXT:EXEC 44539:NEXT:GOTO 160
750 GOTO 50
760 'DATA
770 DATA ZZXXZZ,,XZXZXZ,ZXXZX,ZZZZZ,XZZZZ,XXZZZ,XXXZZ,XXXXZ,XXXXX,ZXXXX,ZZXXX,ZZZXX,ZZZZX,ZZZXXX,ZXZXZX,ZXZZX,ZXXXZ,ZXZZXZ,XXZZXX,,XZ,ZXXX,ZXZX,ZXX,X,XXZX,ZZX,XXXX,XX,XZZZ,ZXZ,XZXX,ZZ,ZX,ZZZ,XZZX,ZZXZ,XZX,XXX,Z,XXZ,XXXZ,XZZ,ZXXZ,ZXZZ,ZZXX
780 DATA CQCQ,CQDX,CQTEST,CQCHESS,QST,QRZ?,QRT,QRX,PSEQSY,PSEQSL,QRL?,RST479,NAME,OP,HR,QTH,RIG,RIG1TTW,ANT,WX,QSL,73,88,CUL,CUAGN,SK,HW?,BK,KN,CL,FB,PSEQRS,QRN,QRM,QSB,RST5NN
790 DATA AKRON OH,MONTGOMERY AL,ALBANY GA,NEW HAVEN CN,COLUMBIA SC,SPRINGFIELD MS,GRAND RAPIDS MI,FLAGSTAFF AZ,MESA AZ,OWENSBORO KY,CINCINNATI OH,NASHVILLE TN,DAYTONA BEACH FL,MIAMI FL,INDIANAPOLIS IN
800 DATA 3,2,8,5,4,5,7,7,4,10,14,4,13,16,2,17,20,1,21,26,1
810 DATA K,A,N,W,KB,KA,WA,AA,AB,AC,KC,WB,CE,CO,CP,CT,DL,DK,EA,EI,F,G,GN,GW,GI,HA,HB,HC,HK,I,IK,JA,LA,LU,LZ,OA,OE,OH,OK,ON,OZ,PA,PP,PY,SM,SP,SV,TF,UA,UB,VE,VK,VP,XE,YN,YU,YV,YO
820 DATA RANDOM CHARACTORS,RANDOM CALLSIGNS,TRANSLATE DATA,CHARACTOR HELP,CHARACTOR TEST,QSO HELP,CONTEST SIMULATION
830 'KEYBOARD ROUTINE
840 F=1:Z=1:Y=1
850 TM=321:HL=30:VL=4:GOSUB920:PRINT@354,"PRESS KEY TO HEAR CHARACTOR";:PRINT@389,"PRESS CLEAR TO EXIT";
860 W$=INKEY$:IF W$="" THEN 860
870 IF W$=CHR$(12) THEN F=0:GOTO160
880 N$=W$:GOSUB 560
890 GOTO 860
900 POKE X,128:FOR Z=X+1 TO X+X1:POKE Z,PEEK(Z)-64:NEXT:RETURN
910 'WINDOWING ROUTINE
920 CN=RND(8)*16-16:VL=VL-1
930 PRINT@TM,CHR$(142+CN);:PRINTSTRING$(HL-2,140+CN);:PRINTCHR$(141+CN);
940 FOR X=1 TO VL:Z=TM+X*32:PRINT@Z,CHR$(138+CN);:PRINT@Z+HL-1,CHR$(133+CN);:NEXT
950 PRINT@TM+32*VL,CHR$(139+CN);STRING$(HL-2,131+CN);:PRINTCHR$(135+CN);:RETURN
960 FOR X=0 TO VL:PRINT@TM+32*X,STRING$(HL,32);:NEXT:RETURN
970 FOR ZZ=1 TO K:W$(ZZ)="":NEXT
980 IF N=4 THEN 840 ELSE IF N=3 THEN 1030 ELSE TM=358:HL=18:VL=3:GOSUB 920
990 PRINT@391,"TEST LENGTH: ";:LINEINPUT K$:IF K$="" THEN 160 ELSE K=VAL(K$)
1000 IF N=5 THEN 1030
1010 TM=358:HL=20:VL=3:GOSUB920:PRINT@391,"GENERATING TEST...";
1020 IF F=2 THEN PRINT@58,SS;
1030 ON N GOTO 260,1050,1120,840,2180,1200,1210
1040 'CALLSIGN ROUTINE
1050 FOR Y=1 TO K
1060 Z$=C1$(RND(59))
1070 Z1$=MID$(STR$(RND(10)-1),2,1)
1080 FOR X=1 TO RND(3):Z2$(X)=CHR$(64+RND(26)):NEXT
1090 W$(Y)=Z$+Z1$+Z2$(1)+Z2$(2)+Z2$(3)
1100 NEXT:IF F=3 THEN 1250ELSE320
1110 'LOAD ASCII FILE
1120 TM=356:HL=24:VL=3:GOSUB 920:PRINT@389,;:LINEINPUT"FILE NAME:";F$
1130 IF F$="" THEN 160
1140 IF LEN(F$)>8 AND INSTR(F$,".")<1 THEN 1120
1150 TM=353:HL=30:VL=3:GOSUB 920:PRINT@386,;:LINEINPUT"CASSETTE OR DISK (C OR D)?";CD$
1160 IF CD$="C"THEN CD=-1 ELSE CD=1
1170 OPEN "I",CD,F$:K=0
1180 K=K+1:IF EOF(CD)=-1 THEN CLOSE:GOTO 320
1190 INPUT#CD,W$(K):GOTO 1180
1200 FOR X=1 TO K:W$(X)=W1$(RND(36)):NEXT:GOTO 320
1210 F=3:GOTO320
1220 ' CONTEST SIMULATION
1230 FOR ZZ=0 TO K-1
1240 K=2:GOTO 1050
1250 C1$=W$(1):C2$=W$(2)
1260 M=8*ZZ
1270 W$(1)="CQ TEST DE "+C1$+" K":V=1:T$="A":GOSUB 430
1280 FMM$=W$(1)
1290 W$(2+M)=W$(1)
1300 W$(1)=C1$+" DE "+C2$:T$="B":GOSUB 430
1310 W$(3+M)=W$(1)
1320 W$(1)=C2$+" R R NR"+STR$(RND(9))+STR$(RND(9))+STR$(RND(9)):T$="A":GOSUB 430
1330 W$(4+M)=W$(1)
1340 IF RND(10)<7 THEN R1=5 ELSE R1=RND(5):IF R1<2 THEN 1340
1350 R2=RND(9):IF R2<5 AND R1>3 THEN 1350
1360 W$(1)="RST"+STR$(R1)+STR$(R2)+"9 BK":GOSUB 430
1370 W$(5+M)=W$(1)
1380 W$(1)="QSL NR"+STR$(RND(9))+STR$(RND(9))+STR$(RND(9)):T$="B":GOSUB 430
1390 W$(6+M)=W$(1)
1400 IF RND(10)<7 THEN R1=5 ELSE R1=RND(5):IF R1<2 THEN 1400
1410 R2=RND(9):IF R2<5 AND R1>3 THEN 1410
1420 W$(1)="RST"+STR$(R1)+STR$(R2)+"9 BK":GOSUB 430
1430 W$(7+M)=W$(1)
1440 W$(1)="QSL 73 SK":T$="A":GOSUB 430
1450 W$(8+M)=W$(1)
1460 W$(1)="=":T$="C":GOSUB 430
1470 NEXT:W$(1)="":W$(2)=FMM$
1480 F=0:K=ZZ*8:GOTO 1560
1490 'CHANGE SPEED
1500 FOR X=0 TO 6:PRINT@274+(X*32),W(1,X+1);:NEXT
1510 TM=240:HL=6:VL=9:GOSUB 920
1520 J1=25:J3=1297:X1=3:GOSUB 1670
1530 L=L+1:WPM=W(1,L):U=W(2,L):U2=W(3,L)
1540 GOTO 160
1550 'LIST ANSWERS
1560 CLS:PRINT@7,"CODE TEST ANSWERS":PRINTSTRING$(32,175);:C=0
1570 FOR X=1 TO K
1580 C1=INT(LEN(W$(X))/32)+1:C=C+C1:IF C>13 THEN C=0:GOTO 1610
1590 IF LEN(W$(X))>C1*32 THEN C=C+1
1600 PRINT W$(X):GOTO 1640
1610 A$=INKEY$:IF A$=""THEN1610
1620 IF A$=CHR$(12) THEN 160
1630 FOR S=2 TO 14:PRINT@32*S,STRING$(32,32);:NEXT:PRINT@64,;
1640 NEXT X
1650 EXEC 44539:GOTO 160
1660 'JOYSTICK INPUT
1670 IF KJ=1 THEN KI=0:GOTO 1810
1680 J2=INT(64/(J1/4))
1690 H=JOYSTK(0):V=JOYSTK(1)
1700 X=J3+INT(V/J2)*32:POKE X,127+(16*RND(8)):FOR T=1 TO 50:NEXT:POKE X,143
1710 IF INKEY$=CHR$(12) THEN160
1720 P=PEEK(65280):IF P=254 OR P=126 GOSUB 900:L=INT(V/J2):RETURN
1730 GOTO 1670
1740 'CHANGE TEST
1750 TM=228:HL=23:VL=9:GOSUB920
1760 ZZ=263:FOR X=0 TO 6:Z1=ZZ+(X*32)
1770 PRINT@Z1,T$(X+1);:NEXT
1780 J3=1285:J1=25:X1=20:GOSUB 1670
1790 N=L+1:GOTO 160
1800 'KEYBOARD INPUT
1810 X=J3+KI:POKE X,127+(16*RND(8)):FOR T=1 TO 50:NEXT
1820 A$=INKEY$:IF A$=""THEN1810
1830 POKE X,143
1840 IFA$=CHR$(94)THEN KI=KI-32
1850 IFA$=CHR$(10)THEN KI=KI+32
1860 IF A$=CHR$(13) THEN GOSUB 900:L=KI/32:RETURN
1870 IF KI<0 THEN KI=0
1880 IF KI>J1/4*32 THEN KI=KI-32
1890 GOTO 1810
1900 'TOGGLE KEY. & JOY.
1910 IF KJ=1 THEN KJ=0 ELSE KJ=1
1920 GOTO 1940
1930 'OPTIONS
1940 TM=224:HL=24:VL=8:GOSUB930
1950 FOR X=0 TO 6:PRINT@257+(X*32),STRING$(22,32);:NEXT
1960 PRINT@259,"DESCENDING SPEED";:IF DS=1 THEN POKE 1302,191
1970 PRINT@291,"ACCENDING SPEED";:IF IS=1 THEN POKE 1334,191
1980 PRINT@323,"DEC/ACC SPEED OFF";
1990 PRINT@355,"TOG. RANDOM TONES";:IF RT=1 THEN POKE 1398,191
2000 PRINT@387,"TOGGLE KEY. & JOY.";
2010 PRINT@419,"DISPLAY LINE";:IF DL=1 THEN POKE 1462,191
2020 PRINT@451,"COMMAND MENU";
2030 J1=25:J3=1281:X1=20:GOSUB 1670
2040 ON L+1 GOTO 2050,2050,2150,2130,1910,2160,160
2050 TM=397:HL=18:VL=3:GOSUB930
2060 PRINT@462,STRING$(16,32);
2070 PRINT@430,;:BV=PEEK(1470):INPUT"START SPEED";SS:POKE 1470,BV
2080 PRINT@462,;:INPUT"END SPEED";ES:POKE 1502,BV
2090 IF L=0 THEN DS=1:IS=0 ELSE DS=0:IS=1
2100 FOR XX=0 TO 3:PRINT@398+(32*XX),STRING$(17,32);:NEXT
2110 F=2
2120 GOTO 1940
2130 IF RT=1 THEN RT=0ELSE RT=1
2140 GOTO 1940
2150 DS=0:IS=0:F=0:GOTO 1940
2160 IF DL=1 THEN DL=0 ELSE DL=1
2170 GOTO 1940
2180 PRINT@355," "
2190 PRINT@422,"WHAT DO YOU HEAR"
2200 TM=389:HL=23:VL=3:GOSUB920
2210 CP=PEEK(1467)
2220 F=1:FOR P=1 TO K
2230 R=RND(43):IF R>9 AND R<16 THEN 2230
2240 N$=CHR$(R+48):GOSUB 560
2250 PRINT@439,;:INPUT I$
2260 IF N$<>I$ THEN 2310
2270 TM=243:HL=10:VL=3:GOSUB920
2280 PRINT@276,"CORRECT!";
2290 NR=NR+1
2300 GOTO 2350
2310 TM=240:HL=16:VL=5:GOSUB920
2320 PRINT@277,"SORRY";
2330 PRINT@305,"CHAR. SENT - "N$;
2340 PRINT@337,"CHAR. RECD.- "I$;
2350 EXEC 44539
2360 FOR X=0 TO 4:PRINT@218+(X*32),STRING$(15,32):POKE 1467,CP:NEXT X,P
2370 TM=240:HL=14:VL=3:GOSUB920
2380 PRINT@273,"SCORE:"INT(100/K*NR)"%";:EXEC 44539
2390 F=0:NR=0:GOTO 160
