1 'NOTE RITE
2 'WRITTEN BY JOHN MUSUMECI
3 'COPYRIGHT FEBRUARY 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR 3000
20 POKE150,7:'*BAUD RATE*
30 DIMB$(21)
40 WIDTH 40:T=0
50 CLS5:LOCATE5,10:PRINT"ADJUST PAPER TO PERFORATION. PRESS <ANY KEY> WHEN READY TO WRITE."
60 A$=INKEY$:IF A$="" THEN 60
70 CLS:FOR A=1 TO 21:B$(A)=STRING$(40,32):NEXT A
80 LOCATE0,21:PRINTSTRING$(40,45);
90 LOCATE9,22:PRINT"PRESS <CLEAR> TO PRINTER"
100 LOCATE10,23:PRINT"PRESS <BREAK> TO QUIT";
110 X=0:Y=0:A=1
120 LOCATEX,Y
130 A$=INKEY$:IF A$="" THEN 130
140 IF A$=CHR$(3) THEN END
150 IF A$=CHR$(12) THEN 330
160 IF A$=CHR$(13) THEN Y=Y+1:X=0:A=A+1:GOTO 290
170 IF A$=CHR$(8) THEN X=X-1:IF X<0 THEN X=0:GOTO 120 ELSE 120
180 IF A$=CHR$(9) THEN 280
190 IF A$=CHR$(10) THEN Y=Y+1:A=A+1:IF Y>20 THEN Y=20:A=21:GOTO 120 ELSE 120
200 IF A$=CHR$(94) THEN Y=Y-1:A=A-1:IF Y<0 THEN Y=0:A=1:GOTO 120 ELSE 120
210 IF A$=CHR$(21) THEN X=0:GOTO 120
220 IF A$=CHR$(93) THEN X=39:GOTO 120
230 IF A$=CHR$(91) THEN Y=20:A=21:GOTO 120
240 IF A$=CHR$(95) THEN Y=0:A=1:GOTO 120
250 GOSUB 310
260 MID$(B$(A),X+1,1)=A$
270 LOCATE0,Y:PRINTB$(A);
280 X=X+1:IF X>39 THEN A=A+1:Y=Y+1:X=0
290 GOSUB 310
300 GOTO 120
310 IF Y>20 THEN Y=20:A=21
320 RETURN
330 LOCATE9,22:PRINT"  P R I N T I N G . ."
340 IF T=0 THEN FOR X=1 TO 12:PRINT#-2:NEXT X:T=1:AA=0:GOTO 360
350 IF AA=1 THEN FOR X=1 TO 24:PRINT#-2:NEXT X:AA=0 ELSE AA=1
360 FOR X=1 TO 21:PRINT#-2,TAB(20);B$(X):NEXT X
370 GOTO 70
