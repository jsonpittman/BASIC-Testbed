60000 '***********************
60010 ' BASIC PROGRAM READER
60020 '      (READPROG)
60030 'REQUIRES RS SPEECH/SOUND              CARTRIDGE
60040 '                       
60050 '    BY BOB ROBERTS
60060 ' COPYRIGHT (C) 1987
60070 '***********************
60080 CLEAR2000:DIM T$(120),U$(45),TE$(18):QF=1
60090 CLS:PRINTTAB(9)"PROGRAM READER":PRINT@128,"1. READ FROM START OF PROGRAM.":PRINT:PRINT"2. START READING AT A SPECIFIED    LINE NUMBER.":PRINT:PRINT"3. READ LINE NUMBERS ONLY.":PRINT@448,"ENTER YOUR CHOICE :";
60100 A$=INKEY$:IFA$="" THEN60100 ELSEA=VAL(A$):IFA<1 OR A>3 THENSOUND100,1:GOTO60090 ELSEIFA=2THENCLS:PRINT@224,"";:INPUT"KEY IN LINE NUMBER TO START AT  THEN  PRESS <ENTER> ";A$:LN=VAL(A$) ELSEIFA=3 THENLQ=1
60110 IFA=3 THENCLS:GOTO60150 ELSECLS:PRINT@192,"DO YOU WANT THE COMPUTER TO SAY ";CHR$(34);"SPACE" ;CHR$(34);"  EACH TIME IT FINDS ONE OR TO IGNORE IT ?":PRINT:PRINT"PRESS <S> TO SAY OR <I> TO      IGNORE.";
60120 A$=INKEY$:IFA$="" THEN60120 ELSEIFA$="S" THENSF=1 ELSEIFA$<>"I" THENSOUND100,1:GOTO60120
60130 CLS:PRINT@224,"DO YOU WANT THE COMPUTER TO READREMARKS (Y/N)?";
60140 A$=INKEY$:IFA$="" THEN60140 ELSECLS:IFA$="N" THENRF=1 ELSEIFA$<>"Y" THENSOUND200,1:GOTO60130
60150 READ I$:IF I$<>"XTOKEN" THEN60150
60160 READ I$:IFI$<>"ENDT$" THEN T$(I)=I$:I=I+1:GOTO60160
60170 I=0
60180 READ I$:IFI$<>"ENDU$" THEN U$(I)=I$:I=I+1:GOTO60180
60190 FORI=1 TO18:READ TE$(I):NEXTI
60200 X=&HFF00:Y=&HFF7E:POKEX+1,52:POKEX+3,63:POKEX+35,60:POKE&HFF7D,1:POKE&HFF7D,0:M=256*PEEK(25)+PEEK(26)+2:GOTO60260
60210 P=PEEK(M):M=M+1
60220 IFP>47 AND P<58 THENA$=A$+CHR$(P):NF=1:GOTO60210 ELSEIFNF=1 THENNF=0:M=M-1:GOTO60750
60230 IFP=255 THEN 60420
60240 IFP>127 THEN 60400
60250 IFP<>0 THEN 60300 ELSEP=PEEK(M):IF P=0 THENCLS2:PRINT@229,"REACHED END OF PROGRAM";:END ELSEPRINT:M=M+2
60260 LZ=256*PEEK(M)+PEEK(M+1):IFLZ<LN THEN M=PEEK(M-2)*256+PEEK(M-1)+2:GOTO60260 
60270 IF LZ>=60000 THEN CLS:PRINT@229,"REACHED END OF PROGRAM";:SOUND200,1:END
60280 IFLQ=0 THENA$="LINE NUM BER^":M=M+2 ELSE A$=RIGHT$(STR$(LZ),LEN(STR$(LZ))-1):M=PEEK(M-2)*256+PEEK(M-1)+2:GOTO60760
60290 GOTO 60450
60300 IFP>64 AND P<91 THENA$=CHR$(P):GOTO60450
60310 IF P=32ANDSF=1THENA$="SPACE^ "ELSEIFP=32ANDSF=0 THENA$=" ^ "ELSEIFP=33 THENA$="EXCLAMANATION POINT^!" ELSEIFP=34 THENA$="QUOTE^":QF=QF*-1 ELSEIFP=35 THENA$="CARRAT^#" ELSEIFP=36 THENA$="DOLLAR^$"
60320 IFP=37 THENA$="PER CENT^%" ELSEIFP=38 THENA$="AMBERSAND^&"
60330 IFP=39 THENA$="APOSTRUPHE^'"
60340 IFP=40 THENA$="OPEN PAREN^(" ELSEIFP=41 THENA$="CLOSE PAREN^)" ELSEIFP=42 THENA$="ASTERIK^*" ELSEIFP=43 THENA$="PLUS^+" ELSEIFP=44 THENA$="KAW MAW^," ELSEIFP=45 THENA$="MINE US^-"
60350 IFP=46 THENA$="PEER E ID^." ELSEIFP=47 THENA$="SLASH^/" ELSEIFP>47 AND P<58 THENA$=CHR$(P)+"^"+CHR$(P)  ELSEIFP=58 ANDPEEK(M)=131 THENA$="APOSTRUPHE^'":GOTO60950 ELSEIFP=58 ANDPEEK(M)=132 THEN60210 ELSEIFP=58 THENA$="COLE UN^:"
60360 IFP=59 THENA$="SEM E COALUN^;" ELSEIFP=60 THENA$="LESS THEN^<" ELSEIFP=61 THENA$="E QUALS^=" ELSEIFP=62 THENA$="GREATER THAN^>" ELSEIFP=63 THENA$="QUESTION MARK^?" ELSEIFP=64 THENA$="AT^@" ELSEIFP=94 THENA$="UP.AER OW^^"
60370 IFP=93 THENA$="RIGHT BRACKET^]" ELSEIFP=91 THENA$="LEFT BRACKET^[" ELSEIFP=92 THENA$="BACK SLASH^\" ELSEIFP=95 THENA$="LEFT AER OW^ " ELSEIF P>96 ANDP<123 THENA$=CHR$(P-32)+"^"+CHR$(P)
60380 IFA$<>"" THEN60450
60390 IFP<128 THEN60210
60400 P=P-128
60410 A$=T$(P):IFRF=1 ANDP=2THEN60950 ELSE60450
60420 P=PEEK(M):M=M+1
60430 P=P-128
60440 A$=U$(P)
60450 Z=INSTR(A$,"^"):IFZ=0 THENPRINTA$; ELSEPRINTRIGHT$(A$,LEN(A$)-Z);:A$=LEFT$(A$,Z-1)
60460 IF A$="QUOTE" THENPRINTCHR$(34);
60470 IF LQ=1 THENPRINT
60480 FORA=1 TO LEN(A$)
60490 IF (PEEK(Y) AND 128)=0 THEN60490
60500 POKE Y,ASC(MID$(A$,A,1))
60510 NEXTA
60520 POKE Y,13
60530 IFPEEK(Y) <>255 THEN60530
60540 IFINKEY$<>"" THENGOSUB60940
60550 IFA$="LINE NUM BER" THENA$=RIGHT$(STR$(LZ),LEN(STR$(LZ))-1):NF=1:GOTO60760 ELSEA$=""
60560 IF LQ=1 THEN60260
60570 GOTO60210
60580 DATA XTOKEN, 4 ^FOR,GOW^GO,REMARK^REM,APOSTRUPH E^',ELSE,IF,DATA,PRINT,ON,INPUT,END,NEXT,DIMENSION^DIM,REED^READ,RUN,RE STORE^RESTORE,RE TURN^RETURN,STOP,POKE,CONTINUE^CONT,LIST,CLEAR
60590 DATA NEW,C LOAD^CLOAD,C SAVE^CSAVE,OPEN,CLOSE,L.LIST^LLIST,SET,RE SET^RESET,CLEAR SCREEN^CLS,MOW TER^MOTOR,SOUND,AUDEO^AUDIO,EXECUTE^EXEC,SKIP F^SKIPF,TABH. OPEN PAREN^TAB(,2^TO, SUB^SUB
60600 DATA THEN,NOT,STEP,OFF,PLUS^+,MINE US^-,TYMES^*,DIVIDED BY^/,TO THE POWER OF^^,AND,OR,GREATER THEN^>,E QUALS^=,LESS THEN^<,DELETE^DEL,EDIT,TRACE ON^TRON,TRACE OFF^TROFF,DEFINE FUNCTION^DEFFN,LET,LINE
60610 DATA P CLEAR SCREEN^PCLS,P SET^PSET,P.RE SET^PRESET,SCREEN,P CLEAR^PCLEAR,COLOR,CIRCUL^CIRCLE,PAINT,GET,PUT,DRAW
60620 DATA P COPY^PCOPY,P MODE^PMODE,PLAY,D LOAD^DLOAD,RE NUMBER^RENUM,FUNCTION^FN,USING
60630 DATA DIRECTOREYE^DIR,DRIVE,FEELD^FIELD,FILES,KILL,LOAD,L.SET^LSET,MERGE,RE NAME^RENAME,R.SET^RSET,SAVE,RIGHT,VERIFI^VERIFY,UN LOAD^UNLOAD,DISK INITIALISE^DSKINI,BACK UP^BACKUP,COPY,DISK IN PUT^DSKI$,DISK OUT PUT^DSKO$
60640 DATA  QUESTION^?,WIDTH,PALETTE,H.SCREEN^HSCREEN,L POKE^LPOKE,H.CLEAR SCREEN^HCLS,H.COLOR^HCOLOR,H.PAINT^HPAINT,H.CIRCUL^HCIRCLE,H.LINE^HLINE,H.GET^HGET,H.PUT^HPUT
60650 DATA H.BUFF ER^HBUFF,H.PRINT^HPRINT,AIRER^ERR,BRAKE^BRK,LOCATE,H.STAT US^HSTSTUS,H.SET^HSET,H.RE SET^HRESET,H.DRAW^HDRAW,C.M.P.^CMP,R.G.B.^RGB,ATTRIBUTES^ATTR
60660 DATA ENDT$
60670 'TOKENS WITH LEADING 255.        PUT IN ARRAY U$
60680 DATA SIGN^SGN,INTUHGER^INT,ABSOLUTE  VALUE^ABS,USER^USR,RANDUMB^RND,SINE^SIN,PEEK,LENGTH^LEN,MAKE STRING^STR$,VALUE^VAL,ASKEY^ASC,CAREAKTER^CHR$,END OF FILE^EOF,JOY STICK^JOYSTK,LEFT.DOLLAR^LEFT$,RIGHT.DOLLAR^RIGHT$,MID.DOLLAR^MID$,POINT
60690 DATA  INKEY^INKEY$,MEMORY^MEM,ARK TAN GENT^ATN,CO SIGN^COS,TAN GENT^TAN,EXPONENT^EXP,FIX,LOGRITHUM^LOG,PUHSITION^POS,SQWHERE ROOT^SQR,HEX DOLLAR^HEX$
60700 DATA VARIABLE POINTER^VARPTR,IN STRING^INSTR,TIMER,P POINT^PPOINT,STRING DOLLAR^STRING$
60710 DATA CONVERT NUMBER^CVN,FREE,L O C^LOC,LAST OF FILE^LOF
60720 DATA MAKE NUMBER^MKN$,AHS^AS,333,L PEEK^LPEEK,BUTTON,H.POINT^HPOINT,AIRER NUMBER^ERNO,AIRER LINE^ERLIN
60730 DATA ENDU$
60740 DATA TEN,E LEVHEN.,TWELVE,THIR TEEN,4 TEEN,FIF TEEN,6 TEEN,7 TEEN,8 TEEN,9 TEEN,TWENTY,THIR T,4 T,FIF T,6 T,7 T,8 T,9 T
60750 REM TRANSLATE NUMBERS INTO EXPRESSIONS
60760 L=LEN(A$):I$=A$:Z$="":IFVAL(A$)=0 THEN60450
60770 ON L GOTO 60920,60860,60830,60800 
60780 GOTO 60920
60790 REM 4 DIGITS
60800 Z$=LEFT$(A$,1)+" THOUSAND "
60810 A$=RIGHT$(A$,L-1):L=L-1
60820 REM 3 DIGITS
60830 IFLEFT$(A$,1)<>"0" THENZ$=Z$+LEFT$(A$,1)+" HUN DER ED."
60840 A$=RIGHT$(A$,L-1)
60850 REM 2 DIGITS
60860 IFA$="00" THEN60910
60870 IFLEFT$(A$,1)="0" THENZ$=Z$+" AND "+RIGHT$(A$,1):GOTO60910
60880 IFVAL(A$)<20 THENZ$=Z$+" "+TE$(VAL(A$)-9):GOTO60910
60890 Z$=Z$+TE$(VAL(LEFT$(A$,1))+9)
60900 IF RIGHT$(A$,1)<>"0"THENZ$=Z$+" "+RIGHT$(A$,1)
60910 A$=Z$+"^"+I$
60920 IFNF=1 THENNF=0:A$=A$+" "
60930 GOTO60450
60940 IF INKEY$="" THEN60940 ELSERETURN
60950 IFRF=0 ANDP=58 THEN60210
60960 IFRF=0 ANDP=2 THEN60450
60970 IFP=58 THEN A$="APOSTROPHIE SKIPPING^' SKIPPING"
60980 IFP=2 THEN A$="REMARK SKIPPING^REM SKIPPING"
60990 P=PEEK(M):IFP<>0 THENM=M+1:GOTO60990
61000 GOTO60450
