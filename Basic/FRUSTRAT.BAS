0 CLEAR1500:FORI=0TO15:PALETTEI,0:NEXTI:HBUFF1,310:HBUFF2,310:HBUFF3,310:HBUFF4,420:HBUFF5,420:POKE65497,0:TIMER=RND(1000):DIMLN$(99),C0(15),C1(22),C2(22)
10 HBUFF6,100:HBUFF7,100:HBUFF8,576:HBUFF9,270:HBUFF10,270:HBUFF11,928:HBUFF12,928:HBUFF13,576:HBUFF14,576:HBUFF15,576:HBUFF16,180:HBUFF17,680
20 ON BRK GOTO1350
30 PLAY"O3V31L255T255":HSCREEN2:GOSUB930
40 HCOLOR0:HLINE(28,1)-(298,21),PSET,B:HLINE(34,5)-(292,17),PSET,B:HPAINT(30,2),15,0
50 GOSUB1140
60 SC=0:TM=50:RN=1
70 GOSUB800:GOSUB1280
80 HCOLOR1:FORX=27TO260STEP34:HLINE(X+2,37)-(X+32,37),PSET:HLINE(X,38)-(X+30,38),PSET:NEXT:FORY=40TO176STEP20:HLINE(265,Y-1)-(265,Y+16),PSET:HLINE(266,Y-2)-(266,Y+14),PSET:HLINE(264,Y)-(264,Y+17),PSET:NEXT
90 X=40:Y=50:GOSUB760
100 HCOLOR4:HPRINT(5,1),"SCORE:":GOSUB460:HPRINT(17,1),"TIMER:":GOSUB1220
110 HCOLOR5:PLAY"V31":HLINE(5,184)-(150,192),PRESET,BF:IFBO<>1THENHPRINT(3,24),"LEVEL":HPRINT(9,24),LN$(RN):PALETTE5,C0(5) ELSEHPRINT(3,24),"BONUS ROUND":PALETTE5,C0(5):PLAY"O3DFDFDFDFFDFDFDFDFDFDF"
120 PALETTE6,RND(30)-1:J0=JOYSTK(0):J1=JOYSTK(1):IFZT>=ZP THEN480ELSEIFBO=0ANDTM<10THENPALETTE7,0
130 IFBO=0ANDTM<10THENPALETTE7,0
140 XX=X:YY=Y:IFJ0<15THENM=1:X=X-34ELSEIFJ0>48THENM=1:X=X+34
150 IFJ1<15THENMM=2:Y=Y-20ELSEIFJ1>48THENMM=2:Y=Y+20
160 IFX<40THENX=40ELSEIFX>244THENX=244
170 IFY<50THENY=50ELSEIFY>170THENY=170
180 IFM=1THENHPUT(XX-12,25)-(XX+16,36),4,PSET:M=0
190 IFMM=2THENHPUT(270,YY-10)-(278,YY+7),6,PSET:MM=0
200 HPUT(X-12,25)-(X+16,36),5,PSET:HPUT(270,Y-10)-(278,Y+7),7,PSET:IFBO=0ANDTM<10THENPALETTE7,38
210 IFBO=1THEN710ELSET1=T1+1:IFT1>15-TV THENT1=0:GOSUB470
220 IFBO=1THENGOSUB660:GOTO240
230 IFRN>13THENGOSUB530ELSEIFRND(14-RN)=1THENGOSUB530
240 IFVA=2THEN250ELSEBT=0
250 IFVA=3THENGOSUB290ELSEIFBUTTON(0)=1THENGOSUB290ELSEBT=0
260 IFG1>0THENG1=G1+1:IFG1>50THENGOSUB620
270 I$=INKEY$:IFI$="R"THEN1340
280 GOTO120
290 IFBT=1THENRETURNELSEBT=1:X1=X-16:Y1=Y:P=HPOINT(X,Y)
300 IFBO=1THEN700ELSEIFG1<1THEN310ELSEIF((X1-1)=(X2-10) AND (Y1-10)=(Y2-4)) THEN380
310 IFP=2THENHCOLOR12 ELSEIFP=12THENHCOLOR3
320 IFP=3THEN360ELSEIFP=10THEN370
330 IFRN<4THENHCOLOR3
340 ZT=ZT+1:IFRN<4 ORP=12THENHPUT(X1,Y1-10)-(X1+33,Y1+10),14,PSET ELSEHPUT(X1,Y1-9)-(X1+32,Y1+9),15,PSET
350 PLAY"O2DO3C"
360 RETURN
370 PLAY"O1":FORV=30TO1STEP-3:PLAY"V"+STR$(V)+"AEC":PALETTE8,RND(63):NEXTV:GOTO1340
380 IFG5=1THEN430ELSEIFG4=1THENPLAY"O3CDEFGABO4CD":SC=SC+(5*RN):GOSUB460:GOSUB620:RETURN ELSEIFG3=1THEN410 ELSEIFG2=1THEN390IFG2=1THEN390ELSEIFG3=1THEN410
390 G2=0:SC=SC-(5*RN):PLAY"O4DCO3BAGFEDC":IFSC<0THENSC=0
400 GOSUB460:GOSUB620:RETURN
410 T=TM:TM=TM+10:IFTM>50THENTM=50
420 HCOLOR7:FORI=185+(T*2) TO185+(TM*2):HLINE(I,9)-(I,14),PSET:PLAY"O4AO1C":NEXT:GOSUB620:RETURN
430 PLAY"O4CDEFGFEDC":GOSUB620
440 I=RND(3):IFI=VA THEN440ELSEVA=I
450 RETURN
460 HCOLOR4,8:HLINE(88,8)-(120,16),PRESET,BF:HPRINT(10,1),SC:RETURN
470 T=185+TM*2:HLINE(T,9)-(T,14),PRESET:PLAY"O2F":TM=TM-1:IFTM>0THENRETURNELSEPLAY"O4":FORI=1TO18:PLAY"CE":PALETTE7,RND(63):NEXTI:IFBO=1THEN730ELSEGOTO1340
480 IFBO=1THENBO=0:GOTO500ELSEIFRN/3<>INT(RN/3) THEN500
490 BO=1
500 FORI=30TO5STEP-2:PALETTE3,63:PLAY"V"+STR$(I)+"O4GFED":PALETTE3,7:PLAY"O5CFEFE":NEXTI:RN=RN+1:SC=SC+(RN*10)+(SK*2):GOSUB460
510 GOSUB1140:GOSUB410:IFBO=1THENXE=161:YE=81:HGET(XE,YE)-(XE+29,YE+18),11
520 G1=0:G2=0:G3=0:G4=0:GOTO110
530 X1=6+RND(7)*34:Y1=21+RND(7)*20:P=HPOINT(X1,Y1):IFRND(ABS(15-RN))=1THEN640
540 IFP=12THENHCOLOR2 ELSEIFP=3THENHCOLOR12 ELSEIFP=2THEN580ELSERETURN
550 IFRN<4THENHCOLOR2
560 ZT=ZT-1:IFRN<4ORP=12THENHPUT(X1-16,Y1-1)-(X1+17,Y1+19),17,PSET ELSEHPUT(X1-15,Y1)-(X1+17,Y1+18),15,PSET
570 PLAY"O2FEFO1EFF":RETURN
580 IFG1>0THEN620ELSEG1=1
590 X2=X1-7:Y2=Y1+3:IFRND(3)=1THENG2=1:HPUT(X2,Y2)-(X2+22,Y2+12),3:RETURN
600 IFRND(2)=1THENG5=1:G2=0:HPUT(X2,Y2-2)-(X2+12,Y2+13),16,PSET:RETURN
610 IFRND(4)=1THENG3=1:G2=0:HPUT(X2,Y2-2)-(X2+18,Y2+13),9,PSET:RETURN ELSEHPUT(X2,Y2)-(X2+22,Y2+12),2,PSET:G4=1:RETURN
620 G4=0:G2=0:G1=0:HPUT(X2,Y2)-(X2+22,Y2+12),1,PSET:IFG3<>1ANDG5<>1THENRETURNELSEHPUT(X2,Y2-2)-(X2+18,Y2+13),10,PSET:G3=0:G5=0:RETURN
630 RETURN
640 IFP=6THENRETURNELSESK=SK+1:X1=X1-16:HPUT(X1,Y1)-(X1+31,Y1+18),8,PSET:PLAY"O1DEFO2DEFO1CDE":IFP=3THENRETURNELSEZT=ZT+1:IFRN>3ANDP=2THENZT=ZT+1
650 RETURN
660 T9=T9+1:IFT9<16-RN THENRETURN
670 HPUT(XE,YE)-(XE+29,YE+18),11,PSET
680 XE=RND(7)*34:YE=RND(7)*20:XE=XE-9:YE=YE+21:HGET(XE,YE)-(XE+29,YE+18),11:HPUT(XE,YE)-(XE+29,YE+18),12,PSET:PLAY"O2EAO1A":T9=0
690 RETURN
700 IFP<>13THENRETURNELSEHPUT(XE,YE)-(XE+29,YE+18),11,PSET:SE=SE+1:D$="O5CDEFG":FORV=30TO5STEP-8:PLAY"V"+STR$(V)+D$:NEXTV:PLAY"V31":GOTO680
710 T1=T1+1:IFT1>2THENT1=0:GOSUB470
720 GOTO220
730 SC=SC+5*SE:GOSUB460:SE=0:FORI=1TO4:PLAY"O"+STR$(I)+"CDEFGAB":NEXTI
740 PALETTE7,38:BO=2:HPUT(XE,YE)-(XE+29,YE+18),11:T=TM:TM=50:GOSUB420:GOSUB1140:GOTO110
750 'SET UP PALETTE
760 PALETTE5,60:FORI=0TO15:READC0(I):PALETTEI,C0(I):NEXT:FORI=0TO8:READC2(I),C1(I):NEXT:FORI=1TO25:READLN$(I):NEXT:FORI=26TO99:LN$(I)=STR$(I):NEXT:RETURN
770 DATA 18,36,11,7,63,48,9,38,0,32,16,9,25,13,4,47
780 DATA 11,36,11,36,41,9,32,63,17,36,36,11,20,36,49,17,63,11
790 DATA ONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN,EIGHT,NINE,TEN,ELEVEN,TWELVE,THIRTEEN,FOURTEEN,FIFTEEN,SIXTEEN,SEVENTEEN,EIGHTEEN,NINETEEN,TWENTY,TWENTY-ONE,TWENTY-TWO,TWENTY-THREE,TWENTY-FOUR,TWENTY-FIVE,26,27,28,29,30,31,32,33,34,35
800 HGET(33,44)-(55,56),1:D1$="R2U2R2D2R2D2L2D2L2U2L2U2
810 HCOLOR12:HDRAW"BM33,48;S9;XD1$;":HPAINT(38,49),11,12
820 HGET(33,44)-(55,56),2:HPUT(33,44)-(55,56),1,PSET
830 D2$="R6D2L6U2":HCOLOR3:HDRAW"BM33,48;S9;XD2$;":HPAINT(35,50),14,3
840 HGET(33,44)-(55,56),3
850 HPUT(33,44)-(55,56),1,PSET
860 HGET(28,28)-(56,39),4:HCOLOR9,8:D3$="R4G2H2":HDRAW"BM28,28;S20;XD3$;":HPAINT(40,30),10,9
870 HGET(28,28)-(56,39),5:HPUT(28,28)-(56,39),4,PSET
880 HGET(267,61)-(275,78),6:D4$="BR2D4H2E2":HDRAW"BM267,61;S18;XD4$;":HPAINT(275,74),10,9
890 HGET(267,61)-(275,78),7:HLINE(267,60)-(278,79),PRESET,BF
900 HGET(30,43)-(48,58),10:HCOLOR4:D5$="R6D2L2D3L2U3L2U2":HDRAW"BM30,43;S12;XD5$;":HPAINT(34,45),0,4
910 HGET(30,43)-(48,58),9:HPUT(30,43)-(48,58),10,PSET
920 RETURN
930 HCLS6:HCOLOR9:HCIRCLE(128,96),9,,.8:HPAINT(128,96),5,9
940 HLINE(125,100)-(131,104),PSET,B
950 HCOLOR10:HCIRCLE(125,97),3,,.9:HCIRCLE(131,97),3,,.9:HPAINT(125,97),8,10:HPAINT(131,97),8,10
960 HLINE(128,100)-(129,101),PSET,B
970 HGET(112,88)-(143,106),8:HCLS8
980 HCOLOR1:HCLS8:HGET(113,88)-(142,106),11
990 HCOLOR10:HCIRCLE(128,96),9
1000 HCIRCLE(128,100),9,,1,.50,0
1010 HPAINT(128,100),4,10:HPAINT(128,91),9,10
1020 HCIRCLE(128,95),4,13,1.2:HPAINT(128,95),13,13:HSET(129,93,8):HSET(131,93,8):HSET(130,93,4)
1030 HCOLOR3:HLINE(122,99)-(114,104),PSET:HLINE(123,101)-(115,106),PSET:HLINE(124,103)-(116,109),PSET
1040 HLINE(134,99)-(142,104),PSET:HLINE(133,101)-(141,106),PSET:HLINE(132,103)-(140,108),PSET
1050 HGET(113,88)-(142,106),12:HCLS8
1060 HCOLOR3:HLINE(24,41)-(58,59),PSET,BF:FORI=43TO59STEP2:HSET(26,I,8):NEXTI:FORI=46TO59STEP2:HSET(28,I,8):NEXTI:FORI=49TO59STEP2:HSET(30,I,8):NEXT:FORI=51TO59STEP2:HSET(32,I,8):NEXT:FORI=54TO59STEP2:HSET(34,I,8):NEXT
1070 FORI=54TO59STEP2:HSET(36,I,8):NEXT:HSET(38,57,8):HSET(38,59,8):HSET(40,59,8)
1080 HCOLOR1:HLINE(23,40)-(57,60),PSET,B:HGET(24,40)-(57,60),14
1090 HCOLOR12:HLINE(24,41)-(58,59),PSET,BF:HCOLOR1:HLINE(23,40)-(57,60),PSET,B:HGET(24,41)-(56,59),15
1100 HCLS2:D$="U1E1R1F1D1G2D1BD1D1":HCOLOR6:FORI=128TO132:HDRAW"BM"+STR$(I)+",96;S8;XD$;":NEXTI:HDRAW"BM132,96;C8;XD$;":HGET(126,91)-(138,106),16
1110 HCLS2:HCOLOR1:HLINE(23,40)-(57,60),PSET,B::HGET(24,40)-(57,60),17
1120 HCLS8
1130 RETURN
1140 FORI=1TO3:PALETTEI,0:NEXTI:FORI=8TO14:PALETTEI,0:NEXTI:PALETTE5,0:PALETTE6,0:R=RN:TV=R:II=0:HCOLOR2,8:SK=0:ZT=0:IFRN<4THENZP=49ELSEZP=98:IFTV>10THENTV=10
1150 IFR>8THENR=R-8:GOTO1150
1160 IFBO=2THENBO=0:GOTO1180ELSEFORIX=23TO250STEP34:FORIY=40TO170STEP20:HPUT(IX+1,IY)-(IX+33,IY+20),17,PSET:NEXTIY,IX
1170 HCOLOR1:HLINE(23,40)-(23,180),PSET:HGET(24,41)-(56,59),13
1180 A$="FRUSTRATOR":A=36:IFBO=1THENPALETTE1,23:PALETTE2,7ELSEPALETTE1,C1(R):PALETTE2,C2(R)
1190 HCOLOR4:L=LEN(A$):FORI=1TOL:HPRINT(A,I*2+2),MID$(A$,I,1):NEXT
1200 PALETTE3,C0(3):PALETTE4,C0(4):FORI=6TO14:PALETTEI,C0(I):NEXT
1210 RETURN
1220 I$=INKEY$:PLAY"V5":HCOLOR9:HLINE(0,184)-(150,192),PRESET,BF:HPRINT(3,24),"SELECT DIFFICULTY LEVEL (1-9) ":VA=1
1230 GOSUB1320:I$=INKEY$:IFI$="V"THEN1290ELSEV=VAL(I$):GOSUB1330:IFV<1ORV>9THEN1230ELSERN=V:R=RN
1240 IFR>8THENR=R-8:GOTO1240
1250 TV=RN:PLAY"V31":PALETTE1,C1(R):PALETTE2,C2(R):HCOLOR4,8:HLINE(0,184)-(284,192),PRESET,BF
1260 IFRN<4THENZP=49ELSEZP=98
1270 RETURN
1280 HCOLOR7:FORI=185TO185+(TM*2):HLINE(I,9)-(I,14),PSET:NEXT:RETURN
1290 WIDTH32:PALETTE12,63:PRINT@136,"SELECT VARIATION:":PRINT@198,"(1) SINGLE TOUCH":PRINT@230,"(2) RELEASE AND PRESS":PRINT@262,"(3) CONTINUOUS":
1300 V=VAL(INKEY$):IFV<1ORV>3THEN1300
1310 VA=V:POKE&HE6C6,33:HSCREEN2:POKE&HE6C6,141:PALETTE12,C0(12):GOTO1230
1320 FORI=5TO3STEP-1:PLAY"O"+STR$(I):FORII=12TO1STEP-1:PLAYSTR$(II):NEXTII,I:RETURN
1330 FORI=1TO3:PLAY"O"+STR$(I):FORII=1TO12:PLAYSTR$(II):NEXTII,I:RETURN
1340 PALETTE8,0:SC=0:TM=50:GOSUB1220:GOSUB460:GOSUB1280:GOTO510
1350 POKE65496,0:HSCREEN0:CMP:END
1360 PRINT@320,B:INPUTA:B=B+A:GOTO1360
