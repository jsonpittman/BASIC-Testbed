1 'CHKDSK UTILITY
2 'WRITTEN BY MARK PAGE
3 'COPYRIGHT (C) DECEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 '*****************************
10 POKE&HD89F,65:POKE&HD8A0,66:POKE&HD7C0,0:POKE&HD816,20
20 POKE150,7:POKE65497,0:CLS:CLEAR2500:DIML$(78),GR(78),BT(78):PRINT"CHECK DISK ON WHICH DRIVE ? ";:EXEC44539:D=VAL(INKEY$):PRINTD:IFD>3THEN20ELSEPRINT:PRINT"DO YOU WANT A PRINT-OUT ? ";:EXEC44539:PO$=INKEY$:PRINTPO$
30 PRINT:PRINT"CHECKING DISK ON DRIVE";D"..."
40 POKE65496,0:DSKI$ D,17,2,AA$,BB$:POKE65497,0
50 FOR QQ=1TO128:LE$=MID$(AA$,QQ,1):IFLE$=CHR$(0)THEN60ELSENEXTQQ
60 GS=QQ-1:FORCG=1TOGS:IFMID$(AA$,CG,1)=CHR$(255)THEN80ELSE70
70 NEXTCG:GOTO90
80 GR=GR+1:GOTO70
90 DF=0:FORX=3TO11
100 POKE65496,0:DSKI$ D,17,X,CC$,DD$:POKE65497,0
110 FORK=1TO128STEP32:GOSUB120:NEXTK:CC$=DD$:FORK=1TO128STEP32:GOSUB120:NEXTK,X:GOTO270
120 SB$=MID$(CC$,K,16):AC=ASC(MID$(SB$,13,1)):IFAC=0THENII$="B"ELSEII$="A"
130 I$=LEFT$(SB$,1):IFASC(I$)=0THENRETURNELSEIFASC(I$)=255THEN200ELSEDF=DF+1
140 Y=ASC(MID$(SB$,14,1))+1
150 GR(DF)=GR(DF)+1:Z=ASC(MID$(AA$,Y,1)):IFZ<78THENY=Z+1:GOTO150ELSEIFZ>192THENGOSUB160:GOTO170
160 BE=Z-192:BT(DF)=BE*256:RETURN
170 L$(DF)=LEFT$(SB$,8)+"."+MID$(SB$,9,3)+STR$(ASC(MID$(SB$,12,1)))+" "+II$+STR$(GR(DF))+STR$(((GR(DF)-1)*2304)+BT(DF)):TG=TG+(((GR(DF)-1)*2304)+BT(DF))
180 TT=TT+GR(DF)
190 RETURN
200 CLS:PRINT"DIRECTORY OF DRIVE";D:PRINT:IFPO$<>"Y"THEN210ELSEIFPEEK(65314)<>4THEN210ELSEPRINT#-2,"  Directory of Drive";D:PRINT#-2
210 FORHM=1TODF:CL=CL+1:IFCL>13THENGOSUB230ELSEPRINT" ";L$(HM):IFPO$<>"Y"THEN220ELSEIFPEEK(65314)<>4THEN220ELSEPRINT#-2,TAB(10);L$(HM)
220 NEXTHM:GOSUB230:GOTO240
230 PRINT:PRINT" STRIKE ANY KEY...":EXEC44539:CL=0:PRINT:RETURN
240 GF=GR:IFPO$<>"Y"THEN270ELSEIFPEEK(65314)<>4THEN270
250 PRINT#-2:PRINT#-2,(GS+2)*2304;"Bytes total disk space in";GS+2;"clusters":PRINT#-2,TG;"Bytes in";DF;"user files in";GS-GR;"clusters":PRINT#-2,((GS+2)-GF)*2304-TG;"Bytes in Directory and control data"
260 PRINT#-2,GF*2304;"Bytes free in";GF;"clusters";CHR$(12)
270 GF=GR:PRINT:PRINT(GS+2)*2304;"BYTES TOTAL DISK SPACE":PRINT" IN";GS+2;"CLUSTERS":PRINT:PRINTTG;"BYTES IN";DF;"USER FILES":PRINT" IN";(GS-GR)-HF;"CLUSTERS"
280 PRINT:PRINT((GS+2)-GF)*2304-TG;"BYTES IN DIRECTORY":PRINT" AND CONTROL DATA":PRINT:PRINTGF*2304;"BYTES FREE":PRINT" IN";GF;"CLUSTERS"
290 CL=0:PRINT:PRINT" R)eview     D)rive     Q)uit":EXEC44539:KY$=INKEY$:IFKY$="R"THENPO$="N":GOTO200ELSEIFKY$="D"THENRUNELSEIFKY$="Q"THEN300ELSE290
300 POKE65496,0:WIDTH32:RGB:CLS:EXEC&HC000
