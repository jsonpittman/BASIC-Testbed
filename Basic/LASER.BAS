0 'COPYRIGHT 1990 FALSOFT, INC.
10 '****************************
20 '*       LASER CYCLES       *
30 '*  COPYRIGHT  (C) 1985 BY  *
40 '*      ROBERT  ROGERS      *
50 '*  VERSION 1.1 JUNE 1986   *
60 '* FOR 4K COLOR BASIC COCOS *
70 '****************************
80 GOSUB390
90 CLS0:PRINT@228,"PRESS ANY KEY WHEN READY";
100 EXEC44539
110 X(1)=9:Y(1)=15:C(1)=3:Z(1)=1:J(1)=2:K(1)=3
120 X(2)=54:Y(2)=15:C(2)=4:Z(2)=2:J(2)=0:K(2)=1
130 CLS0:E=0
140 FORT=1TO10:B$=B$+CHR$(128):NEXTT
150 Q=RND(8):IFQ=3 OR Q=4 THEN150
160 FORX=1TO62:SET(X,1,Q):SET(X,30,Q):NEXTX
170 FORY=1TO30:SET(1,Y,Q):SET(62,Y,Q):NEXTY
180 PRINT@235,"GET READY!";:SOUND125,20
190 PRINT@235,"GET SET!!!";:SOUND147,15
200 PRINT@235,"GO!!!!!!!!";:SOUND218,10
210 PRINT@235,B$;
220 FORT=1TO2
230 A=JOYSTK(J(T)):B=JOYSTK(K(T))
240 IFA<10 THEN Z(T)=1:GOTO280
250 IFA>53 THEN Z(T)=2:GOTO280
260 IFB<20 THEN Z(T)=3:GOTO280
270 IFB>43 THEN Z(T)=4:GOTO280
280 ONZ(T)GOTO290,300,310,320
290 X(T)=X(T)-1:GOTO330
300 X(T)=X(T)+1:GOTO330
310 Y(T)=Y(T)-1:GOTO330
320 Y(T)=Y(T)+1:GOTO330
330 IFPOINT(X(T),Y(T))<>0THENSOUND1,10:SET(X(T),Y(T),5):FORQ=1TO1000:NEXTQ:GOTO490
340 SET(X(T),Y(T),C(T))
350 NEXTT
360 IFPEEK(345)=247THENSOUND230,10:EXEC44539
370 E=E+1
380 GOTO220
390 A=1:B=63:C=1:D=31
400 CLS0
410 L=RND(4):FORX=A-1 TO B:SET(X,C-1,L):NEXTX:B=B-1:A=A+1:SOUNDRND(255),1
420 FORY=C TO D:SET(B+1,Y,L):NEXTY:D=D-1:C=C+1:SOUNDRND(255),1
430 IFC>17 THEN480
440 FORX=B TO A-1 STEP-1:SET(X,D+1,L):NEXTX:B=B-1:A=A+1:SOUNDRND(255),1
450 FORY=D TO C STEP-1:SET(A-2,Y,L):NEXTY:C=C+1:D=D-1:SOUNDRND(255),1
460 IFC>17 THEN480
470 GOTO410
480 PRINT@266,"LASER CYCLES";:SOUND200,6:PRINT@456,"BY ROBERT ROGERS";:FORQ=1TO1000:NEXTQ:RETURN
490 IFT=1THENT=2:ELSET=1
500 CLS(C(T)):PRINT@224,"         PLAYER"T"WON!"
510 FORS=1TO2:RESTORE:FORQ=1TO13:READA,B:SOUNDA,B:NEXTQ:NEXTS
520 DATA 204,2,204,4,193,4,204,2,204,4,193,4,185,2,193,2,197,2,193,4,185,2,193,4,176,4
530 Q=2048-E:Q=INT(Q*.1):E=E*10:E=E+Q:PRINT@224," YOUR SCORE IS"E"PLAYER"T
540 FORQ=1TO1000:NEXTQ
550 RUN
