1 CLS:PRINT@232,"COCO ACCOUNTANT"
2 'J.A.PHILLIPS
3 '12 WILBUR BLVD.
4 'POUGHKEEPSIE, NY 12603
5 '3/8/84
10 GOTO30
12 CLS:PRINT@231,"SORTING ENTRY #"N:RETURN
15 CLS:PRINT@230,"MAX OF "E" ENTRIES":FORJ=1TO1000:NEXT
17 GOTO100
30 CLEAR 7000
40 E=360
50 DIMD$(E+1,2):DIMD(E+1,2):DIMA$(26):DIMA1$(26):DIMY$(2):DIMY(2):DIMT(26):DIM MT(12)
60 DE$="==(DELETED)=="
70 A2$=STRING$(8,".")
75 S$=STRING$(31," ")
80 FORI=1TO26
82 A$(I)=CHR$(64+I)
84 A1$(I)=A2$
86 NEXT
90 FORX=1TO200:NEXT
100 CLS:PRINT@72,"COCO ACCOUNTANT"
105 PRINT@132,"<1> LOAD OLD DATA"
110 PRINT@164,"<2> DEFINE/VIEW ACCOUNTS"
115 PRINT@196,"<3> ADD NEW DATA"
120 PRINT@228,"<4> VIEW/DELETE DATA"
125 PRINT@260,"<5> PRINT SUMMARIES"
130 PRINT@292,"<6> SORT DATA"
135 PRINT@324,"<7> SAVE NEW DATA"
140 PRINT@388,"ENTER <1-7>";:INPUTQ
145 IFQ>7ORQ<1THEN100
150 ON Q GOTO200,1300,400,700,1000,1600,1900
200 CLS:N=1
205 PRINT@196,"INSERT DATA DISK NOW"
210 PRINT@228,"PRESS <ENTER> TO LOAD"
215 PRINT@260,"OR ENTER <M>ENU  ";:LINEINPUTQ$
220 IFQ$<>""ANDQ$<>"M"THEN200
225 IFQ$=""THEN230ELSE100
230 OPEN "I",#1,"ACCOUNT/DAT"
235 FOR I=1TO26
240 INPUT#1,A1$(I)
245 NEXT
250 IF EOF(1)THEN270
255 INPUT#1,D$(N,1)
256 INPUT#1,D$(N,2)
257 INPUT#1,D(N,1)
258 INPUT#1,D(N,2)
260 N=N+1:GOTO250
270 FOR I=N TO E
280 D$(I,1)="":D$(I,2)="":D(I,1)=0:D(I,2)=0
290 NEXT
300 CLOSE#1:GOTO100
400 N=1
405 IFD$(N,2)=""THEN415
410 N=N+1:GOTO405
415 IF N>E THEN15
420 CLS:PRINT@228,"ENTER MONTH <1-12>";:INPUTQ
425 IFQ<1ORQ>12THEN420
430 M=Q
435 D(N,1)=M
440 A=1:GOTO1305
445 D$(N,1)=A$(I)
450 CLS:PRINT@68,"ACCOUNT DATA ENTRY #";N
455 PRINT@129,"MONTH = ";D(N,1)
457 PRINT@141,"ACCOUNT = ";A1$(I)
460 PRINT@193,"DESCRIPTION = ";:LINEINPUTQ$
470 IFQ$<>""THEN475
472 D$(N,2)="NONE":GOTO480
475 D$(N,2)=LEFT$(Q$,14)
480 PRINT@193,S$
485 PRINT@193,"DESCRIPTION = ";D$(N,2)
490 PRINT@257,"AMOUNT = ";:INPUTQ
500 D(N,2)=Q
505 N=N+1:IF N>E THEN 15
510 GOTO 435
700 N=1
705 CLS:PRINT " #   M A DESCRIPTION     AMOUNT"
710 FORI=1TO13
715 IFD$(N,2)=""THEN 770
720 PRINT USING"###";N;:PRINT TAB(4);:PRINT USING"##";D(N,1);:PRINT TAB(7);
730 PRINTD$(N,1);TAB(9);
740 PRINT USING"%            %";D$(N,2);:PRINT TAB(24);
750 PRINT USING"####.##";D(N,2)
760 N=N+1
765 NEXT
770 PRINT@450,"<ENTER>=SCROLL   <999>=QUIT"
775 PRINT@482,"<ENTRY#>=VIEW/DELETE";:INPUTQ
777 IFD$(N,2)=""THEN N=1
780 IFQ=999THEN100
785 IF Q>E THEN15
790 IFQ=0THEN705
792 CLS:PRINT@75,"ENTRY #"Q
793 PRINT@131,"DESCRIPTION: "D$(Q,2)
794 PRINT@230,"<D>ELETE OR <V>IEW"
795 Q$=INKEY$:IFQ$<>"D"ANDQ$<>"V"THEN795
796 IFQ$="D"THEN800
798 N=Q:GOTO705
800 D$(Q,2)=DE$:N=Q
810 GOTO705
1000 CLS:N=1
1005 PRINT@73,"PRINT OPTIONS"
1010 PRINT@132,"<1> Y-T-D ACCOUNT TOTALS"
1015 PRINT@164,"<2> 1-MONTH ACCOUNT TOTALS"
1017 PRINT@196,"<3> SINGLE ACCOUNT SUMMARY"
1019 PRINT@228,"<4> MONTHLY NETS"
1020 PRINT@260,"<5> ACCOUNT DATA (PRINTER)"
1025 PRINT@292,"<6> ACCOUNT NAMES(PRINTER)"
1027 PRINT@324,"<7> MAIN MENU"
1030 PRINT@388,"ENTER <1-7>";:INPUTQ
1035 IFQ<1ORQ>7THEN1000
1036 ON Q GOTO1040,1038,2100,2250,1180,2000,100
1038 CLS:PRINT@228,"ENTER MONTH <1-12>";:INPUTM1
1039 IF M1<1ORM1>12THEN1038
1040 CLS:PRINT@227,"CALCULATING ACCOUNT TOTALS"
1050 Z=0
1055 FOR I=1TO26:T(I)=0:NEXT
1065 IFD$(N,2)=""THEN1100
1072 IFQ=2AND D(N,1) <> M1 THEN1095
1073 IFD$(N,2)=DE$THEN1095
1080 I=ASC(D$(N,1))-64
1090 T(I)=T(I)+D(N,2)
1095 N=N+1:GOTO1065
1100 CLS
1105 IFQ=1THENPRINT#-Z,TAB(6);"YEAR-TO-DATE TOTALS"
1108 IFQ=2THENPRINT#-Z,TAB(9);"MONTH"M1"TOTALS"
1110 IFZ=2THENPRINT#-Z
1115 FOR I=1TO13
1120 PRINT#-Z,TAB(3);A$(I);TAB(6);
1125 PRINT#-Z,USING"######.##";T(I);
1130 PRINT#-Z,TAB(17);A$(I+13);TAB(20);
1135 PRINT#-Z,USING"######.##";T(I+13)
1140 NEXT
1145 IF Z=2THENPRINT#-Z ELSE1155
1150 PRINT#-Z:GOTO1160
1155 PRINT@483,"<P>RINT OR <M>ENU OR <N>ET";
1160 Q$=INKEY$:IFQ$<>"P"ANDQ$<>"M"ANDQ$<>"N"THEN1160
1165 IFQ$="M"THEN1000
1167 IFQ$="N"THEN1250
1170 Z=2:GOTO1105
1180 P=1:GOTO1305
1184 CLS:PRINT@226,"PRINTING DATA FOR ACCOUNT ";A$(I)
1185 N=1
1190 PRINT#-2,TAB(11);A$(I)"="A1$(I):PRINT#-2
1195 PRINT#-2,"MONTH";TAB(8);"DESCRIPTION";TAB(23);"  TOTAL"
1200 IFD$(N,2)=""THEN1235
1202 IFD$(N,2)=DE$THEN1230
1210 IFD$(N,1)<>A$(I)THEN1230
1215 PRINT#-2,USING"###";D(N,1);
1216 PRINT#-2,TAB(8);
1220 PRINT#-2,USING"%            %";D$(N,2);
1221 PRINT#-2,TAB(23);
1225 PRINT#-2,USING"####.##";D(N,2)
1230 N=N+1:GOTO1200
1235 PRINT#-2:PRINT#-2
1240 GOTO1180
1250 Z=0:T1=0:T2=0:T3=0
1252 FOR I=1TO26
1254 IFLEFT$(A1$(I),1)="-"THENT2=T2+T(I)
1256 IFLEFT$(A1$(I),1)="+"THENT1=T1+T(I)
1260 NEXT
1262 T3=T1-T2
1264 CLS
1266 IFZ=0THENPRINT
1268 IFQ=1THENPRINT#-Z,TAB(8);"YEAR-TO-DATE NET"
1270 IFQ=2THENPRINT#-Z,TAB(10);"MONTH"M1"NET"
1272 PRINT#-Z
1274 PRINT#-Z,TAB(5);"INCOME";TAB(18);
1276 PRINT#-Z,USING"######.##";T1
1278 PRINT#-Z,TAB(5);"DEBITS";TAB(18);
1280 PRINT#-Z,USING"######.##";T2
1282 PRINT#-Z,TAB(18);STRING$(9,"-")
1284 PRINT#-Z,TAB(5);"NET";TAB(18);
1286 PRINT#-Z,USING"######.##";T3
1288 PRINT#-Z:PRINT#-Z:IFZ=2THEN1294
1290 PRINT@294,"<P>RINT OR <R>ETURN"
1292 PRINT@353,"NOTE: YOU CAN DECLARE ACCOUNTS":PRINT@385,"AS INCOME/DEBITS BY PLACING A":PRINT@417,"+/- IN THE FIRST CHARACTER OF"
1293 PRINT@449,"THE ACCOUNT NAME USING OPTION":PRINT@481,"<2> OF THE MAIN MENU.";
1294 Q$=INKEY$:IFQ$<>"P"ANDQ$<>"R"THEN1294
1296 IFQ$="R"THENZ=0ELSE1298
1297 GOTO1100
1298 Z=2:GOTO1266
1300 D=1
1305 CLS:PRINT@12,"ACCOUNTS"
1310 FORI=1TO13
1315 PRINT@(3+I*32),A$(I)"  "A1$(I)
1320 PRINT@(17+I*32),A$(I+13)"  "A1$(I+13)
1325 NEXT
1330 PRINT@480,"PRESS <A-Z> OR <CLEAR> TO QUIT";
1335 Q$=INKEY$:IFQ$=""THEN1335
1340 IFQ$=CHR$(12)THEN1400
1345 FORI=1TO26:IFQ$=CHR$(64+I)THEN1350
1346 NEXT
1347 GOTO1335
1350 IFP=1THEN1380
1351 IFP1=1THEN1385
1352 IFA=1THEN1390
1353 PRINT@480,S$;
1354 PRINT@480,"ENTER ACCOUNT NAME  ";:LINEINPUTQ$
1360 IFQ$=""THENA1$(I)=A2$ELSE1370
1365 GOTO1305
1370 A1$(I)=LEFT$(Q$,8):GOTO1305
1380 P=0:GOTO1184
1385 P1=0:GOTO2110
1390 A=0:GOTO445
1400 IFP=1ORP1=1THEN1410
1405 IFD=1ORA=1THEN1420
1410 P=0:P1=0:GOTO1000
1420 D=0:A=0:GOTO100
1600 CLS:N=1
1605 PRINT@106,"SORT OPTIONS"
1607 PRINT@166,"<1> SORT BY MONTH"
1610 PRINT@198,"<2> SORT BY ACCOUNT"
1615 PRINT@230,"<3> MAIN MENU"
1617 PRINT@294,"ENTER <1-3>";:INPUTQ
1620 IFQ<0ANDQ>3THEN1600
1625 ON Q GOTO1630,1720,100
1630 FOR I=1TO12
1635 GOSUB12
1640 IFD$(N,2)=""THEN100
1645 IF D(N,1)=I THEN1650ELSE1655
1650 N=N+1:GOTO1635
1655 S=N
1660 S=S+1:IFD$(S,2)=""THEN1700
1665 IFD(S,1)<>I THEN1660
1670 Y$(1)=D$(N,1):Y$(2)=D$(N,2):Y(1)=D(N,1):Y(2)=D(N,2)
1675 D$(N,1)=D$(S,1):D$(N,2)=D$(S,2):D(N,1)=D(S,1):D(N,2)=D(S,2)
1680 D$(S,1)=Y$(1):D$(S,2)=Y$(2):D(S,1)=Y(1):D(S,2)=Y(2)
1685 N=N+1:GOSUB12
1690 IF Q=2 THEN1765
1695 GOTO1660
1700 NEXT
1720 FOR I=1TO26
1725 GOSUB12
1730 IFD$(N,2)=""THEN100
1735 IFD$(N,1)=A$(I)THEN1740 ELSE1745
1740 N=N+1:GOTO1725
1745 S=N
1750 S=S+1:IFD$(S,2)=""THEN1770
1755 IFD$(S,1)<>A$(I)THEN1750
1760 GOTO1670
1765 GOTO1750
1770 NEXT
1900 CLS:N=1
1905 PRINT@194,"INSERT DATA DISK NOW"
1910 PRINT@226,"PRESS <ENTER> TO SAVE OR"
1915 PRINT@258,"ENTER <M>ENU  ";:LINEINPUTQ$
1920 IFQ$<>""ANDQ$<>"M"THEN1900
1925 IFQ$=""THEN1930ELSE100
1930 OPEN "O",#1,"ACCOUNT/DAT"
1935 FOR I=1TO26
1940 PRINT#1,A1$(I)
1945 NEXT
1950 IF D$(N,2)=""THEN1965
1952 IF D$(N,2)=DE$THEN1960
1955 PRINT#1,D$(N,1)
1956 PRINT#1,D$(N,2)
1957 PRINT#1,D(N,1)
1958 PRINT#1,D(N,2)
1960 N=N+1:GOTO1950
1965 CLOSE#1:GOTO100
2000 CLS:PRINT@229,"PRINTING ACCOUNT NAMES"
2003 PRINT#-2:PRINT#-2,TAB(11);"ACCOUNTS"
2004 PRINT#-2
2005 FOR I=1TO13
2010 PRINT#-2,TAB(3);A$(I)"="A1$(I);TAB(17);A$(I+13)"="A1$(I+13)
2015 NEXT
2020 PRINT#-2:PRINT#-2
2025 GOTO1000
2100 N=1:Z=0
2105 P1=1:GOTO1305
2110 CLS:PRINT@227,"CALCULATING ACCOUNT TOTALS"
2115 FORX=1TO12:MT(X)=0:NEXT
2120 IFD$(N,2)=""THEN2145
2125 IFD$(N,1)<>A$(I)THEN2140
2130 IFD$(N,2)=DE$THEN2140
2135 MT(D(N,1))=MT(D(N,1))+D(N,2)
2140 N=N+1:GOTO2120
2145 CLS
2150 PRINT#-Z,TAB(12);A1$(I):IFZ=2THENPRINT#-Z
2155 PRINT#-Z,TAB(8);"MONTH";TAB(19);"TOTAL"
2160 FORX=1TO12
2165 PRINT#-Z,TAB(6);:PRINT#-Z,USING"#####";X;
2170 PRINT#-Z,TAB(15);:PRINT#-Z,USING"######.##";MT(X)
2175 NEXT
2180 PRINT#-Z
2183 IFMN=1THEN2310
2185 IFZ=0THEN2195
2190 PRINT#-Z:GOTO2200
2195 PRINT@483,"<P>RINT OR <N>EXT ACCOUNT";
2200 Q$=INKEY$:IFQ$<>"P"ANDQ$<>"N"THEN2200
2205 IFQ$="N"THEN2100
2210 Z=2:GOTO2150
2250 CLS:PRINT@228,"CALCULATING MONTHLY NETS"
2255 N=1:Z=0
2260 FORX=1TO12:MT(X)=0:NEXT
2265 IFD$(N,2)=""THEN2295
2270 IFD$(N,2)=DE$THEN2290
2275 I=ASC(D$(N,1))-64
2280 IFLEFT$(A1$(I),1)="-"THENMT(D(N,1))=MT(D(N,1))-D(N,2)
2285 IFLEFT$(A1$(I),1)="+"THENMT(D(N,1))=MT(D(N,1))+D(N,2)
2290 N=N+1:GOTO2265
2295 CLS
2297 PRINT#-Z,TAB(11);"NET INCOME"
2300 IFZ=2THENPRINT#-Z
2305 MN=1:GOTO2155
2310 MN=0:IFZ=0THEN2312
2311 PRINT#-Z:GOTO2313
2312 PRINT@487,"<P>RINT OR <M>ENU";
2313 Q$=INKEY$:IFQ$<>"P"ANDQ$<>"M"THEN2313
2315 IFQ$="M"THEN1000
2320 Z=2:GOTO2297
