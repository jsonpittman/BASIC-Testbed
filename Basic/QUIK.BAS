1 'QUIK
2 'BY GEORGE & ELLEN AFTAMONOW
3 'COPYRIGHT (C) 1993
4 'FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 PALETTE0,0:PALETTE5,63:HSCREEN2:HCLS0:HCOLOR5,0
15 HPRINT(10,10),"(C)MP OR (R)GB?"
20 I$=INKEY$:IFI$=""THEN 20
30 IFI$="C"THEN40ELSEIFI$="R"THEN50ELSE20
40 PALETTECMP:PALETTE0,0:PALETTE1,17:PALETTE2,39:PALETTE3,57:PALETTE4,53:PALETTE5,63:PALETTE6,41:PALETTE7,57:PALETTE8,44:PALETTE12,39:PALETTE14,51:GOTO70
50 PALETTERGB:PALETTE0,0:PALETTE1,17:PALETTE2,38:PALETTE3,29:PALETTE4,55:PALETTE5,63:PALETTE6,45:PALETTE7,57:PALETTE8,44:PALETTE12,38:PALETTE13,42:PALETTE14,15
70 DATA6,6,1,17,"H",160,6,2,38,"U",315,6,3,29,"E",315,90,4,55,"R",315,174,5,63,"F",160,174,6,45,"D",6,174,7,57,"G",6,90,8,44,"L"
75 FORZ=1TO8:READX(Z),Y(Z),P(Z),C(Z),C$(Z):NEXT
80 HCLS0:HSCREEN2:HCOLOR12
90 HLINE(60,50)-(270,130),PSET,B
100 HDRAW"BM2,4R34G10F4G12H6G10U30":HDRAW"BM160,4F22L14D6L16U6L14E22":HDRAW"BM318,4D32H10G6H12E6H10R32":HDRAW"BM318,90G22U14L6U16R6U14F22":HDRAW"BM2,90E22D14R6D16L6D14H22"
105 HDRAW"BM2,178U32F10E6F12G6F10L32":HDRAW"BM160,178H22R16U6R12D6R16G22":HDRAW"BM318,178L32E10H6E12F6E10D32"
106 FORZ=1TO8:HPAINT(X(Z),Y(Z)),P(Z),12:NEXT
110 HDRAW"BM100,80L6NU16H2U12E2R6F2D12G2NF2BR14L6NU16H2U14R2BR8D14G2BR8L2U16R2D16BR14L6NU16H2U12E2R6F2BD12G2BR8L2U16R2D8NE8NF8D8"
115 HDRAW"BM190,80L6NU16L2NLU16NLR8F2D12G2BR8NU16L2U16R8F2D4G2L6F8BR6NU16L2U14E2R6F2D6NL8D8BR6H2U14R2D16E6NU4F6U16"
120 HPRINT(20,12),"BY":HPRINT(9,14),"GEORGE & ELLEN AFTAMONOW"
130 FORQ=1TO3:PLAY"T255O5BADBADBAD":NEXTQ
140 HCOLOR0:HLINE(62,52)-(268,128),PSET,BF
150 HCOLOR5:HPRINT(10,10),"NEED INSTRUCTIONS Y/N?"
160 I$=INKEY$:IFI$=""THEN160
200 HCOLOR0:HPRINT(10,10),"NEED INSTRUCTIONS Y/N?":IF I$="N" THEN 214
202 HCOLOR3:HPRINT(9,8),"THERE ARE EIGHT ARROWS.":HPRINT(9,9),"EACH IS POINTING IN THE":HPRINT(9,10),"DIRECTION THAT COINCIDES":HPRINT(9,11),"WITH THE COMPUTER'S DRAW":HPRINT(9,12),"COMMAND.":HPRINT(13,14),"PRESS ANY KEY"
203 I$=INKEY$:IFI$=""THEN 203
204 HCOLOR0:HLINE(62,52)-(268,128),PSET,BF
205 HDRAW"BM164,90C3NL12NH12NU12NE12NR12NF12ND12G12":HPRINT(15,11),"L":HPRINT(25,11),"R":HPRINT(20,8),"D"
206 HCOLOR2:HPRINT(17,9),"H":HPRINT(23,9),"E":HPRINT(23,13),"F":HPRINT(17,13),"G"
207 I$=INKEY$:IFI$=""THEN207
208 HCOLOR0:HLINE(110,60)-(210,120),PSET,BF
210 HCOLOR4:HPRINT(9,8),"AN ARROW WILL RANDOMLY":HPRINT(9,9),"FLICKER. THE OBJECT IS":HPRINT(9,10),"TO PRESS THE KEY THAT":HPRINT(9,11),"REPRESENTS THE ARROW'S":HPRINT(9,12),"DIRECTION.":HPRINT(13,14),"PRESS ANY KEY"
212 I$=INKEY$:IFI$=""THEN212
213 HCOLOR0:HLINE(62,52)-(266,126),PSET,BF
214 HCOLOR4:HPRINT(15,8),"CHOOSE 1-3":HPRINT(14,10),"1) SHORT GAME":HPRINT(14,11),"2) MED. GAME":HPRINT(14,12),"3) LONG GAME"
220 I$=INKEY$:IFI$=""THEN220
222 IFI$="1"THENG=600ELSE IFI$="2"THENG=1200ELSEIFI$="3"THENG=1800ELSE222
225 P$="T250O2L200GEA":PLAYP$
230 HCOLOR0:HLINE(106,62)-(220,104),PSET,BF
240 T=0:SC=0
250 C=RND(8)
260 T=TIME
300 T=T+1:I$=INKEY$:IFT=G THEN340ELSEIFI$="" THEN PALETTEC,0:PALETTEC,C(C):GOTO300
310 IFI$<>C$(C) THEN 300
320 SC=SC+1:GOTO250
340 PLAY"T200L200EAGAEAGA":HCOLOR2:HPRINT(15,8),"YOU SCORED":HPRINT(11,10),STR$(SC)+" IN"+STR$(G/20)+" SECONDS."
350 HPRINT(14,14),"PLAY AGAIN?"
360 I$=INKEY$:IFI$=""THEN360
370 IFI$="Y" THEN213 ELSEIFI$="N" THEN CLS:RGB:END:ELSE360
