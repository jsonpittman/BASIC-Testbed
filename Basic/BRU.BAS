10 'BACK UP AND RESTORE DIRECTORY
20 'COPYRIGHT (C) 1987
30 'CARL ENGLAND
40 CLS:CLEAR5000
50 PRINT"<1> BACKUP DIRECTORY
60 PRINT"<2> RESTORE DIRECTORY
70 I$=INKEY$:IFI$<"1"ORI$>"2"THEN70
80 ONVAL(I$)GOTO90,260
90 FORI=13TO18
100 DSKI$0,17,I,A$,B$
110 IFB$="BRU"+STRING$(125,255)THEN160
120 IFA$=STRING$(128,255)ANDB$=A$THEN150
130 PRINTA$B$:PRINT"TRACK 17, SECTOR"I:PRINT"CONTAINS DATA.  DO YOU WISH TO  OVERWRITE IT?
140 I$=INKEY$:IFI$="N"THENENDELSEIFI$<>"Y"THEN140
150 NEXT
160 DSKI$0,17,2,A$,B$:B$="BRU"+STRING$(125,255):DSKO$0,17,13,A$,B$
170 FORI=3TO12STEP2
180 DSKI$0,17,I,A$,B$:DSKI$0,17,I+1,C$,D$
190 A$=A$+LEFT$(B$,112):C$=C$+LEFT$(D$,112)
200 B$="":D$=""
210 FORJ=0TO7
220 B$=B$+MID$(A$,J*32+1,16):D$=D$+MID$(C$,J*32+1,16)
230 NEXT
240 DSKO$0,17,(I+25)/2,B$,D$
250 NEXT:END
260 DSKI$0,17,13,A$,B$:IFB$<>"BRU"+STRING$(125,255)THENCLS:PRINT"ERROR:",,"DISK DOES NOT CONTAIN BRU FILE":END
270 DSKO$0,17,2,A$,B$
280 FORI=3TO12STEP2
290 DSKI$O,17,(I+25)/2,A$,B$
300 GOSUB350
310 DSKO$0,17,I,C$,D$
320 A$=B$:GOSUB350
330 DSKO$0,17,I+1,C$,D$
340 NEXT:END
350 C$="":D$=""
360 FORJ=0TO3
370 C$=C$+MID$(A$,J*16+1,16)+STRING$(16,255)
380 D$=D$+MID$(A$,J*16+65,16)+STRING$(16,255)
390 NEXT:RETURN
