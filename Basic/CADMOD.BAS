0 ' COPYRIGHT 1989  FALSOFT,INC
10 CLEAR 500,&H64FF:CLS:INPUT"COCOCAD VR 2.0 P.KERCKHOFF      CLEAR PAGE MEMORY (Y/N)";A$:GOTO120
20 X=JOYSTK(0)*4:Y=JOYSTK(1)*4:P=ABS((1 AND PEEK(&HFF00))*3-3):IF X<3 THEN X=3 ELSE IF X>252 THEN X=252
30 IF Y<3 THEN Y=3 ELSE IF Y>188 THEN Y=188
40 RETURN
50 GET(0,0)-(6,6),C1,G:LINE(0,0)-(6,6),PRESET,BF:DRAW"BM3,3XC$(N);":GET(0,0)-(6,6),C2,G:PUT(0,0)-(6,6),C1,PSET:RETURN
60 GET(X-3,Y-3)-(X+3,Y+3),C1,G:PUT(X-3,Y-3)-(X+3,Y+3),C2,AND:RETURN
70 PUT(X-3,Y-3)-(X+3,Y+3),C1,PSET:RETURN
80 IF ASC(A$)>62 THEN 90 ELSE ON ASC(A$)-31 GOTO 1120,1130,1140,1150,1160,1170,1180,1190,1200,1210,1220,1230,1240,1250,1260,1270,1280,1290,1300,1310,1320,1330,1340,1350,1360,1370,1380,1390,1400,1410,1420
90 ON ASC(A$)-62 GOTO 1430,1440,1450,1460,1470,1480,1490,1500,1510,1520,1530,1540,1550,1560,1570,1580,1590,1600,1610,1620,1630,1640,1650,1660,1670,1680,1690,1700
100 FOR C=1 TO LEN(T$):DRAW"A0;BM=TX;,=TY;":A$=MID$(T$,C,1):GOSUB 80:TX=TX+5:NEXT C:RETURN
110 LINE(221,9)-(236,13),PRESET,BF:T$=STR$(768-AD(PG)+A(PG)):T$=RIGHT$(T$,LEN(T$)-1):TX=221:TY=13:GOSUB100:IFLEN(T$)=1THENPLAY"EEO3EEO2EEO1EEO4EE":RETURN ELSE RETURN
120 DIM C$(3),A(8),AD(8),C1(1),C2(1),C3(1),L1(6),L2(6),L3(6),L4(6),CM(15),CO(15),MD(255),MO(255):B$="V31L100O4B":NF$="NONE"
130 C$(0)="BHNL2U2BR2D2R2BD2L2D2BL2U2L2":C$(1)="BH3ND2R2BR2R2D2BD2D2L2BL2L2U2":C$(2)="R3":C$(3)="NF2H2ND3R3":FOR X=0 TO 8:A(X)=X*768+&H6500:IF A$="Y" THEN POKEA(X),0:NEXT ELSE NEXT
140 PG=0:GOSUB1710:MN=0:N=3:GOSUB50
150 GOSUB20:GOSUB60:IF P<>3 OR Y>20 THEN GOSUB 70:GOTO 150 ELSE GOSUB 70: F=INT(X/20):IF F>6 THEN 150 ELSE LINE(F*19+1,1)-(F*19+20,20),PRESET,B:F=F+1:PLAYB$
160 GOSUB 20:IF Y<20 OR P=3 THEN 160 ELSE IF MN=0 THEN ON F GOTO 190,290,440,290,520,290,180 ELSE ON F GOTO180,580,290,680,700,740,820
170 DRAW"A0":PLAYB$:F=F-1:LINE(F*19+1,1)-(F*19+20,20),PSET,B:N=3:GOSUB50:GOTO150
180 LINE(0,0)-(134,20),PRESET,BF:FORX=0TO6:LINE(X*19+1,1)-(X*19+20,20),PSET,B:NEXT:IF MN=1 THEN MN=0:GOSUB 1760:GOTO170 ELSE MN=1:GOSUB 1770:GOTO170
190 N=0:GOSUB50:LX=-1
200 GOSUB20:IFP=3THEN200
210 GOSUB20:GOSUB60:IFY>20THEN220ELSEGOSUB70:IFLX=-1THEN170ELSEPUT(LX-3,LY-3)-(LX+3,LY+3),C3,PSET:GOTO170
220 GOSUB70:IFP<>3THENX1=X:Y1=Y:GOTO210ELSEPLAYB$:X=X1:Y=Y1
230 IF LX=-1 THEN LX=X:LY=Y:GET(LX-3,LY-3)-(LX+3,LY+3),C3,G:DRAW"BM=LX;,=LY;XC$(1);":GOTO200
240 PUT(LX-3,LY-3)-(LX+3,LY+3),C3,PSET:IF Y<27 THEN 170
250 X=INT(X/4)*4:Y=INT(Y/4)*4
260 LINE(X,Y)-(LX,LY),PSET:POKEAD(PG),52:POKEAD(PG)+1,X:POKEAD(PG)+2,Y:POKEAD(PG)+3,LX:POKEAD(PG)+4,LY:AD(PG)=AD(PG)+5:POKEAD(PG),0:GOSUB110:LX=-1:GOTO200
270 GET(PX,PY)-(LX,PY),L1,G:GET(PX,PY)-(PX,LY),L2,G:GET(LX,LY)-(PX,LY),L3,G:GET(LX,LY)-(LX,PY),L4,G:LINE(PX,PY)-(LX,LY),PSET,B:RETURN
280 PUT(PX,PY)-(LX,PY),L1,PSET:PUT(PX,PY)-(PX,LY),L2,PSET:PUT(LX,LY)-(PX,LY),L3,PSET:PUT(LX,LY)-(LX,PY),L4,PSET:RETURN
290 N=0:GOSUB50
300 GOSUB20:GOSUB60:IF Y<20 THEN GOSUB70:GOTO170 ELSE IF P<>3 THEN GOSUB70:GOTO300
310 GOSUB70:LX=X:LY=Y:PX=LX:PY=LY
320 GOSUB20:IFP=3THEN320
330 GOSUB270:GOSUB 20:IF Y<20 THEN GOSUB 280:GOTO 170 ELSE IF P<>3 THEN GOSUB 280:PX=X:PY=Y:GOTO 330
340 POKEAD(PG)+1,LX:POKEAD(PG)+2,LY:POKEAD(PG)+3,PX:POKEAD(PG)+4,PY:POKEAD(PG)+5,0:IF F=2 THEN POKE AD(PG),53:AD(PG)=AD(PG)+5:GOSUB110:GOTO290
350 IF F<>4 THEN 410 ELSE LINE(LX,LY)-(PX,PY),PRESET,B:POKEAD(PG),55:
360 X=PX:Y=PY:IF X>LX THEN PX=LX:LX=X:X=PX
370 IF Y>LY THEN PY=LY:LY=Y:Y=PY
380 AD(PG)=AD(PG)+5:GOSUB110:FOR X1=X TO LX STEP 8:Y1=X1+4:IF Y1>LX THEN Y1=LX
390 LINE(X1,Y)-(Y1,Y),PSET:LINE(X1,LY)-(Y1,LY),PSET:NEXT:FOR X1=Y TO LY STEP8:Y1=X1+4:IF Y1>LY THEN Y1=LY
400 LINE(X,X1)-(X,Y1),PSET:LINE(LX,X1)-(LX,Y1),PSET:NEXT:GOTO290
410 LINE(LX,LY)-(PX,PY),PRESET,BF:IF PX>LX THEN X=PX:PX=LX:LX=X
420 IF PY>LY THEN Y=PY:PY=LY:LY=Y
430 PX=INT(PX/8)*8:PY=INT(PY/8)*8+4:FOR X=PX TO LX STEP 8:FOR Y=PY TO LY STEP 8:PSET(X,Y):NEXT Y,X:POKEAD(PG),54:AD(PG)=AD(PG)+5:GOSUB110:GOTO290
440 R=1:T$="COMPONENTS":GOSUB850:T$="NEXT   USE":TX=55:TY=115:GOSUB100:T$="PREV   ROT":TX=55:TY=107:GOSUB100:N=3:GOSUB50:N=1:DRAW"BM80,80":GOSUB870
450 GOSUB20:GOSUB60:GOSUB70:IF Y<27 THEN PLAYB$:PUT(50,50)-(110,120),MO,PSET:GOTO170 ELSE IF Y<100 OR Y>118 OR X<55 OR X>110 OR P<>3 THEN 450 ELSE PLAY B$
460 IF X>80 AND Y>109 THEN GOTO461 ELSE GOTO463
461 IF INT(R/2)=R/2 THEN XW=YY:YW=XX:GOTO462 ELSE XW=XX:YW=YY
462 HX=80-(XW/2):VX=80-(YW/2):GET(HX,VX)-(HX+XW,VX+YW),CM,G:PUT(50,50)-(110,120),MO,PSET:GOTO480
463 IF X>80 AND Y<109 THEN DRAW"BM80,80C5":GOSUB870:R=R+1 ELSE GOTO465
464 IF R>4 THEN R=1: DRAW"BM80,80C0":GOSUB870:GOTO450 ELSE DRAW"BM80,80C0":GOSUB870:GOTO450
465 IF X<80 THEN DRAW"BM80,80C5":GOSUB870:R=1:DRAW"A0;BM80,80C0":IF Y<109 THEN 470 ELSE N=N+1:IF N<20 THEN GOSUB870:GOTO450 ELSE N=1:GOSUB870:GOTO450
470 N=N-1:IF N>0 THEN GOSUB870:GOTO450 ELSE N=19:GOSUB870:GOTO450
480 GOSUB20
490 GET(X,Y)-(X+XW,Y+YW),CO,G:PUT(X,Y)-(X+XW,Y+YW),CM,AND:LX=X:LY=Y:GOSUB20:IF P<>3 AND Y>26 THEN PUT(LX,LY)-(LX+XW,LY+YW),CO,PSET: GOTO 490
500 IF Y<27 THEN PUT(LX,LY)-(LX+XW,LY+YW),CO,PSET:DRAW"A0":GOTO 170
510 PLAYB$:POKEAD(PG),N:POKEAD(PG)+1,(LX+(XW/2)):POKEAD(PG)+2,(LY+(YW/2)):POKEAD(PG)+3,R:AD(PG)=AD(PG)+4:POKEAD(PG),0:GOSUB110:GOTO480
520 N=2:GOSUB50
530 GOSUB20:GOSUB60:GOSUB70:IF P<>3 AND Y>26 THEN 530 ELSE Y=Y-2
540 IFY<27 THEN 170 ELSE PLAYB$:POKEAD(PG),51:POKEAD(PG)+1,X:POKEAD(PG)+2,Y:AD(PG)=AD(PG)+3:GOSUB110:X1=AD(PG)
550 GOSUB60:A$=INKEY$:GOSUB70:IF A$="" THEN 550 ELSE PLAY B$:IF A$=CHR$(13) THEN POKE AD(PG),255:AD(PG)=AD(PG)+1:POKEAD(PG),0:GOSUB110:GOTO520
560 IF A$=CHR$(8) THEN IF AD(PG)>X1 THEN AD(PG)=AD(PG)-1:X=X-5:LINE(X,Y)-(X+5,Y-6),PRESET,BF:GOTO550 ELSE GOTO550
570 POKEAD(PG),ASC(A$):AD(PG)=AD(PG)+1:DRAW"BM=X;,=Y;":GOSUB80:X=X+5:GOSUB110:GOTO550
580 N=0:GOSUB50
590 GOSUB20:GOSUB60:IF P<>3 AND Y>26 THENGOSUB70:GOTO590ELSEGOSUB70:IF Y<27 THEN170
600 PLAYB$:LX=X:LY=Y:PX=LX:PY=LY
610 GOSUB20:IFP=3THEN610
620 GOSUB270:GOSUB20:IFY<26THENGOSUB280:GOTO170ELSEIFP<>3THENGOSUB280:PX=X:PY=Y:GOTO620
630 GOSUB280:PLAYB$:XW=ABS(PX-LX):YW=ABS(PY-LY):IF (XW*YW)>10000 THEN 580 ELSE GET(LX,LY)-(PX,PY),MD,G:POKEAD(PG)+1,PX:POKEAD(PG)+2,PY:POKEAD(PG)+3,LX:POKEAD(PG)+4,LY
640 GOSUB20:GET(X,Y)-(X+XW,Y+YW),MO,G:PUT(X,Y)-(X+XW,Y+YW),MD,AND:PUT(X,Y)-(X+XW,Y+YW),MO,PSET:IFY<27THEN170ELSEIFP<>3THENX1=X:Y1=Y:GOTO640ELSEX=X1:Y=Y1:PUT(X,Y)-(X+XW,Y+YW),MD,AND
650 POKEAD(PG),56:POKEAD(PG)+5,X:POKEAD(PG)+6,Y:AD(PG)=AD(PG)+7:POKEAD(PG),0:GOSUB110:GOTO580
680 LINE(166,13)-(171,8),PSET,BF:A$=INKEY$:LINE(166,13)-(171,8),PRESET,BF:IF A$<"0" OR A$>"8" THEN 680
690 PG=VAL(A$):DRAW"BM166,13":GOSUB80:LINE(8,28)-(247,183),PRESET,BF:GOSUB1790:FORX=8TO248STEP8:FORY=28TO184STEP8:PSET(X,Y):NEXTY,X:GOSUB110:GOTO170
700 T$="  CLEAR":GOSUB850:T$="YES     NO":TX=55:TY=115:GOSUB100:N=3:GOSUB50:TX=63:TY=70:T$="DO YOU":GOSUB100:TX=60:TY=80:T$="WANT TO":GOSUB100:TX=63:TY=90:T$="CLEAR":GOSUB100:TX=55:TY=100:T$="THE PAGE?":GOSUB100
710 GOSUB20:GOSUB60:GOSUB70:IFY<27 THEN PLAYB$:PUT(50,50)-(110,120),MO,PSET:GOTO170 ELSE IF Y<100 OR Y>120 OR X<55 OR X>110 OR P<>3 THEN 710 ELSE PLAY B$
720 IF X>80 THEN PUT(50,50)-(110,120),MO,PSET:GOTO170
730 POKEA(PG),0:AD(PG)=A(PG):GOSUB110:LINE(8,28)-(247,183),PRESET,BF:FORX=8TO248STEP8:FORY=28TO184STEP8:PSET(X,Y):NEXTY,X:GOTO170
740 T$="   DISK":GOSUB850:T$="NAME":TX=70:TY=70:GOSUB100:T$="LOAD":TX=70:TY=80:GOSUB100:T$="SAVE":TX=70:TY=90:GOSUB100:N=3:GOSUB50
750 GOSUB20:GOSUB60:GOSUB70:IF Y<27 THEN PLAYB$:PUT(50,50)-(110,120),MO,PSET:GOTO170 ELSE IF Y<60 OR Y>90 OR X<70 OR X>90 OR P<>3 THEN 750 ELSE PLAY B$
760 IF Y>70 THEN 770 ELSE CLS:LINEINPUT"FILE NAME =>";NF$:SCREEN1,1:LINE(170,5)-(210,0),PRESET,BF:TY=5:TX=170:T$=NF$:GOSUB100:GOTO750
770 IF NF$="" OR NF$="NONE" THEN 750 ELSE IF Y>80 THEN 780 ELSE TX=60:TY=110:T$="LOADING":GOSUB100:LOADM NF$:A$="0":GOTO 690
780 IF Y>90 THEN 750 ELSE TX=60:TY=110:T$="SAVING":GOSUB100:SAVEM NF$,&H6500,&H7FFF,&H6500:LINE(60,110)-(90,105),PRESET,BF:GOTO750
820 T$="   QUIT":GOSUB850:TX=60:TY=70:T$="SAVE TO":GOSUB100:TX=70:TY=80:T$="DISK":GOSUB100:T$="YES     NO":TX=55:TY=115:GOSUB100:N=3:GOSUB50
830 GOSUB20:GOSUB60:GOSUB70:IF Y<27 THEN PLAYB$:PUT(50,50)-(110,120),MO,PSET:GOTO170 ELSE IF Y<100 OR Y>120 OR X<50 OR X>110 OR P<>3 THEN 830 ELSE PLAY B$
840 IF X<80 THEN PUT(50,50)-(110,120),MO,PSET:GOTO740 ELSE CLS:INPUT"DO YOU WANT A PRINTOUT (Y/N)";A$:IF A$="Y" THEN 1970 ELSE PRINT "OK...BYE.":STOP
850 GET(50,50)-(110,120),MO,G:LINE(50,50)-(110,120),PRESET,BF:LINE(51,51)-(108,118),PSET,B:LINE(52,119)-(109,119),PSET:LINE-(109,52),PSET:LINE(51,60)-(108,60),PSET:TX=54:TY=57:GOSUB100:RETURN
870 ON R GOTO 871,872,873,874
871 DRAW"A0":GOTO875
872 DRAW"A1":GOTO875
873 DRAW"A2":GOTO875
874 DRAW"A3":GOTO875
875 ON N GOTO 880,890,900,910,920,930,940,950,960,970,980,990,1010,1030,1050,1060,1070,1080,1100
880 DRAW"BG10BU2U16R13FRF4DFD2GDG4LGL13":XX=20:YY=16:RETURN:'AND
890 DRAW"BG12BU4U16R12FRF4DFDE2RF2G2LH2DGDG4LGL12":XX=24:YY=16:RETURN:'NAND
900 DRAW"BG10BU2E2UEU2EU2HU2HUH2R9FRFRF2RF4G4LG2LGLGL9":XX=20:YY=16:RETURN:'OR
910 DRAW"BG12BU4E2UEU2EU2HU2HUH2R9FRFRF2RF4E2F2G2H2G4LG2LGLGL9":XX=24:YY=16:RETURN:'NOR
920 DRAW"BG8U16FRFRFRFRFR2FRFRFGLGLGL2GLGLGLGLG":XX=16:YY=16:RETURN:'BUFFER
930 DRAW"BG10BU2U16FRFRFRFRFR2FRFRFE2F2G2H2GLGLGL2GLGLGLGLG":XX=20:YY=16:RETURN:'INVTR
940 DRAW"BG10BU2E2UEU2EU2HU2HUH2BR3R6FRFRF2RF4G4LG2LGLGL6E2UEU2EU2HU2HUH2":XX=20:YY=16:RETURN:'XOR
950 DRAW"BG12BU4E2UEU2EU2HU2HUH2BR3R6FRFRF2RF4E2F2G2H2G4LG2LGLGL6E2UEU2EU2HU2HUH2":XX=24:YY=16:RETURN:'XNOR
960 DRAW"BG10U20FRFRFRFRFR2FRFRFRFRFGLGLGLGLGL2GLGLGLGLGU6BR3R2LDU2BU7LR2":XX=20:YY=20:RETURN:'OPAMP
970 DRAW"BG8BU8BR2R6NU5ND5E6U2D2G6F5L2E2D2FD2":XX=12:YY=16:RETURN:'NPN
980 DRAW"BG8BU8BR2R6NU5ND5E6U2D2G2U2F2L2G4F6D2":XX=12:YY=16:RETURN:'PNP
990 DRAW"BG16BU16R4E2F4E4F4E4F4E2R4":XX=32:YY=8:RETURN:'REST
1010 DRAW"BG6BU6R4NU4ND4BR4NU4ND4R4":XX=12:YY=8:RETURN:'CAP
1030 DRAW"BG4BU4U2R2F2NR4G2L2U2":XX=8:YY=8:RETURN:'I/O
1050 DRAW"BG2BU2UER2FD2GL2HU":XX=8:YY=8:RETURN:'NOT DOT
1060 DRAW"BG2BU2NR4BUNR4BER2BFBD2NL4BGL2":XX=8:YY=8:RETURN:'CON DOT
1070 DRAW"BG4BU8BR4D5L3F3E3L3":XX=8:YY=8:RETURN:'GND
1080 DRAW"BG8BU8R6ND4U4F4NG4NU4ND4R6":XX=16:YY=8:RETURN:'DIODE
1100 DRAW"BG16BU14R4E2F4E4UNH2NE2U5D6F4E4F4E2R4":XX=32:YY=12:RETURN:'POT
1120 DRAW"BR4":RETURN
1130 DRAW"RBU2U2LD2":RETURN
1140 DRAW"BU2U2BR2D2":RETURN
1150 DRAW"BRU4BR2D4URL4RU2LR4":RETURN
1160 DRAW"R3U2L3U2R3LD4":RETURN
1170 DRAW"E4BL3DBR3BD2D":RETURN
1180 DRAW"BR2NU4H2R3":RETURN
1190 DRAW"BRBU4D2":RETURN
1200 DRAW"BR2HU2E":RETURN
1210 DRAW"BREU2H":RETURN
1220 DRAW"BUE2GHF2":RETURN
1230 DRAW"BU2R2LUD2":RETURN
1240 DRAW"BRU2":RETURN
1250 DRAW"BU2R3":RETURN
1260 DRAW"BRURD":RETURN
1270 DRAW"UE2U":RETURN
1280 DRAW"U4R3D4L3":RETURN
1290 DRAW"R2LU4G":RETURN
1300 DRAW"NR3U2R3U2L3":RETURN
1310 DRAW"R3U2NL2U2L3":RETURN
1320 DRAW"BU4D2R3U2D4":RETURN
1330 DRAW"R3U2L3U2R3":RETURN
1340 DRAW"NU2R3U2L3U2R3":RETURN
1350 DRAW"BU4R3DGD2":RETURN
1360 DRAW"NU4R3U2NL3U2L3":RETURN
1370 DRAW"R3U2L3U2R3D2":RETURN
1380 DRAW"RULBU2RUL":RETURN
1390 DRAW"BU4RDLBD2RD":RETURN
1400 DRAW"BU2NE2F2":RETURN
1410 DRAW"BUR3BU2L3":RETURN
1420 DRAW"E2H2":RETURN
1430 DRAW"BU3UR3D2LBDD":RETURN
1440 DRAW"NR3U3ER2BD2LDRU":RETURN
1450 DRAW"U3ERFDNL3D2":RETURN
1460 DRAW"U4R2FGNL2FGL2":RETURN
1470 DRAW"BRREGLHU2ERF":RETURN
1480 DRAW"U4R2FD2GL2":RETURN
1490 DRAW"NR3U2NR2U2R3":RETURN
1500 DRAW"U2NR2U2R3":RETURN
1510 DRAW"BU4BR3L2GD2FREUL":RETURN
1520 DRAW"U4D2R3U2D4":RETURN
1530 DRAW"R2LU4NLR":RETURN
1540 DRAW"BUFREU3":RETURN
1550 DRAW"U2RNF2NE2LU2":RETURN
1560 DRAW"NR3U4":RETURN
1570 DRAW"U4FRED4":RETURN
1580 DRAW"U4F3DU4":RETURN
1590 DRAW"BUU2ERFD2GLH":RETURN
1600 DRAW"U4R2FGL2":RETURN
1610 DRAW"BUU2ERFD2GNUNRLH":RETURN
1620 DRAW"U4R2FGL2RF2":RETURN
1630 DRAW"R2EHLHER2":RETURN
1640 DRAW"BU4R2LD4":RETURN
1650 DRAW"NU4R3U4":RETURN
1660 DRAW"BU4D3FREU3":RETURN
1670 DRAW"NU4ERFU4":RETURN
1680 DRAW"UE2UDGHUDF2D":RETURN
1690 DRAW"BU4DFEUDGD2":RETURN
1700 DRAW"NR2UE2UL2":RETURN
1710 PMODE4,1:SCREEN1,1:COLOR0,1:PCLS:DRAW"C0S4":TX=140:TY=5:T$="NAME: "+NF$:GOSUB100:TX=140:TY=13:T$="PAGE:":GOSUB100:TX=184:TY=13:T$="MEMORY:":GOSUB100
1720 FORX=0TO6:LINE(X*19+1,1)-(X*19+20,20),PSET,B:NEXT:LINE(7,27)-(248,184),PSET,B:LINE(6,26)-(249,185),PSET,B:GOSUB1760
1730 FORX=8TO248STEP8:LINE(X,27)-(X,23),PSET:LINE(X+4,27)-(X+4,25),PSET:LINE(X,184)-(X,188),PSET:LINE(X+4,184)-(X+4,186),PSET:NEXT:LINE(252,23)-(252,191),PRESET
1740 FORY=28TO184STEP8:LINE(7,Y)-(3,Y),PSET:LINE(7,Y+4)-(5,Y+4),PSET:LINE(248,Y)-(252,Y),PSET:LINE(248,Y+4)-(250,Y+4),PSET:NEXT
1750 GOSUB1790:FORX=8TO248STEP8:FORY=28TO184STEP8:PSET(X,Y):NEXTY,X:GOSUB110:A$=RIGHT$(STR$(PG),1):DRAW"BM166,13":GOSUB80:RETURN
1760 DRAW"BM4,4R6D12R6 BM24,4R12D12L12U12 BM43,10R4NU3D3E3NH3RNU3ND3R3 BM62,4R2BR3R2BR3R2D2BD3D2BD3D2L2BL3L2BL3L2U2BU3U2BU3U2 BM81,4R12L6D12 BM102,4D4NR6GFNR6GFNR6GFND4R6ND4EHEHEHU4L6 BM119,8ND4R6U4F6G6U4L6":RETURN
1770 DRAW"BM4,10E6D4R6D4L6D4H6 BM100,4R12D3L2DR2D8L6U3E2H2LG2F2D3L5U12D3R6U3 BM28,8U4R4D4L4BD2F2E2BD4R4D4L4U4BL4L4D4R4U4 BM46,4D4NR6GFNR6GFNR6GFND4R6ND4EHEHEHU4L6 BM62,4R4ND12R4ND12R4D4NL12D4NL12D4L12U12 BM82,4NR3D4R3BDD4R3BDND4R3D2L2NF2L"
1780 DRAW"BM122,6R4F2D4GNHFRLHGL4H2U4E2":RETURN
1790 AD(PG)=A(PG)
1800 D=PEEK(AD(PG)):IF D=0 THEN RETURN
1805 IF D>50 THEN 1825
1810 X=PEEK(AD(PG)+1):Y=PEEK(AD(PG)+2):R=PEEK(AD(PG)+3):AD(PG)=AD(PG)+4
1820 DRAW"BM=X;,=Y;":N=D:GOSUB870:GOTO1800
1825 X=PEEK(AD(PG)+1):Y=PEEK(AD(PG)+2):AD(PG)=AD(PG)+3
1830 IF D<>51 THEN 1850
1840 D=PEEK(AD(PG)): AD(PG)=AD(PG)+1:IF D=255 THEN 1800 ELSE A$=CHR$(D):DRAW"BM=X;,=Y;":GOSUB80:X=X+5:GOTO 1840
1850 LX=PEEK(AD(PG)):LY=PEEK(AD(PG)+1):AD(PG)=AD(PG)+2
1860 IF D=52 THEN LINE(X,Y)-(LX,LY),PSET:GOTO 1800
1870 IF D=53 THEN LINE(X,Y)-(LX,LY),PSET,B:GOTO1800
1880 IF D=54 THEN LINE(X,Y)-(LX,LY),PRESET,BF:GOTO1800
1890 IF D<>55 THEN 1950
1900 IF LX<X THEN X1=LX:LX=X:X=X1
1910 IF LY<Y THEN Y1=LY:LY=Y:Y=Y1
1920 FOR X1=X TO LX STEP 8:Y1=X1+4:IF Y1>LX THEN Y1=LX
1930 LINE(X1,Y)-(Y1,Y),PSET:LINE(X1,LY)-(Y1,LY),PSET:NEXT:FOR X1=Y TO LY STEP8:Y1=X1+4:IF Y1>LY THEN Y1=LY
1940 LINE(X,X1)-(X,Y1),PSET:LINE(LX,X1)-(LX,Y1),PSET:NEXT:GOTO1800
1950 IF D<>56 THEN AD(PG)=AD(PG)-5:POKE AD(PG),0:RETURN
1960 GET(X,Y)-(LX,LY),MD,G:LX=ABS(X-LX):LY=ABS(Y-LY):X=PEEK(AD(PG)):Y=PEEK(AD(PG)+1):PUT(X,Y)-(X+LX,Y+LY),MD,AND:AD(PG)=AD(PG)+2:GOTO1800
1970 PRINT:PRINT"CHECKING FREE DISK SPACE":X=FREE(0):IF X<28 THEN PRINT "NOT ENOUGH ROOM ON THE DISK.":STOP ELSE PRINT "CREATING SUB PAGE IMAGE FILES.":FOR X=1 TO 1000:NEXT
1980 FOR PG=0 TO 8:FL$="PRT.PG"+RIGHT$(STR$(PG),1):PMODE4,1:SCREEN1,1:COLOR0,1:PCLS:GOSUB1790:SAVEM FL$,&H0E00,&H25FF,&H0E00:NEXTPG
1990 RUN "CADPRINT"
