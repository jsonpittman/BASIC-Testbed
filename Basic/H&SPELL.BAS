1 REM**************************
2 REM*  HEAR IT AND SPELL IT  *
3 REM*    BY FRED B.SCERBO    *
4 REM*   COPYRIGHT (C) 1986   *
5 REM*60 HARDING AV.N.ADAMS,MA*
6 REM**************************
7 REM* THIS PROGRAM WILL NOT  *
8 REM* WORK WITHOUT A SPEECH  *
9 REM*SOUND-PAK BY RADIO SHACK*   REM**************************
10 CLS0:CLEAR2000:PRINTSTRING$(32,252);:FORI=1TO320:READA:PRINTCHR$(A+128);:NEXT
15 DATA60,58,48,62,56,61,60,60,58,53,60,60,58,52,62,60,60,58,,84,94,85,92,93,92,93,80,16,19,27,18,
20 DATA,59,51,58,,53,51,50,48,53,51,51,58,,59,51,51,58,,80,90,,,85,,,,17,27,19,19,
25 DATA,58,,58,,53,,,48,53,,,58,,58,53,50,,,80,90,,,85,,,,21,16,16,16,
30 DATA51,58,,59,50,55,51,51,58,55,,,59,49,58,,61,51,48,81,91,,,87,82,,,20,28,29,28,24
35 DATA99,99,99,99,98,99,99,99,99,98,99,99,99,99,99,98,99,98,,,99,98,,,35,34,35,35,35,35,35,34
40 DATA106,,,96,104,101,,,,106,101,96,,,,104,101,,,,101,,,,37,,40,,37,,,40
45 DATA106,,,,,101,,,,106,101,,,,,,101,,,,101,,,,37,,,,37,,,
50 DATA108,108,108,108,106,101,108,108,108,104,101,108,108,108,104,,101,,,,101,,,,37,,,,37,,,
55 DATA,,,,106,101,,,,,101,,,,,,101,,,98,101,96,,98,37,,,,37,,,
60 DATA107,99,99,99,106,103,,,,,103,99,99,99,99,106,103,99,99,106,103,99,99,106,39,34,,,39,34,,
65 PRINTSTRING$(32,243);
70 T=80:K=1:SW=41:MU=RND(-TIMER)
75 DIM B$(SW),W$(SW),P$(SW),P(SW),Q(41),C(7)
80 FORI=1TO7:C(I)=(I*16)+143:NEXT
85 XX=&HFF00:YY=&HFF7E
90 POKEXX+1,52:POKEXX+3,63
95 POKEXX+35,60
100 GOTO140
105 FORII=1TOLEN(A$)
110 IF PEEK(YY)AND 128=0 THEN110
115 POKEYY,ASC(MID$(A$,II,1))
120 NEXTII
125 IFPEEK(YY)AND128=0THEN125
130 POKEYY,13
135 FORHH=1TO70:NEXTHH:RETURN
140 A$="HEAR IT AND SPELL IT.BY FRED B  SKERBO..COPPEERIGHT NI HN TEEN EIGHTY SIX":GOSUB105
145 PRINT@422,"  BY FRED B.SCERBO  ";
150 PRINT@454," COPYRIGHT (C) 1986 ";
155 FORI=1TO20:READW$(I),P$(I)
160 IFW$(I)="END"THEN170
165 NEXTI
170 A$="PRESS ENTER TO BE GIN":GOSUB105
175 LN=I-1
180 FORI=1TOLN
185 P(I)=RND(LN):IF Q(P(I))=1THEN185
190 Q(P(I))=1:NEXTI
195 IFINKEY$<>CHR$(13)THEN195
200 FOR PY=1TO LN:C=PY
205 G=RND(7)
210 FT=0:NT=0
215 CLS0:PRINT@0,STRING$(64,C(G)):PRINT@96,"  O.K.  LET'S TRY THIS ONE !"
220 A$="O K LETS TRY.THIS ONE":GOSUB105
225 PRINT@128,"  HOW WOULD YOU SPELL .....?":PRINT:PRINTSTRING$(64,C(G));:A$="HOW WOOD YOU SPELL ":GOSUB105
230 FORDL=1TO1400:NEXTDL:PRINT
235 C$=W$(P(C)):A$=P$(P(C)):GOSUB105:PRINT
240 P=LEN(C$):PRINT@299,STRING$(P,"*"):PRINT
245 GOSUB105
250 PRINTSTRING$(64,C(G));
255 FORDL=1TO900:NEXTDL
260 FORI=1TOP:B$(I)=MID$(C$,I,1):NEXTI
265 PRINT@448," PRESS <SPACEBAR> TO SAY WORD.  ";
270 FORI=1TOP
275 PRINT@330+I,"^";
280 X$=INKEY$
285 IFX$=" "THENGOSUB105
290 IFX$="@"THEN400
295 PRINT@330+I," ";
300 IFX$<"A"THEN275
305 IFX$>"Z"THEN275
310 PRINT@330+I," ";:IFX$=""THEN275
315 PRINT@298+I,X$;:A$=X$:GOSUB105:IFX$=B$(I)THEN340
320 FORY=1TO5:NEXTY
325 NT=NT+1:A$="NO NOT "+X$:GOSUB105
330 FORDL=1TO500:NEXTDL:A$=P$(P(C))
335 PRINT@298+I,"*";:GOTO275
340 A$=P$(P(C)):NEXTI
345 A$="VERY GOOD.":GOSUB105
350 A$=P$(P(C))+" IS SPELLED.":GOSUB105
355 FORDL=1TO2600:NEXTDL
360 FORI=1TO P:A$=B$(I):GOSUB105:V=ASC(B$(I))
365 FORG=1TO6:PRINT@298+I,CHR$(V+32);:FORDL=1TO25:NEXTDL:PRINT@298+I,B$(I);:FORDL=1TO25:NEXTDL:NEXTG:NEXTI
370 FORDL=1TO200:NEXTDL:A$=P$(P(C)):GOSUB105:GOSUB105
375 IF NT=>1THEN NW=NW+1 ELSE NC=NC+1
380 PRINT@448,"   PRESS <ENTER> TO CONTUNUE.   ";
385 FORDL=1TO900:NEXTDL:A$="PRESS ENTER TO CON TIN U":GOSUB105
390 IFINKEY$<>CHR$(13)THEN390
395 NEXTPY
400 CLS:PRINT@101,"YOU TRIED"NC+NW"WORDS AND":PRINT@165,"SPELLED"NC"CORRECTLY"
405 PRINT@229,"WHILE DOING"NW"WRONG."
410 NQ=NC+NW:IF NQ=0THEN NQ=1
415 MS=INT(NC/NQ*100)
420 PRINT@293,"YOUR SCORE IS"MS"%."
425 PRINT@357,"ANOTHER TRY (Y/N/C) ?";
430 X$=INKEY$:IFX$="Y"THEN RUN
435 IFX$="N"THENCLS:END
440 IFX$="C"THEN205
445 GOTO430
1000 DATA ALLEGIANCE,AH LEEJENTS
1010 DATA MIXTURE,MIXTURE
1020 DATA ENCYCLOPEDIA,EN SI CLO PE DEEAH
1030 DATA MECHANIC,MEHKAHNIK
1040 DATA INDUSTRIAL,INDUHSTREE AHL
1050 DATA ALCOHOL,AL COHALL
1060 DATA ELECTRICITY,E LEK TRISSITY
1070 DATA SOLUTION,SO LU SHUN
1080 DATA INVENTORY,INNVVIN TORY
1090 DATA SUPERMARKET,SOO PPER MAR KET
5000 DATA END,END
