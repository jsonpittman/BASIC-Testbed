0 'COPYRIGHT 1990 FALSOFT, INC.
10 CLEAR500,&H7000-1:DEFUSR0=&H7000:IF PEEK(&H7000)<>189 THEN LOADM"INPUT":POKE &HFF40,0
20 CLS:E=EXP(1):P=4*ATN(1)
30 PRINT"F(X)=";:ZZ=USR0(40)
40 DEF FN F(X)=                           
50 PRINT@32,"";:INPUT"HOW MANY INTEGRALS [1] ";XX:IF XX<=0 THEN XX=1 ELSE IF XX>2 THEN 50
60 FOR QQ=1 TO XX:PRINT"INTEGRAL"QQ"-"
70 PRINT"LOWER LIMIT ";:ZZ=USR0(80)
80 A(QQ)=                           
90 PRINT"UPPER LIMIT ";:ZZ=USR0(100)
100 B(QQ)=                           
110 PRINT:NEXTQQ:TA=0:AA=0:RR=0:MX=0:N=1024
120 FOR QQ=1 TO XX:SU=0:SA=0
130 XH=(B(QQ)-A(QQ))/N:X2=XH/2:XR=X2/SQR(3)
140 S1=A(QQ)-X2-XR:S2=A(QQ)-X2+XR
150 S=SGN(FNF(S1+XH)+FNF(S2+XH))
160 FOR K=1 TO N
170 SS=SGN(FNF(S1+K*XH)+FNF(S2+K*XH)):IF SS<>S THEN S=SS:RR=RR+1:R(RR)=A(QQ)+(B(QQ)-A(QQ))*(K-1)/N
180 VV=FNF(S1+K*XH)+FNF(S2+K*XH):IF ABS(VV)>MX THEN MX=ABS(VV)
190 SU=SU+VV:SA=SA+ABS(VV)
200 PRINT@32*(4*QQ-2)+13,USING"#### +###.########";K,SU*X2
210 PRINT@32*(4*QQ-1)+18,USING"+###.########";SA*X2:NEXTK
220 SU=SU*X2:TA=TA+SU:SA=SA*X2:AA=AA+SA:NEXTQQ
230 PRINT@32*(4*XX+1),USING"TOTAL AREA  +###.######";TA
240 PRINTUSING"ABS   AREA  +###.######";AA
250 IF RR<>0 THEN PRINT"ROOT CHANGES AT -":FOR R=1 TO RR:PRINTUSING"+###.###### P*+##.##";R(R),R(R)/P:NEXTR
260 INPUT"ANY NEW LIMITS (Y/N) -";A$:IF A$<>"Y" THEN 280
270 FOR N=1 TO 14:PRINT@32*N,STRING$(31,32);:NEXT:GOTO 50
280 INPUT"WANT A PLOT (Y/N) -";A$:IF A$="Y" THEN 300
290 STOP
300 PMODE4,1:PCLS:SCREEN1,1
310 LINE(128,0)-(128,191),PSET:LINE(0,96)-(255,96),PSET
320 A=A(1):B=B(XX):YF=180/MX:IF ABS(B)>ABS(A) THEN XF=100/ABS(B) ELSE XF=100/ABS(A)
330 LINE(128+XF*A,96)-(128+XF*A,96),PSET
340 FOR N=A TO B STEP 1/(B-A)/XF
350 Y=FN F(N):Y=INT(YF*Y+.5)
360 IF ABS(Y)>90 THEN 380
370 LINE-(128+XF*N,96-Y),PSET
380 NEXT
390 LINE(128+XF*B,96-Y)-(128+XF*B,96),PSET
400 EXEC &HADFB
410 GOTO 260
