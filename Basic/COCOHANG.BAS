1 'COCO HANGMAN
2 'BY JOHN SAYA
3 'COPYRIGHT (C) JANUARY 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
40 POKE&HE414,0:POKE&HE42A,0
50 POKE65497,0:CLEAR1000:DIMA$(25)
60 A$(1)="FLOWER BASKET"
70 A$(2)="GINGER BREAD"
80 A$(3)="MERRY CHRISTMAS"
90 A$(4)="BACON AND EGGS"
100 A$(5)="TOASTED MARSHMALLOWS"
110 A$(6)="ORANGE JUICE"
120 A$(7)="BAKED CHICKEN"
130 A$(8)="FRUIT AND SALAD"
140 A$(9)="BREAD CRUMBS"
150 A$(10)="PAPER DOLLS"
160 A$(11)="COLOR COMPUTER"
170 A$(12)="CORN BREAD"
180 A$(13)="HEALTHY SNACKS"
190 A$(14)="OLIVE OIL"
200 A$(15)="SOUR GRAPES"
210 A$(16)="DISK DRIVE"
220 A$(17)="CORN FLAKES"
230 A$(18)="WHOLE WHEAT BREAD"
240 A$(19)="CARAMEL SWEETS"
250 A$(20)="MEAT AND VEGETABLES"
260 A$(21)="CORN SYRUP"
270 A$(22)="PEANUT BUTTER AND JELLY"
280 A$(23)="SUB SANDWICH"
290 A$(24)="LOLLI POP"
300 A$(25)="SLOPPY JOE"
310 PALETTE CMP
320 HSCREEN2:O=14:I=0:J=0:A$="":B$="":V$=""
330 PALETTE0,0:PALETTE1,63:PALETTE2,18:PALETTE3,36:PALETTE4,11:PALETTE5,7:PALETTE6,31:PALETTE7,9:PALETTE8,38:HCLS 1
340 HCOLOR7,7:HPRINT(15,0),">>> COCO HANG MAN <<<"
350 HCOLOR0,0:HLINE(96,0)-(96,191),PSET
360 HCOLOR0,0:HLINE(96,88)-(0,88),PSET
370 HCOLOR5,5:HPRINT(1,0),"* TRIES *"
380 HCOLOR4,4:HPRINT(1,12),"* WRONG *"
390 HCOLOR0,0:HLINE(96,9)-(0,9),PSET
400 HCOLOR0,0:HLINE(96,104)-(0,104),PSET
410 HCOLOR0,0:HLINE(96,94)-(0,94),PSET
420 HPAINT(0,89),2,0
430 HCOLOR0,0:HLINE(241,26)-(241,11),PSET
440 HCOLOR0,0:HLINE(241,26)-(229,26),PSET
450 HCOLOR0,0:HLINE(229,26)-(229,17),PSET
460 HCOLOR0,0:HLINE(241,11)-(135,11),PSET
470 HCOLOR0,0:HLINE(229,17)-(144,17),PSET
480 HCOLOR0,0:HLINE(135,11)-(135,167),PSET
490 HCOLOR0,0:HLINE(144,17)-(144,167),PSET
500 HCOLOR0,0:HLINE(106,167)-(172,177),PSET,B
510 HPAINT(240,25),5,0
520 HPAINT(107,168),4,0
530 A=RND(25):A$=A$(A):X=LEN(A$)
540 FORZ=1TOX:IF MID$(A$,Z,1)=" " THEN B$=B$+" ":NEXTZ ELSE B$=B$+"-":NEXTZ
550 HCOLOR0,0:HPRINT(13,24),B$
560 X$=INKEY$:IFX$=""THEN560
570 FORZ=1TOX:IF MID$(A$,Z,1)=X$ THEN 580 ELSE NEXTZ:GOTO590
580 H=13:FORZ=1TOX:IF MID$(A$,Z,1)=X$ THEN MID$(B$,Z,1)=MID$(A$,Z,1):HCOLOR1,1:HPRINT(H,24),"-":HCOLOR2,2:HPRINT(H,24),X$:H=H+1:NEXTZ:PLAY"T8O3ADBFCBGA":IF B$=A$ THEN 660 ELSE 700 ELSE H=H+1:NEXTZ:PLAY"T8O3ADBFCBGA":IF B$=A$ THEN 660 ELSE 700
590 HCOLOR0,0:HPRINT(J,O),X$:J=J+1:GOSUB600:I=I+1:IF I=6 THEN 670 ELSE PLAY"T5O4BO3BO2BT1O1B":IF J=11 THEN J=0:O=O+1:GOTO700 ELSE 700
600 IF I=0 THEN HCOLOR0,0:HCIRCLE(235,54),29:RETURN
610 IF I=1 THEN HCOLOR0,0:HLINE(236,80)-(236,133),PSET:RETURN
620 IF I=2 THEN HCOLOR0,0:HLINE(236,133)-(208,157),PSET:RETURN
630 IF I=3 THEN HCOLOR0,0:HLINE(236,133)-(257,157),PSET:RETURN
640 IF I=4 THEN HCOLOR0,0:HLINE(236,96)-(210,83),PSET:RETURN
650 IF I=5 THEN HCOLOR0,0:HLINE(236,96)-(260,83),PSET:RETURN
660 HCOLOR5,5:HPRINT(19,3),"TERRIFIC":SOUND175,25:HCLS1:T=T+1:GOTO60
670 HCOLOR0,0:HPRINT(19,3),"HANG MAN":SOUND100,25:HCLS1:HCOLOR5,5:HPRINT(5,5),"YOU WON":HPRINT(12,5),T:HPRINT(18,5),"GAMES !!!":HCOLOR0,0:HPRINT(10,10),"PLAY AGAIN?"
680 V$=INKEY$:IFV$=""THEN680
690 IF V$="Y" THEN 50 ELSE WIDTH32:CMP:CLS:POKE65496,0:POKE&HE414,3:POKE&HE42A,3:POKE113,0:EXEC40999
700 HCOLOR1,1:HPRINT(4,5),K:K=K+1:HCOLOR0,0:HPRINT(4,5),K:GOTO560
