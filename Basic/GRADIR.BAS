10 CLEAR2000:DIML$(59),M(98),V(20),Q(100),DF$(30),GP(22,2)
20 GOTO190
30 '*******DRAW LETTERS******
40 DRAW"BM"+STR$(INT(PW))+","+STR$(INT(PQ))+"C"+STR$(K2)+"S"+STR$(S2)+";"
50 FOR L=1 TO LEN(N$)
60 Q$=MID$(N$,L,1):IF ASC(Q$)>97THENQ$=CHR$(ASC(Q$)-32)
70 IF ASC(Q$)-31<1 THEN RETURN
80 IF Q$=CHR$(34) THEN DRAWL$(3):GOTO100
90 DRAWL$(ASC(Q$)-31)
100 NEXTL:RETURN
110 'check for file length
120 OPEN"D",1,F$,1:FIELD #1,1 AS A$:P=1:GOSUB150
130 GOSUB150:L=C*256:GOSUB 150:L=L+C:GOSUB150:S=C*256:GOSUB 150:S=S+C:Y=L+S-1:P=P+L:GOSUB150:GOSUB150:Q=C:GOSUB150:GOSUB150:L=C*256:GOSUB150:L=C+L
140 CLOSE#1:RETURN
150 IF P>LOF(1) THEN C=1:RETURN
160 GET #1,P:C=ASC(A$):P=P+1:RETURN
170 RETURN
180 CLS:PRINTCHR$(128);"chris";CHR$(128);"brown's    GRAPHICS DIR  ";STRING$(32,128):POKE1036,39:POKE1038,128:POKE1039,128:RETURN
190 GOSUB180:PRINT:PRINT"PLEASE STAND BY WHILE           THE PROGRAM INITIATES.                                  THANKS."
200 FOR I=1TO22:READGP(I,1),GP(I,2):NEXTI:DATA 0,0,51,0,103,0,155,0,207,0,0,38,51,38,103,38,155,38,207,38,0,76,51,76,103,76,155,76,207,76,0,114,51,114,103,114,155,114,207,114,0,152,51,152
210 L$(1)="BR7":L$(2)="BR1RULDBU2U6RD6BD2BR4":L$(3)="BR3BU8D2BR2U2BD8BR3":L$(4)="BR1BU5U3BR2D3U1R1NL4BU1NL4BD7BR1":L$(5)="BU8NR4D4R4D4L4R2D1U10D9BR5":L$(6)="BU8R1D1L1U1R1BR3D2G4D2BR3R1U1L1D1R1BR3":L$(7)="BR7H6U2R2D2G3D3R3E2BD2BR3"
220 L$(8)="BR1BU6U2RD2BD6BR3":L$(9)="BR1H1U7E1BD9BR1":L$(10)="BR1E1U7H1BD9BR3":L$(11)="BR3BU3U3D1NR2NL2NE2NG2NF2NH2BR4D5":L$(12)="BR6U4D2L2R4BR4BD2":L$(13)="BR1U1R1D3U2L1BR4"
230 L$(14)="BR1BU3R4BD3BR1":L$(15)="BR1U1R1D1L1BR4":L$(16)="BR1E5BD5BR1":L$(17)="BR1BU1U3BU1BR1R3BR1NG4BD1D3BD1BL1NL3BR3":L$(18)="BR1BU4URD5BR2":L$(19)="BU5R4DG3DR4BR1":L$(20)="BU5R4G2R2D3L4BR7":L$(21)="BU5D3NR5R3U3D5BR4":L$(22)="R4U3L4U2R3BD5BR3"
240 L$(23)="BR1NR3U3NU2R3D3BR3":L$(24)="BR1BU5R4D2G3BR5":L$(25)="BR1U3NR3U2R3D5NL3BR1":L$(26)="BR1BU2NR4U3R4D5BR1":L$(27)="BR1BU4BR4URDLBD3RDLUBD1BR1":L$(28)="BR1BU4RBD3D2ULBR3"
250 L$(29)="BR1BU5BR4G2F2BR1":L$(30)="BR1BU5NR4BD2R4BD3BR1":L$(31)="BR4BU4F2G2BR3":L$(32)="BR1BU2U3R4D1G2D3BR1":
260 L$(34)="BR1U5R3D2NL3D3BR1":L$(35)="BR1U5R3D2RNL4D3NL3BR1":L$(36)="BR1U5NR3D5R3BR1":L$(37)="BR1U5R3BR1BD1D3BD1BL1L3BR5":L$(38)="BR1U5NR3D2NR3D3R3BR1":L$(39)="BR1U5NR3D2R3BD3BR1":
270 L$(40)="BR1BU1U3BU1BR1R3BD2NL1D3BL1L2BR3BR1":L$(41)="BR1U5D2R3U2D5BR1":L$(42)="BR3NU5BR3":L$(43)="BR1BU3D2BD1BR1R2BR1BU1U4BD5BR1":L$(44)="BR1U5D2R2NE2F3BR1"
280 L$(45)="BR1NU5R3BR1":L$(46)="BR1U5F2E2D5BR1":L$(47)="BR1U5F4U4D5BR1":L$(48)="BR1BU1U3BU1BR1NR2BD5R2BR1BU1U3BD4BR1":L$(49)="BR1U5R2BR1BD1D1BD1NL2BD2BR1"
290 L$(50)="BR1BU1U3BU1BR1R3BR1BD1D3BD1BL1L3BR2NF2BR5":L$(51)="BR1U5R3BR1BD1L1BD1BL1NL1F3BR1":L$(52)="BR1BU5BR3L3DF3DL3BR5":L$(53)="BR4BU5NR3NL3D5BR5":L$(54)="BR1BU5D4BD1BR1R2BR2BU1U4BD5BR1"
300 L$(55)="BR1BU5D2F3E3U2BD5BR1":L$(56)="BR1BU5D4FRENU4FREU4BD5BR1":L$(57)="BR1E5BL5F5BR1":L$(58)="BR1BU5F2ND3E2BD5BR1":L$(59)="BR1BU5R5G5R5BR1"
310 PMODE4,1:PCLS5:LINE(1,1)-(44,32),PRESET,BF:N$="LONG":K2=5:S2=4:PW=10:PQ=13:GOSUB40:N$="FILE":PQ=23:GOSUB40:LINE(6,4)-(38,28),PSET,B
320 GET(1,1)-(44,32),M,G:PCLS5
330 PMODE4,5:PCLS5:PMODE4,1:PCLS
340 'start up routine
350 PRINT:INPUT"ENTER DISK NAME (8 CHAR. MAX)   -->";DN$:IF LEN(DN$)>8 THEN350
360 LINEINPUT"DATE (ANY FORMAT)";DT$
370 PRINT:PRINT"PUT DISK IN DRIVE 0 AND PRESS   ENTER":INPUTQ$:GOSUB180
380 K2=4:S2=4:CT=0:CN=0:PRINT@64,"READING DIRECTORY"
390 '**READ DIRECTORY TRACK            main loop
400 FOR SE= 3 TO 11:DSKI$0,17,SE,A$,B$:A$=A$+LEFT$(B$,114):FOR DE= O TO 7:PT=DE*32:NA$=MID$(A$,PT+1,8):EXT$=MID$(A$,PT+9,3)
410 IF EXT$<>"PIC" THEN 450
420 IF LEFT$(NA$,1)=CHR$(255) THEN 450
430 IF LEFT$(NA$,1)=CHR$(0) THENGOTO450
440 CT=CT+1:DF$(CT)=NA$+"/"+EXT$
450 NEXTDE,SE:FR=FREE(0)
460 IF CT=0 THENPRINT"SORRY, NO PICS ON THIS DISK.": GOTO750
470 PRINT"CHECKING FILE LENGTHS":FOR TT=1 TO CT:F$=DF$(TT):GOSUB120:LONG=Y-S
480 PRINTUSING"## %          % #####";TT,DF$(TT),LONG
490 IF LONG>6143 THEN DF$(TT)=DF$(TT)+"MAX"
500 NEXTTT
510 'process graphic screens
520 PMODE4,1:SCREEN1,0:FOR I = 1 TO CT
530 IFDF$(I)=""THEN560
540 IFRIGHT$(DF$(I),3)="MAX"THENN$="MAX":DF$(I)=LEFT$(DF$(I),8):PMODE4,1:PUT(1,1)-(44,32),M,PSET:CN=CN+1:GOSUB580:GOTO560
550 PMODE4,1:SCREEN1,0:PCLS5:LOADM DF$(I):GOSUB 570
560 NEXTI:GOTO630
570 F=-1:E=0:CN=CN+1:FOR Y=0 TO 192 STEP 6:F=F+1:E=0:FOR X=0 TO 255 STEP 6:E=E+1:PMODE4,1:GET(X,Y)-(X+1,Y),V,G:PUT(E,F)-(E+1,F),V,PSET:PUT(E,F)-(E+1,F),V,PSET:NEXTX,Y
580 GET(2,2)-(43,30),Q,G:PMODE4,5:SCREEN1,0:PUT(GP(CN,1),GP(CN,2))-(GP(CN,1)+41,GP(CN,2)+28),Q,PSET
590 K2=4:S2=4:PW=GP(CN,1):PQ=GP(CN,2)+35:N$=LEFT$(DF$(I),8):GOSUB40
600 IF CN=CT THEN 630
610 RETURN
620 'prepare for save
630 FORI=8TO5STEP-1:PCOPY I TO I-4:NEXT
640 N$=DN$
650 PMODE4,1:SCREEN1,0:LINE(103,158)-(255,192),PRESET,B:S2=4:PW=105:PQ=168:GOSUB40
660 PW=105:PQ=178:N$=DT$:GOSUB40
670 PW=105:PQ=188:N$="FREE GRANS = "+STR$(FR):GOSUB40
680 Z$=INKEY$:SOUND10,2:SOUND100,2:IFZ$="" THEN680
690 GOSUB180:PRINT"REMOVE PICTURE DISK AND PUT     GRAPHICS DIRECTORY DISK IN      DRIVE ZERO FOR save":INPUT"PRESS ENTER TO CONTINUE";Z$
700 GOSUB180:F1=FREE(0):PRINT:PRINT"FREE GRANS ON THIS DIRECTORY    DISK:"F1
710 IF F1<3 THENPRINT"- -  not enough room! - -        START NEW DIRECTORY DISK":INPUT"PRESS ENTER WHEN READY";Z$:GOTO700
720 PRINT"SAVE THIS DIRECTORY AS          "DN$" ";:INPUT"(Y/N)";Z$:IF Z$="Y" THEN 730 ELSE INPUT"NEW FILENAME OR <ENTER>";DN$:IF DN$=""THEN740
730 DN$=DN$+"/PIC":SAVEM DN$,3584,9727,40999
740 GOSUB180:IF Z$="Y"THENPRINT"GRAPHIC DIRECTORY               "DN$" SAVED!"
750 PRINT:PRINT"r > RESTART PROGRAM":PRINT"e > END PROGRAM":PRINT"m > GRAPHIC SYSTEN MENU":PRINT:PRINT"PRESS KEY OF CHOICE."
760 Z$=INKEY$:IF Z$=""THEN760
770 IF Z$="R" THEN RUN
780 IF Z$="E" THENEND
790 IF Z$="M" THEN 800 ELSE760
800 GOSUB180:PRINT"PLACE SYSTEM DESIGN DISK IN     DRIVE ZERO";:LINEINPUT" AND PRESS ENTER";Z$:RUN"MENU/BAS:0"
