1 'VOCABULARY AID
2 'WRITTEN BY KEIRAN KENNY
3 'COPYRIGHT 1991 FALSOFT, INC.
10 CLS
20 DIMA$(11),B$(11)
30 P=0
40 FORT=1TO11:READA$(T),B$(T):IFT=1ANDA$(T)=""THEN340ELSEIFA$(T)=""THEN50ELSEPRINT@P,A$(T)": "B$(T):P=P+32:NEXT
50 T=T-1:PRINT:PRINT"READ THESE WORDS AND THEIR MEAN-INGS, AND LEARN TO SPELL THE    WORDS. PRESS THE <SPACEBAR> WHENYOU ARE READY FOR A TEST."
60 K$=INKEY$
70 K$=INKEY$:IFK$<>CHR$(32)THEN70ELSECLS
80 FORX=1TO T
90 PRINT@32,"THIS WORD MEANS:"
100 R=RND(T):IFB$(R)=""THEN100ELSEPRINT@112-LEN(B$(R))/2,B$(R)
110 B$(R)=""
120 FORL=1TO LEN(A$(R))
130 IFL/2=INT(L/2)THENC$="*"ELSEC$=MID$(A$(R),L,1)
140 D$=D$+C$
150 NEXT
160 P=176-LEN(D$)/2
170 PRINT@P,D$;
180 PRINT@451,"TYPE THE MISSING LETTERS."
190 FORPL=1TOLEN(D$)
200 E$=MID$(D$,PL,1)
210 IFE$<>"*"THEN240
220 K$=INKEY$:IFK$=""ORK$<"A"ORK$>"Z"THEN220
230 PRINT@P,K$;:E$=K$
240 F$=F$+E$
250 P=P+1
260 NEXT
270 IFF$=A$(R)THENR$="RIGHT!":RT=RT+1ELSER$="SORRY!"
280 R$=R$+" "+"IT WAS "+CHR$(34)+A$(R)+CHR$(34)+"."
290 PRINT@240-LEN(R$)/2,R$
300 PRINT@451,"PRESS ANY KEY."
310 EXEC44539:CLS:D$="":F$="":NR=NR+1:K$=INKEY$
320 NEXT
330 CLS:GOTO30
340 CLS:PRINT@138,"END OF TEST"
350 PRINT@195,"YOU HAD"RT"RIGHT OUT OF"NR;CHR$(8)"."
360 PRINT@257,"PRESS r TO REPEAT OR e TO END."
370 K$=INKEY$:IFK$=""THEN370
380 IFK$="R"THENCLS:RUN
390 IFK$="E"THENCLS:END
400 GOTO370
1000 DATA SUFFICIENT,ENOUGH
1001 DATA LETHARGY,LACK OF ENERGY
1002 DATA MATURE,GROW UP
1003 DATA TENEBROUS,DARK
1004 DATA MULTITUDE,CROWD
1005 DATA NARRATIVE,STORY
1006 DATA INDEPENDENCE,FREEDOM
1007 DATA INFANTILE,CHILDISH
1008 DATA INDIGNATION,ANGER
1009 DATA NAUSEATING,SICKLY
1010 DATA "",""
1100 DATA OBLITERATE,WIPE OUT
1101 DATA NOURISHMENT,FOOD
1102 DATA PROCRASTINATE,DELAY
1103 DATA SHUFFLE,MIX
1104 DATA SUPERFLUOUS,MORE THAN ENOUGH
1105 DATA REQUIREMENT,NEED
1106 DATA MILITARISTIC,WAR LIKE
1107 DATA QUADRILATERAL,FOUR SIDED
1108 DATA MONUMENTAL,VERY LARGE
1109 DATA HIRSUTE,HAIRY
1110 DATA "",""
1200 DATA GIGANTIC,VERY LARGE
1201 DATA DIMINUTIVE,SMALL
1202 DATA EXECRABLE,BAD
1203 DATA DISEASED,SICK
1204 DATA "",""
2000 DATA "",""
