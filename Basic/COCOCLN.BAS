0 ' COPYRIGHT 1989 FALSOFT, INC
10 CLS:PRINT"  "STRING$(29,42):PRINT"  * COCO DISK CLEANUP UTILITY *":PRINT"  *     COPYRIGHT (C) 1986    *":PRINT"  *       STEVE RICKETTS      *":PRINT"  *      BORING, OR 97009     *":PRINT"  "STRING$(29,42):PRINT"  THIS PROGRAM IS DESIGNED FOR"
20 PRINT"  PEOPLE LIKE ME WHO ARE CON-":PRINT"  STANTLY SAVING/KILLING FILES":PRINT"  THEN HAVING TO ZAP THE DISK":PRINT"  FOR WHATEVER REASON AND RUN-":PRINT"  NING INTO PROBLEMS BECAUSE ":PRINT"  OF ALL THE GARBAGE LEFT OVER"
30 PRINT"  ON THE TARGET DISK.":PRINT"      any key to continue":EXEC44539
40 CLS:CLEAR5500:C$=STRING$(128,CHR$(255)):D$=C$:DIMF$(78),M$(82),G$(20),H$(20):M=1:N=1:D=1
50 CLS:PRINT" THIS PROGRAM WILL SEARCH THRU":PRINT"  THE DISK AND CLEAN IT OF ALL":PRINT"   OLD INFORMATION LEFT OVER":PRINT"  FROM PREVIOUSLY KILLED FILES"
60 PRINT:PRINT"    any key to begin cleanup":EXEC44539
70 DSKI$0,17,2,A$,B$:G=1
80 G$=MID$(A$,G,1):T=(G-1)/2
90 IF T=INT(T) THEN S=1 ELSE S=10
100 T=INT(T):IF T>16 THEN T=T+1
110 IF G$=CHR$(255) THEN GOSUB130
120 IF G=68 THEN 180 ELSE G=G+1:GOTO80
130 DSKI$0,T,S,E$,F$
140 IF E$=C$ THEN RETURN
150 PRINT"      CLEANING GRANULE";(G-1)
160 FOR K=S TO S+8:DSKO$0,T,K,C$,D$
170 NEXT K:RETURN
180 CLS:PRINT"  UNALLOCATED GRANULES ON THIS":PRINT"      DISK ARE NOW CLEAN.":PRINT
190 PRINT:PRINT"   SORTING OUT OLD DIRECTORY":PRINTSTRING$(10,32)"ENTRIES NOW"
200 'LOOK AT AND CLEAN UP KILLED
210 'DIRECTORY ENTRIES
220 FOR L=3 TO 11
230 DSKI$0,17,L,A$,B$
240 FOR C=1 TO 2
250 IF C=1 THEN C$=A$ ELSE C$=B$
260 FOR S=1 TO 127 STEP 32
270 F$(N)=MID$(C$,S,32):IF LEFT$(F$(N),1)<>CHR$(0) THEN N=N+1
280 NEXT S:NEXT C:NEXT L
290 FOR M=1 TO N STEP 8
300 IF M>68 THEN 320
310 FOR L=0 TO 3:G$(D)=G$(D)+F$(M+L):H$(D)=H$(D)+F$(M+L+4):NEXTL:D=D+1:NEXTM
320 PRINT:PRINT"   WRITING OUT NEW DIRECTORY"
330 FOR L=1 TO 8:DSKO$0,17,L+2,G$(L),H$(L):NEXTL
340 CLS:PRINT@236,"FINISHED":EXEC44539:CLS
