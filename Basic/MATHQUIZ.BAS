10 REM**************************
15 REM* LIFE SKILLS MATH QUIZ  *
20 REM*  NUMBER RELATIONSHIPS  *
25 REM*   RECOGNITION SKILLS   *
30 REM*    BY FRED B.SCERBO    *
35 REM*     60 HARDING AVE     *
40 REM*  NORTH ADAMS,MA 01247  *
45 REM*   COPYRIGHT (C) 1986   *
50 REM**************************
55 CLS0
60 FORI=1TO32:PRINTCHR$(172);:NEXT
65 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
70 FORI=1TO32:PRINTCHR$(163);:NEXT
75 DATA109,104,96,109,104,100,110,108,106,109,108,109,,,30,28,26,29,,,30,20,30,20,30,16,20,30,,21,28,29
80 DATA101,,,101,,,106,96,104,101,,100,,,26,,24,21,16,22,16,,26,,26,,16,26,,21,,20
85 DATA101,,,101,,,107,106,96,101,99,98,,,27,19,18,21,22,16,,,26,,26,,16,26,,21,19,19
90 DATA101,,,101,,,106,104,,101,,,,,,,26,21,20,18,,,26,,26,,16,26,,,,21
95 DATA101,,106,101,,,106,,,101,,97,,16,26,,26,21,,20,18,,26,,26,21,16,26,21,21,,21
100 DATA103,99,106,103,98,97,107,,,103,99,103,,,27,19,26,23,18,,27,17,27,17,27,23,17,27,23,21,19,23
105 PRINT@293," NUMBER RELATIONSHIPS ";
110 PRINT@325,"  RECOGNITION SKILLS  ";
115 PRINT@357,"   BY FRED B.SCERBO   ";
120 PRINT@389,"  COPYRIGHT (C) 1986  ";
125 PRINT@421," (F)RACTION,(D)ECIMAL ";:PRINT@453,"  OR (W)HOLE NUMBERS  ";
130 X$=INKEY$:IFX$=""THEN130
135 IFX$="F"THEN FR=1:GOTO190
140 IFX$="D"THEN FR=0:VL=32:A=0:GOTO155
145 IFX$="W"THEN FR=0:VL=32:GOTO190
150 GOTO130
155 PRINT@421," SELECT THE NUMBER OF ";:PRINT@453," DECIMAL PLACES (2-6)";
160 X$=INKEY$:IFX$=""THEN160
165 X=VAL(X$):IFX<=1THEN160
170 IFX>6THEN160
175 N$=".":FORI=1TOX:N$=N$+"#":NEXT
180 GOTO230
185 GOTO185
190 PRINT@421," SELECT THE NUMBER OF ";:PRINT@453," NUMBER PLACES  (1-5)";
195 X$=INKEY$:IFX$=""THEN195
200 X=VAL(X$):IFX<=0THEN195
205 IFX>5THEN195
210 A=10^X:N$="#":FORI=1TOX:N$=N$+"#":NEXT
215 D$="  ":FORI=1TO6-X:D$=D$+" ":NEXT:FORI=1TOX:D$=D$+"-":NEXT:GOTO230
220 VL=32
225 N$="###":GOTO230
230 L$=CHR$(91):R$=CHR$(93)
235 AR$="=>"
240 PRINT@421," DO YOU WANT TO HAVE  ";:PRINT@453," HINTS GIVEN ? (Y-N) ";
245 X$=INKEY$:IFX$=""THEN245
250 IFX$="Y"THEN265
255 IFX$="N"THEN285
260 GOTO245
265 E$(1)="GREATER THAN.."
270 E$(2)="LESS THAN....."
275 E$(3)="EQUAL TO......"
280 GOTO290
285 FORI=1TO3:FORY=1TO14:E$(I)=E$(I)+".":NEXTY,I
290 IF FR=1 THEN FG=X-5
295 B=RND(A):C=RND(A):K=RND(10):IFK=10THENC=B
300 IF FR=0THEN315
305 B(2)=B:C(2)=C:B(1)=RND(A/10):C(1)=RND(A/10):IFK=10THENC(1)=B(1)
310 GOTO330
315 B(1)=B:C(1)=C:IF FR<>1THEN330
320 K=RND(2):IFK=1THEN330
325 C(1)=C(1)*2:C(2)=C(2)*2
330 CLS:PRINT@34,"LOOK AT THE TWO VALUES BELOW.   THE FIRST NUMBER IS .........   THE SECOND NUMBER."
335 PRINT@168+VL-FG,"";:PRINTUSINGN$;B(1);:PRINT@178+VL-FG,"";:PRINTUSINGN$;C(1)
340 IF FR<>1THEN355
345 PRINT@198,D$;:PRINT@208,D$;:PRINT@232-FG,"";:PRINTUSINGN$;B(2);:PRINT@242-FG,"";:PRINTUSINGN$;C(2);
350 B=B(1)/B(2):C=C(1)/C(2)
355 PRINT@294,"A) "E$(1)L$">"R$
360 PRINT@326,"B) "E$(2)L$"<"R$
365 PRINT@358,"C) "E$(3)L$"="R$
370 X$=INKEY$:IFX$=""THEN370
375 IFX$="@"THEN465
380 IFX$<"A"THEN370
385 IFX$>"C"THEN370
390 IF B>C AND X$="A"THEN410
395 IF B<C AND X$="B"THEN410
400 IF B=C AND X$="C"THEN410
405 NW=NW+1:PRINT@422,"SORRY, TRY AGAIN !!";:FORI=1TO1000:NEXTI:PRINT@422,STRING$(22,32);:GOTO370
410 NR=NR+1
415 IF B>C THEN SL=291:GOTO430
420 IF B<C THENSL=323:GOTO430
425 IF B=C THEN SL=355:GOTO430
430 NC=NC+1:PRINT@418,"VERY GOOD!  THAT IS CORRECT!";:PRINT@483,"PRESS <ENTER> TO CONTINUE!";
435 X$=INKEY$
440 PRINT@SL,AR$;
445 FORI=1TO10:NEXT:PRINT@SL,"  ";:FORI=1TO10:NEXT
450 IFX$="@"THEN465
455 IFX$<>CHR$(13)THEN435
460 GOTO290
465 CLS:PRINT@101,"YOU TRIED"NC+NW"PROBLEMS &":PRINT@165,"ANSWERED"NC"CORRECTLY"
470 PRINT@229,"WHILE DOING"NW"WRONG."
475 NQ=NC+NW:IF NQ=0THEN NQ=1
480 MS=INT(NC/NQ*100)
485 PRINT@293,"YOUR SCORE IS"MS"%."
490 PRINT@357,"ANOTHER TRY (Y/N/C) ?";
495 X$=INKEY$:IFX$="Y"THEN RUN
500 IFX$="N"THENCLS:END
505 IFX$="C"THEN290
510 GOTO495
