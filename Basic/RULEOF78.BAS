100 PALETTE0,0:WIDTH80:CLS1:ATTR3,0:POKE150,1
110 'SIMPLE INTEREST VS RULE OF 78'S
120 DEF FNR(X)=INT(100*X+.5)/100:'ROUND TO NEAREST WHOLE CENT
130 F$="#######.##":I$="###":I1=0:I0=0
140 PRINT TAB(6):INPUT"OUTPUT TO SCREEN <1>, PRINTER <2> OR BOTH <3>";Z
150 PRINT TAB(6);"PRESS THE SPACEBAR TO HOLD SCREEN PRINTING"
160 PRINT TAB(6);"AND PRESS <ENTER> T0 CONTINUE"
200 PRINT TAB(6):INPUT"ENTER THE LOAN AMOUNT";B:B7=B
210 PRINT TAB(6):INPUT"ENTER THE TERM OF THE LOAN IN MONTHS";T
220 PRINT TAB(6):INPUT"ENTER THE ANNUAL INTEREST RATE AS A PERCENT";R
230 R=R/1200:'MONTHLY INTEREST AS A DECIMAL
240 M=B*R/(1-(1+R)^(-T)):'MONTHLY PAYMENT
250 F=M*T-B:'FINANCE CHARGE
260 M=FNR(M):'WHOLE CENT PAYMENT
270 U=T*(T+1)/2:'UNITS FOR THIS TERM
275 IF Z=2THEN 290
280 GOSUB500:'PRINT HEADER ON SCREEN
285 IFZ=1THEN300
290 GOSUB700:'PRINT HEADER ON PRINTER
300 FORN=1TOT:'<<<<LOOP<<<<<<<<<<<<<<<<<<<<<
310 I=FNR(B*R):'MONTH'S INTEREST PAYMENT
320 P=M-I:'MONTH'S PRINCIPAL PAYMENT
330 B=B-P:'BALANCE REMAINING AFTER PAYMENT
340 I7=FNR(F*(T-N+1)/U):'78'S MONTHLY INTEREST
350 P7=M-I7:'78'S MONTHLY PRINCIPAL
360 B7=B7-P7:'78'S BALANCE REMAINING
370 I1=I1+I:I8=I8+I7:'RUNNING TOTALS OF INT. PMNTS.
380 P1=P1+P:P8=P8+P7:'RUNNING TOTALS OF PRINC. PMNTS.
385 IFZ=2THEN400:'
386 Z$=INKEY$:IFZ$<>CHR$(32)THEN390ELSE387
387 Z$=INKEY$:IFZ$=CHR$(13)THEN 390ELSE387
390 PRINT TAB(6):PRINTUSINGI$;N;:PRINTUSINGF$;I;P;B;I7;P7;B7;I8-I1:'
395 IFZ=1THEN410
400 PRINT#-2,USING I$;N;:PRINT#-2,USING F$;I;P;B;I7;P7;B7;I8-I1:'
410 NEXTN:'>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
415 IFZ=2 THEN 450
420 GOSUB600:'PRINT UNDERLINES ON SCREEN
430 PRINT TAB(6);"SUM";:PRINT USING F$;I1;P1;B;I8;P8;B7;I8-I1
435 IF Z=1 THEN 460
440 GOSUB 800:'PRINT UNDERLINE ON PRINTER
450 PRINT#-2,"SUM";:PRINT#-2,USING F$;I1;P1;B;I8;P8;B7;I8-I1
455 IF Z=2 THEN 820
460 END
500 CLS: PRINT TAB(25);"COMPARISON OF SIMPLE INTEREST"
510 PRINT TAB(30);"AND RULE OF 78'S":PRINT
520 PRINT TAB(30);"LOAN =";B
530 PRINT TAB(30);"TERM =";T;"MONTHS"
540 PRINT TAB(30);"INTEREST =";1200*R;"PERCENT PER YEAR"
550 PRINT TAB(30);"MONTHLY PAYMENT = $";M: PRINT:PRINT
560 PRINT TAB(19);"SIMPLE INTEREST";TAB(15);"RULE OF 78'S";TAB(12);"78'S"
570 L$=STRING$(28,"-"):PRINT TAB(6);"     ";L$;"  ";L$"   EXCESS"
580 PRINT TAB(6);"      MONTHS    MONTHS    BALANCE   MONTHS    MONTHS    BALANCE  INTEREST"
590 PRINT TAB(6);"     INTEREST  PRINCIPAL REMAINING INTEREST  PRINCIPAL REMAINING  TO-DATE"
600 PRINT TAB(6);"   ";:FORI=1TO7:PRINT"  --------";:NEXT I:PRINT
610 RETURN
700 PRINT#-2:PRINT#-2:PRINT#-2,TAB(20);"COMPARISON OF SIMPLE INTEREST AND RULE OF 78'S"
710 PRINT#-2
720 PRINT#-2,TAB(30);"LOAN =";B
730 PRINT#-2,TAB(30);"TERM =";T;"MONTHS"
740 PRINT#-2,TAB(30);"INTEREST =";1200*R;"PERCENT PER YEAR"
750 PRINT#-2,TAB(30);"MONTHLY PAYMENT = $";M:PRINT#-2:PRINT#-2
760 PRINT#-2, TAB(13);"SIMPLE INTEREST";TAB(44);"RULE OF 78'S";TAB(68);"78'S"
770 L$=STRING$(28,"-"):PRINT#-2, "     ";L$;"  ";L$;"   EXCESS"
780 PRINT#-2,"      MONTHS    MONTHS    BALANCE   MONTHS    MONTHS    BALANCE  INTEREST"
790 PRINT#-2,"     INTEREST  PRINCIPAL REMAINING INTEREST  PRINCIPAL REMAINING  TO-DATE"
800 PRINT#-2,"   ";:FOR I=1 TO 7:PRINT#-2,"  --------";:NEXT I:PRINT#-2
810 RETURN
820 END
