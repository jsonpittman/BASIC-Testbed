0 ' COPYRIGHT 1990 FALSOFT, INC
2 CLEAR7000
4 DIM P(52),V$(13)
6 ON BRK GOTO144
8 RGB:WIDTH40:CLS4:ATTR3,3
10 PRINT"This is 3-D Graphing program. There are three main options to it."
12 PRINT:PRINT"1)Regular size graph":PRINT"2)Expanded scale graph"
14 PRINT"3)Printing of either type of graph"
16 PRINT:PRINT"**Note that you can plot from 1-31      points (range=0 to 99999) use -1 to end"
18 A=1
20 LOCATE0,15:PRINT"Enter  number ":LOCATE13,15:PRINTA:LOCATE16,15:PRINT"to plot";:INPUT P(A):LOCATE24,15:PRINT"       "
22 IF P(A)<0AND A=1THEN20
24 IF P(A)=-1 THEN A=A-1:GOTO30
26 IF P(A)>99999 OR P(A)<0THEN 20
28 A=A+1:IF A>52THEN A=A-1:GOTO30ELSE20
30 '**** INITIALIZING ****
32 SM=P(1):LG=P(1):FOR Q=1TO A:IF P(Q)>LG THEN LG=P(Q)
34 IF P(Q)<SM THEN SM=P(Q)
36 NEXT:Q=Q-1:FL=1
38 EX=0:Z=-2:IF LG=0THEN RUN
40 Y2=(53-Q)*2:SC=(180-Y2)/LG:PC=(182-Y2)/LG
42 G=(295/Q)*2:G2=G/2
44 '**** SET SCREEN ****
46 HSCREEN4:HCOLOR1:PALETTE3,63:PALETTE2,3:PALETTE0,0
48 ON BRK GOTO 144
50 POKE65497,0
52 HLINE(40,0)-(40,180),PSET
54 FOR L=180 TO 0 STEP-16
56 V=(180-L)/SC+EX:IF V<1000THEN V=INT(V/.1+.5)*.1ELSE V=INT(V+.5)
58 V$=STR$(V):U=LEN(V$):V$=RIGHT$(V$,U-1):A=A+1
60 HPRINT(0,L/8),V$
62 FOR B=44TO630STEP G:HLINE(B-4,L)-(B,L),PSET:NEXT
64 NEXT
66 HPRINT(0,23),"<E>=expansion <P>=printer <R>=regular size <A>nother plot <ESC>=quit"
68 '**** PLOT BARS ****
70 A=1
72 FOR X=44TO630STEP G:Y=INT(180-P(A)*SC+.5):HCOLOR3
74 IF X>630THEN88
76 HLINE(X,183)-(X+G2,Y),PSET,BF
78 HLINE(X,Y)-(X+G2*.66,Y-G2*.13),PSET:HLINE(X+G2,Y)-(X+G2+G2*.66,Y-G2*.13),PSET:HLINE(X+G2,183)-(X+G2+G2*.66,183-G2*.13),PSET'///lines
80 HLINE(X+G2*.66,Y-G2*.13)-(X+G2+G2*.66,Y-G2*.13),PSET:HLINE(X+G2+G2*.66,183-G2*.13)-(X+G2+G2*.66,Y-G2*.13),PSET
82 HPAINT(X+G2,Y-1),2,3
84 HPAINT(X+G2+1,181),2,3
86 A=A+1:NEXT
88 A$=INKEY$:IF A$="P"THEN98ELSE IF A$="R"THEN96ELSE IF A$="A"THEN HSCREEN0:RUN
90 IF A$<>"E"THEN88
92 IF FL=2THEN88ELSE HCLS:FOR A=1TO Q:P(A)=P(A)-SM:NEXT:LG=LG-SM:EX=SM:IF LG=0THEN LG=1
94 FL=2:GOTO40
96 IF FL=1THEN88ELSE FOR A=1TO Q:P(A)=P(A)+SM:NEXT:LG=LG+SM:FL=1:GOTO38
98 '**** PRINTER ROUTINE ****
100 POKE65496,0:POKE150,18
102 R$=CHR$(28)+CHR$(6)
104 G$=CHR$(18):N$=CHR$(30)+CHR$(27)+CHR$(20)
106 P$=CHR$(27)+CHR$(16)
108 LF$=CHR$(27)+CHR$(51)
110 SP=PEEK(65314)AND1
112 IF SP>0THEN PLAY"V2;L100;C":POKE &HE6E4,&HE6:HSCREEN0:POKE &HE6E4,&HE7:WIDTH40:ATTR2,3:PRINT"TURN THE PRINTER ON !!!":GOTO110ELSE116
114 A$="":POKE &HE6E4,&HE6:HSCREEN4:POKE &HE6E4,&HE7
116 D=0:FOR A=182TO0STEP-14:V=(182-A)/PC:IF V<100THEN V=INT(V/.1+.5)*.1ELSE V=INT(V+.5)
118 V$=STR$(V+EX):U=LEN(V$):V$(D)=RIGHT$(V$,U-1):D=D+1:NEXT
120 D=14:FOR A=0TO26
122 IF A/2=INT(A/2)THEN D=D-1:PRINT#Z,N$;:PRINT#Z,USING"%     %";V$(D);:PRINT#Z,STRING$(Q*2+1,".");
124 T=1:FOR J=45TO(Q*14+35)STEP14
126 IF J>255THEN F1=INT(J/256):F=J+1-F1*256ELSE F1=0:F=J
128 YY=INT(182-P(T)*PC+.5):IF A*7<YY OR YY>181THEN NN=0:GOTO138
130 IF A*7=YY THEN NN=128:GOTO136
132 IF A*7>YY+7 THEN NN=255:GOTO136
134 NN=0:D2=7-(A*7-YY):FOR K=D2 TO 6:NN=NN+INT(2^K):NEXT:NN=NN+128
136 PRINT#Z,N$;P$;CHR$(F1);CHR$(F);G$;R$;CHR$(NN);
138 T=T+1:NEXT:PRINT#Z,G$;LF$
140 T=0:NEXT
142 POKE65497,0:GOTO88
144 HSCREEN0:RGB:WIDTH40:POKE65496,0:END
