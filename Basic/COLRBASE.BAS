0 ' COPYRIGHT 1990 FALSOFT, INC
1 CLEAR 5000
10 GOSUB 2300
20 GOTO 60
30 IF PEEK(341)=247 THEN Y=Y-1:RETURN:ELSE IF PEEK(342)=247 THEN Y=Y+1:RETURN:ELSE IF PEEK(343)=247 THEN X=X-1:RETURN:ELSE IF PEEK(344)=247 THEN X=X+1:RETURN
40 IF CO=1 THEN RETURN
50 FOR TZ=1 TO 1:FOR TX=338 TO 345:POKE TX,255:NEXT TX,TZ:A$=INKEY$:IF A$="" THEN 50 ELSE RETURN
60 '                          ColorBase v1.00                                                             Written by Shane Messer                                                  Copyright (c) 1989, All Rights Reserved
70 '
80 '
90 PCLEAR1:FILES 1,400:DIM CX(100),CY(100),CN(100),DF$(23),FM$(24),N$(100),S$(100),SF$(75),K$(100):DW=1 '0= REGULAR DIRECTORY, 1= WIDE DIRECTORY WITH NAMES AND EXTENSIONS
100 ON ERR GOTO 2990
110 FF$=CHR$(12) ' FORMFEED FOR DMP-132
120 POKE 150,40 ' HALF YOUR BAUD RATE
130 LO=65496:HI=65497:FW=32:'THIS WILL BE THE WIDTH OF THE SCREEN UPON EXITING
140 FORM2=1 TO 24:FM$(M2)=STRING$(80," "):NEXTM2:KK=1:CMP:GOSUB 1960:FOR M1=32 TO 122:AC$=AC$+CHR$(M1):NEXT:MID$(AC$,63,1)=CHR$(127):GOSUB2340:WIDTH 80:CLSS1:ATTR S2,S3:PALETTE 10,S4:PALETTE 4,S5:IF KK=1 THEN PLAY"T255L255O1":K$="C":ELSE K$=""
150 '----------------------------------------------------------------------------
160 '
170 GOSUB 1980
180 GOSUB 2150:IF ASC(A$)=12 THEN 220
190 GOSUB 2160:IF M1>0 THEN 200 ELSE 180
200 ON M1 GOTO 220,950,1670,1900
210 '---------------------------
220 'CREATE
230 POKE HI,0:GOSUB 2090
240 GOSUB 2150:GOSUB 2160:IF M1>0 THEN 250 ELSE 240
250 IF M1=10 THEN 160
260 ON M1 GOTO 270,390,430,460,510,550,610,690,750
270 'CREATE FORM
280 CLS:FOR M1=0 TO 22:PRINT FM$(M1+1);:NEXT:LOCATE 0,23:PRINT"    Use Arrow keys to move around page.      Press 'CLEAR' to return to menu.";
290 X=40:Y=11
300 GOSUB 30:IF ASC(A$)=12 THEN 220
301 IF A$=CHR$(13) THEN X=1:Y=Y+1
310 IF PEEK(342)=191 THEN A$=CHR$(127)
320 IF INSTR(AC$,A$)>0 THEN LPOKE 442368+(X-1)*2+Y*160,ASC(A$):MID$(FM$(Y+1),X,1)=A$:X=X+1:PLAY K$
330 IF X>80 THEN X=1
340 IF Y>22 THEN Y=0 ELSE IF Y<0 THEN Y=22
350 IF X<1 THEN X=80
360 LOCATE X-1,Y
370 GOTO 300
380 GOTO 230
390 'DIRECTORY
400 IF DW=0 THEN CLS:POKE LO,0:DIR:PRINT"Free ->";FREE(0):GOSUB 50:POKE HI,0
410 IF DW=1 THEN GOSUB 2770
420 GOTO 230
430 'VERIFY
440 POKE LO,0:FOR M1=0 TO 34:FOR M2=1 TO 18:DSKI$ DR,M1,M2,M1$,M2$:NEXT M2,M1:POKE HI,0
450 GOTO 230
460 'KILL FORM
470 M$="Kill Form":GOSUB 2140
480 GOSUB 2180:IF F$="" THEN 230
490 PRINT"     Kill "F$" Are you sure";:LINEINPUT"? Y/N ->";C$:IF C$="Y" THEN POKE LO,0:KILL F$:POKE HI,0:GOTO 230 ELSE 230
500 GOTO 230
510 'RENAME FORM
520 PRINT:PRINT:PRINT"    SOURCE:":GOSUB 2180:IF F$<>"" THEN 530 ELSE 540
530 J$=F$:PRINT:PRINT:PRINT"    DESTINATION":GOSUB 2180:IF F$<>"" THEN RENAME J$ TO F$
540 GOTO 230
550 'SAVE FORM
560 M$="Save Form":GOSUB 2140
570 GOSUB 2180:IF F$="" THEN 230
580 POKE LO,0:OPEN "O",#1,F$
590 FOR T=1 TO 23:WRITE #1,FM$(T):NEXT:CLOSE:POKE HI,0
600 GOTO 230
610 'LOAD FORM
620 M$="Load Form":GOSUB 2140
630 GOSUB 2180:IF F$="" THEN 230
640 POKE LO,0:OPEN "I",#1,F$
650 FOR T=1 TO 23:INPUT #1,FM$(T):NEXT:CLOSE:POKE HI,0
660 GOTO 230
670 GOTO 230
680 GOTO 230
690 'ERASE FORM
700 M$="Erase Current form":GOSUB 2140
710 PRINT"                Do you really wish to erase the current form? Y/N"
720 GOSUB 2150:IF A$="Y" THEN 730 ELSE 230
730 FOR T=0 TO 22:FM$(T)=STRING$(80," "):NEXTT:GOTO 230
740 GOTO 230
750 'MAKE DATABASE
760 FC=0:M$="Make Database":GOSUB2140
770 PRINT"                  Converting data to database form. Please wait ..."
780 FOR T=1 TO 23
790 N=INSTR(FM$(T),CHR$(127)):IF N=0 THEN DF$(T)=FM$(T):NEXT
800 IF N>0 THEN NI=NI+1:CX(NI)=N:CY(NI)=T:FOR WA=N TO 79:IF MID$(FM$(T),WA,1)=CHR$(127) THEN CN(NI)=CN(NI)+1:MID$(FM$(T),WA,1)=" ":NEXT WA:ELSE
810 IF T=24 THEN 830
820 GOTO 790
830 FOR T=1 TO NI:FC=FC+CN(T):NEXT T:IF FC>255 THEN PRINT" Sorry! You have too many entries! Please re-edit!":GOSUB 50:GOTO 230
840 PRINT:PRINT"                                    Done!"
850 PRINT:PRINT"    You are ready to save your database form. (DEFAULT is DATAFORM.DAT)
860 GOSUB 2180:IF F$="" THEN F$="DATAFORM.DAT"
870 PRINT:PRINT"      Save "F$" to disk? Y/N ->";
880 LINEINPUT A$:IF A$="Y" THEN 890 ELSE 230
890 POKE LO,0:OPEN"O",#1,F$
900 WRITE #1,NI:FOR T=1 TO NI:WRITE #1,CX(T),CY(T),CN(T):NEXT:FOR T=1 TO 23:WRITE #1,DF$(T):NEXT:CLOSE:POKE HI,0
910 LINEINPUT"      Use form to create a database? Y/N ->";A$:IF A$="Y" THEN POKE LO,0:OPEN"O",#1,"MISC.DAT":WRITE #1,0:CLOSE:GOSUB 912:GOSUB 2180:IF F$="" THEN F$="DATABASE.DAT":OPEN"O",#1,F$:CLOSE:GOTO 230:ELSE OPEN"O",#1,F$:CLOSE:GOTO 230
911 GOTO 920
912 PRINT"    You are ready to create your database. (DEFAULT is DATABASE.DAT)":RETURN
920 GOTO 230
930 GOTO 230
940 '---------------------------
950 'MAINTAIN
960 SW=0:PRINT"     Please enter then name of your database form (DEFAULT is DATAFORM.DAT)":GOSUB2180:IF F$="" THEN D1$="DATAFORM.DAT" ELSE D1$=F$
970 PRINT"      Please enter the name of your database file (DEFAULT is DATABASE.DAT)":GOSUB 2180:IF F$="" THEN D2$="DATABASE.DAT" ELSE D2$=F$
980 GOSUB 2350
990 CLOSE:GOSUB 2060
1000 PRINT:PRINT:PRINTM$"No. of records ->";NF
1010 GOSUB 2150:GOSUB 2160:IF M1>0 THEN 1020 ELSE 1010
1020 IF M1=6 THEN 160
1030 ON M1 GOTO 1040,1210,1370,1540,1590
1040 'ADD
1050 GOSUB 2360
1060 CLS:FOR T=1 TO 23:PRINT DF$(T);:NEXT
1070 PRINT"                             Press 'CLEAR' to leave.";
1080 C=1
1090 IF C<1 THEN C=1:ELSE IF C>NI THEN C=NI
1100 GOSUB 2380
1110 IF N$(C)>"" THEN PRINT N$(C);
1120 GOSUB 50
1130 IF ASC(A$)=13 THEN LOCATE CX(C)-1,CY(C)-1:PRINT STRING$(CN(C)," ");:LOCATE CX(C)-1,CY(C)-1:PRINT N$(C);:PRINTMID$(DF$(CY(C)),CX(C)+CN(C),1);:C=C+1:GOTO 1090
1140 IF ASC(A$)=94 THEN C=C-1:GOTO 1090
1150 IF ASC(A$)=12 THEN 2400
1160 IF INSTR(AC$,A$)>0 AND LEN(N$(C))<RM THEN PRINT A$;:N$(C)=N$(C)+A$
1170 IF ASC(A$)=8 AND LEN(N$(C))>0 THEN N$(C)=LEFT$(N$(C),LEN(N$(C))-1):GOSUB 2380:PRINT STRING$(CN(C)," ");:GOSUB 2380:PRINT N$(C);
1180 GOTO 1120
1190 GOTO 990
1200 '--------------------------
1210 'SEARCH
1220 SW=0
1230 GOSUB 2360:CLS:GOSUB 2600
1240 IF XX<1 THEN XX=1 ELSE IF XX>NF THEN XX=NF
1250 SW=SW+1:IF SW=1 THEN GOSUB 2570:ELSE GET #1,XX:V$(1)=K$(1):GOSUB 2850:FOR T=1 TO NI:LOCATE CX(T)-1,CY(T)-1:PRINT S$(T);:NEXT:POKE HI,0
1260 IF SW=1 THEN 1250
1270 LOCATE 0,23:PRINT"          <ENTER> for Commands. <Clear> To quit. Rec no.->";XX;
1280 GOSUB 50
1290 IF ASC(A$)=94 THEN XX=XX+10:GOTO 1240:ELSE IF ASC(A$)=10 THEN XX=XX-10:GOTO 1240:ELSE IF ASC(A$)=8 THEN XX=XX-1:GOTO 1240 ELSE IF ASC(A$)=9 THEN XX=XX+1:GOTO 1240
1300 IF A$="S" THEN LOCATE 0,23:PRINT STRING$(78," ");:LOCATE 0,23:LINEINPUT "Search for what string? ->";S$:IF S$="" THEN A$=" ":GOSUB 2880:GOSUB 5000:GOTO 1302 ELSE IF S$<>"" THEN GOSUB 2880:GOSUB 5000:IF REC > NF THEN 1302
1301 IF Z>NF THEN 1302 ELSE IFA$="S"THENLOCATE0,23:PRINT"Press <SPACE> to go to next occurence or <ENTER> to exit. RECORD:";Z;:EXEC44539:Z$=INKEY$:IFZ$=" "THENXX=XX+1:IF XX>NF THEN 1302 ELSE GOSUB2880:GOSUB5000:GOTO 1301
1302 LOCATE 0,23:PRINTSTRING$(75," ");
1310 IF A$="K" THEN LOCATE0,23:PRINT STRING$(78," ");:LOCATE 0,23:PRINT"         Delete current record? Are you sure?";:GOSUB 50:IF A$="Y" OR A$="y" THEN GOSUB 2570:IF T=NF THEN GOSUB 3000 ELSE FOR T=XX TO NF-1:GET#1,T+1:PUT #1,T:NEXT:NF=NF-1:GOSUB 
1320 IF A$="F" THEN XX=1:GOTO 1240:ELSE IF A$="L" THEN XX=NF:GOTO 1240
1330 IF ASC(A$)=12 THEN 990
1340 IF ASC(A$)=13 THEN GOSUB 2610:GOTO 1240
1350 GOTO 1240
1360 '--------------------------
1370 'EDIT
1380 LOCATE 0,18:LINEINPUT"Which record to edit? ->";E$:E=VAL(E$):IF E<1 OR E>NF THEN 990
1390 GOSUB 2360:GOSUB 2570:GET #1,E:V$(1)=K$(1):GOSUB 2850:FOR T=1 TO NI:N$(T)=S$(T):NEXT
1400 CLS:FOR T=1 TO 23:PRINT DF$(T);:NEXT:FOR T=1 TO NI:LOCATE CX(T)-1,CY(T)-1:PRINT N$(T);:NEXT T
1410 LOCATE 0,23:PRINT"                             Press 'CLEAR' to leave.";
1420 C=1
1430 IF C<1 THEN C=1:ELSE IF C>NI THEN C=NI
1440 GOSUB 2380
1450 IF N$(C)>"" THEN PRINT N$(C);
1460 GOSUB 50
1470 IF ASC(A$)=13 THEN LOCATE CX(C)-1,CY(C)-1:PRINT STRING$(CN(C)," ");:LOCATE CX(C)-1,CY(C)-1:PRINT N$(C);:PRINTMID$(DF$(CY(C)),CX(C)+CN(C),1);:C=C+1:GOTO 1430
1480 IF ASC(A$)=94 THEN C=C-1:GOTO 1430
1490 IF ASC(A$)=12 THEN OO=1:GOTO 2400
1500 IF INSTR(AC$,A$)>0 AND LEN(N$(C))<RM THEN PRINT A$;:N$(C)=N$(C)+A$
1510 IF ASC(A$)=8 AND LEN(N$(C))>0 THEN N$(C)=LEFT$(N$(C),LEN(N$(C))-1):GOSUB 2380:PRINT STRING$(CN(C)," ");:GOSUB 2380:PRINT N$(C);
1520 GOTO 1460
1530 GOTO 990
1540 'DIRECTORY
1550 IF DW=0 THEN POKE HI,0:CLS:DIR:PRINT"Free ->";FREE(0):GOSUB 50:POKE LO,0:GOTO 990
1560 IF DW=1 THEN GOSUB 2770
1570 GOTO 990
1580 GOTO 990
1590 'PRINT
1600 CLS:M$="Print record(s)":GOSUB 2140:PRINT:PRINT:PRINT:LINEINPUT"WHICH RECORD TO PRINT ('*' FOR ALL) ->";N$:IF N$="*" THEN 1620
1610 IF VAL(N$)<1 OR VAL(N$)>NF THEN 1650
1620 LINEINPUT"FORMFEEDS? ->";YF$:PRINT"PRESS ANY KEY TO BEGIN.....":GOSUB 50
1630 IF N$="*" THEN FOR FF=1 TO NF:GOSUB 2900:NEXT FF:GOTO 1650
1640 FF=VAL(N$):GOSUB 2900
1650 GOTO 990
1660 '---------------------------
1670 'SETUP
1680 GOSUB 2020
1690 GOSUB 2150:GOSUB 2160:IF M1>0 THEN 1700 ELSE 1690
1700 IF M1=3 THEN 160
1710 ON M1 GOTO 1720,1850
1720 'SET DISPLAY
1730 CO=1:X=S4:Y=S5
1740 M$="Setup Display":GOSUB 2140
1750 LOCATE 10,5:PRINT"Foreground ->"S4
1760 LOCATE 10,6:PRINT"Background ->"S5
1770 LOCATE 15,8:PRINT"Press 'ENTER' to exit."
1780 IF INKEY$=CHR$(13) THEN CO=0:GOTO 1680
1790 GOSUB 30:S4=X:S5=Y
1800 IF S5>63 THEN S5=0 ELSE IF S5<0 THEN S5=63
1810 IF S4>63 THEN S4=1 ELSE IF S4<0 THEN S4=63
1820 X=S4:Y=S5
1830 PALETTE 4,S5:PALETTE 10,S4
1840 GOTO 1750
1850 'SAVE DEFAULT
1860 POKE LO,0:OPEN "O",#1,"SETUP":WRITE #1,S1,S2,S3,S4,S5:CLOSE:POKE LO,0
1870 GOTO 1670
1880 GOTO 160
1890 '---------------------------
1900 'EXIT
1910 M$="Exit":GOSUB 2140:PRINT"                   Do you really wish to exit? (Y/N) ->":GOSUB 2150
1920 IF A$="Y" THEN CLS:WIDTH FW:END:ELSE 160
1930 GOTO 160
1940 '----------------------------------------------------------------------------
1960 POKE LO,0:OPEN"I",#1,"SETUP":INPUT #1,S1,S2,S3,S4,S5:CLOSE:POKE HI,0:RETURN
1970 'PRINT MENU 1
1980 M$=" Main Menu ":GOSUB 2140
1990 M$=STRING$(30," ")
2000 PRINT M$"C - Create Database":PRINTM$"M - Maintain Database":PRINTM$"S - Screen Dafault":PRINTM$"X - Exit Program to BASIC":SL$="CMSX"
2010 RETURN
2020 'PRINT MENU 2
2030 M$="Setup Display":GOSUB 2140:M$=STRING$(30," "):PRINTM$"C - Customize Display":PRINTM$"S - Save Default":PRINTM$"X - Exit to Main Menu":SL$="CSX":RETURN
2040 RETURN
2050 'PRINT MENU 3
2060 M$="Maintain Database":GOSUB 2140:M$=STRING$(30," "):PRINTM$"A - Add Record":PRINTM$"S - Search records":PRINTM$"E - Edit Record":PRINTM$"D - Directory":PRINTM$"P - Print Record":PRINTM$"X - Exit to Main Menu":SL$="ASEDPX":RETURN
2070 RETURN
2080 'PRINT MENU 4
2090 M$="Create Database":GOSUB 2140:M$=STRING$(30," "):PRINTM$"C - Create Form":PRINTM$"D - Directory":PRINTM$"V - Verify Disk":PRINTM$"K - Kill Form":PRINTM$"R - Rename Form":PRINTM$"S - Save Form":PRINTM$"L - Load Form":SL$="CDVKRSLEMX"
2100 PRINTM$"E - Erase current form":PRINTM$"M - Make Database":PRINTM$"X - Exit to main menu":RETURN
2110 RETURN
2120 'PRINT MENU 5
2130 RETURN
2140 ATTR 2,4:CLS:M1=LEN(M$):M1=80-M1:M1=INT(M1/2):ATTR 3,2:PRINTSTRING$(M1-1," ");:ATTR S2,S3,U:PRINTM$" ";:ATTR 3,2:PRINT:PRINT:ATTR 2,4:PRINT:PRINT:RETURN
2150 EXEC 44539:A$=INKEY$:PLAY"T10L10O4A":RETURN
2160 M1=INSTR(SL$,A$):RETURN
2170 '--------------------------------------------------------------------------
2180 LINEINPUT"    PLEASE INPUT THE FILENAME->";F$
2190 IF F$="" THEN RETURN
2200 IN=INSTR(F$,"."):IM=INSTR(F$,"/")
2210 IF (IN OR IM)<1 AND LEN(F$)>8 THEN 2280
2220 IF (IN OR IM)<1 THEN F$=F$+".DAT"
2230 IF IM>1 THEN MID$(F$,IM,1)="."
2240 IF LEN(F$)-INSTR(F$,".")>3 THEN 2280
2250 IF INSTR(F$,"/")>0 THEN 2280
2260 RETURN
2270 RETURN
2280 PRINT "    BAD FILENAME":SOUND 1,2:GOTO 2180
2290 '--------------------------------------------------------------------------
2300 POKE 65497,0:WIDTH 40:CLS3:ATTR 2,4:CLS:LOCATE 0,3:FOR S=1 TO 8:READ C1$:GOSUB 2320:PRINT:NEXT:POKE 65496,0
2310 RETURN
2320 L=LEN(C1$):L=40-L:L=INT(L/2):PRINTSTRING$(L," ");C1$:RETURN
2330 DATA ColorBase v1.20,For 128k CoCo with disk drive,Written By Shane Messer,"Copyright (c), 1989",Modified 1/16/90,All Rights Reserved,Configuring Program,Please Wait ...
2340 LOCATE 0,17:PRINT:LOCATE0,17:C1$="Done.":GOSUB 2320:PRINT:C1$="Press any key to continue ...":GOSUB2320:PLAY"L20T10O3A":EXEC 44539:RETURN
2350 CLOSE:POKE LO,0:OPEN "I",#1,"MISC.DAT":INPUT #1,NF:CLOSE:POKE HI,0:RETURN
2360 CLOSE:POKE LO,0:OPEN "I",#1,D1$:INPUT#1,NI:FOR T=1 TO NI:INPUT #1,CX(T),CY(T),CN(T):NEXT:FOR T=1 TO 23:INPUT #1,DF$(T):NEXT:CLOSE:POKE HI,0:RETURN
2370 '--------------------------------------------------------------------------
2380 LOCATE CX(C)-1,CY(C)-1
2390 RM=CN(C):LM=1:RETURN
2400 LOCATE 0,23::PRINT"                   Do you wish to save this record? <Y/N)";:FOR TX=1 TO 10:FOR TY=338 TO 345:POKE TY,255:NEXT TY,TX
2410 IF OO=1 THEN GOSUB 50:IF A$="N" THEN FOR T=1 TO NI:N$(T)="":NEXT:GOTO 990
2420 IF OO=1 THEN GOSUB 2480
2430 IF OO=1 THEN OO=0:GOTO 990
2440 GOSUB 50:IF A$="N" THEN FOR T=1 TO NI:N$(T)="":NEXT:GOTO 2450 ELSE GOSUB 2480
2450 LOCATE 0,23:PRINT"              recordsDo you wish to keep adding files? <Y/N>";
2460 FOR T=1 TO NI:N$(T)="":NEXT
2470 GOSUB 50:IF A$="N" THEN 990:ELSE IF A$="Y" THEN 1060 ELSE 2470
2480 IF OO<>1 THEN NF=NF+1
2490 CLOSE:POKE LO,0:OPEN "D",#1,D2$,255
2500 S$="":FOR Q=1 TO NI:GOSUB 2860:S$=S$+N$(Q):NEXT:FIELD #1,255 AS K$(1)
2510 LSET K$(1)=S$
2520 IF OO=1 THEN PUT#1,E:CLOSE:POKE HI,0 ELSE PUT #1,NF:CLOSE:POKE HI,0
2530 '
2540 CLOSE:OPEN"O",#1,"MISC":WRITE #1,NF:CLOSE:POKE HI,0
2550 RETURN
2560 '--------------------------------------------------------------------------
2570 CLOSE:POKE LO,0:OPEN "D",#1,D2$,255:FIELD #1, 255 AS K$(1)
2580 RETURN
2590 '--------------------------------------------------------------------------
2600 FOR T=1 TO 23:PRINT DF$(T);:NEXT:RETURN
2610 '--------------------------
2620 LOCATE 0,0
2630 FOR T=1 TO 23:PRINT STRING$(80," ");:NEXT:LOCATE 0,0:M$=STRING$(20," "):PRINT"          "M$"SEARCH COMMANDS":PRINT:PRINT
2640 PRINTM$"<ENTER>         -     Takes you to this menu"
2650 PRINTM$"<CLEAR>         -     To take you out of search mode"
2660 PRINTM$"<DOWN ARROW>    -     Takes you ten records backwards"
2670 PRINTM$"<UP ARROW>      -     Takes you ten records forward"
2680 PRINTM$"<LEFT ARROW>    -     Takes to back one record"
2690 PRINTM$"<RIGHT ARROW>   -     Takes you forward one record"
2700 PRINTM$"<K>             -     Delete the current record"
2710 PRINTM$"<F>             -     Takes you to first record"
2720 PRINTM$"<L>             -     Takes you to last record"
2730 PRINTM$"<S>             -     Searches for entered string"
2740 PRINT:PRINT:PRINT:PRINTM$"  Press any key to return to search mode ...":
2750 GOSUB 50:GOSUB 2600:RETURN
2760 '--------------------------------------------------------------------------
2770 POKE LO,0:I=0:S=0:T=0:CLS:FOR T=3TO11:DSKI$0,17,T,A$,B$:C$=A$+LEFT$(B$,127):FORQ=1TO255STEP32:IFMID$(C$,Q,1)=CHR$(0)THEN2790ELSEIFMID$(C$,Q,1)=CHR$(255)THEN2800
2780 SF$(I+T-3)=MID$(C$,Q,11):I=I+1
2790 NEXT Q,T
2800 POKE HI,0:FOR S=0 TO I+3:SF$(S)=LEFT$(SF$(S),8)+"."+MID$(SF$(S),9,3):NEXTS:FOR S=0TOI+3:IF LEN(SF$(S)) <> 1 THEN PRINT "  ";SF$(S);"  ";:ELSE 2810
2810 NEXTS:PRINT:POKE LO,0:PRINT"Free ->";FREE(0):POKE HI,0
2820 FOR S=0 TO I+3:SF$(S)="":NEXT
2830 GOSUB 50
2840 RETURN
2850 POKE 65497,0:PS=1:FOR T=1 TO NI:S$(T)=MID$(V$(1),PS,CN(T)):PS=PS+CN(T):NEXT:PS=0:POKE 65496,0:RETURN
2860 IF LEN(N$(Q))<CN(Q) THEN N$(Q)=N$(Q)+STRING$(CN(Q)-LEN(N$(Q))," ")
2870 RETURN
2880 GOSUB 2570:IF XX>NF THEN RETURN ELSEFOR Z=XX TO NF:GET #1,Z:IF INSTR(K$(1),S$)>0 THEN XX=Z:RETURN
2890 NEXT Z:LOCATE 0,23:PRINT STRING$(79," ");:LOCATE 0,23:SOUND 1,1:ATTR 2,4,B:FOR T=1 TO 10:PRINT:NEXT T:PRINT"                     SPCIFIED STRING IS NOT ABLE TO BE LOCATED!":EXEC 44539:ATTR 2,4:A$=" ":RETURN
2900 'PRINT OUTS
2910 SF=1:GOSUB 2360:GOSUB 2570:GET #1,FF:V$(1)=K$(1):GOSUB 2850
2920 POKE 65497,0:FOR T=1 TO 23
2930 IF CY(SF)>T OR SF=>NI THEN PRINT #-2,DF$(T):NEXT T:GOTO 2970
2940 FOR K=1 TO LEN(DF$(T))
2950 IF CX(SF)=K AND CY(SF)=T THEN PRINT #-2,S$(SF);:K=K+LEN(S$(SF)):SF=SF+1:ELSE PRINT #-2,MID$(DF$(T),K,1);
2960 NEXT:PRINT#-2:NEXT
2970 IF LEFT$(YF$,1)="Y" THEN PRINT #-2,FF$
2980 RETURN
2990 WIDTH 40:CLS:PRINT"SYSTEM ERROR!!":SOUND 1,1:PRINT"ERROR #";ERNO" IN LINE ";ERLIN:LINEINPUT"RERUN PROGRAM? ->";Y$:IF LEFT$(Y$,1)="Y" THEN RUN ELSE END
3000 A$=STRING$(255," "):LSET K$(1)=A$:PUT #1,1:CLOSE:NF=NF-1:GOSUB 2540:RETURN
5000 POKE 65497,0:CLS:GOSUB 2600:POKE 65496,0:IF XX>NF THEN REC=NF+1:RETURN ELSE REC=NF-1
5001 GET #1,XX:V$(1)=K$(1):GOSUB 2850:POKE 65497,0:FOR T=1 TO NI:LOCATE CX(T)-1,CY(T)-1:PRINT S$(T);:POKE 65496,0:NEXT:RETURN
