5 CLS
10 REM THIS PROGRAM (C) 1985 BY RALPH D. MILLER, POB 13322, TALLAHASSEE, FL 32317 (904) 386-3618---ALL RIGHTS RESERVED---
22 PRINT
24 PRINT"           presdent"
26 PRINT
28 PRINT" ******************************"
29 PRINT" ***** MEMORIZATION HELPER ****"
30 PRINT" ************ FOR *************"
31 PRINT" ****** U.S. PRESIDENTS *******"
32 PRINT" ******************************"
33 PRINT" * (C)1985 BY RALPH D. MILLER *"
34 PRINT" ******* (904) 386-3618 *******"
35 PRINT" ******************************"
36 PRINT" **** ALL RIGHTS  RESERVED ****"
37 PRINT" ******************************"
40 FOR T=1 TO 5000:NEXT T
100 DIM A$(40),B$(40),C$(40)
105 CLS:V=0
110 PRINT:PRINT:PRINT"     eASY (LAST NAME ONLY)           hARD (ENTIRE NAME)              rETURN (TO DISK MENU)           qUIT (RETURN TO BASIC)          pRINT (HARDCOPY LIST)"
120 Z$=INKEY$:IF Z$="" THEN 120
130 IF Z$<>"E" AND Z$<>"H" AND Z$<>"R" AND Z$<>"Q" AND Z$<>"P" THEN 105
140 IF Z$="E" THEN E$="LAST"
150 IF Z$="H" THEN E$="ENTIRE"
160 IF Z$="R" THEN RUN "DIRECTRY"
170 IF Z$="Q" THEN POKE&H71,0:EXEC&HA027
172 IF Z$="P" THEN GOTO 710
175 RESTORE
180 CLS
190 PRINT"BEGINNING WITH THE FIRST, INPUT EACH PRESIDENT'S "E$" NAME,"
200 LINEINPUT"IN CHRONOLOGICAL ORDER OF THEIR PRESIDENCY: ";F$
210 IF F$="" THEN GOTO 105
220 X=1:W=0
230 GOSUB 630
270 X=X+1
280 LINEINPUT"NEXT: ";F$
285 IF F$="" THEN GOTO 800
300 GOTO 230
630 READ A$(X),B$(X),C$(X)
635 IF C$(X)="REAGAN" THEN GOTO 900
637 IF Z$="E" THEN 672
640 IF B$(X)="N" THEN B$(X)="":D$=A$(X)+" "+C$(X):GOTO 690
650 IF B$(X)<>"N" THEN D$=A$(X)+" "+B$(X)+" "+C$(X)
660 IF LEN(B$(X))>1 THEN 690
670 D$=A$(X)+" "+B$(X)+"."+" "+C$(X)
672 IF Z$="E" THEN D$=C$(X)
690 IF F$<>D$ THEN PRINT "SORRY. YOU SHOULD HAVE ENTERED: "D$:W=W+1:GOTO 695
695 RETURN
710 V=PEEK(65314):IF V=4 OR V=6 THEN 740
720 CLS:PRINT@200,"printer off line":V$=INKEY$:IF V$="" THEN 720
730 V=PEEK(65314):IF V=4 OR V=6 THEN 740
735 GOTO 105
740 POKE149,0:POKE150,17
742 CLS:PRINT@200,"now printing list"
750 RESTORE
755 FOR Y=1 TO 40
760 READ A$(Y),B$(Y),C$(Y)
765 IF B$(Y)="N" THEN B$(Y)="":D$=A$(Y)+" "+C$(Y):GOTO 790
770 IF B$(Y)<>"N" THEN D$=A$(Y)+" "+B$(Y)+" "+C$(Y)
772 IF LEN(B$(Y))>1 THEN 790
774 D$=A$(Y)+" "+B$(Y)+"."+" "+C$(Y)
790 PRINT#-2,D$
792 NEXT Y
794 PRINT#-2,CHR$(12)
795 GOTO 105
800 PRINT:PRINT"YOU HAVE GOTTEN THROUGH"X-1
810 PRINT"PRESIDENTS."
820 PRINT"YOU GAVE"W"WRONG ANSWER(S)."
830 PRINT:PRINT:PRINT"       strike any key"
840 M$=INKEY$:IF M$="" THEN 840
845 GOTO 105
900 CLS
905 PRINT:PRINT"YOU HAVE GOTTEN THROUGH ALL     FORTY PRESIDENTS.":PRINT
910 PRINT"YOU GAVE"W"WRONG ANSWER(S).":PRINT
915 IF W=0 THEN L$="INCREDIBLY STUPENDOUS,          MOVE OVER, EINSTEIN!":GOTO 950
920 IF W<6 THEN L$="OUTSTANDING!                    YOU WILL VERY SOON              HAVE THEM ALL MASTERED.":GOTO 950
925 IF W<11 THEN L$="VERY GOOD!                      IT'S ALL DOWNHILL               FROM HERE.":GOTO 950
930 IF W<21 THEN L$="YOU'RE HALFWAY THERE!":GOTO 950
935 IF W<31 THEN L$="YOU'VE MASTER A QUARTER OF      THEM! PRACTICE MAKES PERFECT!"
950 PRINT L$:PRINT:PRINT:PRINT"       strike any key"
960 M$=INKEY$:IF M$="" THEN 960
970 GOTO 105
1000 DATA GEORGE,N,WASHINGTON,JOHN,N,ADAMS,THOMAS,N,JEFFERSON,JAMES,N,MADISON,JAMES,N,MONROE,JOHN,QUINCY,ADAMS,ANDREW,N,JACKSON,MARTIN,VAN,BUREN,WILLIAM,HENRY,HARRISON,JOHN,N,TYLER
1001 DATA JAMES,N,POLK,ZACHARY,N,TAYLOR,MILLARD,N,FILLMORE,FRANKLIN,N,PIERCE,JAMES,N,BUCHANAN,ABRAHAM,N,LINCOLN,ANDREW,N,JOHNSON,ULYSSES,S,GRANT,RUTHERFORD,B,HAYES,JAMES,N,GARFIELD
1002 DATA CHESTER,N,ARTHUR,GROVER,N,CLEVELAND,BENJAMIN,N,HARRISON,GROVER,N,CLEVELAND,WILLIAM,N,MCKINLEY,THEODORE,N,ROOSEVELT,WILLIAM,H,TAFT,WOODROW,N,WILSON,WARREN,G,HARDING,CALVIN,N,COOLIDGE
1003 DATA HERBERT,N,HOOVER,FRANKLIN,D,ROOSEVELT,HARRY,S,TRUMAN,DWIGHT,D,EISENHOWER,JOHN,F,KENNEDY,LYNDON,B,JOHNSON,RICHARD,M,NIXON,GERALD,N,FORD,JAMES,E,CARTER,RONALD,N,REAGAN
