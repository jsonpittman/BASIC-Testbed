10 CLS:A$=STRING$(32,"*"):PRINTA$;"* GRAPHICS TRANSFER UTILITY #2 ** (C) 1987 MONTGOMERY SOFTWARE *";A$;
20 PRINT" PLEASE SELECT A NUMBER (1-5)":PRINT
30 PRINT"(1) LOAD A TAPE GRAPHICS FILE":PRINT
40 PRINT"(2) LOAD A DISK GRAPHICS FILE":PRINT
50 PRINT"(3) VIEW GRAPHICS IN MEMORY":PRINT
60 PRINT"(4) SAVE A GRAPHICS FILE TO DISK"
70 PRINT"(5) SAVE A GRAPHICS FILE TO TAPE";
80 X$=INKEY$: X=VAL(X$):IF X<1 OR X>5 THEN 80
90 ON X GOTO 100,200,300,400,500
100 REM ANDY KLUCK'S TAPE LOAD          ROUTINE FROM 10/84              RAINBOW ARTICLE.
110 CLS:PRINTA$;:PRINT"*PRESS ANY KEY TO LOAD GRAPHICS** OR <ENTER> TO RETURN TO MENU *";A$
115 L$=INKEY$:IF L$=""THEN 115 ELSE IF L$=CHR$(13) THEN 10
117 PRINT:PRINT"LOADING GRAPHICS......"
120 POKE&H78,0
130 POKE&H1D1,0
140 EXEC&HA648
150 POKE&H1E7,PEEK(&HBC):POKE&H1E8,0
160 EXEC&HA505
170 GOTO 10
200 CLS:PRINTA$;:PRINT"* NAME OF DISK GRAPHICS FILE TO** LOAD (FILENAME/EXT) OR DIR OR**<ENTER> TO RETURN TO MAIN MENU*";A$
210 PRINT"> ";:LINEINPUTF$:IF F$="DIR"THEN CLS:DIR:PRINT:LINEINPUT"FILENAME TO LOAD (FILENAME/EXT) > ";F$
220 IF F$="" THEN 10
230 LOADM F$:GOTO 10
300 CLS:PRINTA$;:PRINT"* PMODE 3 OR 4 GRAPHICS SCREEN?** (PRESS EITHER NUMBER 3 OR 4) ** PRESS ENTER WHEN DONE VIEWING*";A$
310 P$=INKEY$:P= VAL(P$):IF P<3 OR P>4 THEN 310
320 PMODE P,1:SCREEN1,1:EXEC44539
330 GOTO 10
400 CLS:PRINTA$;:PRINT"* FILENAME/EXT TO SAVE TO DISK ** OR <ENTER> TO RETURN TO MENU *";A$
410 LINEINPUT"> ";D$
420 IF D$=""THEN 10
430 SAVEM D$,3584,9727,44539
440 GOTO 10
500 CLS:PRINTA$;:PRINT"* FILENAME TO SAVE TO TAPE OR  ** <ENTER> TO RETURN TO MENU    *";A$:
520 LINEINPUT"> ";R$
522 IF R$=""THEN 10
530 IF PEEK(188)=14 THEN PRINT"THE TAPE YOU ARE NOW CREATING   WILL NOT BE NON-DISK COMPATIBLE.YOU MUST LOAD IN GRAFTRAN ON THETAPE-ONLY SYSTEM AND LOAD IN THEGRAPHIC ON THIS TAPE AND SAVE ITBACK OUT AGAIN TO TAPE."
535 PRINT
540 PRINT"PRESS ANY KEY TO CONTINUE"
550 EXEC44539
560 PRINT"SAVING ";R$;" TO TAPE"
570 CSAVEM R$,PEEK(188)*256,PEEK(188)*256+6143,44539
580 GOTO 10
