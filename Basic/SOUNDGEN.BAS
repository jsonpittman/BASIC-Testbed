10 'SOUND GENERATOR
20 'WRITTEN BY WALT NOON
30 'COPYRIGHT (C) DECEMBER 1991
40 'BY FALSOFT, INC.
50 'RAINBOW MAGAZINE
80 '************************
90 GOTO280
100 A$=CHR$(128)
110 CLS(0):FORA=1TO64:PRINTCHR$(RND(10)+145);:NEXT:PRINTSTRING$(34,A$);"coco";A$;"sound";A$;"effects";A$;"generator";STRING$(34,A$);:FORA=1TO351:PRINTCHR$(RND(10)+145);:NEXT
120 PRINT@239,"by";:PRINT@299,"walt";A$;"noon";:PRINT@396,"any";A$;"key";
130 FORA=1TO20:B$=INKEY$:IFB$=""THENNEXT:N=RND(5):D=RND(255):E=23:S=24:GOSUB500:GOTO130
140 CLS2:PRINT@37,"coco";A$;"game";A$;"sounds";A$;"menu";:SOUND1,1:FORI=98TO124:PRINT@I,A$;:FORJ=1TO10:NEXTJ:NEXTI:SOUND1,1:PRINT@166,"a";A$;"information";:PRINT@236,"b";A$;"manual";A$;"entry";:PRINT@290,"c";A$;"random";A$;"sound";A$;"creator";
150 PRINT@360,"d";A$;"exit";A$;"program";
160 B$=INKEY$:IFB$=""THEN160 ELSESOUND255,1
170 IF B$="A" OR B$="a" THEN 760
180 IF B$="B" OR B$="b" THEN 700
190 IF B$="C" OR B$="c" THEN 570
200 IF B$="D" OR B$="d" THEN CLS:END
210 GOTO 160
220 '**************************
230 '... BASIC SOUND DRIVER ...
240 'MAKE THE NEXT 10 LINES
250 'THE FIRST LINES OF YOUR
260 'BASIC PROGRAM...
270 '**************************
280 CLEAR200,&H3EFF
290 DATA 182,255,1,132,247,183,255,1,182,255,3
300 DATA 132,247,183,255,3,182,255,35,138,8,183
310 DATA 255,35,16,190,63,0,190,63,3,166,128,132,255,183
320 DATA 255,32,141,10,188,63,5,38,242,49,63,38,235,57
330 DATA 182,63,2,74,38,253,57
340 FORI=&H3F07 TO &H3F07+56
350 READ A:POKE I,A
360 NEXT I
370 DEFUSR0=&H3F07
380 '*************************
390 GOTO100
400 'N=TIMES (0-255)
410 'D=DELAY (0-255)
420 'S=START (0-255)
430 'E=END (0-255)
440 '*************************
450 '... SOUND PLAY ...
460 'ADD THE NEXT 5 LINES TO
470 'YOUR PROGRAM TO PLAY BACK
480 'SOUNDS FROM BASIC.
490 '************************
500 POKE &H3F00,INT(N/256):POKE &H3F01,N-INT(N/256)*256
510 POKE &H3F02,D
520 POKE &H3F03,INT(S/256):POKE &H3F04,S-INT(S/256)*256
530 POKE &H3F05,INT(E/256):POKE &HF06,E-INT(E/256)*256
540 A=USR0(0)
550 '************************
560 RETURN
570 Z=0:PRINT@453,"keep";A$;"sounds";A$;"short";A$;"y/n";
580 B$=INKEY$:IFB$=""THEN580 ELSEIFB$="Y" OR B$="y"THENZ=1
590 CLS0:FORI=1TO15:SOUND255/I,1:PRINTSTRING$(32,CHR$(169));:NEXT:PRINT@69,"generate";A$;"random";A$;"sounds";
600 PRINT@150,"    ";:PRINT@204,"    ";:PRINT@269,"    ";:PRINT@330,"    ";:N=RND(255):D=RND(255):S=RND(255):E=RND(255):GOSUB610:GOTO600
610 IFZ=1 THENN=RND(5):D=RND(255):E=23:S=24
620 PRINT@131,"n";A$;A$;"number";A$;"of";A$;"times";A$;N;:PRINT@195,"d";A$;A$;"delay";A$;D;:PRINT@259,"s";A$;A$;"start";A$;S;:PRINT@323,"e";A$;A$;"end";A$;E;:PRINT@389,A$;A$;A$;A$;A$;"playing";A$;"sound";A$;A$;A$;A$;A$;
630 GOSUB500
640 PRINT@389,"spacebar";A$;"for";A$;"next";A$;"sound";:PRINT@453,"a";A$;A$;"again";A$;A$;A$;A$;A$;A$;A$;A$;"m";A$;A$;"menu";
650 B$=INKEY$:IFB$=""THEN650
660 IFB$="A" OR B$="a" THEN A=USR0(0):GOTO650
670 IFB$="M" OR B$="m" THEN140
680 RETURN
690 A$=CHR$(128)
700 CLS0:Z=0:FORI=1TO15:SOUND255/I,1:PRINTSTRING$(32,CHR$(185));:NEXT:PRINT@39,A$;A$;"create";A$;"a";A$;"sound";A$;A$;
710 N=2:D=117:S=24:E=23:GOSUB500
720 PRINT@196,"n";A$;"is";A$;"for";A$;"number";A$;"of";A$;"times";:A=USR0(0):PRINT@260,"d";A$;"is";A$;"for";A$;"delay";:A=USR0(0):PRINT@324,"s";A$;"is";A$;"for";A$;"start";:A=USR0(0):PRINT@388,"e";A$;"is";A$;"for";A$;"end";:A=USR0(0)
730 N=5:D=1:S=1:E=1:GOSUB500
740 PRINT@96,STRING$(32," ");:PRINT@96,"ENTER NUMBER 0-255 FOR N:";:INPUTN:A=USR0(0):PRINT@121,"    ":PRINT@119,"D:";:INPUTD:A=USR0(0):PRINT@121,"     ";:PRINT@119,"S:";:INPUTS:A=USR0(0):PRINT@122,"    ";:PRINT@119,"E:";:INPUTE:A=USR0(0)
750 PRINT@92,CHR$(185);:B$=STRING$(32,CHR$(185)):FORI=1TO10:PRINTB$;:NEXT:GOSUB620:GOTO700
760 CLS0:CLEAR1000:DIMB$(37):B$(1)="WELCOME TO THE COCO SOUND":B$(2)="GENERATOR!  THIS PROGRAM WILL":B$(3)="ALLOW YOU TO DESIGN REAL":B$(4)="ARCADE SOUNDS FOR YOUR BASIC":B$(5)="PROGRAMS.  BY ADDING JUST A FEW":B$(6)="LINES OF THIS PROGRAM TO YOUR"
770 B$(7)="BASIC PROGRAM, YOU WILL BE ABLE":B$(8)="TO ADD ANY SOUND YOU MAKE HERE":B$(9)="TO YOUR GAME.  FIRST, USE THIS":B$(10)="PROGRAM TO MAKE A SOUND YOU":B$(11)="LIKE, THEN WRITE DOWN THE":B$(12)="NUMBERS (N,D,S,E) THAT MADE"
780 B$(13)="THE SOUND. WHEN YOU HAVE AS":B$(14)="MANY SOUNDS AS YOU LIKE, ADD":B$(15)="THE 'SOUND PLAY' LINES IN THIS":B$(16)="PROGRAM TO YOUR PROGRAM. RUN":B$(17)="THE 'POKE' ROUTINE TO SET UP":B$(18)="THE ML, THEN SIMPLY GIVE"
790 B$(19)="THE VARIABLES (N,D,S,E) IN":B$(20)="YOUR BASIC PROGRAM THE VALUES":B$(21)="FOR THE SOUND YOU WANT AND GOTO":B$(22)="THE 'SOUND PLAY' PART OF THIS":B$(23)="PROGRAM THAT YOU COPIED TO":B$(24)="YOUR PROGRAM.  NOTE:YOU MAY"
800 B$(25)="WANT TO KEEP SOME VARIABLES":B$(26)="(SUCH AS 'S' AND 'E') SMALL":B$(27)="SO THAT THE SOUND EFFECT WON'T":B$(28)="GO ON TOO LONG.":B$(29)="(SOME SOUND COMBINATIONS":B$(30)="PRODUCE VERY LONG SOUND"
810 B$(31)="PLAYBACKS...) HAVE FUN WITH THE":B$(32)="PROGRAM, AND, GOOD LUCK WITH":B$(33)="ALL YOUR PROGRAMMING EFFORTS.":B$(34)="THANKS!":B$(35)="WALT NOON":B$(36)="3283 BELVEDERE":B$(37)="RIVERSIDE, CA 92507   <ANY KEY>"
820 N=1:D=37:S=24:E=23:GOSUB500
830 I=I+1:PRINT
840 IFI=10 THEN N=3:D=9:S=24:E=23:GOSUB500
850 IFI=15 THEN N=1:D=121:S=116:E=199:GOSUB500
860 IFI=20 THEN N=1:D=40:S=24:E=23:GOSUB500
870 IFI=29THEN N=1:D=211:S=24:E=23:GOSUB500
880 IFI=30THEN N=5:D=13:S=24:E=23:GOSUB500
890 IF I=35 THEN N=4:D=28:S=24:E=23:GOSUB500
900 FOR B=1 TO LEN(B$(I)):PRINT MID$(B$(I),B,1);
910 A=USR0(0)
920 NEXTB
930 IFI<37THEN830
940 B$=INKEY$:IFB$=""THEN940 ELSERUN
