0 ' COPYRIGHT 1989  FALSOFT,INC
100 REM ROSTERMAKER
110 REM DELBERT BAKER COPYRIGHT 1987
120 REM SET UP A NEW ROSTER FILE
130 CLEAR 5000
140 DIM N$(20), A$(20)
150 DIM C$(20), P$(20)
160 C1$="Albany, OR 97321":C2$="Corvallis, OR 97330"
170 CLS:PRINT@9,"NEW ROSTER FILE":PRINT
180 INPUT"WHAT SEASON IS THIS";Y$:PRINT
190 F1$="ROSTER"+RIGHT$(Y$,2)
200 X=1:CLS:PRINT@4,"ENTER PLAYER INFORMATION"
210 LINE INPUT"LAST NAME: ";NA$
220 IF NA$="STOP" THEN 300
230 LINE INPUT"FIRST NAME: ";NB$
240 N$(X)=NA$+","+NB$
250 LINE INPUT"ADDRESS: ";A$(X)
260 LINE INPUT"CITY, STATE ZIP  1  2  INPUT    :";C$(X)
270 IF C$(X)="1" THEN C$(X)=C1$ ELSE IF C$(X)="2" THEN C$(X)=C2$ ELSE 280
280 LINE INPUT"PHONE NUMBER: ";P$(X):PRINT
290 X=X+1:GOTO210
300 X=X-1:CLS:PRINT@12,"SORTING"
310 F=0
320 FOR Y=1 TO X-1
330 IF N$(Y)<=N$(Y+1) THEN 370 ELSE 340
340 S1$=N$(Y):S2$=A$(Y):S3$=P$(Y):S4$=C$(Y)
350 N$(Y)=N$(Y+1):A$(Y)=A$(Y+1):P$(Y)=P$(Y+1):C$(Y)=C$(Y+1)
360 N$(Y+1)=S1$:A$(Y+1)=S2$:P$(Y+1)=S3$:C$(Y+1)=S4$:F=1
370 NEXT Y
380 IF F<>0 THEN 310 ELSE 390
390 PRINT"SORTING DONE.":PRINT
400 PRINT"<S>CREEN OR <D>ISK OR <Q>UIT"
410 PRINT:PRINT"PRESS <S> OR <D> OR <Q>"
420 B$=INKEY$:IF B$="S" THEN 430 ELSE IF B$="D" THEN 510 ELSE IF B$="Q" THEN END ELSE 420
430 Y=1
440 PRINT"PRESS ANY KEY TO CONTINUE.";
450 B$=INKEY$:IF B$="" THEN 450
460 CLS:FOR K=Y TO Y+2
470 PRINT N$(K):PRINT A$(K):PRINT C$(K);TAB(20)P$(K):PRINT
480 IF K=X THEN 400 ELSE 490
490 NEXT K
500 Y=Y+3:GOTO 440
510 OPEN"D",#2,F1$,128:SB$=STRING$(45,"0")
520 FIELD #2,25 AS NF$,30 AS AF$,20 AS CF$,8 AS PF$,45 AS SF$
530 FOR K=1 TO X
540 LSET NF$=N$(K): LSET AF$=A$(K):LSET CF$=C$(K):LSET PF$=P$(K):LSET SF$=SB$
550 PUT #2,K:NEXT K
560 CLOSE 2:PRINT"ROSTER STORED AS ";F1$: GOTO 400
