0 'FLEXIQIZ' by Keiran Kenny,      Sydney, 1988.
10 CLS
20 READA$,B$:IFA$="END"THEN160ELSECLS
30 PRINT@32,A$
40 FORT=1TOLEN(B$):E$=MID$(B$,T,1):D$=D$+E$
50 IFE$=" "THENZ=Z+1ELSE80
60 IFZ=1THENF$=D$:L1=LEN(F$):I$=LEFT$(F$,1)+"."+" ":GOTO80
70 IFZ=2THENG$=RIGHT$(D$,T-L1):L2=LEN(G$):J$=LEFT$(G$,1)+"."+" "
80 NEXT
90 H$=RIGHT$(B$,LEN(B$)-L1-L2)
100 PRINT@128,"";:INPUTC$
110 IFC$=B$ ORC$=H$ ORC$=F$+H$ ORC$=G$+H$ ORC$=I$+G$+H$ ORC$=F$+J$+H$ ORC$=I$+J$+H$ ORC$=I$+H$ ORC$=J$+H$ THENR=R+1:PRINT@205,"RIGHT!"ELSEW=W+1:Q=1:PRINT@205,"WRONG!"
120 IFQ ORC$=B$ THENAN$="THE ANSWER IS:"ELSEAN$="THE FULL NAME IS:"
130 PRINT@256,AN$:PRINT@336-LEN(B$)/2,B$
140 PRINT@393,"PRESS ANY KEY."
150 EXEC44539:D$="":Q=0:Z=0:L1=0:L2=0:GOTO20
160 IFR=0THENR$=" NONE"ELSER$=STR$(R)
170 IFW=0THENW$=" NONE"ELSEW$=STR$(W)
180 N$=A$+":"+R$+" RIGHT;"+W$+" WRONG."
190 PRINT@400-LEN(N$)/2,N$
200 PRINT:END
1000 DATA WHICH PRESIDENT WAS ASSASSINATEDIN 1963?,JOHN FRANCIS KENNEDY
1010 DATA WHICH PRESIDENT INTRODUCED THE 'NEW DEAL',FRANKLIN DELANO ROOSEVELT
1020 DATA WHO LED THE RAID ON HARPER'S    FERRY?,JOHN BROWN
1030 DATA WHO WAS THE ORIGINATOR OF THE   THEORY OF RELATIVITY?,ALBERT EINSTEIN
1040 DATA WHO COMPOSED THE MUSIC FOR 'THE PHANTOM OF THE OPERA'?,ANDREW LLOYD WEBBER
1050 DATA WHICH PHILOSOPHER WAS THE HERO  OF PLATO'S DIALOGUES?,SOCRATES
1060 DATA WHO WROTE THE FAMOUS STAGE PLAY 'PYGMALION'?,GEORGE BERNARD SHAW
1070 DATA WHICH PRESIDENT PRECEDED ANDREW JACKSON?,JOHN QUINCY ADAMS
1080 DATA WHICH AMERICAN ADMIRAL SAID     'DAMM THE TORPEDOES'?,JOHN PAUL JONES
1090 DATA WHICH ACTOR ASSASSINATED ABRAHAMLINCOLN?,JOHN WILKES BOOTH
2000 DATA END,END
