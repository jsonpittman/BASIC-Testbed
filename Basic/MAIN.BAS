5 ' *******REMEMBER********         *********V1.01*********
10 '*****COPYRIGHT 1985****         ***********************
15 '***********************         ***BY LARRY ANDERSON***
20 '***********************         ***BOX 180 BENTON LA***
25 '***********************         ***********************
30 '
35 '
40 CLS3:CLEAR5000
45 POKE150,1'***** 9600BAUD POKE
50 GOTO1350'*****  PCLEAR ROUTINE
55 DIMA$(150),B$(150),C$(150)
60 U$="r e m e m b e r":U1$=STRING$(8,128)+"MAIN MENU":U2$=STRING$(6,128)+"NEW VEHICLE":Z$=CHR$(128)
65 W$=STRING$(8,128)+"@escapes"+Z$+"to"+Z$+"menu"+STRING$(7,128)
70 CLS3:PRINTU$;:GOTO855
75 CLS3:PRINT@ 0,U$;:PRINT@15,U1$;:PRINT@105,"Vehicle"+Z$+"entry";STRING$(3,128);:PRINT@169,"Check"+Z$+"if"+Z$+"service";:PRINT@201," is"+Z$+"due";
80 PRINT@208,STRING$(9,128);
85 PRINT@265,"Update"+Z$+"vehicle";STRING$(2,128);:PRINT@297," history";STRING$(8,128);:PRINT@361,"Index";STRING$(11,128);
90 PRINT@425,"Quit";STRING$(12,128);
95 GOSUB1440
100 B$="VCUIQ":A=INSTR(B$,A$)
105 ON A GOTO385,450,745,1375,1310:GOTO95
110 '***** ENTER MAINTENANCE SCHEDULE
115 CLS:PRINT@0,U$+STRING$(17,128);:PRINT@32," ENTER INTERVALS FOR:  ";N$;:PRINT@64,STRING$(32,128);
120 PRINT@96," oIL CHANGE EVERY  ..... MILES":PRINT@128,"        OR WITHIN     .. MOS."
125 PRINT@192," lUBRICATE EVERY   ..... MILES":PRINT@224,"        OR WITHIN     .. MOS."
130 PRINT@288," tUNE UP EVERY     ..... MILES":PRINT@320,"        OR WITHIN     .. MOS."
135 PRINT@384," bELTS/ETC.EVERY   ..... MILES":PRINT@416,"        OR WITHIN     .. MOS."
140 PRINT@480,W$;:POKE1535,128:L=114:Q=1
145 FORP=1TO4:FORC=1TO2:IFC=1THENN=5ELSEN=C
150 D$="":PRINT@L,CHR$(133);
155 GOSUB1440
160 IFA$="@"THEN1355ELSEIFASC(A$)=8THEN710
165 IFASC(A$)=13THEN Z=LEN(D$):PRINT@L,STRING$(Z+1,143);:PRINT@L,D$;:D(Q)=VAL(D$):GOTO195
170 IFASC(A$)<48 ORASC(A$)>57 THEN155ELSED$=D$+A$:PRINT@L,D$;:D(Q)=VAL(D$)
175 IFLEN(D$)=>N THEN 180 ELSEPRINT@L+LEN(D$),CHR$(133);:GOTO155
180 PRINT@L+LEN(D$),CHR$(133);
185 GOSUB1440
190 IFASC(A$)=8THEN710ELSEIFASC(A$)<>13THEN185
195 PRINT@L+LEN(D$),CHR$(143);:PRINT@L+N+1,CHR$(143);:L=L+35:Q=Q+1
200 NEXT C
205 L=L+26
210 NEXTP
215 PRINT@0,STRING$(32,191):PRINT@32,"    entries"+Z$+"correct";:POKE1075,63:PRINT@52,"Yes";:POKE1079,47:PRINT@56,"No"
220 GOSUB1440
225 B$="NY@":W=INSTR(B$,A$)
230 ONW GOTO115,240,1355:GOTO220
235 '***** REQUEST CURRENT DATA
240 CLS0:PRINT@0,U$;:PRINT@32,"CURRENT DATA FOR: ";N$ :PRINT@64,STRING$(32,128);:PRINT"LAST OIL CHG.    ......   MILES?";:PRINT" DATE PERFORMED  ......   ?"
245 PRINT:PRINT"LAST LUBED       ......   MILES?";:PRINT" DATE PERFORMED  ......   ?"
250 PRINT:PRINT"LAST TUNED       ......   MILES?";:PRINT" DATE PERFORMED  ......   ?"
255 PRINT:PRINT"CKD BELTS&HOSES  ......   MILES?";:PRINT" DATE PERFORMED  ......   ?":PRINT@480,W$;:POKE1535,128
260 IFA=3THENPRINT@82,"new";STRING$(6,128);"old";
265 IFA=3THENPRINT@121,"      ";:PRINT@128-LEN(H$(1)),H$(1);:PRINT@152,LEFT$(H$(2),2);"/";MID$(H$(2),3,2);"/";RIGHT$(H$(2),2);:PRINT@217,"      ";:PRINT@224-LEN(H$(3)),H$(3);:PRINT@248,LEFT$(H$(4),2);"/";MID$(H$(4),3,2);"/";RIGHT$(H$(4),2);
270 IFA=3THENPRINT@312,"      ";:PRINT@320-LEN(H$(5)),H$(5);:PRINT@344,LEFT$(H$(6),2);"/";MID$(H$(6),3,2);"/";RIGHT$(H$(6),2);:PRINT@409,"      ";:PRINT@416-LEN(H$(7)),H$(7);:PRINT@440,LEFT$(H$(8),2);"/";MID$(H$(8),3,2);"/";RIGHT$(H$(8),2);
275 '
280 L=112:Q=1:N=6
285 FORP=1TO4:FORC=1TO2
290 D$="":PRINT@L,CHR$(133);
295 GOSUB1440
300 IFA$="@"THEN1355ELSEIFASC(A$)=8THEN705
305 IFASC(A$)=13THEN Z=LEN(D$):PRINT@L,STRING$(Z+1,143);:PRINT@L,D$;:H$(Q)=D$:GOTO335
310 IFASC(A$)<48 ORASC(A$)>57 THEN295ELSED$=D$+A$:PRINT@L,D$;:H$(Q)=D$ 
315 IFLEN(D$)=>N THEN320 ELSEPRINT@L+LEN(D$),CHR$(133);:GOTO295
320 PRINT@L+LEN(D$),CHR$(133);
325 GOSUB1440
330 IFASC(A$)=8THEN705ELSEIFASC(A$)=13THEN335 ELSE325
335 PRINT@L+LEN(D$),CHR$(143);:PRINT@L+N+1,CHR$(143);:L=L+32:Q=Q+1
340 NEXT C
345 L=L+32
350 NEXTP
355 PRINT@0,STRING$(32,191):PRINT@32,"    entries"+Z$+"correct";:POKE1075,63:PRINT@52,"Yes";:POKE1079,47:PRINT@56,"No" 
360 GOSUB1440
365 B$="NY@":W=INSTR(B$,A$)
370 ONW GOTO240,800,1355:GOTO360
375 '***** (A=1) NEW VEHICLE ENTRY
380 Z$=CHR$(128)
385 CLS3:PRINT@0,U$+U2$;:PRINT@199,"name"+Z$+"you"+Z$+"will"+Z$+"use"+Z$;:PRINT@231,Z$+"for"+Z$+"this"+Z$+"vehicle"+Z$;:PRINT@480,W$;:POKE1535,128
390 PRINT@364,"        ";:PRINT@364,"";:LINEINPUTN1$
395 PRINT@372,STRING$(12,175);:IFN1$=""THEN385ELSEIF N1$="@"THEN75
400 FORQ=1TO LEN(N1$):IFASC(MID$(N1$,Q,1))<48 OR ASC(MID$(N1$,Q,1))>90 THEN N1$="":GOTO385
405 NEXT Q
410 TEMP$=N$:N$=N1$:GOTO1170
415 CLS3:PRINT@266,"SAVING DATA";
420 OPEN"O",#1,N$+"/DAT"
425 FOR Q=1TO8:WRITE#1,D(Q),H$(Q)
430 NEXT Q
435 CLOSE#1
440 GOTO965
445 '***** (A=2) CHECK FOR SERVICE DUE
450 CLS3:PRINT@0,U$;:PRINT@24,N$;:PRINT@169,Z$+"what"+Z$+"is"+Z$+"the"+Z$+Z$;:PRINT@201,Z$+"vehicle"+Z$+"name"+Z$;:PRINT@480,W$;:POKE1535,128:PRINT@332,"        ";:PRINT@332,"";
455 LINEINPUTN1$:PRINT@340,STRING$(12,175);:TEMP$=N$:IFN1$=""ANDN$<>""THEN790ELSEIFN1$="@"THEN75ELSEN$=N1$:GOTO1170
460 IFN$=""ORN$="@"THEN75
465 OPEN"I",#1,N$+"/DAT"
470 FOR Q=1TO8:INPUT#1, D(Q),H$(Q)
475 H(Q)=VAL(H$(Q))
480 J(Q)=0:J1(Q)=0
485 NEXT Q
490 CLOSE#1
495 IFA=3THEN240
500 CLS3:PRINT@0,U$;:PRINT@24,N$;:PRINT@196,Z$+"enter"+Z$+"current"+Z$+"mileage"+Z$;:PRINT@300,"      ";:PRINT@480,W$;:POKE1535,128
505 D$="":N=1
510 GOSUB1440
515 IFA$="@"THEN75ELSEIFASC(A$)=8THEN700ELSEIFASC(A$)=13THEN545
520 IFASC(A$)<48 ORASC(A$)>57 THEN510 ELSE525
525 D$=D$+A$:PRINT@300,D$;
530 N=N+1:IF N<=6 THEN 510
535 GOSUB1440
540 IFASC(A$)=8THEN700ELSEIFASC(A$)=13THEN545ELSE535
545 M(0)=VAL(D$)
550 FORQ=1TO7STEP2:IFM(0)<VAL(H$(Q))THEN1365
555 NEXTQ
560 CLS3:PRINT@0,U$;:PRINT@24,N$;:PRINT@197,Z$+"enter"+Z$+"current"+Z$+"date"+Z$;:PRINT@300,"      ";:PRINT@480,W$;:POKE1535,128:D$="":N=1
565 GOSUB1440
570 IFA$="@"THEN75ELSEIFASC(A$)=8THEN715ELSEIFASC(A$)=13THEN625
575 IFASC(A$)<48 ORASC(A$)>57 THEN565
580 D$=D$+A$:PRINT@300,D$;
585 IFN=2THENV$=MID$(D$,1,2):IFVAL(V$)>12 OR VAL(V$)=0 THEN 1030
590 IFN=4THENV$=MID$(D$,3,2):IFVAL(V$)>31 OR VAL(V$)=0THEN1030
595 N=N+1:IFN<=6 THEN 565
600 GOSUB1440
605 IFASC(A$)=8THEN715ELSEIFASC(A$)=13THEN610ELSE600
610 FORQ=2TO8STEP2:IFRIGHT$(D$,2)<RIGHT$(H$(Q),2)THEN1030
615 IFRIGHT$(D$,2)=RIGHT$(H$(Q),2)ANDLEFT$(D$,2)<LEFT$(H$(Q),2)THEN1030
620 NEXT Q
625 H$(0)=D$
630 FORT=1TO300:NEXTT
635 M$=STR$(M(0)):Z=LEN(M$):CLS3:PRINT@0,U$;:PRINT@67,N$;"...":PRINT@85-Z,M(0)" MILES";:PRINT@96, " MAINTENANCE DUE ";:POKE1117,175:POKE1118,175:POKE1119,175
640 PRINT"FOR   /  /     ";:PRINT@117,LEFT$(H$(0),2);:PRINT@120,MID$(H$(0),3,2);:PRINT@123,RIGHT$(H$(0),2);
645 PRINT@128,STRING$(32,32);
650 FORQ=1TO7STEP2:M(Q)=D(Q)+H(Q)
655 T$(1)="OIL CHANGE":T$(3)="LUBE":T$(5)="TUNE UP":T$(7)="BELTS/HOSES":IF M(0)>=M(Q)+1000THEN725
660 IF M(0)<M(Q)+1000 AND M(0)>M(Q)-500 THEN735ELSEPRINTCHR$(143);T$(Q);" IN";M(Q)-M(0);"MILES":J(Q)=3
665 NEXT Q
670 GOSUB910
675 PRINT@487,"Print";:POKE1516,47:PRINT@493,"Expenses";:POKE1525,47:POKE1526,128:POKE1527,64
680 GOSUB1440
685 B$="PE@":W=INSTR(B$,A$)
690 ONW GOTO1040,1210,75:GOTO680
695 '***** BACKSPACE/CORRECTION
700 Z=LEN(D$):IFZ<1THEN510ELSED$=LEFT$(D$,Z-1):PRINT@300,"      ";:PRINT@300,D$;:N=N-1:GOTO510
705 Z=LEN(D$):IFZ<1THEN295ELSE D$=LEFT$(D$,Z-1):PRINT@L,STRING$(Z-1,143);CHR$(133);CHR$(143);:PRINT@L,D$;:GOTO295
710 Z=LEN(D$):IFZ<1THEN155ELSE D$=LEFT$(D$,Z-1):PRINT@L,STRING$(Z-1,143);CHR$(133);CHR$(143);:PRINT@L,D$;:GOTO155
715 Z=LEN(D$):IFZ<1 THEN 565ELSED$=LEFT$(D$,Z-1):PRINT@300,"      ";:PRINT@300,D$;:N=N-1:GOTO565
720 '***** SERVICE 'FLAGS'
725 PRINTCHR$(191); T$(Q);" OVERDUE":J(Q)=2
730 GOTO665
735 PRINT CHR$(159);T$(Q);" SVC DUE":J(Q)=1
740 GOTO665
745 '***** (A=3) UPDATE RECORDS
750 CLS3:PRINT@0,U$;:PRINT@24,N$;:PRINT@233,"Service"+Z$+"update";:PRINT@297,"Expense"+Z$+"record";
755 GOSUB1440
760 B$="ES@":W=INSTR(B$,A$)
765 ONW GOTO1300,775,75:GOTO755
770 '***** SERVICE UPDATE
775 CLS3:PRINT@0,U$;:PRINT@24,N$;:PRINT@232,Z$+"new"+Z$+"data"+Z$+"for"+Z$+Z$;:PRINT@264,Z$+"vehicle"+Z$+"named"+Z$;:PRINT@480,W$;:POKE1535,128:PRINT@331,"        ";:PRINT@331,"";
780 LINEINPUTN1$:IFN1$="@"THEN75ELSEIFN1$=""ANDN$<>""THEN790ELSEIFN1$<>""THENN$=N1$
785 GOTO1170
790 PRINT@332,N$;:PRINT@340,STRING$(12,175);:GOTO460
795 IFA=2THEN445ELSEIFA=3THEN775:GOTO75
800 IFA=1THEN415ELSEIFA=3THEN810:GOTO75
805 '***** WRITE UPDATES TO "/DAT" FILE
810 CLS3:PRINT@266," SAVING DATA ";
815 OPEN"I",#1,N$
820 OPEN"O",#2,"TEMP/DAT"
825 FOR Q=1TO8:INPUT#1, B(Q),E$(Q)
830 IF H$(Q)<>"" THEN E$(Q)=H$(Q)
835 WRITE#2,B(Q),E$(Q)
840 NEXTQ
845 CLOSE
850 KILLN$+"/DAT":RENAME"TEMP/DAT"TO N$+"/DAT":GOTO75
855 '***** CHK. VEH/NDX AND CRNT/NDX
860 OPEN"I",#1,"VEH/NDX"
865 FORQ=1TO5
870 INPUT#1,N$(Q):IFN$(Q)=""THEN885
875 IF EOF(1)=-1 THEN885
880 NEXTQ
885 CLOSE#1
890 IFA=0THENOPEN"I",#1,"CRNT/NDX"ELSE75 ' *****READ FIRST TIME ONLY
895 INPUT#1,N$
900 CLOSE#1:GOTO75
905 '*****  DATE CALCULATIONS
910 FOR Q=2TO8STEP2
915 MO(Q)=VAL(LEFT$(H$(Q),2)):DA(Q)=VAL(MID$(H$(Q),3,2)):YR(Q)=VAL(RIGHT$(H$(Q),2))
920 M0=VAL(LEFT$(H$(0),2)):D0=VAL(MID$(H$(0),3,2)):Y0=VAL(RIGHT$(H$(0),2))
925 Y(Q)=(Y0-YR(Q))*360
930 T$(2)="OIL CHANGE":T$(4)="LUBE":T$(6)="TUNE UP":T$(8)="BELTS/HOSES"
935 I(Q)=(((M0-1)*30+D0+Y(Q))-((MO(Q)-1)*30+DA(Q)))
940 IF I(Q)>D(Q)*30-15 THEN PRINTCHR$(175); T$(Q);" BY CALENDAR":J1(Q-1)=1
945 NEXTQ
950 RETURN
955 CLS3:PRINT@0,U$+STRING$(6,128);:PRINT@21,"NEW VEHICLE";:PRINT@231,"NAME ALREADY USED";:SOUND200,5:SOUND150,10:FORT=1TO425:NEXT:GOTO385
960 '***** WRITE NEW VEH. TO FILE AND OPEN BLANK "/DTL" FILE
965 OPEN"O",#1,"VEH/NDX"
970 FORN=1TO5:WRITE#1,N$(N)
975 NEXT N
980 CLOSE#1
985 OPEN"O",#1,N$+"/DTL"
990 WRITE#1,A$(0),B$(0),C$(0)
995 CLOSE#1:GOTO75
1000 '***** CHECK MAX. # VEHICLES
1005 N=1
1010 IFN$(N)=N$THEN955
1015 IFN$(N)=""THENN$(N)=N$:M=N:GOTO115
1020 N=N+1:IFN>5THENCLS3:PRINT@260,"LIMIT 5 VEHICLES";:FORT=0TO300:NEXTT:GOTO75
1025 GOTO1010
1030 CLS3:PRINT@264,"NOT A VALID DATE";:FORX=0TO300:NEXT
1035 GOTO560
1040 '***** PRINTOUT OF SERVICE DUE
1045 PRINT@448," BE SURE THE PRINTER IS ONLINE "
1050 PRINT#-2,STRING$(6,13)
1055 PRINT@448,STRING$(32,175);
1060 PRINT#-2,TAB(28)"***MAINTENANCE FOR "N$"***"
1065 PRINT#-2,TAB(28)LEFT$(H$(0),2);"/";MID$(H$(0),3,2);"/";RIGHT$(H$(0),2);TAB(44)M(0)" MILES":PRINT#-2,STRING$(80,61):PRINT#-2,"Item:";TAB(30)"Due";TAB(40)"Overdue";TAB(55)"Miles to go"
1070 FORQ=1TO7STEP2
1075 P(Q)=0:IFJ(Q)=2THENJ$(Q)="M":T=43:GOTO1090 '****SVC. OVERDUE 'FLAG'
1080 P(Q)=0:IFJ(Q)=1THENJ$(Q)="M":T=31:GOTO1090 '****SVC. DUE 'FLAG'
1085 P(Q)=0:IFJ1(Q)=1THENJ$(Q)="C":T=31ELSE1095 '****DUE BY CALENDAR 'FLAG'
1090 P(Q)=1:PRINT#-2,TAB(3)T$(Q);TAB(T)J$(Q)
1095 M$=STR$(M(Q)-M(0)):IFJ(Q)=3THENPRINT#-2,TAB(3)T$(Q);TAB(65-LEN(M$))M$
1100 NEXTQ
1105 GOTO1130
1110 FORQ=1TO7STEP2:IFP(Q)=1THEN1120 '****'FLAG' TO PRINT SVC. SUMMARIES
1115 NEXTQ
1120 IFQ=1THEN1135ELSEIFQ=3THEN1140ELSEIFQ=5THEN1145ELSEIFQ=7THEN1155
1125 IFQ<7THEN1115ELSEPRINT#-2,"PERFORMED ABOVE ITEMS .................(MILEAGE & DATE FOR SERVICE UPDATE)";STRING$(5,13):GOTO1210
1130 PRINT#-2,CHR$(13);TAB(15)"C=Calendar interval, M=Mileage interval";STRING$(3,13):GOTO1110
1135 PRINT#-2,"CHECK LIST:";CHR$(13);CHR$(13);TAB(3)"Change oil and filter...check fluid levels under hood...check for any           leakage, battery corrosion, loose belts, etc.";STRING$(2,13):GOTO1125
1140 PRINT#-2,CHR$(13);TAB(3)"Lube all fittings...oil door hinges and strikers, hood hinges, etc...           check transmission & differential.  Inspect exhaust system.  Check for          damage under car.";STRING$(2,13):GOTO1125
1145 PRINT#-2,CHR$(13);TAB(3)"Tune up engine...change spark plugs, air and fuel filters, check plug           wires and ignition system...check PCV and breather...check timing and "
1150 PRINT#-2,TAB(3)"idle speed...choke action when applicable...check overall performance.";STRING$(2,13):GOTO1125
1155 PRINT#-2,CHR$(13);TAB(3)"Belts/hoses...check belt tension and condition...hoses for softness or          deterioration...clamps for tightness...radiator should be free of debris"
1160 PRINT#-2,TAB(3)"in cooling fins, and should be free from leakage. Also inspect brake linings    and operation.  Check tires for tread depth and even wear.";STRING$(2,13):GOTO1125
1165 '***** CHECK MAX. 8 CHARACTERS
1170 IFLEN(N$)>8THENCLS3:PRINT@0,U$;:PRINT@260,"ONLY 8 LETTERS ALLOWED ";:N$=TEMP$:SOUND200,5:SOUND150,10:FORT=1TO400:NEXT T:ON A GOTO385,445,775,855
1175 ON A GOTO1005,1185,1185,855
1180 '*****  CHK. FOR NAME IN "VEH/NDX"
1185 FORQ=1TO5:IFN$(Q)=N$THEN460
1190 IFN$(Q)=""THENCLS3:PRINT@0,U$;:PRINT@260,"NO VEHICLE BY THAT NAME";:SOUND200,5:SOUND150,10:FORT=1TO450:NEXTT:N$=TEMP$:GOTO795
1195 NEXTQ
1200 CLS3:PRINT@260,"NO VEHICLE BY THAT NAME";:FORT=1TO350:NEXT T:GOTO795
1205 '***** CPTM COMPUTATION
1210 CLS3:SUM=0:PRINTU$;:PRINT@24,N$:PRINT@266,"please"+Z$+"wait";
1215 FORQ=1TO150:B$(Q)="":NEXTQ
1220 OPEN"I",#1,N$+"/DTL"
1225 FORQ=1TO150:INPUT#1,A$(Q),B$(Q),C$(Q):IFA$(Q)=""THEN1245
1230 IFEOF(1)=-1THEN1245
1235 SUM=SUM+VAL(B$(Q))
1240 NEXTQ
1245 CLOSE#1
1250 CLS3:PRINT@229,"start"+Z$+"figuring"+Z$+"costs"+Z$+Z$+Z$;:PRINT@261,Z$+Z$+Z$+"at"+Z$+"what"+Z$+"mileage";STRING$(5,128);:PRINT@364,"      ";
1255 PRINT@0,U$;:PRINT@24,N$;:PRINT@364,"";:LINEINPUTMI$:PRINT@370,STRING$(14,175);:Z=LEN(MI$):IFMI$=""THEN1250ELSEIFMI$="@"THEN75
1260 FORQ=1TOZ:IFASC(MID$(MI$,Q,1))<48ORASC(MID$(MI$,Q,1))>57THENMI$="":GOTO1250
1265 NEXTQ
1270 MI=VAL(MI$):Y=(M(0)-MI)/1000
1275 CLS3:PRINTU$;:PRINT@24,N$;:PRINT@99,STRING$(25,32);:PRINT@99,"FROM"MI"TO"M(0)"MILES";
1280 PRINT@163,STRING$(25,32);:PRINT@163,"EXPENSE TOTAL:";:PRINT@179,USING"$$###.##";SUM;
1285 PRINT@259," COST PER THOUSAND MILES ";:PRINT@291," OF DRIVING:    ";:PRINT@307,USING"$$###.##";INT(SUM*100/Y)/100;:PRINTCHR$(32);
1290 PRINT@480,W$;:POKE1535,128
1295 A$=INKEY$:IFA$="@"THEN75ELSE1295
1300 '***** LEAVE "MAIN/BAS" PROGRAM
1305 '
1310 OPEN"O",#1,"CRNT/NDX"
1315 WRITE#1,N$
1320 CLOSE
1325 IFA=5THEN1340 '*****CALLING IT QUITS 
1330 LOAD"EXPNS/BAS",R
1335 '***** (A=5)  QUIT
1340 CLS:NEW
1345 '
1350 PCLEAR1:GOTO55
1355 IFA=1THENN$=TEMP$:N1$="":A$="":N$(M)="":GOTO75ELSE GOTO75
1360 '*****
1365 CLS3:PRINT@264,"NOT VALID MILEAGE";:FORX=0TO300:NEXT X:GOTO500
1370 '***** (A=4) VEHICLE INDEX SCREEN
1375 CLS:PRINTU$;:PRINT@19,"vehicle";CHR$(128);"index";
1380 PRINT:PRINT
1385 FORM=1TO5:PRINTN$(M),:NEXTM
1390 PRINT@480,W$;:POKE1535,128
1395 A$=INKEY$:IFA$<>"@"THEN1395ELSE75
1400 '***** UTILITY TO INITIALIZE "VEHICLE INDEX" FILE   
1405 'N$(1)="SAMPLE"
1410 OPEN"O",#1,"VEH/NDX"
1415 FOR Q=1TO5:WRITE#1,N$(Q):NEXTQ
1420 CLOSE#1
1425 OPEN"O",#1,"CRNT/NDX"
1430 WRITE#1,N$(0)
1435 CLOSE#1:END
1440 A$=INKEY$:IFA$=""THEN1440ELSE RETURN
1445 '*****MAIN MODULE/ BY L. ANDERSON/ (C)1985
