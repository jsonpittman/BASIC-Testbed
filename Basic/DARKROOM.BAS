0 'COPYRIGHT 1989, FALSOFT INC.
10 MOTOROFF:BEEP=10:ENLARGER=30:EO=2'EO=2 IF ENLARGER OFF;EO=0 IF ENLARGER ON
20 I$="*":BL$=CHR$(128):P1=32*5+4:P2=P1+12:P3=P1+24:J0=30:TIMER=0:BCOUNT=0:PAUSE=80
99 REM MAIN LOOP
100 CLS0:PRINT@32*1+3,"COMPUTER"+BL$+"OPERATED"+BL$+"DARKROOM";:PRINT@32*3+8,"(C)"BL$"D."BL$"BODNAR"BL$"1986";
107 IF J0<20 THEN PRINT@P1,I$;:PRINT@P2,BL$;:PRINT@P3,BL$; ELSE IF J0>40 THEN PRINT@P3,I$;:PRINT@P1,BL$;:PRINT@P2,BL$; ELSE PRINT@P2,I$;:PRINT@P1,BL$;:PRINT@P3,BL$;
110 PRINT@32*7+2,"beep";:PRINT@32*8+2,"time";:PRINT@32*7+12,"enlarger";:PRINT@32*10+15,"on";:PRINT@32*12+15,"off";:PRINT@32*7+24,"enlarger";:PRINT@32*8+26,"time";
115 PRINT@32*10+2,BEEP;:PRINT@32*10+26,ENLARGER;
120 GOSUB 1000 ' READ JOYSTICKS 0 AND 1 AND BUTTON
125 IF BOFF<>-1  THENPRINT@32*12+2,BEEP-INT(TIMER/60);:IF TIMER>=BEEP*60 THEN GOSUB 900'SOUND BEEP IF ENABLED
130 IF (J0<40 AND J0>20) AND J1<20 THEN EO=0:GOSUB 500 ' ENLARGER OFF
140 IF (J0<40 AND J0>20) AND J1>40 THEN EO=2:GOSUB 500 ' ENLARGER ON
150 IF (J0<40 AND J0>20) AND BU=0 THEN GOSUB 600' ENLARGER ON
160 IF J0<20 THEN GOSUB 700:GOTO107'SET BEEP
170 IF J0>40 THEN GOSUB 800:GOTO107'SET ENLARGER TIME
180 PRINT@32*15+3,BL$;BL$;"button"BL$"to"BL$"start"BL$"enlarger";BL$;
400 GOTO 107
499 REM ENLARGER ON/OFF TO FRAME
500 IF EO=0 THEN GOSUB 2000 ELSE GOSUB 3000
505 IF EO=0 THEN PRINT@32*10+13,I$;:PRINT@32*12+13,BL$; ELSE PRINT @32*10+13,BL$;:PRINT@32*12+13,I$;
510 RETURN
599 REM ENLARGER ON FOR TIME
600 EO=0:PRINT@32*15+3,BL$;BL$;BL$;"button";BL$;"to";BL$;"stop";BL$;"enlarger";BL$;:TIMER=0:GOSUB2000:GOSUB500
605 FF=0'FF IS TO AVOID DOUBLE HIT ON BUTTON - DELAY
610 FF=FF+1:GOSUB1000:IF (BU=0AND FF>10) THEN GOTO 620 ELSE PRINT@32*12+26,ENLARGER-INT(TIMER/60);:IF TIMER<ENLARGER*60 THEN 610
620 EO=2:GOSUB500:PRINT@32*12+25,STRING$(5,128);:BCOUNT=0:RETURN
699 REM SET BEEP
700 PRINT@32*15+3,"button"BL$"to"BL$"set"BL$"beep"BL$"time";BL$;BL$;BL$;:GOSUB 1000
710 IF BU=1 THEN BCOUNT=0:RETURN
720 IF J1>40 THEN BEEP=BEEP-1 ELSE IF J1<20 THEN BEEP=BEEP+1
730 IF BEEP<1 THEN BEEP=0:BOFF=-1 ELSE BOFF=0
735 PRINT@32*10+2,BEEP;
737 FOR DE=1 TO PAUSE:NEXT ' DELAY TO MAKE SETTING MORE EASY TO CONTROL
740 GOTO 700
799 REM ENLARGER TIME SET
800 PRINT@32*15+3,"button"BL$"to"BL$"set"BL$"enlarger"BL$"time";:GOSUB 1000
810 IF BU=1 THEN BCOUNT=0:RETURN
820 IF J1>40 THEN ENLARGER=ENLARGER-1 ELSE IF J1<20 THEN ENLARGER=ENLARGER+1
830 IF ENLARGER<1 THEN ENLARGER=1
835 PRINT@32*10+26,ENLARGER;
837 FOR DE=1 TO PAUSE:NEXT ' DELAY TO MAKE SETTING MORE EASY TO CONTROL
840 GOTO 800
899 REM BEEP
900 IF BOFF THEN RETURN ELSETIMER=0:BCOUNT=BCOUNT+1:IF BCOUNT>6 THEN BCOUNT=1
910 FOR X=1 TO BCOUNT
920 PLAY"T20AP30"
930 NEXT X
940 RETURN
1000 J0=JOYSTK(0):J1=JOYSTK(1):BU=PEEK(&HFF00)AND1
1010 RETURN
1999 REM MOTORON
2000 POKE&HFF21,PEEK(&HFF21)OR 8:RETURN
2999 REM MOTOROFF
3000 POKE&HFF21,PEEK(&HFF21)AND&HF7:RETURN
59999 REM D. BODNAR 11-11-86
60000 VERIFYON:SAVE"DARKROOM":SAVE"DARKROOM:1"
