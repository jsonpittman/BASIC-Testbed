1 'FREEDOM
2 'WRITTEN BY RICHARD ESTRADO
3 'COPYRIGHT (C) APRIL 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 '********************
20 '*     FREEDOM      *
30 '*                  *
40 '*  by R.ESTRADO    *
50 '********************
60 '
70 '        OMMIT LINE 80 FOR COCO1,COCO2
80 POKE 65497,0
90 '        INITIALIZE
100 DIM R(20),P(20),Q(20),S(20)
110 PMODE 4,1:SCREEN 1,1:PCLS
120 P(0)=128:Q(0)=100:R(0)=128:S(0)=100
130 P=1:PP=2
140 '       MAIN PROGRAM
150 A=RND(0)*2-1:B=RND(0)*2-1:C=RND(0)*2-1:D=RND(0)*2-1
160 A=A*5:B=B*5:C=C*5:D=D*5
170 FOR X=1 TO 15+RND(20)
180 GOSUB 290
190 IF P=20 GOSUB 350
200 IF ABS(P(P)-R(P))>50 THEN A=-A:C=-C
210 IF ABS(Q(P)-S(P))>40 THEN B=-B:D=-D
220 LINE (P(PP),Q(PP))-(R(PP),S(PP)),PRESET
230 LINE (P(P),Q(P))-(R(P),S(P)),PSET
240 P=P+1:IF P=21 THEN P=1
250 PP=PP+1:IF PP=21 THEN PP=1
260 NEXT X
270 GOTO 140
280 '      CHECK BOUNDARIES
290 P(P)=P(P-1)+A:IF P(P)>250 OR P(P)<5 THEN A=-A:P(P)=P(P)+A
300 Q(P)=Q(P-1)+B:IF Q(P)>192 OR Q(P)<5 THEN B=-B:Q(P)=Q(P)+B
310 R(P)=R(P-1)+C:IF R(P)>250 OR R(P)<5 THEN C=-C:R(P)=R(P)+C
320 S(P)=S(P-1)+D:IF S(P)>250 OR S(P)<5 THEN D=-D:S(P)=S(P)+D
330 RETURN
340 '      LINK ARRAY
350 P(0)=P(20):Q(0)=Q(20):R(0)=R(20):S(0)=S(20)
360 RETURN
