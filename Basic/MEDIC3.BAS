0 'COPYRIGHT 1989, FALSOFT INC.
1 '****************************
2 '*          MEDIC3          *
3 '*                          *
4 '*         RON HINTON       *
5 '*      50339 STAGECOACH    *
6 '*   E.LIVERPOOL, OH 43920  *
7 '*     (c)opyright 1989     *
8 '****************************
9 V=10:H=6:Y=00
10 PALETTERGB:PALETTE14,9:PALETTE1,0:PALETTE10,55:PALETTE13,45
15 GOTO40
20 LOCATE TA+1,1:ATTR7,1:PRINT"MEDICAL RECORDS";Y:LOCATE0,3:ATTR3,1:PRINTPR$:RETURN
40 CLEAR5000
50 DIM I(150),N$(150),S$(150),D$(150),A(150),IN(150)
55 ONBRKGOTO90:ONERRGOTO10000
60 W$="$####.##"
70 X$="$$####.##"
80 W=80 'CHANGE TO 40 FOR COMPOSITE OR TV
81 IF W=40 THENPALETTECMP: PALETTE14,25:PALETTE1,0:PALETTE10,38:PALETTE13,45
82 PR$=STRING$(W,"-")
85 POKE65497,0
86 WIDTH W
90 CLS2:IF W=80THEN TA=30ELSE TA=12:GOSUB 20
92 GOSUB20
95 V=30:H=6
120 ATTR3,1:LOCATETA,H:PRINT" 1) LIST ALL RECORDS"
130 PRINT:PRINTTAB(TA)" 2) LIST BY NAME"
140 PRINT:PRINTTAB(TA)" 3) EDIT RECORDS"
150 PRINT:PRINTTAB(TA)" 4) ADD RECORDS"
160 PRINT:PRINTTAB(TA)" 5) LOAD"
170 PRINT:PRINTTAB(TA)" 6) SAVE"
180 PRINT:PRINTTAB(TA)" 7) BEGIN NEW RECORD"
190 PRINT:PRINTTAB(TA)" 8) PRINT"
200 PRINT:PRINTTAB(TA)" 9) EXIT"
210 A$=INKEY$:IFA$=""THEN210
215 K=VAL(A$):IF K<1 OR K>9 THEN80
220 ON K GOTO500,1360,1010,440,890,850,250,4000,12000
240 SOUND100,1:GOTO80
250 CLS:LOCATE5,5:ATTR7,1:INPUT " Enter Year...";Y
255 I=0
260 CLS
270 I=I+1
280 GOSUB20
282 IF W=80 THEN TA=20ELSE TA=10
285 LOCATETA,H
290 PRINT "Item Number";(I):ATTR1,1
295 PRINT
300 POKE282,255:PRINTTAB(TA)"Name.......";:ATTR3,1:INPUT N$(I):ATTR1,1
305 IF W=80THEN LOCATE78,10:ATTR6,1:PRINTCHR$(125):ATTR1,1
310 POKE282,0:LOCATE TA,10:PRINT"Service....";:ATTR3,1:LINEINPUT S$(I):ATTR1,1
312 IF LEN(S$(I)) >45 THEN SOUND100,2:ATTR6,1,B:PRINT"                    ** STRING TOO LONG! TRY AGAIN **":FORQ=1TO200:NEXT:GOTO305
315 POKE282,255
320 PRINT:PRINTTAB(TA)"Date.......";:ATTR3,1:INPUTD$(I):ATTR1,1
330 PRINT:PRINTTAB(TA)"Amount.....";:ATTR3,1:INPUT A(I):ATTR1,1
340 PRINT:PRINTTAB(TA)"Ins. Rec'vd";:ATTR3,1:INPUT IN(I):ATTR1,1
350 PRINT:PRINT:T=0:C=0
360 IF I>149THEN410
365 LOCATETA-10,H+15:ATTR7,1
370 PRINT" DO YOU WISH TO ENTER MORE  ITEMS INTO THE RECORDS? <Y/N>"
380 A$=INKEY$:IFA$=""THEN380
390 IF A$="N"THEN450
395 IF A$="n"THEN450
400 GOTO260
410 LOCATE35,15
420 ATTR7,1,B:PRINT I;" ITEMS":LOCATE35,H+17:PRINT"BUFFER FULL"
430 SOUND200,1:FORQ=1TO300:NEXT:GOTO80
440 CLS:GOTO270
450 Z=I:GOTO80
500 CLS
510 T=0:C=0:H=6:I=1
530 GOSUB20
535 LOCATE V-27,5:ATTR7,1
540 IF W=80THENPRINT"  NAME     DATE                 SERVICE                            AMOUNT"
545 IF W=40THENPRINT" NAME  DATE    AMOUNT       SERVICE"
550 ATTR3,1:PRINT
555 IF W=40THEN565ELSE560
560 IF W=80THEN LOCATE0,H+I:PRINTI;
561 LOCATE4,H+I:PRINTN$(I);
562 LOCATE14,H+I:PRINTD$(I);
563 ATTR2,1:LOCATE22,H+I:PRINTS$(I);
564 ATTR3,1:LOCATE68,H+I:PRINTUSING W$;A(I):GOTO570
565 IF W=40THEN PRINTI;
566 PRINTN$(I);
567 PRINTTAB(2)D$(I);
568 PRINTTAB(2);:PRINTUSING W$;A(I)
569 ATTR2,1:PRINTS$(I):ATTR3,1
570 T=T+A(I)
580 C=C+IN(I)
590 IF LEN(N$(I))=0THEN I=I-1:GOTO640
610 I=I+1
615 IF I=7AND W=40GOTO800ELSEIF I=14THEN H=-7:GOTO710
616 IF I=21AND W=40GOTO802ELSE IF I=28THEN H=-21:GOTO710
617 IF I=35AND W=40GOTO804ELSEIF I=42THEN H=-35:GOTO710
618 IF I=49AND W=40GOTO806ELSEIF I=56THEN H=-49:GOTO710
619 IF I=63ANDW=40GOTO808ELSEIF I=70THEN H=-63:GOTO710
620 IF I=77ANDW=40GOTO810ELSEIF I=84THEN H=-77:GOTO710
621 IF I=91ANDW=40GOTO812ELSEIF I=98THEN H=-91:GOTO710
622 IF I=105ANDW=40GOTO814ELSEIF I=112THEN H=-105:GOTO710
623 IF I=119ANDW=40GOTO816ELSEIF I=126THEN H=-119:GOTO710
624 IF I=133ANDW=40GOTO818ELSEIF I=140THEN H=-133:GOTO710
625 IF I=147ANDW=40GOTO820ELSEIF I=154THEN H=-147:GOTO710
626 IF I=161ANDW=40GOTO822ELSEIF I=168THEN H=-161:GOTO710
630 GOTO555
640 ATTR7,1:PRINT:PRINT " TOTAL      :      ";
650 ONERRGOTO10000:PRINTUSING X$;T
670 PRINT:PRINT" INSURANCE  :      "; 
680 PRINTUSING X$;C
690 PRINT:PRINT" BALANCE    :      ";
700 PRINTUSING X$;T-C
705 ATTR3,1:GOTO760
710 ATTR7,1:PRINT"PRESS ANY KEY TO CONTINUE"
715 ONBRKGOTO90
720 A$=INKEY$
730 IF A$=""THEN720
740 CLS
750 GOTO530
760 PRINT:PRINT"PRESS <ENTER> FOR MENU"
765 ONBRKGOTO90
770 A$=INKEY$:IF A$=""THEN770
780 Z=I:GOTO80
790 ONERR GOTO10000:CLS:Z=I
800 H=0:GOTO710
802 H=-14:GOTO710
804 H=-28:GOTO710
806 H=-35:GOTO710
808 H=-56:GOTO710
810 H=-70:GOTO710
812 H=-84:GOTO710
814 H=-98:GOTO710
816 H=-112:GOTO710
818 H=-126:GOTO710
820 H=-140:GOTO710
822 H=-154:GOTO710
850 ONERR GOTO10000:CLS:Z=I
860 YR$=STR$(Y)
865 OPEN"O",#1,YR$
868 PRINT#1,Y,Z
870 FOR I=1TO Z
872 WRITE#1,N$(I),S$(I),D$(I),A(I),IN(I)
874 NEXTI
880 CLOSE#1
885 GOTO80
890 CLEAR5000:CLS
892 DIM I(150),N$(150),S$(150),D$(150),A(150),IN(150)
893 ONERR GOTO10000
894 W$="$####.##":X$="$$####.##"
895 PRINT:PRINT"What year do you wish to load";
896 INPUT YR
900 YR$=STR$(YR)
950 IF LEN(YR$)>2 THEN YR$=CHR$(32)+RIGHT$(YR$,2)
975 OPEN"I",#1,YR$
978 INPUT#1,Y,Z
980 FORI=1TO Z
985 INPUT#1,N$(I),S$(I),D$(I),A(I),IN(I)
987 NEXTI
995 CLOSE #1
1000 I=I-1:GOTO80
1010 CLS
1020 GOSUB20:V=10:H=6:LOCATEV,H
1030 PRINT"ENTER NUMBER OF ITEM ";
1035 IF W=40THEN V=1
1040 INPUT N
1050 LOCATEV,H+2:PRINT"WHICH ENTRY DO YOU WANT CHANGED?"
1060 LOCATEV+5,H+4:ATTR1,1
1070 PRINT" 1) NAME     : "N$(N)
1080 PRINTTAB(V+5)" 2) SERVICE  : "S$(N)
1090 PRINTTAB(V+5)" 3) DATE     : "D$(N)
1100 PRINTTAB(V+5)" 4) AMOUNT   : "A(N)
1110 PRINTTAB(V+5)" 5) INSURANCE: "IN(N)
1111 ATTR6,1:PRINT:PRINTTAB(V+5)" 6) ABORT":ATTR3,1
1120 A$=INKEY$:IFA$=""THEN1120
1125 E=VAL(A$):IF E<1 OR E>6 THEN1050
1129 IFW=40THEN V=21
1130 ON E GOTO 1140,1150,1160,1170,1180,80
1140 IF W=80THEN LOCATEV+19,H+4ELSE IF W=40THEN LOCATEV-1,H+4:ATTR6,1:POKE282,255:INPUTN$(N):ATTR1,1:GOTO1200
1142 ATTR6,1:POKE282,255:INPUTN$(N):ATTR1,1:GOTO1200
1150 IF W=80THEN LOCATEV+19+47,H+5:ATTR6,1:PRINTCHR$(125):LOCATEV+19,H+5:POKE282,0:LINEINPUTS$(N):ATTR1,1:POKE282,255
1152 IF W=40THENLOCATEV,H+5:ATTR6,1:POKE282,0:LINEINPUTS$(N):ATTR1,1:POKE282,255
1155 IF LEN(S$(N))>45 THENSOUND100,2:ATTR6,1:PRINT:PRINT"                 ** STRING TOO LONG! UNDER 45 CHARS. **":FORQ=1TO250:NEXT:GOTO1150
1156 GOTO1200
1160 IF W=80THEN LOCATEV+19,H+6ELSE IF W=40THEN LOCATEV-1,H+7:ATTR6,1:INPUT D$(N):ATTR1,1:GOTO1200
1162 ATTR6,1:INPUT D$(N):ATTR1,1:GOTO1200
1170 IF W=80THEN LOCATEV+19,H+7ELSE IF W=40 THEN LOCATEV-1,H+8:ATTR6,1:INPUT A(N):ATTR1,1:GOTO1200
1172 ATTR6,1:INPUT A(N):ATTR1,1:GOTO1200
1180 IF W=80THEN LOCATEV+19,H+8ELSE IF W=40THEN LOCATE V-1,H+9:ATTR6,1:INPUT IN(N):ATTR1,1:GOTO1200
1182 ATTR6,1:INPUT IN(N):ATTR1,1:GOTO1200
1200 IF W=80THEN LOCATE10,19ELSELOCATE5,19
1205 PRINT"<A>nother change       <M>enu"
1210 A$=INKEY$:IFA$=""THEN1210
1220 IFA$="A"THEN1240ELSE1230
1230 T=0:C=0:Z=I:GOTO80
1240 LOCATE4,19:ATTR4,1:PRINT"                                  ":GOTO1120
1360 CLS 'LIST BY NAME
1370 T=0:C=0: V=10:HO=6:R=0:I=0:WI=1
1380 GOSUB20:LOCATE2,4:PRINT "NAME: ";
1390 ATTR6,1:INPUTP$:ATTR3,1
1410 IF W=80THEN LOCATE4,6:PRINT"DATE                        SERVICE                        AMOUNT  INSURANCE"
1415 IF W=40THENLOCATE4,6:PRINT"DATE    AMOUNT     INS.      SERVICE"
1420 FOR I=1TOZ
1430 IF N$(I)=P$ THEN 1435ELSE1510
1435 IF W=80GOTO1440ELSE1445' IF W=40 GOTO1445
1440 IF W=80THEN LOCATE0,HO+WI:PRINTI:LOCATE4,HO+WI:PRINT D$(I):ATTR2,1:LOCATE13,HO+WI:PRINT S$(I):ATTR3,1:LOCATE61,HO+WI:PRINTUSING W$;A(I):LOCATE71,HO+WI:ATTR5,1:PRINTUSING W$;IN(I):ATTR3,1
1445 IF W=40THENPRINTI;:PRINTTAB(1) D$(I);:PRINTTAB(1);:PRINTUSING W$;A(I);:PRINTTAB(1);:ATTR5,1:PRINTUSING W$;IN(I):ATTR2,1:PRINT S$(I):ATTR3,1
1475 R=R+1:A=A+1:WI=WI+1
1480 IF R=7ANDW=40GOTO1620ELSEIF R=14THEN HO=-8:GOTO1640
1481 IF R=21ANDW=40GOTO1622ELSEIF R=28THEN HO=-22:GOTO1640
1482 IF R=35AND W=40GOTO1624ELSEIF R=42THEN HO=-35:GOTO1640
1483 IF R=49AND W=40GOTO1626ELSEIF R=56THEN HO=-50:GOTO1640
1484 IF R=63AND W=40GOTO1628ELSEIF R=70THEN HO=-64:GOTO1640
1485 IF R=77AND W=40GOTO1630ELSEIF R=84THEN HO=-78:GOTO1640
1486 IF R=91AND W=40GOTO1632ELSEIF R=98THEN HO=-92:GOTO1640
1487 IF R=105AND W=40GOTO1634ELSEIF R=112THEN HO=-106:GOTO1640
1490 T=T+A(I)
1500 C=C+IN(I)
1510 NEXTI
1530 ATTR7,1:PRINT:PRINT"  TOTAL     : ";
1540 PRINTUSING X$;T
1550 PRINT:PRINT"  INSURANCE : ";
1560 PRINTUSING X$;C
1570 PRINT:PRINT"  BALANCE   : ";
1580 PRINTUSING X$;T-C
1585 ATTR3,1
1590 A$=INKEY$
1600 IF A$=""THEN1590
1610 Z=I:GOTO80
1620 HO=0:GOTO1640
1622 HO=-14:GOTO1640
1624 HO=-28:GOTO1640
1626 HO=-42:GOTO1640
1628 HO=-56:GOTO1640
1630 HO=-70:GOTO1640
1632 HO=-84:GOTO1640
1634 HO=-98:GOTO1640
1640 ATTR7,1:PRINT"PRESS ANY KEY TO CONTINUE"
1641 A$=INKEY$
1650 IFA$=""THEN1641
1651 ATTR3,1
1658 IFW=40 THENCLS:GOSUB20:LOCATE3,4:ATTR6,1:PRINTP$:ATTR3,1:LOCATE4,6:PRINT"DATE   AMOUNT   INS.     SERVICE"
1660 GOTO1430
1669 IF W=80THENCLS:GOSUB20:LOCATE3,4:ATTR6,1:PRINT P$:ATTR3,1:LOCATE4,6:PRINT"DATE                        SERVICE                        AMOUNT  INSURANCE"
4000 CLS
4005 POKE150,7' 9600BAUD AT DOUBLE SPEED
4006 IF W=80THENV=30ELSEV=10
4010 GOSUB20:PRINT:PRINTTAB(V)"1> PRINT ALL RECORDS":PRINT:PRINTTAB(V)"2> PRINT BY NAME":PRINT:PRINTTAB(V)"3> MAIN MENU"
4020 PRINT:PRINT:PRINTTAB(V+2)"< PRESS ONE >"
4030 A$=INKEY$:IF A$=""THEN4030
4040 IFA$="1"THEN 5000
4050 IFA$="2"THEN6000
4055 IF A$="3"THEN80
4060 GOTO4030
5000 T=0:C=0
5010 I=1
5015 PRINT#-2,CHR$(27);CHR$(104);CHR$(1):PRINT#-2,TAB(10)"MEDICAL RECORDS  ";Y
5030 PRINT#-2,STRING$(40,"-")
5040 PRINT#-2,CHR$(27);CHR$(104);CHR$(0);:PRINT#-2,"      NAME    DATE                    SERVICE                           AMOUNT"
5045 PRINT#-2,STRING$(80,"-")
5050 PRINT#-2,I;:PRINT#-2,TAB(5)N$(I);:PRINT#-2,TAB(14)D$(I);:PRINT#-2,TAB(23)S$(I);
5060 T=T+A(I)
5070 C=C+IN(I)
5080 IF LEN(N$(I))=0THEN I=I-1:GOTO5160
5090 PRINT#-2,TAB(70);:PRINT#-2,USING W$;A(I)
5100 I=I+1
5150 GOTO5050
5160 PRINT#-2:PRINT#-2:PRINT#-2,"     TOTAL:            ";
5170 PRINT#-2,USING X$;T
5180 PRINT#-2
5190 PRINT#-2,"     INSURANCE:        ";
5200 PRINT#-2,USING X$;C
5210 PRINT#-2
5220 PRINT#-2,"     BALANCE:          ";
5230 PRINT#-2,USING X$;T-C
5240 Z=I:GOTO80
6000 T=0:C=0
6010 PRINT"NAME: ";
6020 INPUTP$
6030 PRINT#-2,CHR$(27);CHR$(104);CHR$(1):PRINT#-2,TAB(10)"MEDICAL RECORDS";Y
6035 PRINT#-2,STRING$(40,"-");
6040 PRINT#-2,CHR$(27);CHR$(104);CHR$(0);:PRINT#-2,TAB(3)P$
6060 PRINT#-2,"   DATE                     SERVICE                          AMOUNT   INS/REC."
6070 PRINT#-2,STRING$(80,"-")
6080 FOR I=1TO Z
6090 IF N$(I)=P$THEN6100ELSE6180'
6100 PRINT#-2,TAB(3)D$(I);:PRINT#-2,TAB(12)S$(I);
6110 PRINT#-2,TAB(59);
6120 PRINT#-2,USING W$;A(I);
6130 PRINT#-2,TAB(70);;
6140 PRINT#-2,USING W$;IN(I)
6150 R=R+1:A=A+1
6160 T=T+A(I)
6170 C=C+IN(I)
6180 NEXTI
6190 PRINT#-2:PRINT#-2
6200 PRINT#-2,"    TOTAL:       ";
6210 PRINT#-2,USING X$;T
6220 PRINT#-2
6230 PRINT#-2,"    INSURANCE:   ";
6240 PRINT#-2,USING X$;C
6250 PRINT#-2:GOTO6260
6260 PRINT#-2,"    BALANCE:     ";
6270 PRINT#-2,USING X$;T-C
6280 Z=I:GOTO80
10000 IF ERNO=26THEN LOCATE24,5:ATTR6,1,B:PRINT"*** FILE NOT ON DISK ***":FORQ=1TO500:ATTR3,1:NEXT:GOTO895
10005 IF ERNO=28 AND ERLIN=865THEN LOCATE28,8:ATTR6,1,B:PRINT"** DISK FULL **":ATTR6,1:LOCATE20,10:PRINT"Press any key to return to menu":LOCATE20,12:PRINT"and save file to a different disk":EXEC44539:GOTO80
10010 PRINTERNO:PRINT ERLIN:PRINT"PRESS ANY KEY FOR MENU":EXEC44539:GOTO80
12000 CLS:PRINT"BE SURE TO SAVE CURRENT FILE":PRINT:PRINT"ARE YOU SURE":INPUT S$
12010 IF S$="Y"THEN13000ELSE80
13000 POKE65496,0:NEW
