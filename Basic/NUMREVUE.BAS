10 REM"NUMBER REVIEW"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1987
30 CLEAR 2000:QT=RND(-TIMER):GOSUB 390:R=RND(8)
40 PMODE 3,1:PCLS:SCREEN 1,1
50 COLOR 6:LINE(55,3)-(200,25),PSET,B:LINE(50,0)-(205,28),PSET,B:PAINT(52,2),6,6
60 DRAW"C7A2S12BM80,5"+N$+U$+M$+B$+E$+R$+S$
70 DRAW"C0A2S8BM20,40"+W$+H$+I$+C$+H$
80 DRAW"BM110,40"+N$+U$+M$+B$+E$+R$
90 DRAW"BM210,40"+C$+O$+M$+E$+S$
100 IF R=1 THEN Z$=N1$ ELSE IF R=2 THEN Z$=N2$ ELSE IF R=3 THEN Z$=N3$ ELSE IF R=4 THEN Z$=N4$ ELSE IF R=5 THEN Z$=N5$ ELSE IF R=6 THEN Z$=N6$ ELSE IF R=7 THEN Z$=N7$ ELSE Z$=N8$
110 A=RND(2)
120 IF A=1 THEN QQ$=A$+F$+T$+E$+R$:R=R+1
130 IF A=2 THEN QQ$=B$+E$+F$+O$+R$+E$:R=R-1
140 DRAW"BM60,70"+QQ$
150 DRAW"C7BM150,70"+Z$:GOTO 280
160 REM"DRAW A TRIANGLE"
170 LINE(0,90)-(255,92),PSET,BF:LINE(0,176)-(255,178),PSET,BF
180 B=RND(150):B1=100+RND(10)
190 C=50+RND(100):C1=110+RND(50)
200 D=RND(50):D1=100+RND(40)
210 LINE(B,B1)-(C,C1),PSET
220 LINE-(D,D1),PSET:LINE-(B,B1),PSET
230 REM"DRAW A SQUARE"
240 LINE(C+50,C1+20)-(C+100,C1-20),PSET,BF
250 REM"DRAW A CIRCLE"
260 CIRCLE(C+RND(20),B1+20+RND(20)),3+RND(15)
270 PAINT(0,93),8,7:RETURN
280 EN$=INKEY$
290 IF EN$="" THEN 280
300 IF ASC(EN$)>57 OR ASC(EN$)<48 THEN 280
310 IF EN$="" THEN 280
320 IF VAL(EN$)=R THEN 330 ELSE PLAY"AAA":GOTO 280
330 PLAY"L100O4CEGCEGBAGFDC":GOSUB 160
340 COLOR8:DRAW"BM50,180"+P$+R$+E$+S$+S$+SP$
350 DRAW+SP$+SP$+E$+N$+T$+E$+R$
360 AN$=INKEY$
370 IF AN$=CHR$(13) THEN RUN ELSE IF EN$="E" THEN 380 ELSE 360
380 CLS:END
390 REM"HERE ARE THE STRINGS FOR THE LETTERS AND NUMBERS
400 A$="BEHUNU2R4NU2DGL2BGBL6"
410 B$="BEHENR3HER3D4L3BGBL6"
420 C$="BU4ER2FD2GL2HBG2BL4"
430 E$="BER3U2NL2U2L4BG5BL2"
440 F$="BUR4U2NL3U2BG5BL5"
450 H$="BUU2NU2R4NU2D2BGBL9"
460 I$="BR2BUU4BU2BD7BL8"
470 M$="BUNU4E2F2U4BG5BL5"
480 N$="BUU4F4U4BG5BL5"
490 O$="BEHU2ER2FD2GL2BGBL6"
500 P$="BER3U2NU2L3GNFBG2BL4"
510 R$="BEHERNH2R2NU2D2L3BGBL6"
520 S$="BU2FR2EHL2HER2FBG4BL6"
530 T$="BUR2NU4R2BDBL10"
540 U$="BUU3ER2FD3BGBL9"
550 W$="BUU4F2E2D4BGBL9"
560 N1$="BE2NU3DEBFBGBL9"
570 N2$="BENR3HER3U2L4BG5BL"
580 N3$="BENR3HENR2HER3BG5BL5"
590 N4$="BENU4E3L4BG4BL2"
600 N5$="BER4U2L3HER3BG5BL5"
610 N6$="BU2FR2EU2NHGL2HER2BG5BL4"
620 N7$="BUNR4UE3BG5BL4"
630 N8$="BER2EHEHL2GFNR2GFBGBL6"
640 SP$="BE4BUBG5BL5"' ***SPACER
650 RETURN
