0 'COPYRIGHT 1990 FALSOFT, INC.
1 'COCOTRIG' by Keiran Kenny,     Sydney, 1988.
10 ONBRKGOTO380
20 POKE65497,0
30 PALETTERGB
40 HCOLOR4,8:HSCREEN2:CLS
50 HBUFF1,128:HGET(0,0)-(15,15),1
60 HPRINT(0,1),"ENTER (A) 5-85 degs:"
70 P=21:V$=""
80 K$=INKEY$:IFK$=""THEN80
90 IFK$=CHR$(13)THEN190
100 IFK$=CHR$(8)THEN110ELSE150
110 HPUT(P*8-8,0)-(P*8,15),1,PSET
120 P=P-1:IFP<21THENP=21:GOTO80
130 V$=LEFT$(V$,LEN(V$)-1)
140 GOTO80
150 HPRINT(P,1),K$
160 V$=V$+K$
170 P=P+1
180 GOTO80
190 V=VAL(V$):IFV<5ORV>85THEN370
200 A=V/57.29577951
210 H=150:X=160:Y=182
220 HLINE(X,Y)-(X+H*COS(A),Y),PSET:HLINE-(X+H*COS(A),Y-H*SIN(A)),PSET:HLINE-(X,Y),PSET
230 HPRINT(19,22),"A"
240 HPRINT(INT((X+8+H*COS(A))/8),22),"B"
250 HPRINT(INT((X+H*COS(A))/8),INT((Y-6-H*SIN(A))/8)),"C"
260 HPRINT(INT((X+(H*COS(A)/2))/8),23),"c"
270 HPRINT(INT((X+8+H*COS(A))/8),INT((Y-(H*SIN(A)/2))/8)),"a"
280 HPRINT(INT((X+(H*COS(A)/2))/8),INT((Y-8-(H*SIN(A)/2))/8)),"b"
290 HPRINT(0,5),"SIN(A) ="+LEFT$(STR$(SIN(A)),6)
300 HPRINT(0,7),"COS(A) ="+LEFT$(STR$(COS(A)),6)
310 HPRINT(0,9),"TAN(A) ="+LEFT$(STR$(TAN(A)),8)
320 HPRINT(0,11),"a ="+LEFT$(STR$(H*SIN(A)),8)
330 HPRINT(0,13),"b = 150"
340 HPRINT(0,15),"c ="+LEFT$(STR$(H*COS(A)),8)
350 HPRINT(0,17),"Press any key."
360 EXEC44539:K$=INKEY$
370 HCLS8:GOTO60
380 POKE65496,0:CLS:END
