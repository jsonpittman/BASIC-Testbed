0 ' COPYRIGHT 1990 FALSOFT, INC
10 CLS
20 R1=90:R2=45:DI=40
30 R=R1-R2:DR=R1/R2:RA=ATN(1)/45
40 GOSUB 200
50 PRINT"SPIROGRAPHICS"
60 PRINT:PRINT"RADIUS OF WHEEL 1 -";R1
70 PRINT"RADIUS OF WHEEL 2 -";R2
80 PRINT"DISTANCE ON WHEEL 2 -";DI
90 PRINT
100 PRINT"THIS WILL HAVE"(R1+R2)/J"LOOPS":PRINT" AND WILL TAKE"R2/J"CYCLES"
110 PRINT:PRINT"PRESS ANY KEY TO START":EXEC &HADFB
120 PMODE4,1:PCLS:SCREEN1,1
130 FOR N=0 TO MX
140 A1=N:A2=A1*DR
150 X=R*COS(A1*RA)+DI*COS(A2*RA)
160 Y=R*SIN(A1*RA)-DI*SIN(A2*RA)
170 PSET(128+X,96-Y)
180 NEXT
190 GOTO 190
200 'GREATEST COMMON DENOMINATOR
210 I=R1:J=R2
220 IF I>=J THEN I=I-J:GOTO 220
230 IF I=0 THEN 250
240 TEMP=I:I=J:J=TEMP:GOTO 220
250 MX=(R2/J)*360:RETURN
