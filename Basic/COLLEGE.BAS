100 '         SAVING FOR COLLEGE
110 '
120 '         COPYRIGHT (C) 1987
130 '
140 '         LARRY P PITTMAN
150 '         11406 MAJORCA PL.
160 '         FENTON, MI 48430
170 '
180 IFPEEK(&HF636)=15THENCO=3:WIDTH40:PALETTE0,0:PALETTE8,63:CLS1:ELSEGOTO220:'CHECK IF COCO 3
190 PRINT"**************COLLEGE COSTS*************":PRINT:PRINT:PRINT"THIS PROGRAM WILL CALCULATE YOUR MONTHLYCOSTS THAT YOU NEED TO SAVE FOR YOUR    CHILDREN FOR COLLEGE.  YOU FIRST NEED TO";
200 PRINT"ENTER SOME GENERAL INFORMATION AND THEN SOME SPECIFIC INFORMATION ABOUT EACH    CHILD.":PRINT:PRINT:PRINT"    ";
210 GOTO250
220 CLS:PRINT"**********COLLEGE COSTS*********":PRINT:PRINT"THIS PROGRAM WILL CALCULATE YOURMONTHLY COSTS THAT YOU NEED TO  SAVE FOR YOUR CHILDREN FOR      COLLEGE.  YOU FIRST NEED TO"
230 PRINT"ENTER SOME GENERAL INFORMATION  AND THEN SOME SPECIFIC          INFORMATION ABOUT EACH CHILD."
240 PRINT:PRINT
250 PRINT"    PRESS ANY KEY TO BEGIN"
260 FORX=1TO200:NEXT:SOUND150,2
270 A$=INKEY$:IFA$=""THEN270
280 CLS:IFCO=3THENPRINT:PRINT
290 PRINT"ENTER THE NUMBER OF CHILDREN";
300 IFCO=3THEN PRINT" YOU WISH TOPUT THROUGH COLLEGE":PRINT:ELSEPRINT"    YOU WISH TO PUT THROUGH COLLEGE":PRINT
310 SOUND150,2:LINEINPUT"              ";NC$:NC=VAL(NC$):'NC=NUMBER OF CHILDREN (TEN MAX W/O ADDING DIMENSION STATEMENTS)
320 IF NC=1THENPRINT:PRINT"WHAT IS HIS/HER NAME?":GOTO340
330 PRINT:PRINT"WHAT ARE THEIR NAMES?"
340 FORX=1TONC:SOUND150,2:LINEINPUT"     ";NM$(X):NEXT:'NM$(X)=NAME OF CHILD(REN)
350 PRINT:SOUND150,2:IFCO=3THENPRINT"ENTER WHAT YOU EXPECT THE AVERAGE       INFLATION RATE TO BE (%): ";:ELSEPRINT"ENTER WHAT YOU EXPECT THE       AVERAGE INFLATION RATE TO       BE (%): ";
360 LINEINPUT" ";IR$:IR=VAL(IR$)/100:'IR=INFLATION RATE
370 PRINT:SOUND150,2:PRINT"ENTER WHAT INTEREST RATE YOU":PRINT"BELIEVE YOU COULD GET FOR":LINEINPUT"YOUR MONEY (%): ";IT$:IT=VAL(IT$)/100:'IT=INVESTMENT RATE
380 PRINT:SOUND150,2:IFCO=3THENPRINT"ENTER THE AMOUNT OF MONEY YOU WOULD LIKETO HAVE AVAILABLE AT THE BEGINNING OF   EACH SCHOOL YEAR (IN SEPT): ";:GOTO400
390 PRINT"ENTER THE AMOUNT OF MONEY YOU   WOULD LIKE TO HAVE AVAILABLE    AT THE BEGINNING OF EACH SCHOOL YEAR (IN SEPT): ";
400 LINEINPUT"$";RA$:RA=VAL(RA$):'RA=REQUIRED AMOUNT PER CHILD PER YEAR
410 PRINT:SOUND150,2:PRINT"ENTER THE CURRENT MONTH (1-12):       ";
420 IFCO=3THENPRINT"               ";
430 LINEINPUT" ";TM$:TM=VAL(TM$):'TM=TODAY'S MONTH
440 IFTM<1ORTM>12THEN410
450 PRINT:SOUND150,2:LINEINPUT"ENTER THE CURRENT YEAR: ";TY$:TY=VAL(TY$):'TY=TODAY'S YEAR
460 IFTY<1987THEN450
470 IFCO=3THENLS=40:ELSELS=32
480 FORX=1TONC:CLS:PRINT"INFORMATION ABOUT ";NM$(X):PRINTSTRING$(LS,"*"):PRINT:SOUND150,2:LINEINPUT"BEGINNING YEAR OF COLLEGE: ";FD$(X):FD(X)=VAL(FD$(X)):'FD(X)=FUTURE COLLEGE DATE FOR CHILD X
490 PRINT:SOUND150,2:PRINT"AMOUNT AVAILABLE TO INVEST NOW      ";:IFCO=3THENPRINT"          ";
500 LINEINPUT"$";OA$(X):OA(X)=VAL(OA$(X)):NEXT:'OA(X)=ORIGINAL AMOUNT AVAILABLE TO INVEST NOW FOR CHILD X
510 '
520 'CALCULATION ROUTINE BEGINS
530 '
540 CLS:PRINT:PRINT:PRINT"CALCULATIONS NOW BEING PERFORMED"
550 MT=0:'MT=MONTHLY PAYMENT TOTAL
560 FORC=1TONC:MP(C)=0:'MP(C)=MONTHLY PAYMENT FOR CHILD C
570 IFTY>FD(C)THEN930
580 '
590 'T1 & T2 USED FOR NO OF PAYMENT PERIODS (MONTHS) CALCULATION
600 '
610 IFTY=FD(C)THENIFTM>8THEN930:ELSET1=FD(C)-TM:GOTO670
620 T1=12-TM+9+(12*(FD(C)-TY-1))
630 AM(C)=OA(C):'SAVE ORIGINAL AMOUNT AVAIL FOR POSSIBLE REVISIONS
640 '
650 'A1 & A2 USED FOR CALCULATING INFLATION EFFECTS
660 '
670 A1=RA * ((1+(IR/12))^T1):IF AM(C)<>0 THEN AM(C)=AM(C)*((1+(IT/12))^T1)
680 IF AM(C) >A1 THEN AM(C)=AM(C)-A1:GOTO700:ELSEA1=A1-AM(C):AM(C)=0
690 MP(C)=A1*((IT/12)/(((1+(IT/12))^T1)-1))
700 T2=T1
710 FORX=1TO3
720 T2=T2+12:A2=RA*((1+(IR/12))^T2):IF AM(C)<>0 THEN AM(C)=AM(C)*((1+(IT/12))^12):IF AM(C)>A2 THEN AM(C)=AM(C)-A2:GOTO770:ELSEA2=A2-AM(C):AM(C)=0
730 MP(C)=MP(C)+(A2*((IT/12)/(((1+(IT/12))^T2)-1))):NEXTX,C
740 '
750 'SUMMARY SCREEN
760 '
770 CLS:PRINTIR*100;"% INFLATION  ";:IFCO=3THENPRINT"        ";
780 PRINTIT*100;"% INTEREST":PRINTSTRING$(LS,"*"):PRINT"MONTHLY AMOUNT REQUIRED FOR "
790 FORX=1TONC:PRINTTAB(9);NM$(X);":";:PRINTTAB(12);:PRINTUSING"$#####.##";MP(X)
800 MT=MT+MP(X):NEXT
810 IFCO=3THENTB=26:ELSETB=15
820 PRINT:PRINT:PRINT"TOTAL MONTHLY PAYMENT REQUIRED:":PRINTTAB(TB);:PRINTUSING"$#####.##";MT
830 IFCO=3THENLOCATE0,21:PRINT"*****************ENTER******************   <E> END       <R> REVISE ENTRIES";:LOCATE0,23:GOTO850
840 PRINT@448,"*************ENTER**************<E> END      <R> REVISE ENTRIES";
850 SOUND150,2
860 A$=INKEY$:IFA$=""THEN860
870 IFA$="E"THENEND
880 IFA$="R"THEN1010
890 GOTO860
900 '
910 'DATE ENTRY ERROR
920 '
930 CLS:PRINTSTRING$(LS,"*"):PRINT:PRINT"TODAY'S DATE IS LATER THAN ";
940 IFCO=3THENPRINT"COLLEGE      START DATE FOR:   ";:ELSEPRINT"     COLLEGE START DATE FOR:":PRINT"       ";
950 PRINTNM$(C):IFCO=3THENPT=15:ELSEPT=6
960 FORX=1TOPT:PRINT:NEXT:PRINT"   ENTER <R> TO REVISE":PRINTSTRING$(LS,"*"):SOUND150,2
970 A$=INKEY$:IFA$=""THEN970ELSEIFA$<>"R"THEN970:ELSECLS:GOTO410
980 '
990 'REVISION MENU
1000 '
1010 CLS:IFCO<>3THENPRINT"************REVISION************":ELSEPRINT"****************REVISION****************"
1020 PRINT:PRINT:PRINTTAB(4)"1. CHANGE INFLATION RATE":PRINTTAB(4)"2. CHANGE INTEREST RATE":PRINTTAB(4)"3. CHANGE ANNUAL AMT AVAIL"
1030 PRINTTAB(4)"4. RECALCULATE":IFCO=3THENPRINT:PRINT:PRINT
1040 PRINT:PRINTTAB(8)"ENTER 1, 2, 3, OR 4"
1050 PRINTSTRING$(LS,"*"):PRINT"  CURRENT SETTINGS:":GOSUB1270:PRINT"         1=";IR*100;"%":GOSUB1270:PRINT"         2=";IT*100;"%":GOSUB1270:PRINT"         3= ";:PRINTUSING"$#####.##";RA;
1060 SOUND150,2
1070 A$=INKEY$:IFA$=""THEN1070
1080 ONVAL(A$)GOTO1120,1180,1240,540
1090 '
1100 'INFLATION RATE REVISION
1110 '
1120 CLS:IFCO=3THENPRINT"*************INFLATION RATE*************":LOCATE5,18:PRINT"CURRENT RATE = ";IR*100;"%":LOCATE11,5:PRINT"ENTER":LOCATE5,6:GOTO1140
1130 PRINT"*********INFLATION RATE*********":PRINT@455,"CURRENT RATE = ";IR*100;"%":PRINT@105,"ENTER":PRINT@131,"";
1140 SOUND150,2:LINEINPUT"NEW INFLATION RATE (%): ";IR$:IR=VAL(IR$)/100:GOTO1010
1150 '
1160 'INVESTMENT RATE REVISION
1170 '
1180 CLS:IFCO=3THENPRINT"*************INVESTMENT RATE************":LOCATE6,16:PRINT"CURRENT RATE = ";IT*100;"%":LOCATE12,5:PRINT"ENTER":LOCATE6,6:GOTO1200
1190 PRINT"********INVESTMENT RATE*********":PRINT@455,"CURRENT RATE = ";IT*100;"%":PRINT@106,"ENTER":PRINT@130,"";
1200 SOUND150,2:LINEINPUT"NEW INVESTMENT RATE (%): ";IT$:IT=VAL(IT$)/100:GOTO1010
1210 '
1220 'ANNUAL AMOUNT REVISION
1230 '
1240 CLS:IFCO=3THENPRINT"*************ANNUAL AMOUNT**************":LOCATE5,18:PRINT"CURRENT AMOUNT  ";:PRINTUSING"$#####.##";RA:LOCATE5,4:PRINT"ENTER NEW ANNUAL AMOUNT TO":PRINT"     BE AVAILABLE:  ";:GOTO1260
1250 PRINT"*********ANNUAL AMOUNT**********":PRINT@450,"CURRENT AMOUNT ";:PRINTUSING"$#####.##";RA:PRINT@130,"NEW ANNUAL AMOUNT TO BE           AVAILABLE:  ";
1260 SOUND150,2:LINEINPUT"$";RA$:RA=VAL(RA$):GOTO1010
1270 IFCO=3THENPRINT:RETURN:ELSERETURN
