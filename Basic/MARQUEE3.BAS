0 ' COPYRIGHT 1989  FALSOFT,INC
1 'MARQUEE TITLE PAGE DEMO         (C) 1989 FROM                   BILL BERNICO SOFTWARE           
2 CLS:PRINT"THIS DEMO PROGRAM WILL SHOW YOU HOW TO MAKE A MARQUEE-TYPE TITLEPAGE FOR YOUR OWN PROGRAM PRE-  SENTATION.  WITH THE USE OF THE PALETTE SWITCHING ABILITIES OF  THE COCO3, YOU CAN CREATE THE   ILLUSION OF A MOVING BORDER
3 PRINT"AROUND YOUR GRAPHICS TITLE PAGE.YOU CAN CHANGE THE COLORS TO    ANYTHING YOU LIKE, JUST MAKE    SURE THAT THE HPAINT NUMBERS ARETHE SAME AS THE NUMBERS SWITCHEDIN THE PALETTE COMMANDS THAT    IMMEDIATELY FOLLOW.
4 PRINT@486,"HIT ENTER TO TRY IT";
5 IFINKEY$<>CHR$(13)THEN5
6 CLEAR1000:ONBRKGOTO32
7 HSCREEN2:HCLS4:HCOLOR8,4:RGB
8 B$="R10D10L10U10R10
9 C$="U10R9D10L9U10
10 D$=C$+C$+C$+C$+C$+C$+C$+C$
11 A$=B$+B$+B$+B$+B$+B$+B$+B$+B$+B$+B$+B$+B$+B$+B$
12 HDRAW"BM0,0"+A$:HDRAW A$+B$+"R9D10L9U10
13 HDRAW"BM0,181"+A$:HDRAW A$+B$+"R9D10L9U10
14 HDRAW D$+D$+"U10BR9D10
15 HDRAW"BM0,181"+D$+D$+"U11R9D11
16 FORX=1TO311STEP20:HPAINT(X,2),12,8:NEXTX
17 FORX=12TO191STEP20:HPAINT(315,X),12,8:NEXTX
18 FORX=309TO0STEP-20:HPAINT(X,190),12,8:NEXTX
19 FORX=163TO0STEP-20:HPAINT(2,X),12,8:NEXTX
20 FORX=11TO311STEP20:HPAINT(X,2),3,8:NEXTX
21 FORX=2TO191STEP20:HPAINT(315,X),3,8:NEXTX
22 FORX=312TO0STEP-20:HPAINT(X,190),3,8:NEXTX
23 FORX=173TO0STEP-20:HPAINT(2,X),3,8:NEXTX
24 HPRINT(9,6),"MARQUEE TITLE PAGE DEMO
25 HPRINT(13,8),"(C)1988 FROM
26 HPRINT(9,10),"BILL BERNICO SOFTWARE
27 HPRINT(7,20),"HIT ANY KEY TO START OVER
28 PALETTE 3,0:PALETTE 12,36:GOSUB31
29 PALETTE 3,36:PALETTE 12,0:GOSUB31
30 IFINKEY$=""THEN28ELSE7
31 FORY=1TO50:NEXTY:RETURN
32 RGB:CLS:END
