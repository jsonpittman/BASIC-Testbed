5 DIM L$(36),L(36),M$(500),J(36):B$=CHR$(128)
10 CLS:INPUT"DIFFICULTY PLEASE.              (0- EASY, 10- ALMOST IMPOSSIBLE)";DF:PRINT:PRINT"PRESS ANY KEY TO BEGIN."
15 A$=INKEY$:IFA$=""THEN15
20 FOR T=1 TO 36:READ L$(T),L(T):NEXT T
30 CLS3:FORT=1TO36:PRINT@L(T),B$;:PRINT@L(T)+32,L$(T);:NEXTT
50 PRINT@458,"SCORE : ";SC;:S=RND(36):L$=L$(S):L=L(S)
55 IF DF>8 AND S<11 THEN S=S+10:L$=L$(S):L=L(S)
60 PRINT@L,CHR$(191);
70 FOR K=1 TO 100-DF*5:A$=INKEY$:IF A$=L$ THEN 90 ELSE IF A$="^" THEN 150 ELSE NEXT K
75 SC=SC-5:IFSC<0THEN SC=0
80 PRINT@L,B$;:M=M+1:M$(M)=L$:NM=NM+1:IF NM>20 THEN DF=DF-1:NM=0
82 J(S)=J(S)-1:IF J(S)<-2 THEN PRINT@L+32,L$;
85 GOTO 50
90 NM=NM-1:SOUND100,1:SC=SC+10:PRINT@L,B$;
92 J(S)=J(S)+1:IF J(S)>2 THEN PRINT@L+32,CHR$(175);:J(S)=0
95 IF NM>-30 THEN 50 ELSE DF=DF+1:NM=0:GOTO 50
100 DATA 1,33,2,36,3,39,4,42,5,45,6,48,7,51,8,54,9,57,0,60
110 DATA Q,129,W,132,E,135,R,138,T,141,Y,144,U,147,I,150,O,153,P,156
120 DATA A,226,S,229,D,232,F,235,G,238,H,241,J,244,K,247,L,250
130 DATA Z,323,X,326,C,329,V,332,B,335,N,338,M,341
150 CLS:PRINT"YOU MISSED ";M;" TIMES.":PRINT"YOU SCORED ";SC;" POINTS.":FORX=1TO500:NEXTX:PRINT"HERE ARE THE LETTERS YOU MISSED.(PRESS <SHIFT> AND <@> KEYS TO  STOP - IF NEEDED)"
160 FOR T=1 TO M:PRINT M$(T);" ";:NEXTT
170 PRINT:INPUT"PLAY AGAIN";Q$:IF LEFT$(Q$,1)="Y" THEN RESTORE:RUN ELSE END
