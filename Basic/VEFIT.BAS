1 'VEFIT
2 'BY THOMAS WONG
3 'COPYRIGHT (C) 1993
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 ' VEFIT 1.1 RSDOS PIX UTILITY
12 WIDTH32:CLEAR 1500,&H5FB9:POKE &HE6C6,&H33
14 DIM PA(15),AA$(2),AB$(68):LA=0:MA=2
16 AA=1:AA$="BDB3EDC304001F018608E684C17F220AC13F2204CB402002C040E7804A26EB3910AE028E60007C5FFFB65FFF814026087F5FFF4A308900804A26F98680E680E7A04A26F939"
18 FOR AB=&H5FBA TO &H5FFE
20 AC=VAL("&H"+MID$(AA$,AA,2)):POKE AB,AC:AA=AA+2
22 NEXT AB
24 DEFUSR0=&H5FBA:DEFUSR1=&H5FDA
26 ON BRK GOTO 458:ON ERR GOTO 452
28 AA$=CHR$(174)+STRING$(30,172)+CHR$(173)+CHR$(170)+STRING$(30,32)+CHR$(165)+CHR$(171)+STRING$(30,163)
30 LA$="000C020E070905101C2C0D1D0B1B0A2B22111221030113321E2D1F2E0F3C2F3D1708150627162636192A1A3A182928381404233325352434203B313E3739303F"
32 SA$="001502143106230421050E0C010A031C07111216302225202C282A0D080B181A38131032363426242E2D290F09191B1E3E3A17333735273C2F3D2B391D1F3B3F"
34 CLS:PRINT@0,AA$;CHR$(167):PRINT@416,AA$;:POKE &H5FF,&HA7:PRINT@33,"VEFIT - RSDOS PICTURE UTILITY";
36 PRINT@449,"CHOOSE A MONITOR TYPE TO BEGIN";
38 GOSUB 320:GOSUB 388
40 GOSUB 408
42 PRINT@131,"LOAD A PICTURE":PRINT@163,"PALETTE CHANGE":PRINT@195,"SAVE (320 X 200 : 4 COLORS)":PRINT@227,"SAVE (320 X 200 : 16 COLORS)":PRINT@259,"SAVE (640 X 200 : 2 COLORS)":PRINT@291,"SAVE (640 X 200 : 4 COLORS)"
44 PRINT@323,"MEMORY DISPLAY":PRINT@355,"MONITOR RESET":PRINT@449,"(ARROWS) TO SELECT AND (ENTER)";
46 AD=1:AE=8:AF=28:AG=130:GOSUB 358
48 IF AB$<>CHR$(13) THEN 46:ELSE GOSUB 388
50 ON AJ GOSUB 56,148,196,196,196,196,294,318
52 GOSUB 388:GOTO 42
54 'LOAD PIX
56 PRINT@449,"(ARROWS) SELECT FILE OR (Q)UIT";
58 GOSUB 428
60 IF AN=0 THEN SOUND 200,1:PRINT@228,"NO PICTURE ON THIS DISK":PRINT@260,"(PRESS ANY KEY FOR MAIN)";:GOSUB394:RETURN
62 IF AN>16 THEN AN=16
64 LB=0:LC=0
66 PRINT@(128+(LB*32)+((LC*16)+2)),AB$((LB*2)+LC+1);
68 LC=LC+1
70 IF AN=((LB*2)+LC) THEN 74
72 IF LC=3 THEN LB=LB+1:LC=0:GOTO 66:ELSE 66
74 AD=2:AE=8:AF=14:AG=129:AH=16:GOSUB 358
76 IF AB$="Q" THEN RETURN
78 LD=((AJ-1)*2)+(AI-1)+1
80 IF LD<=AN THEN LB$=AB$(LD):GOSUB 388:ELSE SOUND 200,1:PRINT@449,"INVALID SELECTION: PRESS A KEY";:GOSUB 394:RETURN
82 PRINT@225,"LOADING PICTURE (";LB$;")":PRINT@257,"STATUS: (";STRING$(20,191);")":PRINT@449,STRING$(30,128);
84 OPEN "D",#1,LB$,1
86 FIELD #1,1 AS LC$
88 GET #1,2:LA=ASC(LC$):IF LA<3 THEN LE=250:LF=12:ELSE LE=125:LF=7
90 LA=VAL(MID$("24013",LA+1,1))
92 FOR LG=3 TO 18
94 GET #1,LG:IF MA=0 THEN PA(LG-3)=VAL("&H"+MID$(LA$,(ASC(LC$)*2)+1,2))+64:ELSE PA(LG-3)=ASC(LC$)+64
96 NEXT LG
98 LD$=""
100 FOR LG=LOF(1)-17 TO LOF(1)
102 GET #1,LG:LD$=LD$+LC$
104 NEXT LG
106 CLOSE #1
108 IF LA=0 THEN GOSUB 388:SOUND 200,1:PRINT@227,"TYPE 2 VEF'S NOT SUPPORTED":PRINT@259,"(PRESS A KEY TO MAIN MENU)":GOSUB 394:RETURN
110 OPEN "D",#1,LB$,128
112 FIELD #1,128 AS LC$
114 LH=0:LI=48
116 POKE &H5FF8,&HA0:POKE &H5FFA,&H80:POKE &H5FFF,0
118 GET #1,LF-1:LE$=RIGHT#(LC$,110)
120 POKE &HFFA3,LI
122 IF LEN(LE$)>128 THEN LF$=LEFT$(LE$,128):LE$=RIGHT$(LE$,LEN(LE$)-128):ELSE 128
124 LF$=USR1(LF$):LH=LH+1
126 IF LH=64 OR LH=128 OR LH=192 THEN LI=LI+1:GOTO 120:ELSE 122
128 IF LF>LE THEN LF$=USR1(LE$+LD$):GOTO 140
130 LF$=LE$
132 GET #1,LF:LF=LF+1:LE$=LC$
134 LJ=128-LEN(LF$):LF$=LF$+LEFT$(LE$,LJ):LE$=RIGHT$(LE$,LEN(LE$)-LJ)
136 LK=INT((LF/LE)*20):PRINT@266,STRING$(LK,175);
138 GOTO 124
140 CLOSE #1
142 GOSUB 418:HSCREEN LA:GOSUB 394:GOSUB 402:HSCREEN 0
144 GOSUB 388:GOTO 56
146 'PALETTES
148 IF LA>0 THEN PRINT@449,"(ARROWS) ROTATE (SPACE) (Q)UIT";:ELSE PRINT@449,"(ARROWS) ROTATE PALETTE (Q)UIT";
150 FOR PA=0 TO 7
152 PB=PA(PA)-64:PA$=RIGHT$(STR$(PB),2):IF PB<10 THEN MID$(PA$,1,1)="0"
154 PB=PA(PA+8)-64:PB$=RIGHT$(STR$(PB),2):IF PB<10 THEN MID$(PB$,1,1)="0"
156 PC$=RIGHT$(STR$(PA+8),2):IF (PA+8)<10 THEN MID$(PC$,1,1)="0"
158 PRINT@(131+(PA*32)),"PAL 0";RIGHT$(STR$(PA),1);": ";PA$;"      PAL ";PC$:": ";PB$
160 NEXT PA
162 AD=2:AE=8:AF=13:AG=130:AH=16:GOSUB 358
164 IF AB$="Q" THEN RETURN
166 PC=USR0(AG):PD=AJ+((AI-1)*8)
168 IF PD<9 THEN PE=139:ELSE PE=155
170 PE=PE+((AJ-1)*32):PF=0
172 PG=PA(PD-1)-64:PD$=RIGHT$(STR$(PG),2):IF PG<10 THEN MID$(PD$,1,1)="0"
174 PRINT@PE,PD$;
176 GOSUB 394
178 IF AB$="Q" THEN GOSUB 402:HSCREEN 0:RETURN
180 IF AB$=CHR$(10) THEN PG=PG-1:IF PG<0 THEN PG=63
182 IF AB$=CHR$(94) THEN PG=PG+1:IF PG>63 THEN PG=0
184 PA(PD-1)=PG+64
186 IF AB$=CHR$(32) THEN PF=PF+1:IF PF>1 THEN PF=0:GOSUB 402:ELSE PF=LA
188 IF PF>0 THEN GOSUB 418
190 HSCREEN PF
192 IF AB$<>CHR$(13) THEN 172:ELSE GOSUB 402:HSCREEN 0:PC=USR0(AG):GOTO 150
194 'SAVE PIX
196 IF AJ=3 OR AJ=5 THEN SA=7:ELSE SA=14
198 IF FREE(0)<SA THEN SOUND 200,1:PRINT@228,"NOT ENOUGH SPACE ON DISK":PRINT@260,"(PRESS ANY KEY FOR MAIN)";:GOSUB 394:RETURN
200 PRINT@449,"TYPE FILENAME: (ENTER) TO MAIN";:PRINT@227,"(ENTER FILENAME: [        ]"
202 SA=0:SB$="":PRINT@244,CHR$(175);
204 GOSUB 394
206 IF AB$<>CHR$(8) THEN 214
208 IF SA>0 THEN SA=SA-1:SB$=LEFT$(SB$,SA):PRINT@244+SA,CHR$(175);CHR$(32);:ELSE SOUND 200,1
210 IF SA=7 THEN PRINT@252,"]"
212 GOTO 204
214 IF AB$<>CHR$(13) THEN 218
216 IF SA=0 THEN RETURN:ELSE 224
218 IF SA=8 THEN SOUND 200,1:GOTO 204:ELSE SB$=SB$+AB$
220 PRINT@244+SA,AB$;:SA=SA+1:IF SA<8 THEN PRINT CHR$(175);
222 GOTO 204
224 IF INSTR(1,SB$,".")>0 OR INSTR(1,SB$,"/")>0 THEN SOUND 200,1:PRINT@449,"NO EXTENSIONS... PRESS ANY KEY";:GOSUB 394:GOTO 200
226 SB$=SB$+STRING$(8-SA,32)+".VEF"
228 GOSUB 428
230 IF AN=0 THEN 244
232 FOR SB=1 TO AN
234 IF SB$<>AB$(SB) THEN 242
236 SOUND 200,1:PRINT@449,"FILE EXISTS: GO ON? [N]O [Y]ES";:GOSUB 394
238 IF AB$<>"N" AND AB$<>"Y" THEN 238
240 IF AB$="N" THEN 200:ELSE 244
242 NEXT SB
244 PRINT@225,"SAVING PICTURE: [";SB$;"]":PRINT@257,"STATUS: [";STRING$(20,191);"]":PRINT@449,STRING$(30,128);
246 AJ=AJ-2:IF AJ=2 OR AJ=4 THEN SC=240:SD=160:ELSE SC=120:SD=80
248 OPEN "O",#1,SB$
250 SE=VAL(MID$("3041",AJ,1))
252 PRINT #1,CHR$(0);CHR$(SE);
254 FOR SF=0 TO 15
256 SG=PA(SF)-64
258 IF MA=0 THEN SG=VAL("&H"+MID$(SA$,(SG*2)+1,2))
260 PRINT #1,CHR$(SG);
262 NEXT SF
264 FOR SF=1 TO 8
266 PRINT #1,STRING$(SD,0);
268 NEXT SF
270 SH=0:SI=48
272 POKE &H5FF8,&H80:POKE &H5FFA,&HA0:POKE &H5FFF,0
274 POKE &HFFA3,SI
276 SC$=USR1(STRING$(128,32))
278 PRINT #1,SC$;
280 SH=SH+1
282 IF SH=64 OR SH=128 OR SH=192 THEN SI=SI+1:GOTO 274
284 SJ=INT((SH/SC)*20):PRINT@266,STRING$(SJ,175);
286 IF SC>SH THEN 276
288 CLOSE #1
290 RETURN
292 'MEMORIZE
294 PRINT@449,"(ARROWS) SELECT OR (Q)UIT MAIN";
296 IF LA=0 THEN SOUND 200,1:PRINT@134,"NO HSCREEN SELECTED!":ELSE PRINT@134,"CHOOSE AN HSCREEN..."
298 PRINT@195,"VIEW [320 X 200: 4 COLORS]":PRINT@227,"VIEW [320 X 200: 16 COLOR]":PRINT@259,"VIEW [640 X 200: 2 COLORS]":PRINT@291,"VIEW [640 X 200: 4 COLORS]"
300 IF LA>0 THEN PRINT@323,"VIEW DEFAULT HSCREEN:NOW";LA
302 AD=1:AF=28:AG=194:IF LA>0 THEN AE=5:ELSE AE=4
304 GOUSUB 358
306 IF AB$="Q" THEN RETURN:ELSE GOSUB 418
308 IF AJ<5 THEN LA=AJ
310 HSCREEN LA
312 GOSUB 394:GOSUB 402:HSCREEN 0
314 GOTO 296
316 'MONITORS
318 PRINT@449,"(ARROWS) SELECT TYPE OR (Q)UIT";
320 PRINT@128,"SELECT A MONITOR TYPE: NOW = ";:IF MA=2 THEN PRINT"???":GOTO 324
322 IF MA=0 THEN PRINT "CMP":ELSE PRINT "RGB"
324 PRINT@198,"COMPOSITE COLOR (TV)":PRINT@230,"RGB COLOR MONITOR"
326 AD=1:AE=2:AF=22:AG=197:GOSUB 358
328 IF MA<2 THEN 336
330 IF AB$="Q" THEN 326
332 IF AJ=1 THEN MA=0:CMP:ELSE MA=1:RGB
334 GOSUB 408:RETURN
336 IF AB$="Q" THEN RETURN
338 IF AJ=2 THEN 348
340 IF MA=0 THEN RETURN:ELSE MA=0:CMP
342 FOR AL=0 TO 15
344 PA(AL)=VAL("&H"+MID$(LA$,((PA(AL)-64)*2)+1,2))+64
346 NEXT AL:RETURN
348 IF MA=1 THEN RETURN:ELSE MA=1:RGB
350 FOR AL=0 TO 15
352 PA(AL)=VAL("&H"+MID$(SA$,((PA(AL)-64)*2)+1,2))+64
354 NEXT AL:RETURN
356 'SELECTOR
358 AI=1:AJ=1:POKE &H5FC3,AF
360 AK=USR0(AG)
362 GOSUB 394:AK=USR0(AG)
364 IF AB$=CHR$(8) THEN AI=AI-1:IF AI<1 THEN AI=AI+1:ELSE AG=AG-AH:GOTO 360
366 IF AB$=CHR$(9) THEN AI=AI+1:IF AI>AD THEN AI=AI-1:ELSE AG=AG+AH:GOTO 360
368 IF AB$=CHR$(94) THEN AJ=AJ-1:IF AJ<1 THEN AJ=AJ+1:ELSE AG=AG-32:GOTO 360
370 IF AB$=CHR$(10) THEN AJ=AJ+1:IF AJ>AE THEN AJ=AJ-1:ELSE AG=AG+32:GOTO 360
372 IF AB$="Q" THEN RETURN
374 IF AB$<>CHR$(13) THEN SOUND 200,1:GOTO 360
376 FOR AL=1 TO 6:AK=USR0(AG):GOSUB 382:NEXT AL
378 RETURN
380 'PAUSE
382 FOR AM=1 TO 20:NEXT AM
384 RETURN
386 'CLS PART
388 FOR AL=3 TO 12:PRINT@(AL*32),STRING$(32,32);:NEXT AL
390 RETURN
392 'GET KEYS
394 FOR AL=341 TO 344:POKE AL,255:NEXT AL
396 AB$=INKEY$:IF AB$="" THEN 396
398 RETURN
400 'PAL BACK
402 IF MA=0 THEN CMP:ELSE RGB
404 RETURN
406 'PAL READ
408 FOR AL=0 TO 15
410 PA(AL)=PEEK(&HFFB0+AL)
412 NEXT AL
414 RETURN
416 'PAL VIEW
418 FOR AL=0 TO 15
420 POKE &HFFB0+AL,PA(AL)
422 NEXT AL
424 RETURN
426 'DIR READ
428 AN=0
430 FOR AO=3 TO 11
432 DSKI$ 0,17,AO,AA$(1),AA$(2)
434 FOR AP=1 TO 2
436 AC$=AA$(AP)
438 FOR AQ=1 TO 128 STEP 32
440 AD$=MID$(AC$,AQ,8)+"."+MID$(AC$,AQ+8,3)
442 IF MID$(AD$,1,1)=CHR$(255) THEN 448
444 IF MID$(AD$,1,1)<>CHR$(0) AND RIGHT$(AD$,3)="VEF" THEN AN=AN+1:AB$(AN)=AD$
446 NEXT AQ,AP,AO
448 RETURN
450 'TRAP ERR
452 GOSUB 388:SOUND 200,1:PRINT@224,"WARNING: ERROR HAS OCCURRED >";ERNO:PRINT@256,"(PRESS ANY KEY: RETURNS TO MENU)"
454 UNLOAD:GOSUB 394:GOSUB 388:GOTO 40
456 'LEAVE IT
458 CLS:PRINT@5,"THANKS FOR USING VEFIT":UNLOAD:END
