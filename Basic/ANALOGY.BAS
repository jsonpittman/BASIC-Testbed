1 REM***************************
2 REM*    QUIZ ON ANALOGIES    *
3 REM*    QUICK WRITING AID    *
4 REM*    BY FRED B. SCERBO    *
5 REM*    60 HARDING AVENUE    *
6 REM*  NORTH ADAMS, MA 01247  *
7 REM***************************
8 CLEAR2000
9 CLS0
10 PRINTSTRING$(32,140)STRING$(32,156)STRING$(32,188);
15 FORI=1TO160 :READ A:PRINTCHR$(A+128);:NEXT
20 PRINTSTRING$(32,179)STRING$(32,147)STRING$(32,131);
25 DATA46,44,44,42,43,,45,37,44,44,45,36,46,,,37,44,44,45,37,44,44,42,45,40,45,44,44,42,46,44,45
30 DATA42,,,42,46,43,37,37,,,37,,42,,,37,,,37,37,,,32,37,,37,,,,42,,
35 DATA46,44,44,42,42,36,47,37,44,44,45,,42,,,37,,,37,37,,44,42,37,,37,44,40,,44,44,45
40 DATA42,,,42,42,,37,37,,,37,,42,,33,37,,,37,37,32,,42,37,,37,,,34,34,,37
45 DATA44,,36,40,44,32,44,36,40,,44,36,44,44,44,36,44,44,44,36,44,44,40,44,40,44,44,44,40,44,44,44
50 PRINT@390," QUIZ ON ANALOGIES ";
55 PRINT@422," BY FRED B. SCERBO ";:PRINT@454," COPYRIGHT (C)1988 ";
60 X$=INKEY$:IFX$<>CHR$(13)THEN60
65 CLS0
70 DIM A$(8,2,6),B(4),S(4),C(8),D(4),R$(7)
75 FORI=1TO7:READ R$(I):NEXT
80 FORI=1TO6:FORZ=1TO8:FORY=1TO2:READ A$(Z,Y,I):NEXTY,Z,I
85 DATA IS SOMETHING YOU,IN THE PAST TENSE IS,SOUNDS JUST LIKE,IS PART OF A,MEANS THE SAME AS,IS THE OPPOSITE OF,IS PART OF AN
90 X=RND(8)
95 Y=RND(6)
100 FX=0:GOSUB105:GOTO120
105 CLS:PRINT@64," CHOOSE THE LETTER WHICH BEST    COMPLETES THE ANALOGY BELOW:":PRINT
110 PRINT"    "A$(X,1,Y)" IS TO "A$(X,2,Y)"...AS":PRINT
115 RETURN
120 FORI=1TO3:B(I)=0:NEXT:FORI=1TO3
125 YZ=RND(-TIMER):B(I)=RND(6):IF B(I)=Y THEN125
130 NEXTI:B(4)=Y
135 FORI=1TO8:C(I)=0:NEXT
140 FORI=1TO4
145 S(I)=RND(4):IF C(S(I))=1THEN145
150 C(S(I))=1:NEXT
155 FORI=1TO8:C(I)=1:NEXT
160 FORI=1TO4
165 D(I)=RND(8):IFC(D(I))=0THEN165
170 IF D(I)=X THEN165
175 C(D(I))=0:NEXT
180 GOSUB185:GOTO205
185 FORI=1TO4
190 PRINTTAB(4);CHR$(64+I)") "A$(D(I),1,B(S(I)))" IS TO "A$(D(I),2,B(S(I)))
195 NEXT
200 RETURN
205 X$=INKEY$:IFX$=""THEN205
210 IF X$="@"THENGOSUB435
215 XX=ASC(X$):XX=XX-64:IFXX<1THEN205
220 IFXX>4THEN205
225 IFB(S(XX))=Y THEN 240
230 IR=IR+1:PRINT:PRINT" SORRY,BUT THAT IS NOT CORRECT!"
235 GOTO245
240 CR=CR+1:PRINT:PRINT" VERY GOOD, YOU ARE CORRECT !"
245 PRINT:PRINT" PRESS <<ENTER>> TO CONTINUE !"
250 FORK=1TO4:IFS(K)=4 THEN W=K
255 NEXTK
260 PRINT@192+(W*32)," =>";
265 FORKK=1TO100:NEXTKK
270 X$=INKEY$:IFX$=CHR$(13)THEN285
275 PRINT@192+(W*32),"   ";
280 FORKK=1TO100:NEXTKK:GOTO260
285 IF B(S(XX))=Y THEN340
290 CLS:PRINT@32," IN THE FOLLOWING ANALOGIES....":PRINT:PRINT@108,A$(X,1,Y)
295 LL$=A$(X,2,Y):IFY<>4THENQ=0:GOTO305
300 GOSUB395
305 PRINTTAB(7)R$(Y+Q):PRINTTAB(12)A$(X,2,Y)
310 PRINT:PRINTTAB(12)"WHILE":PRINT
315 LL$=A$(D(XX),2,B(S(XX))):IFB(S(XX))<>4THEN325
320 GOSUB395
325 PRINTTAB(12)A$(D(XX),1,B(S(XX))):PRINTTAB(7)R$(B(S(XX))+Q):PRINTTAB(12)A$(D(XX),2,B(S(XX)))
330 PRINT:PRINT" PRESS <<ENTER>> TO CONTINUE !"
335 X$=INKEY$:IFX$<>CHR$(13)THEN335
340 CLS:PRINT@32,"  IN THIS CORRECT ANALOGY....":PRINT:PRINT@108,A$(X,1,Y)
345 LL$=A$(X,2,Y):IFY<>4THENQ=0:GOTO355
350 GOSUB395
355 PRINTTAB(7)R$(Y+Q):PRINTTAB(12)A$(X,2,Y)
360 PRINT:PRINTTAB(12)"WHILE":PRINT
365 LL$=A$(D(W),2,Y):IFY<>4THENQ=0:GOTO375
370 GOSUB395
375 PRINTTAB(12)A$(D(W),1,Y):PRINTTAB(7)R$(Y+Q):PRINTTAB(12)A$(D(W),2,Y)
380 PRINT:PRINT" PRESS <<ENTER>> TO CONTINUE !"
385 X$=INKEY$:IFX$<>CHR$(13)THEN385
390 GOTO90
395 LF$=LEFT$(LL$,1):IF LF$="A"THEN430
400 IF LF$="E"THEN430
405 IF LF$="I"THEN430
410 IF LF$="O"THEN430
415 IF LF$="U"THEN430
420 LF$=LEFT$(LL$,2):IF LF$="HO"THEN430
425 Q=0:RETURN
430 Q=3:RETURN
435 CLS:PRINT:PRINT:PRINT
440 PQ=CR+IR:IF PQ=0THEN PQ=1
445 PRINT"    NUMBER CORRECT = "CR
450 PRINT
455 PRINT"    NUMBER WRONG   = "IR
460 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/PQ);"%"
465 PRINT:PRINT"    ANOTHER TRY (Y/N/C)";
470 W$=INKEY$:IF W$=""THEN470
475 IF W$="C"THEN FX=1:GOSUB105:GOSUB185:RETURN
480 IF W$="Y" THEN RUN
485 IF W$="N" THEN CLS:END
490 GOTO470
1000 REM OBJECT-ACTION
1010 DATA HORSE,RIDE
1020 DATA CAR,DRIVE
1030 DATA PLANE,FLY
1040 DATA BUTTON,PUSH
1050 DATA LEVER,PULL
1060 DATA SPOON,STIR
1070 DATA BALL,BOUNCE
1080 DATA BOOK,READ
2000 REM TENSES
2010 DATA COME,CAME
2020 DATA SING,SANG
2030 DATA SPEAK,SPOKE
2040 DATA SEE,SAW
2050 DATA DIG,DUG
2060 DATA RUN,RAN
2070 DATA FLY,FLEW
2080 DATA WASH,WASHED
3000 REM HOMONYMS
3010 DATA HEAR,HERE
3020 DATA SEE,SEA
3030 DATA TEA,TEE
3040 DATA BUY,BY
3050 DATA TWO,TOO
3060 DATA ONE,WON
3070 DATA PEACE,PIECE
3080 DATA EIGHT,ATE
4000 REM PARTS & WHOLE
4010 DATA MINUTE,HOUR
4020 DATA HOUR,DAY
4030 DATA DAY,WEEK
4040 DATA WEEK,MONTH
4050 DATA MONTH,YEAR
4060 DATA SLICE,PIE
4070 DATA TREE,FOREST
4080 DATA CELL,ORGANISM
5000 REM SYNONYMS
5010 DATA HAPPY,GLAD
5020 DATA DULL,BORING
5030 DATA SILLY,FOOLISH
5040 DATA LOUD,NOISY
5050 DATA FAR,DISTANT
5060 DATA SAD,DEPRESSED
5070 DATA WASH,CLEAN
5080 DATA DIRTY,SOILED
6000 REM ANTONYMS
6010 DATA NIGHT,DAY
6020 DATA UP,DOWN
6030 DATA RIGHT,LEFT
6040 DATA PEACE,WAR
6050 DATA EMPTY,FULL
6060 DATA DIRTY,CLEAN
6070 DATA LONG,SHORT
6080 DATA FAST,SLOW
