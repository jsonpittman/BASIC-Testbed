1 'AUTOGRAY SCALER
2 'BY STUART WYSS-GALLIFENT
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
30 ' USES 11 LEVELS OF GRAY IN A 4X5 ARRAY TO REPRESENT A 1X2 PIXEL LOCATION
40 POKE 65497,0:'SPEEDUP POKE
50 POKE 150,41:'1200 BAUD FOR PRINTER (HI SPEED=1200X2=2400 BAUD ON PRINTER)
60 POKE &HE6C6,33:'DEACTIVATE AUTOMATIC HSCREEN CLEAR
70 DIM P(16),D(16),Q(16),P1(12,4),P2(12,4),PC(16),QB(16)
80 ON BRK GOTO 100
90 WIDTH 80:RGB:ATTR 0,0:PALETTE 0,0:PALETTE 8,63:CLS:GOTO 110
100 POKE 65496,0:PALETTE 0,0:PALETTE 8,63:ATTR 0,0:CLS:HSCREEN 2:IF X=316 THEN 830 ELSE HSET(X,Y,H1):GOTO 830
110 LOCATE 35,6:PRINT"AUTOGRAY"
120 PRINT:PRINT"AUTOGRAY scaler is a screen dump program for use with the COCO 3.  It will scan the screen in memory (any HSCREEN 2), then determine how the sixteen colors     should be assigned to the eleven possible gray levels, taking into accoun
130 PRINT" how  to save ink on your printer.":PRINT:PRINT"After pressing any key, the program will take about forty seconds to analyze thescreen in memory."
140 GOSUB 680
150 PRINT:PRINT"PRESS ANY KEY TO BEGIN...":EXEC 44539
160 HSCREEN 2
170 K=0
180 FOR X=4 TO 314 STEP 4:FOR Y=0 TO 191 STEP 4
190 H=HPOINT(X,Y)
200 K=K+1
210 P(H)=P(H)+1
220 NEXT Y,X
230 FOR Z=0 TO 15:PC(Z)=INT(10000*P(Z)/K)/100:NEXT Z
240 P(0)=30000:' SET BLACK (COLOR 0) TO NO DOTS (WHITE ON PRINTER)
250 FOR H=0 TO 15:D(H)=H:NEXT H
260 FOR T=0 TO 14
270 IF P(T)<P(T+1) THEN D=P(T+1):P(T+1)=P(T):P(T)=D:D=D(T+1):D(T+1)=D(T):D(T)=D:CF=1
280 NEXT T:IF CF=1 THEN CF=0:GOTO 260
290 FOR G=0 TO 15
300 READ GP
310 Q(D(G))=GP
320 NEXT G
330 FOR Z=0 TO 15:QB(Z)=Q(Z):NEXT Z
340 HSCREEN 0:CLS
350 I$=INKEY$
360 PRINT"COLOR-------%-------LEVEL"
370 FOR Z=0 TO 15:PRINTUSING"  ##       ##.##      ## ";Z,PC(Z),Q(Z)
380 NEXT Z
390 PRINT"Gray level 0 represents white on paper, level 10 represents solid black"
400 PRINT:PRINT"Press <ENTER> to print, <E> to manually enter the gray levels for each color,   <R> to automatically reassign the levels, or <V> to view the screen."
410 I$=INKEY$:IF I$="" THEN 410
420 IF I$=CHR$(13) THEN 520
425 IF I$="V" THEN HSCREEN 2:EXEC 44539:GOTO 340
430 IF I$="R" THEN FOR Z=0 TO 15:Q(Z)=QB(Z):NEXT Z:GOTO 340
440 PRINT:PRINT"Enter -1 for color 0 if you wish to abort this function."
450 FOR Z=0 TO 15
460 GOTO 480
470 PRINT"*** DATA ENTERED INCORRECTLY. Please try again."
480 PRINT"For color"Z", enter selected Gray Level (0-10) ";:INPUT GL
490 IF Z=0 AND GL=-1 THEN 340
500 IF GL<0 OR GL>10 THEN 470
510 IF INT(GL)<>GL THEN 470 ELSE Q(Z)=GL:NEXT Z:GOTO 340
520 PRINT:PRINT"Press any key when printer head is at the VERY top of the page...":EXEC 44539
530 PRINT#-2,CHR$(30)CHR$(27)CHR$(21)CHR$(27)CHR$(20)CHR$(18)CHR$(27)CHR$(90)CHR$(4)CHR$(13);
540 HSCREEN 2
550 FOR X=4 TO 314 STEP 2
560 FOR Y=191 TO 0 STEP -1
570 H1=HPOINT(X,Y):H2=HPOINT(X+1,Y)
580 IF H1=8 THEN HSET(X,Y,0) ELSE HSET(X,Y,8)
590 FOR Z=1 TO 4
600 PRINT#-2,CHR$(128+P1(Q(H1),Z)+P2(Q(H2),Z));
610 NEXT Z
620 HSET(X,Y,H1)
630 NEXT Y
640 PRINT#-2,CHR$(27)CHR$(90)CHR$(5)CHR$(13);
650 NEXT X
660 POKE 65496,0
670 GOTO 100
680 ' ASSEMBLE STRINGS
690 FOR X=0 TO 10:FOR Z=1 TO 4
700 READ P1(X,Z)
710 NEXT Z,X
720 DATA 0,0,0,0,0,2,0,0,2,0,1,0,4,1,0,2,2,4,1,2,5,2,1,2,3,5,2,1,6,5,2,3
730 DATA 6,3,3,3,7,5,3,3,7,7,3,3
740 FOR X=0 TO 10:FOR Z=1 TO 4
750 READ P2(X,Z)
760 NEXT Z,X
770 DATA 0,0,0,0,0,0,8,0,16,0,0,8,8,0,16,8,8,16,0,20,8,16,8,20,24,16,8,20
780 DATA 8,24,16,28,25,8,28,20,24,8,28,28,24,24,28,28
790 RETURN
800 ' GRAPHICS POSITIONS
810 DATA 0,0,1,1,2,2,3,3,4,4,5,6,7,8,9,10
820 ' END
830 HSCREEN 0:RGB:WIDTH 32:END
