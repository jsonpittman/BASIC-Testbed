10 'INITIALIZE PROGRAM
20 POKE 65497,0
30 ON BRK GOTO 1300
40 HBUFF 1,5900
50 'INITIALIZE TEST STRINGS
60 FOR J=33 TO 64:T1$=T1$+CHR$(J):NEXT
70 FOR J=65 TO 96:T2$=T2$+CHR$(J):NEXT
80 FOR J=97 TO 127:T3$=T3$+CHR$(J):NEXT
90 HSCREEN 2
100 PALETTE 1,0:PALETTE 0,54:PALETTE 2,36:PALETTE 15,38
110 HCOLOR 15,0:HLINE(0,0)-(130,88),PSET,BF:HCOLOR 1,0:HGET(0,0)-(130,88),1
120 'MAIN PROGRAM HERE
130 FOR X=150 TO 310 STEP 20
140 HLINE(X,6)-(X,166),PSET
150 NEXT X
160 FOR Y=6 TO 166 STEP 20
170 HLINE(150,Y)-(310,Y),PSET
180 NEXT Y
190 HPRINT (2,0),"FONT CREATOR"
200 HPRINT (0,1),"By Dave Jenkins"
210 HPRINT (0,3),"Press character"
220 HPRINT (3,4),"to define OR"
230 HPRINT (0,5),"Press SPACE BAR"
240 HPRINT (3,6),"for commands"
250 HPRINT (0,7),"Press <F2>"
260 HPRINT (3,8),"for special"
270 HPRINT (3,9),"characters"
280 D$=INKEY$:IF D$="" THEN 280
290 IF D$=CHR$(4) THEN 1380 'F2 KEY?
300 IF D$=" " THEN 890 ELSE IF ASC(D$)<32 THEN 280
310 GOSUB 1280 'CLEAR COMMAND BOX
320 HPRINT (0,1),"Press ARROWS"
330 HPRINT (1,2),"to select block"
340 HPRINT (0,3),"Press SPACE BAR"
350 HPRINT (1,4),"to set/reset"
360 HPRINT (0,5),"Press ENTER"
370 HPRINT (1,6),"to put in RAM"
380 HPRINT (0,7),"Press BREAK"
390 HPRINT (1,8),"to abort"
400 ON BRK GOTO 1560
410 CH=ASC(D$):CH=CH-33:OF=61605+CH*8
420 HPRINT (17,22),"Defining:":HPRINT(27,22),D$:HPRINT(30,22),"ASCII=":HPRINT(36,22),ASC(D$)
430 FOR Y=0 TO 7 '8 ROWS PER CHARACTER
440 LN(Y)=PEEK(OF+Y) 'GET CHARACTER
450 'DETERMINE WHICH GRID BLOCKS TO SET
460 IF LN(Y) AND 128 THEN HPAINT(155,Y*20+10),2,1
470 IF LN(Y) AND 64 THEN HPAINT(175,Y*20+10),2,1
480 IF LN(Y) AND 32 THEN HPAINT(195,Y*20+10),2,1
490 IF LN(Y) AND 16 THEN HPAINT(215,Y*20+10),2,1
500 IF LN(Y) AND 8 THEN HPAINT(235,Y*20+10),2,1
510 IF LN(Y) AND 4 THEN HPAINT(255,Y*20+10),2,1
520 IF LN(Y) AND 2 THEN HPAINT(275,Y*20+10),2,1
530 IF LN(Y) AND 1 THEN HPAINT(295,Y*20+10),2,1
540 NEXT Y
550 CX=0 'INITIALIZE CURSOR TO 0,0
560 CY=0
570 HLINE(CX*20+150,CY*20+6)-(CX*20+170,CY*20+26),PSET,B
580 E$=INKEY$
590 IF E$=CHR$(94) THEN 690 'UP ARROW
600 IF E$=CHR$(10) THEN 710 'DOWN ARROW
610 IF E$=CHR$(8) THEN 730 'LEFT ARROW
620 IF E$=CHR$(9) THEN 750 'RIGHT ARROW
630 IF E$=" " THEN 790 'SPACE BAR
640 IF E$=CHR$(13) THEN 840 'ENTER
650 HLINE(CX*20+150,CY*20+6)-(CX*20+170,CY*20+26),PRESET,B
660 FOR DLAY=1 TO 100:NEXT DLAY
670 GOTO 570
680 'MOVE CURSOR, WRAP IF NECESSARY
690 CY=CY-1:IF CY<0 THEN CY=7
700 GOTO 570
710 CY=CY+1:IF CY>7 THEN CY=0
720 GOTO 570
730 CX=CX-1:IF CX<0 THEN CX=7
740 GOTO 570
750 CX=CX+1:IF CX>7 THEN CX=0
760 GOTO 570
770 'CHECK IF BLOCK IS SET OR CLEAR AND
780 'FLIP IT
790 BX=2^(7-CX)
800 IF LN(CY) AND BX THEN HPAINT(155+CX*20,11+CY*20),0,1 ELSE HPAINT(155+CX*20,11+CY*20),2,1
810 IF LN(CY) AND BX THEN LN(CY)=LN(CY) AND (255-BX):GOTO 570
820 LN(CY)=LN(CY) OR BX:GOTO 570
830 'STORE CURRENT CHARACTER IN RAM
840 FOR Y=0 TO 7
850 POKE 61605+CH*8+Y,LN(Y)
860 NEXT Y
870 HCLS:GOSUB 1280:GOTO 130
880 'OTHER COMMANDS
890 GOSUB 1280
900 HPRINT (1,0),"OTHER COMMANDS"
910 HPRINT (1,1),"<S>ave Font"
920 HPRINT (1,2),"<L>oad Font"
930 HPRINT (1,3),"<I>nstructions"
940 HPRINT (1,4),"<B>ack to Main"
950 HPRINT (1,5),"<D>isplay font"
960 HPRINT (1,6),"<E>nd Program"
970 D$=INKEY$:IF D$="" THEN 970
980 IF D$="D" OR D$="d" THEN 1310
990 IF D$="B" OR D$="b" THEN GOSUB 1280:GOTO 190
1000 IF D$="E" OR D$="e" THEN POKE 65496,0:RGB:CLS:END
1010 IF D$="L" OR D$="l" THEN POKE 65496,0:WIDTH 40:PRINT"LOAD FONT":INPUT"FILENAME (NO EXTENSION)";F$:F$=F$+".FNT":LOADM F$:POKE 65497,0:HSCREEN2:GOSUB 1280:GOTO 130
1020 IF D$="S" OR D$="s" THEN POKE 65496,0:WIDTH 40:PRINT"SAVE FONT":INPUT"FILENAME (NO EXTENSION)";F$:F$=F$+".FNT":SAVEM F$,61605,62364,0:POKE 65497,0:HSCREEN2:GOSUB1280:GOTO 130
1030 IF D$="I" OR D$="i" THEN 1040 ELSE 970
1040 WIDTH 40
1050 PRINT"FONT CREATOR"
1060 PRINT
1070 PRINT"   This program will create and save a"
1080 PRINT"new font for the HPRINT command to use"
1090 PRINT"on the COCO 3 HI-RES screens. It is"
1100 PRINT"very easy to use.  At the main screen"
1110 PRINT"enter the character you wish to define,"
1120 PRINT"or press <SPACE> for other commands."
1130 PRINT"To define, simply use the arrow keys to"
1140 PRINT"move to the desired block on the grid."
1150 PRINT"Press <SPACE> to either set or clear"
1160 PRINT"the block that is flashing. When you"
1170 PRINT"have defined the character completely,"
1180 PRINT"press <ENTER> to continue.  After you"
1190 PRINT"have defined the entire font, use the"
1200 PRINT"<S>ave command to save it to disk."
1210 PRINT"Then, when you want to use the new font"
1220 PRINT"simply do a LOADM to load it in. DO NOT"
1230 PRINT"EXECUTE!"
1240 PRINT
1250 PRINT "Press a key to begin."
1260 D$=INKEY$:IF D$="" THEN 1260 ELSE HSCREEN2:GOSUB 1280:GOTO 130
1270 'CLEAR COMMAND BLOCK
1280 HPUT(0,0)-(130,88),1,PSET
1290 RETURN
1300 POKE 65496,0:RGB:WIDTH 40:CLS:END
1310 HCLS
1320 HPRINT (0,10),T1$
1330 HPRINT (0,12),T2$
1340 HPRINT (0,14),T3$
1350 HPRINT (0,19),"Press any key when ready to continue."
1360 D$=INKEY$:IF D$="" THEN 1360
1370 HCLS:GOSUB 1280:GOTO 130
1380 GOSUB 1280
1390 HPRINT (0,0),"Press letter"
1400 HPRINT (2,1),"for character"
1410 HPRINT (0,4),"A  B  C  D  E  F"
1420 HPRINT (0,5),CHR$(96)
1430 HPRINT (3,5),CHR$(123)
1440 HPRINT (6,5),CHR$(124)
1450 HPRINT (9,5),CHR$(125)
1460 HPRINT (12,5),CHR$(126)
1470 HPRINT (15,5),CHR$(127)
1480 A$=INKEY$:IF A$<"A" OR A$>"F" THEN 1480
1490 IF A$="A" THEN D$=CHR$(96)
1500 IF A$="B" THEN D$=CHR$(123)
1510 IF A$="C" THEN D$=CHR$(124)
1520 IF A$="D" THEN D$=CHR$(125)
1530 IF A$="E" THEN D$=CHR$(126)
1540 IF A$="F" THEN D$=CHR$(127)
1550 GOTO 310
1560 ON BRK GOTO 1300
1570 HCLS
1580 GOSUB 1280
1590 GOTO 130
