0 ' COPYRIGHT 1990 FALSOFT, INC
10 'The WYSIWYG Label Maker V.2
20 'By Timothy W. Hart
30 'June 26, 1988/Version 1.0
40 'June 22, 1989/Version 2.0
50 '(C) 1989 Falsoft, Inc.
60 '
70 '
80 POKE65497,0:CLEAR500:WIDTH40
90 POKE 150,87:BD=1200:'set initial baud rate (600 at double clock speed = 1200 baud)
100 TB=2:'set initial TAB
110 POKE&H11A,&HFF:'force uppercase
120 UC=1:'set upper/lower case flag on
130 CLS3
140 PALETTE0,63:PALETTE8,0
150 ATTR0,0
160 C$="                                       ":'clear line array
170 U$="                                        ":'underline string
180 FOR C=1 TO 24:PRINT"":NEXTC
190 LOCATE0,0:PRINT"<><><><>The WYSIWYG  Label Maker<><><><>";
200 LOCATE0,1:PRINT"<><><><><> By Timothy W. Hart <><><><><>";
210 LOCATE0,2:PRINT"<><><><><><><>  (C) 1989  <><><><><><><>";
220 LOCATE0,3:PRINT"<><><><><><> Falsoft,  Inc. <><><><><><>";
230 PRINT"<><><><><><><><><><><><><><><><><><><><>";
240 LOCATE0,5:ATTR0,0,U:PRINTU$;:ATTR0,0
250 GOSUB3000
260 LOCATE0,13:ATTR0,0,U:PRINTU$;:ATTR0,0
270 GOSUB4000
280 LOCATE0,14:PRINT"BAUD =";BD;
290 IF TB<10 THEN LOCATE31,14:PRINT"  TAB =";TB;:ELSE IF TB<100 THEN LOCATE31,14:PRINT" TAB =";TB;:ELSE LOCATE31,14:PRINT"TAB =";TB;
300 IF UC=1 THEN LOCATE18,19:ATTR0,0,U:PRINT"MENU";:ATTR0,0 ELSE LOCATE18,19:ATTR0,0,U:PRINT"menu";:ATTR0,0
310 IF UC=1 THEN LOCATE0,21:PRINT"<S>TART LABEL / <E>DIT LABEL / <P>RINT ? ";:ELSE LOCATE0,21:PRINT"<s>tart label / <e>dit label / <p>rint ? ";
320 I$=INKEY$
330 IF I$=CHR$(12) THEN IF UC=1 THEN SOUND220,1:SOUND210,1:SOUND200,1:POKE&H11A,0:UC=0:GOTO280 ELSE SOUND200,1:SOUND210,1:SOUND220,1:POKE&H11A,&HFF:UC=1:GOTO280
340 IF I$="S" OR I$="s" THEN CF=0:C1=0:C2=0:C3=0:C4=0:C5=0:LOCATE1,22:PRINTI$;:PLAY"L64EA":GOSUB 4000:GOSUB 3000:GOSUB 1000:IF PEEK(&H11A)=&HFF THEN UC=1:GOTO280 ELSE UC=0:GOTO280
350 IF I$="E" OR I$="e" THEN GOSUB 2000:IF PEEK(&H11A)=&HFF THEN UC=1:GOTO280 ELSE UC=0:GOTO280
360 IF I$="P" OR I$="p" THEN GOSUB 5000:GOTO280
370 IF I$="C" OR I$="c" THEN GOSUB 6000:GOSUB3000:GOTO280
380 IF I$="1" OR I$="2" OR I$="3" OR I$="4" OR I$="5" THEN GOSUB 6000:GOSUB3000:GOTO280
390 IF I$="B" OR I$="b" THEN GOSUB 7000:GOTO280
400 IF I$="T" OR I$="t" THEN GOSUB 8000:GOTO290
410 IF I$="Q" OR I$="q" THEN GOSUB 9000:GOTO280
420 GOTO320
430 '
1000 '<B>EGIN LABEL
1010 GOSUB4000
1020 LOCATE4,7:LINEINPUTL$(1):L$(1)=LEFT$(L$(1),35):GOSUB3000
1030 LOCATE4,8:LINEINPUTL$(2):L$(2)=LEFT$(L$(2),35):GOSUB3000
1040 LOCATE4,9:LINEINPUTL$(3):L$(3)=LEFT$(L$(3),35):GOSUB3000
1050 LOCATE4,10:LINEINPUTL$(4):L$(4)=LEFT$(L$(4),35):GOSUB3000
1060 LOCATE4,11:LINEINPUTL$(5):L$(5)=LEFT$(L$(5),35):GOSUB3000
1070 RETURN
1080 '
2000 '<E>DIT LABEL
2010 LOCATE1,22:PRINT"Edit which line ";:INPUT LI
2020 IF LI=0 THEN LOCATE 1,22:PRINTC$;:GOTO 280
2030 IF LI<1 OR LI>5 THEN LOCATE 19,22:PRINT"                    ";:GOTO 2010
2040 LOCATE0,14:PRINT"              Edit Line #";LI
2050 LOCATE3,16:PRINT"[":LOCATE4,16:PRINTL$(LI);:LOCATE39,16:PRINT"]";
2060 LOCATE3,17:PRINT"[":LOCATE39,17:PRINT"]":LOCATE4,17:LINEINPUTL$(LI)
2070 LOCATE0,22:PRINTC$:'clear out prompt
2080 IF LI=1 THEN C1=0 ELSE IF LI=2 THEN C2=0 ELSE IF LI=3 THEN C3=0 ELSE IF LI=4 THEN C4=0 ELSE IF LI=5 THEN C5=0
2090 CF=0
2100 L$(LI)=LEFT$(L$(LI),35)
2110 GOSUB3000
2120 LOCATE0,14:FORX=1TO5:PRINTC$:NEXTX:'clear out editor lines
2130 RETURN
2140 '
3000 'LIST LABEL
3010 IF CF=0 AND C1=0 THEN LOCATE0,7:PRINT" 1 ";:ELSE IF CF=1 OR C1=1 THEN LOCATE0,7:PRINT">1<";
3020 LOCATE3,7:PRINT"["
3030 LOCATE4,7:PRINTL$(1);:LOCATE39,7:PRINT"]";
3040 IF CF=0 AND C2=0 THEN LOCATE0,8:PRINT" 2 ";:ELSE IF CF=1 OR C2=1 THEN LOCATE0,8:PRINT">2<";
3050 LOCATE3,8:PRINT"["
3060 LOCATE4,8:PRINTL$(2);:LOCATE39,8:PRINT"]";
3070 IF CF=0 AND C3=0 THEN LOCATE0,9:PRINT" 3 ";:ELSE IF CF=1 OR C3=1 THEN LOCATE0,9:PRINT">3<";
3080 LOCATE3,9:PRINT"["
3090 LOCATE4,9:PRINTL$(3);:LOCATE39,9:PRINT"]";
3100 IF CF=0 AND C4=0 THEN LOCATE0,10:PRINT" 4 ";:ELSE IF CF=1 OR C4=1 THEN LOCATE0,10:PRINT">4<";
3110 LOCATE3,10:PRINT"["
3120 LOCATE4,10:PRINTL$(4);:LOCATE39,10:PRINT"]";
3130 IF CF=0 AND C5=0 THEN LOCATE0,11:PRINT" 5 ";:ELSE IF CF=1 OR C5=1 THEN LOCATE0,11:PRINT">5<";
3140 LOCATE3,11:PRINT"["
3150 LOCATE4,11:PRINTL$(5);:LOCATE39,11:PRINT"]";
3160 IF LEN(L$(5))=35 THEN 3170 ELSE 3210
3170 LOCATE0,12:PRINTC$;
3180 LOCATE0,13:ATTR0,0,U:PRINTU$;:ATTR0,0
3190 LOCATE0,14:PRINT"BAUD =";BD;
3200 IF TB<10 THEN LOCATE31,14:PRINT"  TAB =";TB;:ELSE IF TB<100 THEN LOCATE31,14:PRINT" TAB =";TB;:ELSE LOCATE31,14:PRINT"TAB =";TB;
3210 RETURN
3220 '
4000 'CLEAR LABEL LINE VARIABLES
4010 L$(1)="":L$(2)="":L$(3)="":L$(4)="":L$(5)=""
4020 RETURN
4030 '
5000 '<P>RINT LABEL
5010 LOCATE1,22:PRINT"Quantity to print ";:INPUTQ:IF Q<0 THEN LOCATE21,22:PRINT"                 ";:GOTO 5010:ELSE IF Q=0 THEN LOCATE1,22:PRINTC$;:GOTO 280
5020 LOCATE1,22:PRINTC$;
5030 LOCATE1,22:PRINT"Ready printer, press <ENTER>";:EXEC&HA171
5040 LOCATE1,22:PRINTC$;:LOCATE1,22:PRINT"Printing. . .";
5050 FOR I=1 TO Q
5060 PRINT#-2,TAB(TB)L$(1)
5070 PRINT#-2,TAB(TB)L$(2)
5080 PRINT#-2,TAB(TB)L$(3)
5090 PRINT#-2,TAB(TB)L$(4)
5100 PRINT#-2,TAB(TB)L$(5)
5110 PRINT#-2,"":'linefeed to ready next label
5120 NEXT I
5130 LOCATE1,22:PRINTC$;
5140 RETURN
5150 '
6000 '<C>ENTER LABEL LINES
6010 IF CF=1 THEN LOCATE1,22:PRINT"Label centered.  Press <ENTER>";:EXEC&HA171:LOCATE1,22:PRINTC$;:RETURN
6020 IF (I$="1" AND C1=1) OR (I$="2" AND C2=1) OR (I$="3" AND C3=1) OR (I$="4" AND C4=1) OR (I$="5" AND C5=1) THEN LOCATE1,22:PRINT"Line centered.  Press <ENTER>";:EXEC&HA171:LOCATE1,22:PRINTC$;:RETURN
6030 IF I$="C" OR I$="c" THEN IF (CF=0) AND (C1=0 AND C2=0 AND C3=0 AND C4=0 AND C5=0) THEN LOCATE1,22:PRINT"Want all lines centered (Y/N) ";:INPUTW$:ELSE RETURN
6040 IF W$="N" OR W$="n" THEN LOCATE1,22:PRINTC$;:RETURN
6050 LOCATE1,22:PRINTC$;
6060 IF I$="1" OR I$="2" OR I$="3" OR I$="4" OR I$="5" THEN CL=VAL(I$):ELSE GOTO 6080
6070 ON CL GOTO 6090,6120,6150,6180,6210:'switch to center lines 1,2,3,4,or 5
6080 CF=1:'set center (all) flag on
6090 LT$="":LG=LEN(L$(1)):IF (LG>0) AND (LG<35) THEN IN=(INT(35-LG)/2):FOR SP=1 TO IN:LT$=LT$+" ":NEXT SP:L$(1)=LT$+L$(1)
6100 C1=1:'set line 1 center flag on
6110 IF CF=0 THEN RETURN
6120 LT$="":LG=LEN(L$(2)):IF (LG>0) AND (LG<35) THEN IN=(INT(35-LG)/2):FOR SP=1 TO IN:LT$=LT$+" ":NEXT SP:L$(2)=LT$+L$(2)
6130 C2=1:'set line 2 center flag on
6140 IF CF=0 THEN RETURN
6150 LT$="":LG=LEN(L$(3)):IF (LG>0) AND (LG<35) THEN IN=(INT(35-LG)/2):FOR SP=1 TO IN:LT$=LT$+" ":NEXT SP:L$(3)=LT$+L$(3)
6160 C3=1:'set line 3 center flag on
6170 IF CF=0 THEN RETURN
6180 LT$="":LG=LEN(L$(4)):IF (LG>0) AND (LG<35) THEN IN=(INT(35-LG)/2):FOR SP=1 TO IN:LT$=LT$+" ":NEXT SP:L$(4)=LT$+L$(4)
6190 C4=1:'set line 4 center flag on
6200 IF CF=0 THEN RETURN
6210 LT$="":LG=LEN(L$(5)):IF (LG>0) AND (LG<35) THEN IN=(INT(35-LG)/2):FOR SP=1 TO IN:LT$=LT$+" ":NEXT SP:L$(5)=LT$+L$(5)
6220 C5=1:'set line 5 center flag on
6230 RETURN
6240 '
7000 '<B>AUD RATE CHANGE
7010 IF BD=9600 THEN BD=600 ELSE BD=BD*2
7020 IF BD=600 THEN POKE150,180
7030 IF BD=1200 THEN POKE150,87
7040 IF BD=2400 THEN POKE150,41
7050 IF BD=4800 THEN POKE150,18
7060 IF BD=9600 THEN POKE150,7
7070 PLAY"L64C"
7080 RETURN
7090 '
8000 '<T>AB SETTINGS
8010 LOCATE1,22:PRINT"Enter new TAB -->";:INPUT TB
8020 IF TB<0 OR TB>999 THEN LOCATE 19,22:PRINT"                   ";:GOTO8010
8030 LOCATE0,22:PRINTC$:'clear out prompt
8040 TB=INT(TB)
8050 RETURN
8060 '
9000 '<Q>UIT (exit) PROGRAM
9010 LOCATE1,22:ATTR0,0,B:PRINT"Are you sure (Y/N) ";:INPUTQ$
9020 IF Q$="Y" OR Q$="y" THEN POKE113,0:EXEC40999:ELSE ATTR0,0:LOCATE1,22:PRINTC$;:RETURN
