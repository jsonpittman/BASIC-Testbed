1 'POKE TEXT
2 'BY KEIRAN KENNY
3 'COPYRIGHT (C) DECEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 '*****************************
10 POKE65494,0:CLS0:GOTO60
20 FORX=1TO LEN(A$):AA$=MID$(A$,X,1)
30 IFAA$>CHR$(31)ANDAA$<"@"THENA=ASC(AA$):GOTO50
40 A=ASC(AA$)-64
50 POKEPP,A:PP=PP+1:NEXT:RETURN
60 PP=1032:A$="<<<POKETEXT>>>":GOSUB20
70 PP=1088:A$="BY KEIRAN KENNY, THE HAGUE, 1986":GOSUB20
80 A$="***INSTRUCTIONS***":PP=1167-INT(LEN(A$)/2):GOSUB20
90 PP=1216:A$="1) THE POKE POSITION (PP) IS THEPRINT@ POSITION +1024":GOSUB20
100 PP=1312:A$="2) PUT TEXT IN A STRING LABELLEDA$. ENCLOSE IT IN QUOTES AND    GOSUB20":GOSUB20
110 PP=1440:A$="3) USE THE SPACEBAR TO FILL OUT LINES FOR A NEAT DISPLAY":GOSUB20
120 A$="PRESS ANY KEY.":PP=1519-INT(LEN(A$)/2):GOSUB20
130 EXEC44539:CLS0
140 PP=1024:A$="4) TO CENTER TEXT EASILY FOLLOW THE EXAMPLES IN LINES 80 & 120.":GOSUB20
150 PP=1120:A$="5) YOU CAN ALSO PRINT LOWER CASELETTERS ON A BLACK (CLS0) SCREENBUT YOU CANNOT PRINT NUMBERS OR PUNCTUATION AND MATH SIGNS LIKE THESE: 1234567890#$%&'()*:=-+;?/>.<,[]\!":GOSUB20
160 PP=1344:A$="6) TO USE THE DOUBLE QUOTE AS IN":GOSUB20:POKE1376,34:PP=1377:A$="TITLE":GOSUB20:POKE1382,34:PP=1384:A$="SEE LINE 180.":GOSUB20
170 A$="PRESS ANY KEY.":PP=1455-INT(LEN(A$)/2):GOSUB20:EXEC44539:CLS0
180 PP=1088:A$="I HOPE YOU WILL FIND 'POKETEXT' EASY TO USE IN YOUR PROGRAMS.   IT'S UP TO YOU NOW TO EXPERIMENTWITH IT AND USE IT FOR ATTRAC-  TIVE TITLE SCREENS OR WHATEVER  YOU LIKE TO USE IT FOR.":GOSUB20
190 A$="TO END PROGRAM PRESS ANY KEY.":PP=1327-INT(LEN(A$)/2):GOSUB20:EXEC44539:POKE65494,0:CLS:END
200 GOTO200
