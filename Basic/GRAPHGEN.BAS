10 CLEAR2000
20 PCLEAR8
30 FORZ=0TO6:Z(Z)=INT(2^Z):NEXTZ
40 'INITIALIZE [30-90]
50 T$="%%%%%%%%%%%%%%%%%%%%%%%%%       %   PRINTER  GRAPHICS   %       %   PROGRAM GENERATOR   %       %                       %       %          BY           %       %     ALLEN  SNOOK      %       %%%%%%%%%%%%%%%%%%%%%%%%%"
60 CLS:PRINT"   ";T$
70 PRINT@416,"":PRINT@416,"FILENAME/EXT:";:LINEINPUTF$:IF F$<>"" AND F$<>"D" THENLOADM""+F$
80 IF F$="D" THEN 630
90 PRINT@416,"DISPLAY SCREEN? (Y/N)"
100 EXEC44539:IFINKEY$="Y"THENPMODE4,1:SCREEN1,1:EXEC44539:GOTO70
110 FORX=1TO4:PCOPYX TO X+4:NEXTX
120 'GET BOX    [100-210]
130 PMODE4,1:SCREEN1,1
140 A=JOYSTK(0)*4:B=JOYSTK(1)*3
150 I=PPOINT(A,B):PSET(A,B,ABS(I-5))
160 R=PEEK(65280):IF R=254 OR R=126 THEN SOUND200,1:GOTO180
170 PSET(A,B,I):GOTO140
180 PSET(A,B,I)
190 C=JOYSTK(0)*4:D=JOYSTK(1)*3
200 LINE(A,B)-(C,D),PSET,B
210 LINE(A,B)-(C,D),PRESET,B
220 FORX=8TO5STEP-1:PCOPYX TO X-4:NEXTX
230 R=PEEK(65280):IF R=254 OR R=126 THEN SOUND200,1:GOTO260
240 GOTO 190
250 'PROGRAM    [450-570]
260 GOSUB500
270 'SAVE DATA  [230-430]
280 LN=90
290 FOR Y= B TO D STEP 7
300 N=1
310 FOR X= A TO C
320 Z=-1:E=0
330 FORY2= Y TO Y+6
340 Z=Z+1
350 IF PPOINT(X,Y2)=0 THEN E=INT(E+Z(Z))
360 NEXT Y2
370 IF LEN(C$(N))>230 THEN N=N+1
380 C$(N)=C$(N)+RIGHT$(STR$(E),LEN(STR$(E))-1)+","
390 NEXTX
400 FORQ=1TON
410 LN=LN+10
420 C$(Q)=STR$(LN)+" DATA "+C$(Q)
430 IF Q=N THEN C$(Q)=C$(Q)+"999" ELSE C$(Q)=LEFT$(C$(Q),LEN(C$(Q))-1)
440 PRINT#1,C$(Q):C$(Q)="":NEXTQ
450 NEXTY
460 C$=STR$(LN+10)+" DATA 888"
470 PRINT#1,C$
480 CLOSE#1
490 PRINT@416,"SAVE OF GRAPHIC PROGRAM DONE.":EXEC44539:END
500 REM I/O
510 PRINT@416,"STORE FILE AS:";:LINEINPUTF$
520 IF F$="" THEN 510
530 PRINT@416,"PROCESSING ";F$;", WAIT":PRINT@448,""
540 OPEN"O",#1,F$
550 PRINT#1,"10 PRINT#-2,CHR$(18)"
560 PRINT#1,"20 READN:IF N=888 THEN 90"
570 PRINT#1,"30 IF N=999 THEN 80"
580 PRINT#1,"40 PRINT#-2,CHR$(128+N);:GOTO20"
590 PRINT#1,"80 PRINT#-2:GOTO20"
600 PRINT#1,"90 PRINT#-2,CHR$(30)
610 PRINT#1,"95 END"
620 RETURN
630 'LIST DIRECTORY
640 DIM NA$(50)
650 GOSUB730
660 FORX=1 TO N STEP 22:CLS
670 FORX2=X TO X+21
680 PRINTNA$(X2),
690 NEXT X2
700 PRINT@416,"DIRECTORY:PRESS ANY KEY":EXEC44539
710 NEXTX
720 RUN
730 'DIRECTORY READ
740 N=1
750 FORA=3 TO 11
760 DSKI$ 0,17,A,A$(0),A$(1)
770 FORJ=0 TO 1
780 FORI=0 TO 96 STEP 32
790 EN$=MID$(A$(J),I+1,32)
800 IF ASC(MID$(EN$,1,1))=0 THEN 840
810 IF ASC(MID$(EN$,1,1))=255 THEN 850
820 NA$(N)=LEFT$(EN$,8)+"/"+MID$(EN$,9,3)
830 N=N+1
840 NEXT I,J,A
850 N=N-1
860 FORX=N TO 1 STEP -1
870 FORY=1 TO X-1
880 IF NA$(Y)>NA$(Y+1) THEN B$=NA$(Y):NA$(Y)=NA$(Y+1):NA$(Y+1)=B$:F=1
890 NEXTY
900 IF F<>0 THEN F=0:NEXTX
910 RETURN
