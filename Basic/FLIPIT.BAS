10 REM **** FLIP IT ****
20 REM * BY ERIC TUCKER *
30 CLS:PRINT@64," ******* F L I P   I T ******":PRINT@96,"((( DESIGNED BY ERIC TUCKER )))":PRINT@192," TWO PEOPLE ARE NEEDED TO PLAY.":PRINT@256,"     <<<<PRESS ENTER>>>>":EXEC44539
40 PMODE3:SCREEN1,0:POKE65314,248:PCLS2:CIRCLE(128,96),255,1,1,.65,.35
50 LINE(180,0)-(182,191),PRESET,BF:DRAW"C3":LINE(10,10)-(170,170),PSET,BF
60 FORX=10TO170STEP20:LINE(X,10)-(X,170),PRESET:NEXT:FORY=10TO170STEP20:LINE(10,Y)-(170,Y),PRESET:NEXT:CIRCLE(6,6),4,1:PAINT(6,6),1,1
70 DRAW"C1BM10,8;BR6U6R4D6U3NL4D3BR16U6R4D3L4R6D3NL6BR14U6NR4D6R4BR16U6R2F2D2G2NL2BR18U6NR4D3NR4D3R4BR16U6NR4D3R4BD3BR16U6NR6D6R6U3NL2D3BR14U6D3R4U3D6"
80 DRAW"BM4,10BD4D6R2L4R2U5NL2D5L2BD16R4D3L4D3R4BD16NL4D3NL4D3L4BD14D3R4U3D6BD14L4D3R4D3L4BD14BR4L4D6R4U3L4BD18R4D6BD12L4D6R4U6D3L4C3"
90 LINE(200,10)-(240,30),PSET,BF:LINE(210,10)-(210,30),PRESET:LINE(230,10)-(230,30),PRESET:LINE(198,9)-(242,31),PRESET,B:LINE(180,40)-(255,42),PRESET,BF
100 DRAW"BM196,50C1D6R6U3L6R4U3NL4BR6D6R6BR4U6R4D6U3NL4D3BR4NR4U6R4BR4D6U3R2NE3F3BD4L40":LINE(180,80)-(255,82),PRESET,BF
110 DRAW"BM196,90D6R4NU4R4U6BR4D6U3R4U3D6BR4U6BR4R4L2D6BR6NR4U3NR4U3R4BD10L36":LINE(180,120)-(255,122),PRESET,BF
120 DRAW"BM192,130;ND6R4D3NL4BD3BR4U6R4D3L4R3D2R1D1R2BR4NR4U3NR4U3R4BR4NR4D3R4D3NL4BR4R4U3L4U3R4BR12ND6R4D6U3L4BM204,140D6U3R2NE3F3R1BR4NR4U3NR4U3R4BR4F4NE4D2"
130 DRAW"C4":LINE(204,150)-(228,160),PSET,BF:LINE(204,150)-(228,160),PRESET,B
140 CIRCLE(80,80),6,1:PAINT(80,80),1,1:CIRCLE(100,100),6,1:PAINT(100,100),1,1:CIRCLE(80,100),6,4:PAINT(80,100),4,4:CIRCLE(100,80),6,4:PAINT(100,80),4,4:CIRCLE(220,20),6,1:PAINT(220,20),1,1:PC=1
150 SD$="T6L64V30O1EEEV20EEEV15EEEV10EEEV5EEE"
160 PAINT(220,20),PC,3:GOSUB890:SOUND200,1:EXEC44539:CLS:IF BS+WS=64THEN1000
170 IF BS=0 OR WS=0 THEN 1000
180 REM ask for input
190 IF PC=1 THEN PC=128 ELSE PC=207
200 PRINT"USE 'I,9' TO LOOK AT BOARD AGAIN":PRINT
210 PRINT"PLAYER: ";STRING$(5,PC):IFPC=128THENPC=1ELSEPC=4
220 GOTO 240
230 SOUND10,10
240 INPUT"POSITION(LETTER,NUMBER)";C$,R:SCREEN1,0:POKE65314,248
250 IF C$=""OR R=0THEN160
260 C=ASC(C$)-64
270 IF C=9 OR R=9 THEN 160
280 IF C<0 OR C>8 OR R<0 OR R>8 THEN 230
290 IF C=0 OR R=0 THEN 160
300 X=C*20:Y=R*20:IFPPOINT(X,Y)<>3THEN230
310 CX=X:CY=Y:REM #1 check
320 REM #2 check--(N)
330 CY=CY-20:IF CY=0 THEN390
340 IFPPOINT(CX,CY)=3THEN390
350 IFPPOINT(CX,CY)<>PC THEN330ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
360 CY=CY-20:IF CY=SY THEN 390
370 PLAY SD$
380 CIRCLE(CX,CY),6,PC:PAINT(CX,CY),PC,PC:GOTO360
390 POKE65314,248:CX=X:CY=Y:REM #2 check--(NE)
400 CY=CY-20:CX=CX+20:IF CY=0 OR CX=180 THEN 460
410 IFPPOINT(CX,CY)=3THEN460
420 IFPPOINT(CX,CY)<>PC THEN400ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
430 CY=CY-20:CX=CX+20:IFCX=SX AND CY=SY THEN 460
440 PLAY SD$
450 PAINT(CX,CY),PC,3:GOTO430
460 POKE65314,248:CX=X:CY=Y:REM check--(E)
470 CX=CX+20:IFCX=180THEN530
480 IF PPOINT(CX,CY)=3THEN530
490 IFPPOINT(CX,CY)<>PC THEN470ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
500 CX=CX+20:IF CX=SX AND CY=SY THEN 530
510 PLAY SD$
520 PAINT(CX,CY),PC,3:GOTO500
530 POKE65314,248:CX=X:CY=Y:REM check--(SE)
540 CX=CX+20:CY=CY+20:IF CX=180ORCY=180THEN600
550 IF PPOINT(CX,CY)=3THEN600
560 IFPPOINT(CX,CY)<>PC THEN540ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
570 CX=CX+20:CY=CY+20:IF CX=SX AND CY=SY THEN 600
580 PLAY SD$
590 PAINT(CX,CY),PC,3:GOTO570
600 POKE65314,248:CX=X:CY=Y:REM check--(S)
610 CY=CY+20:IF CY=180THEN670
620 IF PPOINT(CX,CY)=3THEN670
630 IF PPOINT(CX,CY)<>PC THEN610ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
640 CY=CY+20:IF CY=SY THEN 670
650 PLAY SD$
660 PAINT(CX,CY),PC,3:GOTO640
670 POKE65314,248:CX=X:CY=Y:REM check--(SW)
680 CY=CY+20:CX=CX-20:IF CY=180ORCX=0THEN740
690 IF PPOINT(CX,CY)=3THEN740
700 IFPPOINT(CX,CY)<>PC THEN680 ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
710 CX=CX-20:CY=CY+20:IF CX=SX AND CY=SY THEN 740
720 PLAY SD$
730 PAINT(CX,CY),PC,3:GOTO710
740 POKE65314,248:CX=X:CY=Y:REM check--(W)
750 CX=CX-20:IFCX=0THEN810
760 IFPPOINT(CX,CY)=3THEN810
770 IFPPOINT(CX,CY)<>PC THEN750 ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
780 CX=CX-20:IF CX=SX THEN810
790 PLAY SD$
800 PAINT(CX,CY),PC,3:GOTO780
810 POKE65314,248:CX=X:CY=Y:REM check--(NW)
820 CX=CX-20:CY=CY-20:IFCX=0ORCY=0THEN880
830 IFPPOINT(CX,CY)=3THEN880
840 IFPPOINT(CX,CY)<>PC THEN 820ELSE SX=CX:SY=CY:CX=X:CY=Y:CIRCLE(X,Y),6,PC:PAINT(X,Y),PC,PC
850 CX=CX-20:CY=CY-20:IF CX=SX AND CY=SY THEN880
860 PLAY SD$
870 PAINT(CX,CY),PC,3:GOTO850
880 PLAYSD$:IF PC=1THENPC=4:GOTO160ELSEPC=1:GOTO160
890 REM CHECK NUMBER OF PIECES
900 BS=0:WS=0:FORW=20TO160STEP20:FORV=20TO160STEP20:IFPPOINT(W,V)=1THENBS=BS+1ELSEIFPPOINT(W,V)=4THENWS=WS+1
910 NEXT:NEXT
920 BS$=STR$(BS):WS$=STR$(WS):BS$=RIGHT$(BS$,LEN(BS$)-1):WS$=RIGHT$(WS$,LEN(WS$)-1)
930 DRAW"C2":LINE(200,64)-(230,72),PSET,BF:LINE(200,104)-(230,112),PSET,BF:L=LEN(BS$):IFL=2THENDRAW"BM212,65;C1;"ELSEDRAW"BM214,65;C1;"
940 V1=VAL(LEFT$(BS$,1)):IF V1=0 THEN GOSUB 1060 ELSE ON V1 GOSUB 1070,1080,1090,1100,1110,1120,1130,1140,1150
950 IF L=2 THEN V1=VAL(RIGHT$(BS$,1)):IF V1=0 THEN GOSUB 1060 ELSE ON V1 GOSUB 1070,1080,1090,1100,1110,1120,1130,1140,1150
960 L=LEN(WS$):IF L=2THENDRAW"BM212,105;C1;"ELSEDRAW"BM214,105;C1;"
970 V1=VAL(LEFT$(WS$,1)):IF V1=0 THEN GOSUB 1060 ELSEON V1 GOSUB 1070,1080,1090,1100,1110,1120,1130,1140,1150
980 IF L=2 THEN V1=VAL(RIGHT$(WS$,1)):IF V1=0 THEN GOSUB 1060 ELSE ON V1 GOSUB 1070,1080,1090,1100,1110,1120,1130,1140,1150
990 RETURN
1000 REM end of game
1010 CLS:SOUND1,10:FORT=1TO5000:NEXT
1020 PRINT@64," ******* F L I P   I T ********":PRINT@128,"BLACK HAS";BS:PRINT@192,"WHITE HAS";WS
1030 IF WS>BS THEN S$="WHITE"ELSE IF WS<BS THEN S$="BLACK"
1040 IF WS=BS THEN PRINT@256,"   IT'S A TIE GAME."ELSE PRINT@256,"   ";S$;" IS THE WINNER."
1050 PRINT@320,"ANOTHER GAME";:INPUTI$:IFLEFT$(I$,1)="Y"THENRUNELSEIFLEFT$(I$,1)="N"THENENDELSE1050
1060 DRAW"R4D6L4U6R4BR4":RETURN
1070 DRAW"D6BR4BU6":RETURN
1080 DRAW"R4D3L4D3R4BU6BR4":RETURN
1090 DRAW"R4D3NL4D3NL4BR4BU6":RETURN
1100 DRAW"D3R4U3ND6BR4":RETURN
1110 DRAW"NR4D3R4D3NL4BU6BR4":RETURN
1120 DRAW"NR4D6R4U3NL4BU3BR4":RETURN
1130 DRAW"R4ND6BR4":RETURN
1140 DRAW"R4D6L4U6D3R4U3BR4":RETURN
1150 DRAW"R4D3L4NU3BD3R4U6BR4":RETURN
