10 CLEAR1100:DIMD$(68):CLS:PRINT"DIRECTORY ALPHABETIZER":PRINT
20 INPUT"PRESS ENTER TO START";A$
30 PRINT"ONE MOMENT...":Z=0:FORT=3 TO 11:A$="":B$="":DSKI$D,17,T,A$,B$
40 FORQ=1 TO 97 STEP32:C$=MID$(A$,Q,16):IFASC(LEFT$(C$,1))=255 THEN130
50 IFASC(LEFT$(C$,1))=0 THEN70
60 D$(Z)=C$:Z=Z+1
70 NEXT
80 FORQ=1 TO 97 STEP32:C$=MID$(B$,Q,16):IFASC(LEFT$(C$,1))=255THEN130
90 IFASC(LEFT$(C$,1))=0 THEN110
100 D$(Z)=C$:Z=Z+1
110 NEXT
120 NEXT
130 Z=Z-1:IFZ=0THENPRINT"ONLY ONE FILE ON DISK-NO NEED   TO ALPHABETIZE":END
140 FORT=0TOZ-1:FORS=T+1 TOZ
150 IFD$(S)<D$(T) THEN C$=D$(S):D$(S)=D$(T):D$(T)=C$
160 NEXTS,T
170 N$=STRING$(16,0):FORT=0 TO Z+3 STEP8:A$="":B$=""
180 FORU=0 TO 3:IFT+U<=Z THEN A$=A$+D$(T+U)+N$ ELSE 230
190 NEXT:FORU=4 TO 7:IFT+U<=Z THEN B$=B$+D$(T+U)+N$ ELSE240
200 NEXT
210 DSKO$0,17,T/8+3,A$,B$:IFT+U<=Z THENNEXT
220 IFT/8+4 <18 THENA$=STRING$(128,255):B$=A$:DSKO$0,17,T/8+4,A$,B$
225 END
230 A$=A$+STRING$(128-LEN(A$),255)
240 B$=B$+STRING$(128-LEN(B$),255):GOTO210
0 '<><><><><><><><><><><><><><>
1 '<  DIRECTORY ALPHABETIZER  >
2 '<     BY SHAWN M DAVIS     >
3 '<    (C) COPYRIGHT 1987    >
4 '<><><><><><><><><><><><><><>
5 'COPYRIGHT 1990 FALSOFT, INC.
10 CLEAR 5000: DIM P$(40): M=1
20 CLS:PRINT STRING$(32,131);:PRINT TAB(5)"DIRECTORY ALPHABETIZER":PRINT TAB(8)"BY SHAWN M DAVIS":PRINT TAB(7)"(C) COPYRIGHT 1987":PRINTSTRING$(32,140);
30 PRINT"   PLACE THE DESIRED DISK INTO":PRINT"    THE DRIVE AND PRESS ENTER":LINE INPUT K$
40 PRINT"   THE DIRECTORY IS BEING READ":PRINT"    INTO MEMORY FROM THE DISK"
50 FOR S=3 TO 18
60 DSKI$ 0,17,S,A$,B$
70 IF ASC(MID$(A$,1,1))=255 THEN 120
80 FOR P=1 TO 128 STEP 32
90 P$(M+P/32)=MID$(A$,P,32)
100 P$(M+4+P/32)=MID$(B$,P,32)
110 NEXT P: M=M+8: NEXT S
120 M=M-1:PRINT:PRINT TAB(6)"ALL KILLED FILENAMES":PRINT TAB(7)"ARE BEING REMOVED."
130 FOR P=1 TO M
140 IF ASC(MID$(P$(P),1,1))<>0 THEN 160
150 P$(P)=STRING$(32,255)
160 NEXT P
170 PRINT:PRINT" NOW ALPHABETIZING, PLEASE WAIT"
180 LET S = M-1
190 IF S=0 THEN 260
200 FOR P =1 TO S
210 IF P$(P) < P$(P+1) THEN 250
220 H$ = P$(P)
230 P$(P) = P$(P+1)
240 P$(P+1) = H$
250 NEXT P: S= S-1:GOTO190
260 FORI=5 TO 14:PRINT@I*32,STRING$(32,32);:NEXT I
270 PRINT@160,"    THE NEW DIRECTORY CAN NOW":PRINT TAB(6)"BE STORED ON THE DISK"
280 S=3:FOR P =1 TO M STEP 8
290 A$="":B$=""
300 FOR N= 0 TO 3
310 A$=A$+P$(P+N): B$=B$+P$(P+4+N)
320 NEXT N
330 DSKO$ 0,17,S,A$,B$
340 S= S+1: NEXT P
350 PRINT:PRINT TAB(5)"THE PROCESS IS COMPLETE"
360 PRINT:PRINT"  WOULD YOU LIKE TO ALPHABETIZE":PRINT"     ANOTHER DISK DIRECTORY?":PRINT TAB(8)"TYPE 'Y' FOR YES OR":PRINT TAB(8)"TYPE 'N' FOR NO"
370 K$ = INKEY$: IFK$=""THEN 370
380 IFK$ ="Y" THEN 10
390 IFK$<>"N" THEN 370
400 CLS:PRINT"      THE PROGRAM HAS BEEN":PRINT"   TERMINATED.  TYPE 'RUN' TO":PRINT TAB(12)"CONTINUE"
410 END
