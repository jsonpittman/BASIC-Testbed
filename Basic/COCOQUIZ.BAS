0 'COPYRIGHT 1989  FALSOFT,INC
10 'COCOQUIZ
20 'FROM KROMICO SOFTWARE
30 'BY BILL BERNICO
40 '
50 DIMA$(20),B$(20),C$(20),D$(20),E$(20),B(20):FORA=1TO20:READA$(A),B$(A),C$(A),D$(A),E$(A),B(A):NEXTA
60 C=C+1
70 IF C<21 THEN 90
80 C=0:GOTO 60
90 IF B(C)=20 THEN 110
100 CLS:PRINTA$(C):PRINTSTRING$(32,191):PRINTTAB(9)"1.) ";B$(C):PRINTTAB(9)"2.) ";C$(C):PRINTTAB(9)"3.) ";D$(C):PRINTTAB(9)"4.) "E$(C):GOTO130
110 D=D+1:IF D=21 THEN 210
120 GOTO 60
130 PRINT:PRINTTAB(7):INPUT"SELECT 1-4)";E:PRINT
140 IF E<1 OR E>4 THEN 130
150 F=F+1
160 IF E=B(C)THEN G=1:B(C)=20:GOTO180
170 G=0
180 IF G=1 THEN 200
190 PRINTSTRING$(32,175);:SOUND1,2:PRINT"WRONG!  WE'LL TRY THIS ONE LATER";:PRINTSTRING$(32,175);:GOSUB 290:GOTO 60
200 PRINTSTRING$(32,159);:SOUND191,2:PRINT"RIGHT!  THAT'S ONE MORE FOR YOU.";:PRINTSTRING$(32,159);:GOSUB 290:GOTO 60
210 IF F=20 THEN 230
220 CLS:PRINT"YOU MISSED";F-20;"QUESTIONS":GOTO 240
230 CLS:PRINT"VERY GOOD...NO WRONG ANSWERS
240 PRINT@324,"CARE TO TRY AGAIN (Y/N)
250 IN$=INKEY$:IFIN$=""THEN 250
260 IF IN$="Y"THEN RUN
270 IF IN$="N"THEN CLS:LIST-30
280 GOTO 250
290 PRINT@484,"HIT ANY KEY TO CONTINUE";:FORX=1507TO1531:POKEX,PEEK(X)-64:NEXT:EXEC44539:RETURN
300 DATA RESERVES BYTES OF STRING SPACE,CLS,CLEAR,CONT,OPEN,2
310 DATA CHECKS THE KEYBOARD AND RETURNS THE KEY THAT WAS HIT (IF ANY),RIGHT$,SCREEN,JOYSTK,INKEY$,4
320 DATA SETS THE COMPUTER'S POINTER BACKTO THE FIRST ITEM ON THE FIRST  DATA LINE,RENUM,RESTORE,TROFF,PPOINT,2
330 DATA PRINTS A STRING OF CHARACTERS   SPECIFIED BY ASCII CODE,STR$,CHR$,STRING$,LEFT$,3
340 DATA RESETS A POINT TO THE BACKGROUNDCOLOR,RESET,SET,PSET,PRESET,4
350 DATA STORES GRAPHICS FROM SOURCE ONTOSTART/END RECTANGLE ON A SCREEN,GET,PUT,SOUND,INT,2
360 DATA SETS FOREGROUND AND BACKGROUND  COLOR,COLOR,CLS,PAINT,PCLS,1
370 DATA DIMENSIONS ONE OR MORE ARRAYS,DATA,DIM,GOSUB,LIST,2
380 DATA REPLACES A PORTION OF AN OLD    STRING WITH A NEW STRING,LEFT$,RIGHT$,MID$,INKEY$,3
390 DATA ERASES EVERYTHING IN MEMORY,CLS,PCLS,NEW,CLEAR,3
400 DATA SELECTS RESOLUTION AND FIRST    MEMORY PAGE,PCLEAR,PMODE,PCLS,PRESET,2
410 DATA PRINTS NUMBERS IN A SPECIFIED   FORMAT,PRINT,PUT,READ,PRINT USING,4
420 DATA LETS YOU INSERT COMMENTS IN A   PROGRAM LINE,REM,LIST,DEL,SAVE,1
430 DATA RETURNS THE LENGTH OF A STRING,STRING$,LEN,STR$,PEEK,2
440 DATA RETURNS RIGHT PORTION OF ANY    STRING,RIGHT$,LEFT$,STR$,STRING$,1
450 DATA COMMAND USED TO CALL SPECIFIED  SUBROUTINES,GOTO,GOSUB,GET,LIST,2
460 DATA LOADS MACHINE LANGUAGE PROGRAM  FROM DISK,LOADM,CLOADM,LOAD,CLOAD,1
470 DATA JUMPS TO SPECIFIED LINE NUMBER,GOSUB,GET,GOTO,LLIST,3
480 DATA LISTS PROGRAM LINES TO PRINTER,LIST,PRINT,INPUT,LLIST,4
490 DATA SELECTS EITHER GRAPHICS OR TEXT DISPLAY AND THE COLOR SET,SCREEN,PCLS,PPOINT,PMODE,1
