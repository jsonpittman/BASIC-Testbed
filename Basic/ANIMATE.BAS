1 GOTO 800
5 CLS0:PRINT"MAKE YOUR CHOICE:":PRINT"1)DRAW":PRINT"2)READ IN PICTURES FROM TAPE":PRINT"3)TAPE STORED PICTURES" :PRINT"4)VIEW PICTURES ":PRINT"5)COPY LAST DRAWING ON NEXT SCREEN"
6 PRINT "6)SHIFT A PICTURE":PRINT "7)REDO A DRAWING":PRINT "8)ANIMATE":PRINT "9)CHOOSE DIFFERENT PICTURE SCREEN TO BE LAST":PRINT "10)END"
10 INPUT"TYPE NUMBER, THEN PRESS <ENTER>.";AN:IF AN<1 OR AN>10 THEN 5
15 ON AN GOTO 705,305,505,405,1095,720,1110,1020,1175,605
50 PP=POINT(X,Y)
51 A$=INKEY$:SET(X,Y,4):RESET(X,Y):IF A$="" THEN 51
52 IF PP=0 THEN RESET(X,Y):GOTO 55
53 IF PP>0 AND PP<9 THEN SET(X,Y,PP)
55 IF A$="@" THEN GOTO 200
60 IF A$="T" THEN GOTO 1150
65 IF A$="^" OR A$=CHR$(10) OR A$=CHR$(9) OR A$=CHR$(8)  OR A$="W" OR A$="Q" OR A$="S" OR A$="A"THEN GOSUB 100: GOSUB 115
70 IF A$="" THEN 50
75 IF ASC(A$)<81 AND ASC(A$)>71 THEN CC=ASC(A$)-72:GOTO 50
80 IF ASC(A$)=12 THEN CLS0:GOTO 50
85 IF A$<>"D" THEN 95
90 IF DR=0 THEN DR=1:PLAY "CD":GOTO 50 ELSE DR=0:PLAY "B":GOTO 50
95 GOTO 50
100 B$=INKEY$:IF B$="" THEN 100
105 B=VAL(B$)
110 RETURN
115 FOR TI=1 TO B
120 GOSUB 150:IF X<0 THEN X=0
125 IF X>63 THEN X=63
130 IF Y<0 THEN Y=0
135 IF Y>31 THEN Y=31
140 IF DR=0 THEN SET(X,Y,CC):IF CC=0 THEN RESET(X,Y)
144 NEXT TI
145 RETURN 
150 IF A$="^" THEN Y=Y-1:RETURN
155 IF A$=CHR$(10) THEN Y=Y+1:RETURN
160 IF A$=CHR$(8) THEN X=X-1:RETURN
165 IF A$=CHR$(9) THEN X=X+1:RETURN
170 IF A$="W" THEN X=X+1:Y=Y-1:RETURN
175 IF A$="Q" THEN X=X-1:Y=Y-1:RETURN
180 IF A$="S" THEN X=X+1:Y=Y+1:RETURN
185 IF A$="A" THEN X=X-1:Y=Y+1:RETURN
200 N=N+1:IF N>20 THEN N=1:NI=1
205 I=22500 +(N-1)*512
206 IF NFLAG=1 THEN NFLAG=0:N=NSAVE
210 STORE=USR0(I)
215 IF N>=20 THEN CLS0:PRINT"THE PICTURE STORAGE SPACE IS NOW FULL. SAVE PICTURES ON TAPE BEFORE DRAWING MORE, OR YOU WILL LOSE YOUR FIRST DRAWINGS."
220 LINE INPUT "PRESS ENTER FOR NEW CHOICE.";AN$
225 GOTO 5
305 N=N+1:IF N>20 THEN N=1
310 I=22500+(N-1)*512:K=22500+20*(512)-1
315 CLS0:PRINT"POSITION TAPE BEFORE PICTURE FILE. PRESS PLAY BUTTON."
320 LINE INPUT"PRESS <ENTER> WHEN READY";R$
325 OPEN "I",#-1,"PICTURES"
330 FOR LD=I TO K
335 IF EOF(-1) THEN LAST=LD-1:LD=K:CLOSE#-1:XT=1:GOTO 350
340 INPUT#-1,A
345 POKE LD,A
350 NEXT LD
355 IF XT<>1 THEN CLOSE#-1:NI=1:LAST=K:PRINT"PICTURE STORAGE AREA IS FULL." ELSE XT=0
360 LINE INPUT"PRESS <ENTER> TO MAKE NEW CHOICE.";AN$
365 N=(LAST-22499)/512:IF N>INT(N) THEN N=INT(N)+1
370 GOTO 5
405 CLS0:PRINT"PRESS<ENTER> TO SEE STORED PICTURES.PRESS ANY KEY TO SEE NEXT PICTURES."
410 LINE INPUT AN$
412 IF NI=1 THEN N1=20 ELSE N1=N
415 FOR D=1 TO N1
420 I=22500+(D-1)*512
425 DI=USR1(I)
430 AN$=INKEY$:IF AN$="" THEN 430
435 NEXT D
440 GOTO 5
505 CLS0:PRINT"REWIND EMPTY TAPE. PRESS PLAY AND RECORD BUTTONS."
510 LINE INPUT"PRESS <ENTER> WHEN READY.";AN$
512 IF NI=1 THEN N1=20 ELSE N1=N
515 EP=22500+(N1*512)-1
525 OPEN "O",#-1,"PICTURES"
530 FOR I=22500 TO EP
535 A=PEEK(I)
540 PRINT#-1,A
545 NEXT I
550 CLOSE #-1
555 GOTO 5
605 INPUT"DO YOU WANT TO TAPE PICTURES BEFORE ENDING";AN$
610 IF LEFT$(AN$,1)="N" THEN END ELSE GOTO 5
705 CLS0:PRINT"PRESS <^> FOR UP,<DOWN ARROW> FOR DOWN,<BACKSPACE> FOR LEFT,<RIGHT ARROW> FOR RIGHT,<A> FOR DOWN AND LEFT,<S>FOR DOWN AND RIGHT,<W> FOR UP AND RIGHT,<Q> FOR UP AND LEFT.THEN PRESS A KEY(0-9) TO TELL HOW FAR TO DRAW.;"
708 PRINT"PRESS D TO MOVE, NOT DRAW, THEN PRESS D AGAIN WHEN YOU WANT TO DRAW."
709 LINE INPUT"PRESS ENTER FOR MORE DRAWING DIRECTIONS.";AN$
710 CLS0:PRINT"   PRESS THE LETTERS H-P FOR DIFFERENT COLORED LINES. H IS INVISIBLE.PRESS <CLEAR> TO CLEAR SCREEN. TO SAVE A PICTURE PRESS <@>."
711 PRINT"TO ADD TEXT PRESS<T>.ENTER WHEN DONE AND YOU WILL BE ABLE TO DRAW AGAIN."
715 LINE INPUT"PRESS ENTER TO START DRAWING.";AN$:CLS0
716 CC=4:X=32:Y=15:DR=0
717 GOTO 50
720 CLS0:INPUT"CHOOSE SHIFT DIRECTION 1)RIGHT;2)LEFT;3)UP;4)DOWN. TYPE NUMBER AND ENTER.";SD:CLS0:IF SD<1 OR SD>4 THEN 720.
725 I=22500 +(N-1)*512
730 ON SD GOSUB 750,755,760,765
740 CLS0: PRINT"YOU MUST VIEW SCREENS TO SEE RESULT OF SHIFT. PRESS <ENTER> FOR NEW CHOICE.":LINE INPUT AN$
745 GOTO 5
750 FOR T=1 TO 4:A=USR2(I):ST=USR0(I):NEXT T:RETURN
755 FOR T=1 TO 4:A=USR3(I):ST=USR0(I):NEXT T:RETURN
760 FOR T=1 TO 2:A=USR4(I):ST=USR0(I):NEXT T:RETURN
765 FOR T=1 TO 2:A=USR5(I):ST=USR0(I):NEXT T:RETURN
800 REM DRAWFILE C.SOLLA CARROCK 2/87
805 CLEAR 200,22200
815 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E
820 DATA &H04,&H00,&HA6,&HA0,&HA7,&H80,&H10
825 DATA &H8C,&H06,&H00,&H26,&HF6,&H39
830 FOR J=22450 TO 22469
835 READ DA:POKE J,DA
840 NEXT J
845 DEFUSR0=22450
850 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E
855 DATA &H04,&H00,&HA6,&H80,&HA7,&HA0,&H10
860 DATA &H8C,&H06,&H00,&H26,&HF6,&H39
865 FOR J=22470 TO 22489
870 READ DA:POKE J,DA
875 NEXT J
880 DEFUSR1=22470
885 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E,&H04,&H00,&H86,&H10
886 DATA &HB7,&H57,&HB1,&HC6,&H1F,&HA6,&H80,&HA7,&H21,&H31,&H21
887 DATA &H5A,&H26,&HF7,&HF6,&H57,&HB1,&HA6,&H80,&HA7,&HA8,&HE1
888 DATA &H31,&H21,&H5A,&HF7,&H57,&HB1,&H26,&HE5,&H39
889 FOR I=22201 TO 22242
890 READ DA
895 POKE I,DA
900 NEXT I
905 DEFUSR2=22201
910 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E,&H04,&H00,&H86,&H10,&HB7,&H57,&HB1,&HC6
915 DATA &H1F,&HA6,&H80,&HA7,&HA8,&H1F,&H31,&H21,&HA6,&H80,&HA7,&H3F,&H31,&H21,&H5A
920 DATA &H26,&HF7,&HF6,&H57,&HB1,&H5A,&HF7,&H57,&HB1,&H26,&HE5,&H39
925 FOR I=22243 TO 22284
930 READ DA:POKE I,DA
935 NEXT I
940 DEFUSR3=22243
945 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E,&H04,&H00,&HC6,&H20,&HA6,&H80
950 DATA &HA7,&HA9,&H01,&HE0,&H31,&H21,&H5A,&H26,&HF5,&HA6,&H80
955 DATA &HA7,&HA8,&HE0,&H31,&H21,&H10,&H8C,&H06,&H00,&H25,&HF3,&H39
960 FOR I=22285 TO 22320
965 READ DA:POKE I,DA
970 NEXT I
975 DEFUSR4=22285
985 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E,&H04,&H00,&HA6,&H80,&HA7,&HA8,&H20
986 DATA &H31,&H21,&H10,&H8C,&H05,&HE0,&H25,&HF3,&HC6,&H20,&HA6,&H80
990 DATA &HA7,&HA9,&HFE,&H20,&H31,&H21,&H5A,&H26,&HF5,&H39
995 FOR I=22321 TO 22356
1000 READ DA:POKE I,DA
1005 NEXT I
1010 DEFUSR5=22321
1015 GOTO 5
1020 CLS0:INPUT"CHOOSE SPEED:1)VERY HIGH;2)HIGH;3)MEDIUM;4)SLOW;5)VERY SLOW.TYPE NUMBER AND ENTER";SPEED:IF SPEED<1 OR SPEED>5 THEN 1020
1025 IF SPEED=2 THEN SPEED=50
1030 IF SPEED=3 THEN SPEED=500
1035 IF SPEED=4 THEN SPEED=1500
1040 IF SPEED=5 THEN SPEED=3000
1045 CLS0:INPUT"DO YOU WANT TO 1)STOP AFTER ONE SHOW, OR 2)KEEP GOING TIL ANY KEY IS PRESSED(TYPE NUMBER AND ENTER)";KP:IF KP<>1 AND KP<>2 THEN 1045
1050 IF NI=1 THEN N1=20 ELSE N1=N
1055 FOR D=1 TO N1
1060 I=22500+(D-1)*512
1065 DI=USR1(I)
1070 FOR TI=1 TO SPEED:NEXT TI
1075 NEXT D
1080 I$=INKEY$:IF I$="" AND KP=2 THEN GOTO 1055
1085 LINE INPUT"PRESS <R> AND <ENTER> TO RETURN FOR NEW CHOICE, OTHERWISE ENTER TO SEE AGAIN.";AN$
1090 IF LEFT$(AN$,1)="R" THEN 5 ELSE 1055
1095 I=22500+(N-1)*512
1100 A=USR1(I)
1105 GOTO 716
1110 PRINT"WHICH PICTURE(1-20) DO YOU WISH TO CHANGE? IF YOU DON'T KNOW,TYPE 99 TO RETURN TO CHOICES.CHOOSE A DIPLAY OF PICTURES(CHOICE 4) TO FIND OUT THEN RETURN HERE."
1115 INPUT N3
1120 IF N3=99 THEN GOTO 5
1125 IF N3<1 OR N3>20 THEN GOTO 1110
1130 NSAVE=N:NFLAG=1:N=N3-1
1135 I=22500+(N3-1)*512
1140 A=USR1(I)
1145 GOTO 716
1150 PRINT@480,"";
1155 T$=INKEY$:IF T$="" THEN GOTO 1155
1160 IF T$=CHR$(13) THEN GOTO 50
1165 PRINT T$;
1170 GOTO 1155
1175 INPUT"NUMBER OF LAST PICTURE YOU WISH STORED";N:IF N<1 OR N>20 THEN 1175
1180 PRINT "THE NEXT SCREEN TO BE DRAWN ON WILL BE NUMBER";:IF N=20 THEN PRINT"1"; ELSE PRINT N+1;
1185 PRINT"ANY PICTURE ALREADY ON THIS SCREEN WILL BE ERASED IF YOU DRAW AGAIN, AND WON'T SHOW WHEN YOU DISPLAY PICTURES."
1190 LINE INPUT"PRESS<ENTER> FOR NEW CHOICE";AN$
1195 GOTO 5
2000 INPUT N:PRINT N:GOTO 2000
