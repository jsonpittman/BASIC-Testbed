15 REM RAINBOW MAGAZINE PRESENTS
20 REM
25 REM *************************
30 REM *  The Font Master !!!  *
35 REM *   By: Eric A. Wolf    *
40 REM * Don't Panic Software  *
45 REM * For a CoCo 3 and 128k *
50 REM *************************
55 REM
65 ONBRK GOTO 710
70 ONERR GOTO 530
75 PALETTE RGB
80 POKE65497,0
85 RESTORE:FORY=0TO15:READ A:PALETTE Y,A:NEXTY
90 DATA 8,56,0,3,3,63,3,0,63,9,0,0,0,0,0,63
95 ATTR 7,0
100 WIDTH 40
105 HBUFF 1,5000
110 HBUFF 2,128
115 CH=65
120 HSCREEN2:HCLS
125 HCOLOR15:HPRINT(21,1),"FONT MASTER":HCOLOR1:HPRINT(19,2),"By: Eric A. Wolf":HCOLOR6:HPRINT(17,3),"Don't Panic Software":HCOLOR2:HPRINT(16,4),"For the CoCo 3 and 128k"
130 HCOLOR1:HPRINT(6,19),"The Current character set:":HPRINT(0,16),"Character:":HPRINT(0,17),"Number   :":HCOLOR15
135  HPRINT(16,6),"Arrows    -  Move Cursor":HPRINT(16,7),"Spacebar  -  Sets a dot":HPRINT(16,8),"Clear key -  Clears dot":HPRINT(16,9),"'X' key   -  Change Char"
140 HPRINT(16,10),"'C' key   -  Clears char":HPRINT(16,11),"'I' key   -  Inverts chr"
145 HPRINT(16,12),"'L' key   -  Loads a set":HPRINT(16,13),"'S' key   -  Saves a set"
150 HPRINT(22,16),"Edit Box"
155 GOSUB250:GOSUB305:GOSUB260:GOSUB280
160 C1=0:C2=0
165 HGET(20+C1*12,20+C2*12)-(20+C1*12+11,20+C2*12+11),1:HCOLOR15:HLINE(20+C1*12,20+C2*12)-(20+C1*12+11,20+C2*12+11),PSET,BF
170 A$=INKEY$:IFA$=""THEN170 ELSE HPUT(20+C1*12,20+C2*12)-(20+C1*12+11,20+C2*12+11),1,PSET
175 IF A$=CHR$(8) THEN C1=C1-1:IF C1<0 THEN C1=0:SOUND100,1
180 IF A$=CHR$(9) THEN C1=C1+1:IF C1>7 THEN C1=7:SOUND100,1
185 IFA$=CHR$(10) THEN C2=C2+1:IF C2>7 THEN C2=7:SOUND100,1
190 IFA$=CHR$(94) THEN C2=C2-1:IF C2<0 THEN C2=0:SOUND100,1
195 IFA$=CHR$(32) THEN HCOLOR1:HPAINT(24+C1*12,24+C2*12),1,15:HSET(151+C1,128+C2,15):PLAY"V31T255L255;CC"
200 IFA$=CHR$(12) THEN HCOLOR2:HPAINT(24+C1*12,24+C2*12),2,15:HSET(151+C1,128+C2,0):PLAY"V31T255L255;CC"
205 IFA$=CHR$(67) THEN SOUND100,1:GOSUB265
210 IFA$=CHR$(73) THEN GOSUB320
215 IFA$=CHR$(88) THEN GOSUB385:GOSUB340:GOSUB250:GOSUB305:GOSUB265:GOSUB280
220 IFA$=CHR$(76) THEN GOSUB385:Z$=A$:GOTO405
225 IFA$=CHR$(83) THEN GOSUB385:Z$=A$:GOTO405
230 GOTO 165
235 '
240 ' PROGRAM SUBROUTINES
245 '
250 HCOLOR0:HLINE(0,168)-(319,192),PSET,BF:HCOLOR15:T=21:FORY=32 TO 128 STEP 40:A$="":FOR X=Y TO Y+39:A$=A$+CHR$(X):NEXTX:HPRINT(0,T),A$:T=T+1:NEXTY
255 RETURN
260 HCOLOR3:HLINE(12,12)-(116,116),PSET,BF:HCOLOR1:HLINE(16,16)-(120,120),PSET,BF
265 HCOLOR0:HLINE(144,124)-(166,140),PSET,BF:HCOLOR1:HLINE(144,124)-(166,140),PSET,B:
270 HCOLOR2:HLINE(20,20)-(116,116),PSET,BF:HCOLOR15:FOR X=0 TO 8:HLINE(20+X*12,20)-(20+X*12,20+8*12),PSET:HLINE(20,20+X*12)-(20+8*12,20+X*12),PSET:NEXTX:RETURN
275 GOTO 275
280 T=24:T1=0:A=CH-32:A=A*8:FOR Y=&HF09D+A TO &HF09D+A+7:P=PEEK(Y):FOR X=7 TO 0 STEP-1:IF P>=INT(2^X) THEN P=P-INT(2^X):HPAINT(24+((7-X)*12),T),1,15:HSET(151+(7-X),128+T1,15)
285 NEXTX:T=T+12:T1=T1+1:NEXTY:HCOLOR0:HLINE(88,16*8)-(112,18*8),PSET,BF:HCOLOR15:HPRINT(10,17),CH:HPRINT(11,16),CHR$(CH)
290 RETURN
295 NEXTX,Y
300 RETURN
305 W2=INT((CH-32)/40):W1=(CH-32)-W2*40
310 HGET(W1*8,168+W2*8)-(W1*8+7,168+W2*8+7),2:HPUT(W1*8,168+W2*8)-(W1*8+7,168+W2*8+7),2,NOT:RETURN
315 HPUT(W1*8,168+W2*8)-(W1*8+7,168+W2*8+7),2,PSET:RETURN
320 SOUND200,1:FORY=0TO7:FORX=0 TO 7
325 IF HPOINT(24+(X*12),24+(Y*12))=1 THEN HPAINT(24+(X*12),24+(Y*12)),2,15:HSET(151+X,128+Y,0) ELSE HPAINT(24+(X*12),24+(Y*12)),1,15:HSET(151+X,128+Y,15)
330 NEXTX,Y:RETURN
335 GOTO335
340 HCOLOR0:HPRINT(6,19),"The Current character set:":HCOLOR1:HPRINT(8,19),"Select new character":SOUND100,1:GOSUB315
345 GOSUB305
350 A$=INKEY$:IFA$=""THEN350 ELSE GOSUB315
355 IF A$=CHR$(94) THEN CH=CH-40:IF CH<32 THEN CH=CH+40
360 IF A$=CHR$(10) THEN CH=CH+40:IF CH>127 THEN CH=CH-40
365 IF A$=CHR$(8) THEN CH=CH-1:IF CH<32 THEN CH=32
370 IF A$=CHR$(9) THEN CH=CH+1:IF CH>128 THEN CH=127
375 IF A$<>CHR$(13) THEN 345 ELSE SOUND100,1:HCOLOR0:HPRINT(8,19),"Select new character":HCOLOR1:HPRINT(6,19),"The Current character set:"
380 RETURN
385 SOUND200,1:A=CH-32:A=A*8:FORY=0TO7:P=0:FORX=0 TO 7
390 IF HPOINT(24+(X*12),24+(Y*12))=1 THEN P=P+(2^(7-X))
395 NEXTX:POKE &HF09D+A+Y,P:NEXTY
400 RETURN
405 IF Z$="L" THEN 410 ELSE IF Z$="S" THEN 580 ELSE RUN
410 HSCREEN0:ATTR 7,0:CLS:LOCATE9,1:ATTR 7,0,U:PRINT"Load a character set:";:ATTR 7,0:LOCATE 1,7:PRINT"Use arrows to select & Press <ENTER>."
415 CR=1
420 ATTR 7,0:LOCATE 14,3:PRINT"From disk":LOCATE 14,4:PRINT"From tape":LOCATE14,5:PRINT"Exit back"
425 IF CR=1 THEN LOCATE 14,3:ATTR 1,5:PRINT"From disk"; ELSE IFCR=2 THEN LOCATE 14,4:ATTR 1,5:PRINT"From tape"; ELSE LOCATE 14,5:ATTR 1,5:PRINT"Exit back";
430 A$=INKEY$:IFA$=""THEN430
435 IFA$=CHR$(13) THEN 450 ELSE IF A$="^" THEN CR=CR-1:IF CR<1 THEN CR=3
440 IF A$=CHR$(10) THEN CR=CR+1:IF CR>3 THEN CR=1
445 GOTO 420
450 ATTR 7,0:IF CR=3 THEN GOTO120
455 ON CR GOTO 460,475
460 POKE65496,0:CLS:PRINT"INSERT "+CHR$(34)+"CHARACTER SET"+CHR$(34)+" DISK IN DRIVE":PRINT"#0 AND PRESS ANY KEY FOR A DIRECTORY."
465 A$=INKEY$:IFA$=""THEN465 ELSE DIR0
470 GOTO 480
475 CLS:PRINT"Prepare the cassette recorder":PRINT"for loading a character set.....":PRINT
480 PRINT:LINEINPUT"Enter Filename: ";A$
485 IF LEN(A$)=0 THEN 495 ELSE IF LEN(A$)>8 AND CR=2 THEN 490 ELSE IF LEN(A$)>14 AND CR=1 THEN 490 ELSE IF LEN(A$)>8 AND INSTR(A$,"/")=0 THEN 490 ELSE 500
490 PRINT"Filename too long !":GOTO480
495 PRINT"Please enter a filename !!":GOTO480
500 IF INSTR(A$,"/")=0 AND LEN(A$)=<8 AND CR=1 THEN A$=A$+"/BIN:0"
505 ON CR GOTO 510,520
510 LOADM A$
515 GOTO 525
520 CLOADM A$
525 PRINT:PRINT"Loaded ....":FORX=1TO1000:NEXTX:POKE65497,0:GOTO120
530 CLS:B$="Unkwown Error... #"+STR$(ERNO)+" In "+STR$(ERLIN)
535 IF ERNO=20 THENB$="I/O ERROR" ELSEIF ERNO=19 THEN B$="Device number error" ELSE IF(ERNO=31 OR ERNO=26)THEN B$="Can't find correct character file" ELSEIF ERNO=23 THEN B$="Input past end of file ..."\
540 IF ERNO=1 THEN B$="Syntax Error in line "+STR$(ERLIN)
545 FORY=1TO2:LOCATE(20-LEN(B$)/2),6:PRINTB$:NEXTY
550 LOCATE 4,13:PRINT"Do you wish to try again (Y/N)"
555 A$=INKEY$:IFA$="Y" THEN GOTO 405 ELSE IFA$="N" THEN 120 ELSE 555
560 IF Z$="L" THEN 455 ELSE 115
565 ATTR7,1:CLS:LOCATE 5,11:PRINT"File not on device specified":GOTO550
570 CLS:LOCATE10,11:PRINT"Input past end of file":GOTO550
575 CLS:PRINT"ATTENTION USER !!!"::PRINT"ERROR #";ERNO;"IN LINE ";:PRINT ERLIN:PRINT:END
580 HSCREEN0:ATTR 7,0:CLS:LOCATE9,1:ATTR 7,0,U:PRINT"Save a character set:";:ATTR 7,0:LOCATE 1,7:PRINT"Use arrows to select & Press <ENTER>."
585 CR=1
590 ATTR 7,0:LOCATE 14,3:PRINT"To disk":LOCATE14,4:PRINT"To tape":LOCATE14,5:PRINT"Exit back"
595 IF CR=1 THEN LOCATE 14,3:ATTR 1,5:PRINT"To disk"; ELSE IFCR=2 THEN LOCATE 14,4:ATTR 1,5:PRINT"To tape"; ELSE LOCATE 14,5:ATTR 1,5:PRINT"Exit back";
600 A$=INKEY$:IFA$=""THEN600
605 IFA$=CHR$(13) THEN 620 ELSE IF A$="^" THEN CR=CR-1:IF CR<1 THEN CR=3
610 IF A$=CHR$(10) THEN CR=CR+1:IF CR>3 THEN CR=1
615 GOTO 590
620 ATTR 7,0:IF CR=3 THEN GOTO120
625 ON CR GOTO 630,640
630 POKE65496,0:CLS:PRINT"Insert disk to save character set upon  into drive #0"
635 GOTO 645
640 POKE65496,0:CLS:PRINT"Prepare cassette to save character set."
645 PRINT:LINEINPUT"Enter Filename: ";A$
650 IF LEN(A$)=0 THEN 660 ELSE IF LEN(A$)>8 AND CR=2 THEN 655 ELSE IF LEN(A$)>14 AND CR=1 THEN 655 ELSE IF LEN(A$)>8 AND INSTR(A$,"/")=0 THEN 655 ELSE 665
655 PRINT"Filename too long !":GOTO645
660 PRINT"Please enter a filename !!":GOTO645
665 IF LEN(A$)=<8 AND INSTR(A$,"/")=0 AND CR=1 THEN A$=A$+"/BIN:0"
670 PRINT:PRINT"Press any key to save..."
675 B$=INKEY$:IFB$=""THEN675
680 ON CR GOTO 685,695
685 VERIFYON:SAVEM A$,&HF09D,&HF49C,&HF09D
690 GOTO 700
695 CSAVEM A$,&HF09D,&HF49C,&HF09D
700 PRINT:PRINT"Saved ...":FORX=1TO1000:NEXTX:POKE65497,0:GOTO120
705 REM ** END OF PROGRAM **
710 HSCREEN 0:STOP
