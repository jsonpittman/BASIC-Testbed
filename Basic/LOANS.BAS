10 'LOANS
11 'WRITTEN BY STEVE KNAPIX
12 'COPYRIGHT FEBRUARY 1991
13 'BY FALSOFT, INC.
14 'RAINBOW MAGAZINE
20 REM  0.793 K
100 WIDTH32 : CLS
110 CLS:PRINT"  ===> LOAN CALCULATIONS"
120 PRINT:PRINT"WHAT IS THE AMOUNT OF THE LOAN"
130 INPUT"IN WHOLE DOLLARS";A
140 IF A=0 THEN 110
150 A=ABS(A) : A=INT(A)
160 IF A>250000 THEN 410
170 PRINT:INPUT"RATE OF INTEREST";R
180 PRINT:INPUT"NUMBER OF YEARS ";Y
190 N=Y*12 : R=ABS(R) : M=R/1200
200 GOSUB 360 : W=1
210 FOR J = 1 TO N : W=W*(1+M) : NEXT J
220 P=(A*M*W)/(W-1)
230 P=INT(P*100+.99)/100
240 K=N*P : L=K-A
250 K=INT(K) : L=INT(L)
260 SOUND 200,01 : CLS : PRINT
270 PRINT"AMT. OF LOAN : $"A
280 PRINT
290 PRINT"RATE OF INTEREST :"R"%"
300 PRINT
310 PRINT"NUMBER OF YEARS  :"Y
320 PRINT : PRINT : PRINT
330 PRINT"MONTHLY PAYMENTS : $"P
340 PRINT"INTEREST PAID    : $"L
350 PRINT"DEFERRED PRICE   : $"K
355 END
360 CLS : PRINT A;"FOR";N;
370 PRINT"MO. AT";R;"%"
380 PRINT:PRINT">> STAND BY - NOW COMPUTING"
390 SOUND 165,10
400 RETURN
410 SOUND 100,5 : PRINT:PRINT"  NOTE : T O O   L A R G E  !"
420 END
