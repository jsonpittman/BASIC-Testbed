1 'BYTES
2 'BY KEIRAN KENNY
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLS
20 PRINT@42,"**BYTES**"
30 PRINT@193,"BY KEIRAN KENNY, SYDNEY, 1989."
40 GOSUB340
50 CLS:PRINT@96,"A BYTE IS LIKE A ROW OF EIGHT   SPOTLIGHTS. EACH LIGHT IS A BIT.WHEN ALL ARE ON THEY REPRESENT  255 AND THE BINARY NUMBER....";
60 X=294:C=256:GOSUB260:GOSUB340
70 CLS
80 PRINT@128,"WHEN ALL LIGHTS ARE OFF THEY    REPRESENT ZERO AND THE BINARY   NUMBER....";:X=294:C=0:GOSUB260:GOSUB340
90 CLS:PRINT@32,"IN VARIOUS COMBINATIONS OF ON   AND OFF THEY REPRESENT THE CHAR-ACTERS ON YOUR KEYBOARD AND MANYOTHER CHARACTERS. FOR INSTANCE THE LETTER 'A' IS DECIMAL 65 ANDBINARY....";X=294:C=66:GOSUB260:GOSUB340
100 CLS:PRINT@32,"INPUT A DECIMAL NUMBER IN THE RANGE 0-255";:INPUTT
110 IFT>255THENCLS:PRINT@32,"MAXIMUM 255. TRY AGAIN.":FORD=1TO1000:NEXT:GOTO100
120 T$=RIGHT$(STR$(T),LEN(STR$(T))-1):D$="CHR$("+T$+")/CHR$(&H"+HEX$(T)+")"
130 IFT<33ORT=143THEN PRINT@128,D$+" NOT VISIBLE"ELSE PRINT@131,D$+" = "+CHR$(T)
140 PRINT@196,"BINARY "+T$+" =";:X=294:IFT=0THENC=T:GOSUB260:ELSEC=T+1:GOSUB260
150 GOSUB330
160 IFK$="Y"ORK$="y"THEN100
170 IFK$="N"ORK$="n"THENCLS:GOTO190
180 GOTO150
190 PRINT"PRESS ANY KEY ON YOUR KEYBOARD EXCEPT <BREAK>. USE THE <SHIFT> KEY AND LOWER CASE TOO.":GOSUB350
200 T$=RIGHT$(STR$(ASC(K$)),LEN(STR$(ASC(K$)))-1):IFASC(K$)<33ORASC(K$)>122THEN PRINT@131,"KEY";ELSE PRINT@131,K$;
210 PRINT" = CHR$("+T$+")/CHR$(&H"+HEX$(ASC(K$))+")"
220 PRINT@195,"BINARY "+T$+" =":X=294:C=ASC(K$)+1:GOSUB260
230 PRINT@354,"NOW ANOTHER KEY OR <BREAK> TO   END PROGRAM."
240 GOSUB350
250 IFK$<>CHR$(3)THEN200ELSEEND
260 FORP=7TO0STEP-1
270 IFC>=(2^P)THEN PRINT@X,CHR$(159);:PRINT@X-33,1;
280 IFC<(2^P)THENPRINT@X,CHR$(128);:PRINT@X-33,0;:GOTO300
290 C=C-(2^P)
300 X=X+2
310 NEXTP
320 RETURN
330 PRINT@424,"ANOTHER? Y/N":GOTO350
340 PRINT@424,"PRESS ANY KEY";
350 K$=INKEY$:IFK$=""THEN350ELSERETURN
