1 REM ************************
2 REM *   DECODING SKILLS    *
3 REM *   A DECODING GAME    *
4 REM *   BY FRED B.SCERBO   *
5 REM *    60 HARDING AVE.   *
6 REM * NORTH ADAMS,MA 01247 *
7 REM *  COPYRIGHT (C) 1988  *
8 REM ************************
9 CLEAR3000:CLS0:PRINTSTRING$(32,188);STRING$(32,204);:FORI=1TO256:READA:PRINTCHR$(A+128);:NEXT
10 PRINTSTRING$(32,195);STRING$(32,179);
15 DATA29,28,28,26,29,28,29,21,28,28,26,30,28,28,26,29,28,28,29,20,29,28,21,31,18,16,29,24,30,28,28,29
20 DATA21,,,26,21,19,18,21,,,,26,,,26,21,,,21,,21,,16,26,29,18,21,,26,,19,19
25 DATA21,,,26,21,,17,21,,,18,26,,,26,21,,,21,,21,,16,26,16,29,23,,26,,16,21
30 DATA28,28,28,24,28,28,28,20,28,28,24,28,28,28,24,28,28,28,28,20,28,28,20,28,16,96,28,24,28,28,28,28
35 DATA110,108,108,108,108,109,100,110,,97,110,,108,110,104,100,109,108,,,100,109,108,,96,96,110,108,108,108,108,109
40 DATA107,99,99,99,99,99,,107,99,110,96,,,106,,,101,,,,,101,,,,,107,99,99,99,99,99
45 DATA98,,,,,101,,106,,109,98,,96,106,,,101,,,,98,101,,,,98,98,,,,,101
50 DATA108,108,108,108,108,108,100,104,,,108,,108,108,104,100,108,108,108,108,104,108,108,108,108,104,108,108,108,108,108,108
55 PRINT@419,"     BY FRED B.SCERBO     ";
60 PRINT@453,"  COPYRIGHT (C) 1988  ";
65 PRINT@483," <I>NSTRUCTIONS OR <Q>UIZ ";
70 X$=INKEY$:XX=RND(-TIMER):IFX$="I"THEN75ELSEIFX$="Q"THEN135ELSE70
75 CLS:PRINT@34,"THIS PROGRAM IS DESIGNED TO     HELP YOU REVIEW IMPORTANT       INFORMATION IN A FUN AND        EXCITING WAY.":PRINT@194,"YOU WILL BE GIVEN A SENTENCE    WHICH HAS THE SPACES IN IT      JUMBLED AROUND."
80 PRINT@322,"YOU MUST RE-TYPE THE SENTENCE   SO THAT IT IS CORRECT & MAKES   SENSE."
85 PRINT@450,"PRESS <ENTER> TO CONTINUE.";
90 IFINKEY$<>CHR$(13)THEN90
95 CLS:PRINT@34,"HERE IS AN EXAMPLE OF SUCH      A SENTENCE:":PRINT@130,"T HEC OWWA SST ANDING UP."
100 PRINT@194,"YOU WOULD DECODE THIS TO READ:":PRINT@258,"THE COW WAS STANDING UP."
105 PRINT@322,"PRESS <ENTER> WHEN YOU THINK    THAT YOU HAVE THE SENTENCE      RIGHT."
110 PRINT@450,"PRESS <ENTER> TO CONTINUE.";
115 IFINKEY$<>CHR$(13)THEN115
120 CLS:PRINT@34,"IF YOU DO NOT GET THE ANSWER    CORRECT, YOU WILL BE GIVEN A    CHANCE TO TRY IT AGAIN. YOU     MAY CHECK THE SCORECARD BY      PRESSING THE <@> KEY. YOU MAY   THEN RETURN TO THE PROGRAM BY   PRESSING <C> TO CONTINUE"
125 PRINT@290,"BE SURE TO TYPE SLOWLY!":PRINT@354,"PRESS <ENTER> TO BEGIN."
130 IFINKEY$<>CHR$(13)THEN130
135 CLS0
140 DIMAO(50),A$(50),B$(50),NP(50)
145 CLS0:GOTO180
150 D=0
155 IFLEN(J$)<=27THEN170
160 FORT=27TO0STEP-1:IFMID$(J$,T,1)=" "THEN175
165 NEXT
170 W$=J$+C$:F=LEN(W$):PRINT@M+D,W$;STRING$(X-F,32):RETURN
175 W$=LEFT$(J$,T):C$="":W$=W$+C$:F=LEN(W$):PRINT@M+D,W$;STRING$(X-F,32):C$=D$:J$=S$+RIGHT$(J$,(LEN(J$))-T):D=D+32:GOTO155
180 FORJ=1TO50:READ A$(J):IFA$(J)="END"THEN190
185 NEXTJ
190 CLS0:J=J-1
195 FORI=1TOJ
200 AO(I)=RND(J)
205 IFNP(AO(I))=1THEN 200
210 NP(AO(I))=1:NEXTI
215 FORP=1TOJ
220 CLS:PRINT@232,"PLEASE STAND BY":A$="":B$="":R$="":C$="":D$="":S$="   "
225 Q$=A$(AO(P)):K=LEN(Q$)
230 FORN=1TOK:IFMID$(Q$,N,1)=" "THEN240
235 A$=A$+MID$(Q$,N,1)
240 NEXTN
245 K=LEN(Q$):B$=LEFT$(A$,3):FORN=4TOK
250 R=RND(12):IFR>4THEN260
255 B$=B$+" "
260 B$=B$+MID$(A$,N,1):NEXTN
265 CLS:PRINT@35,"DECODE STATEMENT NO.";P:M=96::J$="   "+B$:X=31:GOSUB150
270 M=289:PRINT@289,"=>"+CHR$(128);
275 X=29:C$=CHR$(128):D$=CHR$(128):S$="  "
280 Y$=INKEY$:IFY$="@"THEN345ELSEIFY$=CHR$(13)THEN300ELSEIFY$=CHR$(8)THEN290ELSEIFY$=""THEN280
285 R$=R$+Y$:J$="  "+R$:PRINT@291,"";:GOSUB150:PRINT:GOTO280
290 IFLEN(R$)<1THEN280
295 M=289:L=LEN(R$):R$=LEFT$(R$,L-1):PRINT@M,"";:J$="  "+R$:GOSUB150:PRINT:GOTO280
300 IFR$=Q$THEN305ELSE315
305 PRINT@480,"  YOU ARE ABSOLUTELY CORRECT!! ";
310 CR=CR+1:GOTO335
315 PRINT@480,"  SORRY, THAT IS NOT CORRECT !";
320 IR=IR+1
325 X$=INKEY$:IFX$=CHR$(13)THEN330ELSEIFX$="@"THEN345ELSE325
330 PRINT@480,STRING$(30,32);:GOTO280
335 X$=INKEY$:IFX$=CHR$(13)THEN340ELSEIFX$="@"THEN345ELSE335
340 NEXTP
345 CLS:PRINT@128,"";
350 L=CR+IR:IF L=0 THEN L=1
355 PRINT"    NUMBER CORRECT = "CR
360 PRINT
365 PRINT"    NUMBER WRONG   = "IR
370 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/L);"%"
375 PRINT:PRINT"    ANOTHER TRY (Y/N/C)";
380 W$=INKEY$:IFW$=""THEN380
385 IF W$="Y" THEN RUN
390 IF W$="N" THEN CLS:END
395 IF W$="C" THEN 220
400 GOTO380
990 REM ENTER DATA AT LINE 1000
1000 DATA "FOOD IS AN ESSENTIAL PART OF EVERYONE'S DAILY LIFE."
1010 DATA "WATER IS AN EVEN MORE IMPORTANT PART OF A PERSON'S DAILY INTAKE."
1020 DATA "YOU COULD GO FOR MORE DAYS WITHOUT FOOD THAN YOU COULD WITHOUT WATER."
1030 DATA "HUMANS ALSO NEED A CERTAIN AMOUNT OF SLEEP EACH DAY."
1040 DATA "THE HUMAN BODY MUST BREAK DOWN FOOD BEFORE IT CAN BE ABSORBED."
1050 DATA "MANY PEOPLE BELIEVE THAT ALL-NATURAL FOODS ARE BETTER FOR YOU."
1060 DATA "SOME PEOPLE GET VERY UPSET WITH THE CHEMICALS THEY FIND IN THEIR FOOD."
1070 DATA "GOOD EATING HABITS USUALLY LEAD TO GOOD HEALTH."
1080 DATA "TOO MUCH SUGAR IN YOUR DIET IS NOT GOOD."
1090 DATA "FIBER IS AN IMPORTANT PART OF YOUR DIET, TOO."
5000 DATA END
