1 'WORD FINDER
2 'BY BRAD RENFRO
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 WIDTH 80:ATTR2,4:CLS:RESTORE
10 ON ERR GOTO 465
15 ON BRK GOTO 450
20 'WORDS IN THE EXAMPLE PUZZLE ARE AS FOLLOWED:1.WORDFINDER 2.RAINBOW 3.COCO 4.RGB 5.CMP
25 'THIS PROGRAM HELPS YOU FIND WORDS IN WORD SEARCH PUZZLE. THIS PROGRAM WILL HOLD A PUZZLE WITH 21 LINES AND 70 LETTERS IN EACH LINE. 
26 'LINES 30-99 SHOULD GIVE YOU ENOUGH ROOM TO PLACE EACH LINE IN DATA STATEMENTS. IF YOU DON'T HAVE ENOUGH ROOM YOU CAN USE THE 'RENUM' COMMAND TO MAKE MORE ROOM.
30 DATA WORDFINDERRD,ETEADETPFPRV,COCOIZXCBNMA,RGBFDNCMPUTR,DERDCDBCERTY,DQWPOCVOPUYR,QAZWSXEDWUIP
100 CLS
105 LOCATE 0,5
110 PRINTTAB(34)"Word Finder":PRINT
115 PRINT TAB(33)"By Brad Renfro":PRINT
120 PRINT TAB(31)"Copyright (C) 1989":PRINT:PRINT:PRINT
125 'CHECK FOR NUMBER OF LINES IN PUZZLE AND CHECK EACH LINE TO MAKE SURE IT HAS THE SAME NUMBER OF LETTERS
130 L=1:ON ERR GOTO 155
135 READ A$:IF L=1 THEN NO=LEN(A$) ELSE NP=LEN(A$)
140 IF L>1 AND NO<>NP THEN GOTO 455
145 IF NO>70 THEN 470 ELSE IF NP>70 THEN 470
150 L=L+1:GOTO 135
155 RESTORE:L=L-1:IF L<=0 OR L>21 THEN 460
160 ON ERR GOTO 465
165 PRINT TAB(26)"Number of Lines in Puzzle:"L
170 LOCATE 26,21:PRINT"Press any key to continue...";:EXEC44539
175 DIM A$(L+1)
180 FOR I=1TOL
185 READ A$(I):NEXT
190 CLS
195 GOSUB 435
200 PRINT
205 PRINT"Press <Break> to End":PRINT"What Word will I find?";:LINEINPUTW$
210 IF W$="" THEN 190
215 POKE 65497,0
220 S=LEN(W$):F$=LEFT$(W$,1)
225 T$=MID$(W$,2,1)
230 FOR Y=1 TO L
235 FOR X=1 TO LEN(A$(Y))
240 G$=MID$(A$(Y),X,1)
245 IF G$=F$ THEN 275
250 NEXT X:NEXTY
255 POKE 65496,0
260 PRINT"WORD NOT IN PUZZLE"
265 LOCATE26,23:PRINT"Press any key to continue...";:ATTR2,4:EXEC44539:CLS
270 GOTO195
275 SX=X:SY=Y
280 FOR BX=SX-1 TO SX+1
285 FOR BY=SY-1 TO SY+1
290 IF BX<1 OR BX>LEN(A$(BY)) THEN 310
295 IF BY<1 OR BY>L THEN 310
300 E$=MID$(A$(BY),BX,1)
305 IF E$=T$ THEN 320
310 NEXT BY:NEXT BX
315 GOTO 250
320 DX=BX-X:DY=BY-Y
325 CX=X:CY=Y
330 FOR I=1 TO S
335 IF CX<1 OR CX>LEN(A$(CY)) THEN 310
340 IF CY<1 OR CY>L THEN 310
345 IF MID$(W$,I,1)<>MID$(A$(CY),CX,1) THEN 310
350 CX=CX+DX:CY=CY+DY
355 NEXT I
360 POKE65496,0:REM FIND LETTERS CORRESPODING TO WORD FOUND AND MAKE THEM LOWER CASE AND BLINK BY DETERMINING IT'S ASCII CODE AND ADDING 128 TO THE ATTRIBUTE BYTE
365 CLS
370 GOSUB 435
375 SX=SX-1:SX=SX*2
380 P=442208+SX+(SY*160)
385 DX=DX*2
390 FOR I=1 TO S
395 SE$=MID$(W$,I,1):SE=ASC(SE$)+32:LPOKE P,SE:LPOKE P+1,LPEEK(P+1)+128
400 P=P+DX+(DY*160)
405 NEXT I
410 POKE 65496,0
415 LOCATE 26,23:PRINT"Press any key to continue...";
420 EXEC44539
425 GOTO 190
430 END
435 FOR I=1 TO L
440 PRINT A$(I):NEXTI
445 RETURN
450 POKE 65496,0:END
455 CLS:LOCATE 0,5:PRINT:LOCATE 29,5:ATTR2,4,B:PRINT"EACH LINE SHOULD HAVE":PRINT:PRINT TAB(32)"THE SAME NUMBER";:ATTR2,4:END
460 CLS:LOCATE 0,5:PRINT:LOCATE 28,5:ATTR2,4,B:PRINT"TOO MANY LINES IN PUZZLE":PRINT:PRINT TAB(25)"PLEASE TAKE SOME LINES OFF....";:ATTR2,4:END
465 CLS:LOCATE 0,5:PRINT:LOCATE 27,5:ATTR2,4,B:PRINT"ERROR DETECTED IN PROGRAM":PRINT:PRINT TAB(27)"PLEASE CORRECT IF POSSIBLE..":ATTR2,4:END
470 CLS:LOCATE 0,5:PRINT:LOCATE 27,5:ATTR2,4,B:PRINT"YOU HAVE TOO MANY LETTERS":PRINT:PRINT TAB(32)"IN EACH LINE...":ATTR2,4:END
