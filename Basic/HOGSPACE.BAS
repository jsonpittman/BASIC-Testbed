1 GOTO 630
2 SAVE"HOG":STOP
10 REM**************************
20 REM START DRAW LETTERS SUB
30 REM**************************
40 PLAY"T255;O2":FOR LP=1 TO LEN(A$):LI$=MID$(A$,LP,1):LI=ASC(LI$)-64  
50 LX=LX+12:IF LX>246 THEN LX=0:LY=LY+12
60 IF LI=-19 THEN LI=28
70 IF LI=-18 THEN LI=29
80 IF LI>-17 AND LI<-6 THEN LINE(LX,LY)-(LX,LY),PRESET:GOSUB 420
90 IF LI<0 THEN LI=27
100 DRAW"BM"+STR$(LX)+","+STR$(LY):ON LI GOSUB 130,140,150,160,170,180,190,200,210,220,230,240,250,260,270,280,290,300,310,320,330,340,350,360,370,380,390,400,410
110 PLAY"G"
120 NEXT LP:RETURN
130 DRAW"BM+0,+0;ND8R8D4NL8D4":RETURN 'A
140 DRAW"BM+0,+0;ND8R6D4NL6R2D4L8":RETURN 'B
150 DRAW"BM+0,+0;NR8D8R8":RETURN  'C
160 DRAW"BM+0,+0;ND8R6F2D4G2L6":RETURN 'D
170 DRAW"BM+0,+0;NR8D4NR6D4R8":RETURN 'E
180 DRAW"BM+0,+0;NR8D4NR6D4":RETURN 'F
190 DRAW"BM+0,+0;NR8D8R8U4L3":RETURN 'G
200 DRAW"BM+0,+0;D4ND4R8U4D8":RETURN 'H
210 DRAW"BM+4,+0;L2R4L2D8L2R4":RETURN 'I
220 DRAW"BM+0,+0;BR6D8L4U2":RETURN 'J
230 DRAW"BM+0,+0;D8U4R2NE4NF4":RETURN 'K
240 DRAW"BM+0,+0;D8R8":RETURN 'L
250 DRAW"BM+0,+0;ND8F4E4D8":RETURN 'M
260 DRAW"BM+0,+0;ND8NF8BR8D8":RETURN 'N
270 DRAW"BM+0,+0;D8R8U8L8":RETURN 'O
280 DRAW"BM+0,+0;ND8R8D4L8":RETURN 'P
290 DRAW"BM+0,+0;R8D8NH3NF2L8U8":RETURN 'Q
300 DRAW"BM+0,+0;R8D4L4NF4L4D4U8":RETURN 'R
310 DRAW"BM+0,+0;R8D1U1L8D4R8D4L8U1":RETURN 'S
320 DRAW"BM+0,+0;R4ND8R4":RETURN 'T
330 DRAW"BM+0,+0;D8R8U8":RETURN 'U
340 DRAW"BM+0,+0;D4F4E4U4":RETURN 'V
350 DRAW"BM+0,+0;D6F2E2NU4F2E2U6":RETURN 'W
360 DRAW"BM+0,+0;F8BL8E8":RETURN 'X
370 DRAW"BM+0,+0;F4ND4E4":RETURN 'Y
380 DRAW"BM+0,+0;R8G8R8":RETURN 'Z
390 RETURN 'SPACEBAR
400 DRAW"BM+1,+4;R6":RETURN 'DASH
410 DRAW"BM+2,+6;D2R2U2L2":RETURN ' PERIOD
420 LI=LI+17:ON LI GOSUB 430,440,450,460,470,480,490,500,510,520:LI=LI-17:RETURN 'THE NUMBERS
430 DRAW"BM+0,+0;R8D8L8U8R8G8":RETURN 'ZERO
440 DRAW"BM+4,+0;D8N":RETURN '1
450 DRAW"BM+0,+0;ND2R8D4L8D4R8":RETURN '2
460 DRAW"BM+0,+0;R8D4NL6D4L8":RETURN '3
470 DRAW"D4R8U4D8":RETURN '4
480 DRAW"NR6D4R6D4L6U1":RETURN '5
490 DRAW"NR8D8R8U4L8":RETURN '6
500 DRAW"BD8BR8U8L6D2":RETURN '7
510 DRAW"BR2R6D4L6NU4D4R6U4":RETURN '8
520 DRAW"BR2ND4R6D4NL6D4":RETURN '9
530 REM************************
540 REM END OF DRAW LETTERS SUB
550 REM************************
600 REM************************
610 REM START DRAW PROGRAM
620 REM************************
630 CLS:PRINT"THIS PROGRAM WILL WRITE TO DISK!YOU SHOULD HAVE A BLANK AND     FORMATTED DISKETTE OR A DISK YOUHAVE ALREADY RUN THIS PROGRAM ONIN PLACE. DO YOU STILL WANT TO  CONTINUE?"
640 GOSUB 19999
650 IF X$<>"Y" THEN END
660 SAVE"HOG"
670 PCLEAR4
680 PCLS:PMODE3,1
690 COLOR 3,1:SCREEN 1,0:PCLS
700 REM*************************
710 REM CHECK FOR CORRECT COLOR
720 REM*************************
730 S=PEEK(&HBC)*256:PCLS2:SAVEM"CHECK",S,S+1024*3-1,&HA000:PMODE4,1:COLOR1,0:SCREEN1,1:PCLS:LOADM"CHECK":KILL"CHECK/BIN"
740 A$="IF SCREEN IS BLUE    THEN HIT -B- KEY. IF SCREEN IS ORANGE     THEN HIT -O- KEY."
750 LX=-1:LY=100
760 GOSUB 40
770 X$=INKEY$:IFX$="B" THEN C2=3:C3=2 ELSE IF X$="O" THEN C2=2:C3=3 ELSE 770
800 REM************************
810 REM START DRAWING PICTURES
820 REM************************
830 PCLS:PMODE3,1:SCREEN1,0:COLORC2,1
840 CLS:PRINT@97,"DRAWING PICTURES-WAIT PLEASE."
850 GOSUB 900
860 GOTO970
870 REM************************
880 REM DRAW EMPTY SPACE SUB
890 REM************************
900 LINE(0,0)-(256,95),PSET,BF
910 LINE(25,15)-(225,80),PRESET,BF
920 FOR X=1 TO 100:PSET(RND(200)+25,RND(65)+15,0):NEXTX
930 RETURN
940 REM*************************
950 REM SAVING EMPTY SPACE
960 REM*************************
970 S=PEEK(&HBC)*256
980 SAVEM"EMPTY",S,S+1024*3-1,&HA000
990 REM************************
1000 REM DRAW AND SAVE SUN
1010 REM************************
1020 PCLS
1030 GOSUB 900
1040 CIRCLE(175,48),40,C2,.8
1050 PAINT(175,48),0,C2
1060 SAVEM"SUN",S,S+1024*3-1,&HA000
1070 REM************************
1080 REM DRAW AND SAVE STATION
1090 REM************************
1100 PCLS
1110 GOSUB 900
1120 CIRCLE(125,50),20,C3
1130 CIRCLE(125,50),30,C3
1140 PAINT(125,75),C3,C3
1150 LINE(105,45)-(145,55),PSET,BF
1160 LINE(120,30)-(130,70),PSET,BF
1170 CIRCLE(125,50),10,C3
1180 PAINT(125,50),C3,C3
1190 SAVEM"STATION",S,S+1024*3-1,&HA000
1200 REM************************
1210 REM DRAW ASTEROIDS
1220 REM************************
1230 PCLS
1240 GOSUB 900
1250 CIRCLE(35,60),8,C2
1260 PAINT(35,60),C2,C2
1270 CIRCLE(50,30),10,C2
1280 PAINT(50,30),0,C2
1290 CIRCLE(80,50),4,C3
1300 PAINT(80,50),C3,C3
1310 CIRCLE(125,50),25,C3
1320 PAINT(125,50),C3,C3
1330 CIRCLE(150,70),7,C2
1340 PAINT(150,70),C2,C2
1350 CIRCLE(200,35),15,C2
1360 PAINT(200,35),0,C2
1370 SAVEM"ASTEROID",S,S+1024*3-1,&HA000
1380 REM************************
1390 REM DRAW BOSS-HOG
1400 REM************************
1410 PCLS
1420 GOSUB 900
1430 LINE(95,60)-(155,80),PSET,B
1440 PAINT(126,61),C3,C2
1450 LINE(125,60)-(125,80),PSET
1460 LINE(100,65)-(115,70),PRESET,BF
1470 CIRCLE(125,45),30,C3,.7
1480 PAINT(125,45),C2,C3
1490 CIRCLE(125,50),10,1,.8
1500 CIRCLE(121,49),1,1
1510 CIRCLE(128,49),1,1
1520 CIRCLE(106,25),6,C2
1530 PAINT(108,25),C3,C2
1540 CIRCLE(142,25),6,C3
1550 PAINT(142,25),C2,C3
1560 CIRCLE(116,35),4,1
1570 PAINT(116,35),0,1
1580 CIRCLE(134,35),4,1
1590 PAINT(134,35),0,1
1600 SAVEM"BOSSHOG",S,S+1024*3-1,&HA000
1610 REM***********************
1620 REM SAVE BLANK TEXT SCRN
1630 REM***********************
1640 PCLS4
1650 SAVEM"CLS",S+1024*3,S+1024*6-1,&HA000
1660 REM************************
1670 REM DRAW PLANET OPENMIND
1680 REM************************
1690 PCLS 
1700 GOSUB 900
1710 CIRCLE(100,47),30,C2,.8
1720 PAINT(100,47),C3,C2
1730 CIRCLE(95,40),10,0,.4
1740 PAINT(95,40),0,0
1750 CIRCLE(105,57),10,0,.4
1760 PAINT(105,57),0,0
1770 SAVEM"OPENMIND",S,S+1024*3-1,&HA000
1780 REM************************
1790 REM DRAWING PLANET HIPPO
1800 REM************************
1810 PCLS
1820 GOSUB 900
1830 CIRCLE(150,50),40,C2,.7
1840 PAINT(150,50),C2,C2
1850 LINE(105,70)-(195,70),PRESET
1860 LINE(105,60)-(195,60),PRESET
1870 LINE(105,50)-(195,50),PRESET
1880 LINE(105,40)-(195,40),PRESET
1890 LINE(105,30)-(195,30),PRESET
1900 PAINT(150,29),C3,1
1910 PAINT(150,45),C3,1
1920 PAINT(150,65),C3,1
1930 SAVEM"HIPPO",S,S+1024*3-1,&HA000
1940 REM************************
1950 REM DRAW SWEDISH PLANET
1960 REM************************
1970 PCLS
1980 GOSUB 900
1990 CIRCLE(100,50),30,C2,.8
2000 PAINT(100,50),0,C2
2010 DRAW"BM80,55;U8R4D4U4R4D8"
2020 DRAW"BM+4,+0;U8R6D8L6"
2030 DRAW"BM+10,+0;U8R6D4L4R6D4"
2040 DRAW"BM+4,+0;U8D4R6U4D4R2D4"
2050 SAVEM"SWEDISH",S,S+1024*3-1,&HA000
2060 REM************************
2070 REM DRAWING DOG PLANET
2080 REM************************
2090 PCLS:GOSUB 900
2100 CIRCLE(90,45),25,3,.8
2110 PAINT(90,45),2,3
2120 SAVEM"DOGS",S,S+1024*3-1,&HA000
2130 REM************************
2140 REM DRAWING HOG KISS
2150 REM************************
2160 PCLS:GOSUB900
2170 CIRCLE(125,50),45,C2,.6
2180 CIRCLE(125,50),45,C2,.3
2190 PAINT(125,50),0,C2
2200 CIRCLE(125,50),80,C2,.7
2210 PAINT(125,35),C3,C2
2220 PAINT(125,65),C3,C2
2230 PAINT(60,50),C2,C2
2240 LX=90:LY=45:A$="KISS"
2250 GOSUB 40
2260 SAVEM"KISS",S,S+1024*3-1,&HA000
2270 REM***********************
2280 REM DRAW GRAVE FOR DEAD
2290 REM***********************
2300 PCLS
2310 LINE(0,0)-(256,95),PSET,BF
2320 OX=0:OY=35:FOR X=1 TO 256 STEP 8:Y=35+RND(20)-10:LINE(OX,OY)-(X,Y),PRESET:OX=X:OY=Y:NEXT X:LINE(OX,OY)-(256,35),PRESET
2330 PAINT(1,1),1,1
2340 FOR X=1 TO 100:PSET(RND(256),RND(40),0):NEXTX
2350 PAINT(5,90),C3,1
2360 LINE(85,55)-(168,80),PRESET,B
2370 PAINT(100,70),C2,1
2380 LINE(90,60)-(160,75),PRESET,B
2390 LX=87:LY=64:DRAW"C1":A$="R I P":GOSUB40
2400 DRAW"BM85,80;F50;R83;H50"
2410 PAINT(100,90),1,1
2420 S=PEEK(&HBC)*256:SAVEM"DEAD",S,S+1024*3-1,&HA000
2430 REM***********************
2440 REM END OF PICTURE DRAWING
2450 REM***********************
3000 SOUND1,4:CLS:PRINT"DO YOU NEED INSTRUCTIONS?":GOSUB 19999:IF X$="Y" THEN 3040 ELSE 4000
3010 REM***********************
3020 REM INSTRUCTION SEQUENCE
3030 REM***********************
3040 PCLEAR 4
3050 PCLS:PMODE4,1
3060 COLOR 0,1:SCREEN 1,1:PCLS
3070 REM***********************
3080 REM SHOW EMPTY VIEWSCREEN
3090 REM***********************
3100 LOADM"EMPTY"
3110 LX=-1:LY=100
3120 A$="YOU ARE BEING CALLED ON THE INTERSPACE    TELCOMMER...         HIT -ENTER- TO       ANSWER."
3130 PLAY"O1;T100;ABABABABABABAB"
3140 GOSUB 40
3150 GOSUB 19999
3160 GOSUB 3520
3170 REM***********************
3180 REM BOSS HOG COMES ONSCRN
3190 REM***********************
3200 LOADM"BOSSHOG"
3210 A$="THIS IS YOUR         BOSS-HOG. MISS HOGGY HAS BEEN HOGNAPPED   BY THE INFAMOUS HOGS IN SPACE...          HIT -ENTER- FOR MORE"
3220 GOSUB 40
3230 GOSUB 19999
3240 A$="YOU ARE BUT A FROG-  MAN FROM PLANET      SWAMP BUT YOU ARE    ALL WE HAVE. YOU ARE TO GO TO...          HIT -ENTER- FOR MORE"
3250 GOSUB 3520
3260 GOSUB 40
3270 GOSUB 19999
3280 GOSUB 3520
3290 A$="THE CO-ORDINATES I   AM NOW SENDING TO    YOUR NAV-COMPUTER.   HIT -ENTER- FOR MORE"
3300 GOSUB 40
3310 GOSUB 19999
3320 GOSUB 3520
3330 A$="YOU MOVE AROUND THE  STAR SYSTEM BY INPUT -TING DATA ON YOUR   NAV-COMPUTER.  FOR   EXAMPLE-             HIT -ENTER- FOR MORE"
3340 GOSUB 40
3350 GOSUB 19999
3360 GOSUB 3520
3370 A$="MOVE- 3.4 WOULD MOVE YOU TO A LOCATION    RIGHT 3 AND DOWN 4   FROM THE UPPER LEFT  OF THE SEARCH GRID.  THE GRID COVERS 0.0  TO 8.8 AND YOU START AT 4.4 ON IT.-ENTER-"
3380 GOSUB 40
3390 GOSUB 19999
3400 GOSUB 3520
3410 A$="THE GRAVITONIC       DETECTOR AT THE      RIGHT OF YOUR SCREEN WILL FLASH WHITE TO  SHOW NEARBY GRAVITY  SOURCES...           HIT -ENTER- FOR MORE"
3420 GOSUB 40
3430 GOSUB 19999
3440 GOSUB 3520
3450 A$="REMEMBER- MISS HOGGY IS DEPENDING ON YOU. GOOD LUCK-KERVIT.    HIT -ENTER- TO       ACTIVATE WARP..."
3460 GOSUB 40
3470 GOSUB 19999
3480 GOTO4000
3490 REM***********************
3500 REM CLS SCREEN BOTTOM SUB
3510 REM***********************
3520 LOADM"CLS":LX=-1:LY=100:RETURN
4000 REM************************
4010 REM SET UP AND START GAME
4020 REM************************
4030 PCLEAR 4
4040 PCLS:PMODE4,1
4050 COLOR 0,1:SCREEN 1,1:PCLS
4060 DIMSP$(8,8)
4070 SP$(4,4)="SUN"
4080 REM************************
4090 REM FILL ARRAY WITH PLANETS
4100 REM************************
4110 FORZ=1 TO 4
4120 X=RND(9)-1
4130 Y=RND(9)-1
4140 IF SP$(X,Y)<>"" THEN 4120
4150 READ P$
4160 SP$(X,Y)=P$
4170 NEXT Z
4180 DATA OPENMIND,HIPPO,SWEDISH,DOGS
4190 REM************************
4200 REM FILL ARRAY W/ ASTEROIDS
4210 REM************************
4220 FOR Z=1 TO 8
4230 X=RND(9)-1
4240 Y=RND(9)-1
4250 IF SP$(X,Y)<>"" THEN 4230
4260 SP$(X,Y)="ASTEROID"
4270 NEXT Z
4280 REM************************
4290 REM PUT HOGULA-1 IN ARRAY
4300 REM************************
4310 X=RND(9)-1
4320 Y=RND(9)-1
4330 IF SP$(X,Y)<>"" THEN 4310
4340 SP$(X,Y)="STATION"
4350 FUEL=20
4360 X=4:Y=4
4370 LX=-1:LY=100
5000 REM************************
5010 REM TOP OF MAIN LOOP
5020 REM************************
5030 PCLS:IF SP$(X,Y)="" THEN LOADM"EMPTY":GOTO5050
5040 LOADM SP$(X,Y) 'GET PIC.
5050 A$="FUEL-"+STR$(FUEL)+"  LOC-"+STR$(X)+" ."+STR$(Y)
5060 LOADM"CLS"
5070 GOSUB 40
5075 GOSUB 6030
5080 REM***********************
5090 REM DRAW LOCATOR GRID
5100 REM***********************
5110 LINE(230,40)-(250,55),PSET,BF
5120 LINE(229,40)-(251,55),PRESET,B
5130 LINE(230,45)-(250,45),PRESET
5140 LINE(230,50)-(250,50),PRESET
5150 LINE(237,41)-(237,54),PRESET
5160 LINE(243,41)-(243,54),PRESET
5170 REM***********************
5180 REM PAINT IN LOCATED ITEMS
5190 REM***********************
5200 IF X=8 THEN 5220
5210 IF SP$(X+1,Y)<>"" THEN PAINT(244,46),1,1
5220 IF Y=8 THEN 5240
5230 IF SP$(X,Y+1)<>"" THEN PAINT(238,51),1,1
5240 IF X=0 THEN 5260
5250 IF SP$(X-1,Y)<>"" THEN PAINT(231,46),1,1
5260 IF Y=0 THEN 5280
5270 IF SP$(X,Y-1)<>"" THEN PAINT(238,41),1,1
5280 IF X=8 OR Y=8 THEN 5300
5290 IF SP$(X+1,Y+1)<>"" THEN PAINT(244,51),1,1
5300 IF X=8 OR Y=0 THEN 5320
5310 IF SP$(X+1,Y-1)<>"" THEN PAINT(244,41),1,1
5320 IF X=0 OR Y=8 THEN 5340
5330 IF SP$(X-1,Y+1)<>"" THEN PAINT(231,51),1,1
5340 IF X=0 OR Y=0 THEN 5360
5350 IF SP$(X-1,Y-1)<>"" THEN PAINT(231,41),1,1
5360 IF SP$(X,Y)<>"" THEN PAINT(238,46),1,1
5370 IF SP$(X,Y)<>"" THEN PAINT(238,46),1,1
5380 IF SP$(X,Y)="STATION" THEN 6170 
5390 IF FUEL=0 THEN 7030
5400 REM***********************
5410 REM GET PLAYER MOVE
5420 REM***********************
5430 A$="ENTER MOVE-"
5440 LX=-1:LY=180
5450 GOSUB 40
5460 M$=""
5470 FOR X=1 TO 3
5480 GOSUB 19999
5490 IF INSTR(1,".123456780",X$)=0 THEN SOUND 1,5:GOTO5480
5500 IF X<>2 AND X$="." THEN SOUND 1,5:GOTO5480
5510 IF X=2 AND X$<>"." THEN SOUND 1,5:GOTO5480
5520 A$=X$
5530 GOSUB 40
5540 M$=M$+X$
5550 NEXT X
5560 REM***********************
5570 REM INTERPRET MOVE
5580 REM***********************
5590 LOADM"CLS"  'CLEAR TEXT
5600 Y=VAL(RIGHT$(M$,1))
5610 X=VAL(LEFT$(M$,1))
5620 A$="WARPING OUT..."
5630 LY=100:LX=-1
5640 FUEL=FUEL-1
5650 GOSUB 40
5660 LX=-1:LY=100
5670 GOTO5030
5680 REM***********************
5690 REM BOTTOM OF MAIN LOOP
5700 REM***********************
5710 REM
6000 REM***********************
6010 REM PRINT MESSAGE ROUTINE
6020 REM***********************
6030 IF SP$(X,Y)="SUN" THEN A$="YOU SEE THE HOGULA-1 SYSTEM SUN."
6040 IF SP$(X,Y)="ASTEROID" THEN A$="YOU SEE AN ASTEROID  BELT."
6050 IF SP$(X,Y)="" THEN A$="YOU ARE IN EMPTY     SPACE."
6060 IF SP$(X,Y)="STATION" THEN A$="YOU HAVE FOUND SPACE STATION HOGULA-ONE!"
6070 IF SP$(X,Y)="OPENMIND" THEN A$="YOU SEE THE PLANET   OF OPEN MINDED STEW- ARDESSES..."
6080 IF SP$(X,Y)="HIPPO" THEN A$="YOU SEE THE PLANET   OF HIPPO-BALLERINAS! -HEAVY-"
6090 IF SP$(X,Y)="DOGS" THEN A$="YOU SEE THE DOG      PLANET. THEY ARE     QUITE -SIRIUS- ABOUT IT."
6100 IF SP$(X,Y)="SWEDISH" THEN A$="YOU SEE THE PLANET   OF SWEDISH CHEFS.    THEY ARE SERVING     FROGLEGS TODAY...    MORK-MORK-MORK..."
6110 LX=-1:LY=115
6120 GOSUB 40
6130 RETURN
6140 REM***********************
6150 REM MISS HOGGY IS FOUND!
6160 REM***********************
6170 LOADM"CLS"
6180 A$="YOU HEAR A DISTRESS  CALL FROM THE SPACE  STATION. IT IS MISS  HOGGY..."
6190 LX=-1:LY=100
6200 GOSUB 40
6210 FOR X=1 TO 1000:NEXTX
6220 LOADM"KISS"
6230 FOR X=1 TO 100
6240 IF X/2=INT(X/2) THEN SCREEN 1,1 ELSE SCREEN 1,0
6250 PLAY"T255;O5;ABC"
6260 NEXTX
6270 CLS:PRINT"THE GRATITUDE OF MISS HOGGY IS  TRULY OVERWHELMING......             PLAY AGAIN";
6280 INPUT X$:IF LEFT$(X$,1)="Y" THEN 4030 ELSE END
7000 REM***********************
7010 REM RUN OUT OF FUEL-DIE!
7020 REM***********************
7030 LOADM"CLS"
7040 A$="YOU HAVE RUN OUT OF  FUEL..."
7050 LX=-1:LY=100
7060 GOSUB 40
7070 FOR X=1 TO 1000:NEXTX
7080 LOADM"DEAD"
7090 A$="ANOTHER FROG BITES   THE DUST..."
7100 LOADM"CLS"
7110 LX=-1:LY=100
7120 GOSUB 40
7130 FOR X=1TO5000:NEXTX
7140 CLS:INPUT"WANT TO TRY AGAIN??";X$:IF LEFT$(X$,1)="Y" THEN 4030 ELSE END
19999 X$=INKEY$:IFX$="" THEN 19999 ELSE RETURN
