0 ' COPYRIGHT 1989  FALSOFT,INC
10 '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
20 '$ LIL OLE PERSON'S GOLDPILE $
30 '$   FOR 16K COCO WITH ECB   $
40 '$ (C)1983 BY: F. KALINOWSKI $
50 '$     16 N. ALDER DRIVE     $
60 '$     ORLANDO, FL 32807     $
70 '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
80 '
90 '
100 '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
110 '$ FIRST APPEARED IN RAINBOW $
120 '$   SEPTEMBER 1984, PAGE 51 $
130 '$ MODIFIED FOR THE COCO III $
140 '$      BY: DUKE NORRIS      $
150 '$       P.O. BOX 241,       $
160 '$   SHELBYVILLE, IN 46176   $
170 '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
180 TROFF:CLS0:ON BRK GOTO 450:CLEAR300:PALETTERGB:PALETTE2,1
190 POKE150,18 'POKE 2400 BAUD RATE FOR PRINTER
200 GOTO500
210 GT=0:FORX=0TO10:GT=GT+AA(X):NEXT:RETURN
220 IFPS=0THENRETURNELSEPRINT#-2,STRING$((Y*9)+32,C):RETURN
230 FORX=0TOY-1:IE(X)=DR(X)*AA(X):AA(X)=AA(X)+IE(X):NEXT
240 DI=0:FORX=0TO10:DI=DI+IE(X):NEXT
250 TI=TI+DI:GOSUB210:IFTI>I THEN I=I+U
260 IFPS=0THEN310
270 X=0:IFFL=0THENPRINT#-2,"Start";:FL=1:X=0:ELSEPRINT#-2,USING"###  ";D;
280 PRINT#-2,USING"#####.## ";AA(X);
290 X=X+1:IF X<Y THEN280
300 PRINT#-2,USING" ##.#### ";DI;:PRINT#-2,USING"#####.##  ";TI;:PRINT#-2,USING"#####.##";GT
310 LOCATE0,04:Q$="Day ###":GOSUB930:PRINTTAB(WA)"";:PRINTUSING Q$;D:LOCATE0,06:Q$="Grand Total    $$#,###,###.##  ":GOSUB930:PRINTTAB(WA)"";:PRINTUSING Q$;GT
320 LOCATE0,08:Q$="Daily Interest $$#,###,###.####":GOSUB930:PRINTTAB(WA)"";:PRINTUSING Q$;DI:LOCATE0,10:Q$="Total Interest $$#,###,###.##  ":GOSUB930:PRINTTAB(WA)"";:PRINTUSING Q$;TI
330 D=D+1:IFD<DD+1THEN230ELSED=D-1
340 IFPT<8THENPT=8
350 C=61:GOSUB220
360 LOCATE0,13:ATTR5,2:PRINTSTRING$(80,32):LOCATE0,14:Q$="This way, Lil' Ole "+A$:ATTR5,2:GOSUB920:Q$="can earn: $$#,###.## Total Interest":GOSUB930:LOCATE0,15:PRINTTAB(WA)"";:PRINTUSING Q$;TI
370 IFPS=0THEN400
380 PRINT#-2,TAB(PT-8)"this way Lil' Ole "A$" can earn ";:PRINT#-2,USING"$$#,###.##";TI
390 PRINT#-2,TAB(PT-8)"and end up with ";:PRINT#-2,USING"$$,###.##";GT;:PRINT#-2," in just"D"days."
400 LOCATE0,16:Q$="and end up with at least: $$###,###.##":GOSUB930:PRINTTAB(WA)"";:PRINTUSING Q$;GT:LOCATE0,17:PRINTSTRING$(79,32):LOCATE0,17:Q$="in just"+STR$(D)+" days.":GOSUB920
410 C=36:GOSUB220:IFPS=0THEN430
420 FORX=1TO4:PRINT#-2:NEXT:GOSUB1060
430 PRINT:GOSUB950:LOCATE0,22:Q$="Want To Run More Accounts (Y/N) ":ATTR4,5:GOSUB920:SOUND1,1
440 S$=INKEY$:IFS$="Y"THEN460ELSEIFS$<>"N"THEN440
450 GOSUB970:GOSUB950:CLS5:END
460 FORX=0TO10:AA(X)=0:AN(X)=0:IE(X)=0:IR(X)=0:NEXT
470 GOSUB970:ATTR3,2:Q$="Do You Want A Printout (Y/N) ?":LOCATE 0,22::GOSUB920:SOUND1,1:S$=""
480 GOSUB950:S$=INKEY$:IFS$="Y"THEN PS=1:GOSUB970:GOSUB910 ELSE IF S$=""THEN480 ELSE GOSUB960:PS=0
490 D=0:TI=0::DI=0:IE=0:FL=0:C=22:W=0:CLS5:GOTO590
500 HSCREEN2:HCOLOR2,1:HCLS6:HW=40:FORBN=0TO10:HPRINT(0,BN),STRING$(40,"$"):NEXTBN:Q$="Lil' Ole":Q=12:GOSUB940
510 Q$="Person's":Q=14:GOSUB940
520 HCOLOR1,2:Q$="GOLDPILE":Q=16:GOSUB940:HCOLOR2,1
530 GOSUB970:HCOLOR5,6:Q$="Do You Want A Printout (Y/N) ?":Q=18:GOSUB940:SOUND1,1
540 S$=INKEY$:IFS$="N"THEN PS=0:GOSUB960:GOTO550ELSEIFS$="Y"THEN PS=1:GOSUB970ELSE540
550 HCOLOR1,6:Q$="Screen Width 4=40  8=80  (4/8) ?":SOUND1,1:Q=20:GOSUB940:SOUND1,1
560 S$=INKEY$:IFS$="4"THEN HS=40 ELSE IF S$="8"THEN HS=80 ELSE GOTO560
570 HCOLOR4,6:Q$="PRESS <SPACEBAR> TO START.":Q=22:GOSUB940:SOUND2,1
580 F=RND(7):IF F=6THEN580ELSE S$=INKEY$:HCOLOR F,7:HPRINT(RND(80)-1,RND(11)-1),"$":IF S$<>" "THEN580 ELSE HW=HS
590 WIDTHHW:HSCREEN0:CLS5:ATTR4,1:Q$="Lil' Ole Person's GOLDPILE":LOCATE0,0:GOSUB920:IF HW=80 THEN Q$=STRING$(78,36)ELSE Q$=STRING$(38,36)
600 ATTR3,3:LOCATE0,1:GOSUB920:GOSUB950
610 IFPS=0THEN630
620 LOCATE0,3:Q$="What's The Start Date (MM/DD/YY)":GOSUB920:INPUT" ";B$: IF B$=""THENSOUND1,1:CLS5:GOTO590
630 LOCATE0,5:Q$="What's The Account Owner's Name":GOSUB920:INPUT" ";A$:IF A$=""THEN SOUND1,1:GOTO630
640 LOCATE0,8:Q$="How Many Accounts For Processing":GOSUB920
650 INPUT" ";Y:IFY<1ORY>11THENSOUND1,1:LOCATE0,8:PRINTSTRING$(160," "):GOTO640ELSE IF PS=1THEN GOSUB910
660 IF PS=1 THEN IF Y<6THEN GOSUB1030 ELSE IF Y>5 AND Y<8 THEN GOSUB1040 ELSE IF Y>7 THEN GOSUB1050
670 LOCATE0,11:Q$="How Many Compounding Days":GOSUB920:INPUT" ";DD$:DD=VAL(DD$):IF DD=0 OR DD>3650 THEN LOCATE0,11:PRINTSTRING$(160,32):SOUND1,1:GOTO670
680 LL=15:FORX=1TOY:LOCATE0,LL:PRINTSTRING$(160," "):IF HW=80THENLOCATE0,LL+3:PRINTSTRING$(160," ")
690 LOCATE0,LL:Q$="Account"+STR$(X)+" Starting Amount ($$.$$)":GOSUB920
700 INPUT" ";AA(X-1):IF AA(X-1)=0THEN SOUND1,1:GOTO690 ELSE LL=LL+3
710 LOCATE0,LL:Q$="Account"+STR$(X)+" Interest Rate (00.0000)":GOSUB920
720 INPUT" ";IR(X-1):IF IR(X-1)=0THEN SOUND1,1:GOTO710ELSE IR(X-1)=IR(X-1)*.01
730 LL=LL-3:NEXT:CLS5
740 ATTR4,1:LOCATE0,0:Q$="Lil' Ole Person's GOLDPILE":GOSUB920:ATTR3,3:LOCATE0,1:Q$="(WATCH IT GROW!)":GOSUB920
750 IF HW=80THEN Q$=STRING$(78,36)ELSE Q$=STRING$(38,36)
760 ATTR7,1:GOSUB920:GOSUB950
770 IFPS=0THEN880
780 PT=INT((Y*9)+6)/2
790 PRINT#-2,TAB(PT-1)"Lil' Ole Person's GOLDPILE"
800 PRINT#-2,TAB(PT-1)"for "A$" on "B$"."
810 C=36:GOSUB220:PRINT#-2," No.";:X=1:FORT=6TOY*9STEP9
820 PRINT#-2,TAB(T)"Accnt"X;:X=X+1:NEXT
830 PRINT#-2,TAB(T)"Today's";TAB(T+9)"Accumltd";TAB(T+19)"Grand"
840 PRINT#-2,"Days";:X=0:FORT=5TOY*9STEP9
850 PRINT#-2,TAB(T)IR(X)"%";:X=X+1:NEXT
860 PRINT#-2,TAB(T+1)"Interest Interest";TAB(T+20)"Total"
870 C=61:GOSUB220
880 FORX=0TOY-1:DR(X)=IR(X)/365:NEXT:GOSUB210
890 U=INT(GT/4400):IFU<1THENU=1
900 I=U:GOTO260
910 LOCATE0,22:IF(PEEK(&HFF22)AND1)THEN HCOLOR4,6:Q=22:Q$="PRINTER NOT READY":GOSUB920:GOTO910 ELSE GOSUB1020:LOCATE0,22:PRINTSTRING$(78," ");:RETURN
920 IFLEN(Q$)> HW THENRETURNELSEPRINTTAB((HW/2)-(LEN(Q$)/2));Q$:RETURN                          'CENTERIT
930 IFLEN(Q$)> HW THENRETURNELSE WA=((HW/2)-(LEN(Q$)/2)):RETURN
940 IFLEN(Q$)> HW THENRETURNELSE QQ=(HW/2)-(LEN(Q$)/2):HPRINT(QQ,Q),Q$:RETURN
950 ATTR0,4:RETURN
960 POKE65497,0:RETURN 'SPEED-UP
970 POKE65496,0:RETURN 'SLOW-DOWN
980 '
990 'ALL PRINT CODES ARE FOR A  DMP-132
1000 'CHANGE TO RETURNS IF NOT        WANTED OR CHANGE THE CHR$       CODES
1010 '
1020 PRINT#-2,CHR$(12):RETURN    'SKIP TO TOP-OF-FORM
1030 PRINT#-2,CHR$(27);CHR$(18):RETURN                           'CORRESPONDENCE-10
1040 PRINT#-2,CHR$(27);CHR$(29):RETURN                           'CORRESPONDENCE-12
1050 PRINT#-2,CHR$(27);CHR$(77):RETURN                           'MICROFONT
1060 PRINT#-2,CHR$(7):RETURN     'SOUND PRINTER BELL END-OF-PRINT OPERATION
