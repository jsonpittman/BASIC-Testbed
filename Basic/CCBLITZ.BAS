10 REM"EDUCATIONAL FOOTBALL QUIZ"
20 REM"STEVE BLYN,COMPUTER ISLAND SOFTWARE,STATEN ISLAND,NY,1987
30 Z=20
40 DIM A$(Z),B$(Z)
50 FOR Y=1 TO Z:READ A$(Y),B$(Y):NEXT Y
60 A=303:R=RND(-TIMER)
70 CLS0
80 PRINT@0,STRING$(32,239);
90 PRINT@8,"FOOTBALL  QUIZ";
100 REM SET UP THE FOOTBALL FIELS
110 FOR T=1216 TO 1408 STEP 32:POKE T,159:NEXT T
120 FOR T=1246 TO 1438 STEP 32:POKE T,159:NEXT T
130 FOR T=1216 TO 1246:POKE T,147:NEXT T
140 FOR T=1408 TO 1438:POKE T,156:NEXT T
150 FOR T=1251 TO 1277 STEP 3:POKE T,149:NEXT T
160 FOR T=1283 TO 1308 STEP 3:POKE T,149:NEXT T
170 FOR T=1315 TO 1340 STEP 3:POKE T,149:NEXT T
180 FOR T=1347 TO 1372 STEP 3:POKE T,149:NEXT T
190 FOR T=1379 TO 1404 STEP 3:POKE T,149:NEXT T
200 FOR T=1279 TO 1407 STEP 32:POKE T,207:NEXT T
210 PRINT@160,"   10 20 30 40 50 40 30 20 10"
220 PRINT@A,"*";
230 PRINT@64,"":PRINT@96,""
240 REM ASK A QUESTION
250 PRINT@64,"":PRINT@96,""
260 R= RND(Z)
270 PRINT@64,A$(R)
280 PRINT@96,"? ";
290 LINE INPUT AN$
300 IF AN$=B$(R) THEN PLAY"L100CEGG":A=A+3:PRINT@428,"CORRECT";
310 IF AN$<>B$(R) THEN PLAY "L10C":A=A-3:PRINT@428,B$(R);
320 EN$=INKEY$
330 IF EN$=CHR$(13) THEN 340 ELSE 320
340 PRINT@416,STRING$(32,128);
350 FOR T=1315 TO 1340 STEP 3:POKE T,149:NEXT T
360 PRINT@A,"*";
370 REM CHECK FOR END OF GAME
380 IF A=288 THEN PRINT@456,"SORRY,GAME OVER";
390 IF A=318 THEN PRINT@425,"TOUCHDOWN !!!";:PLAY"L150CEGGFEDCCC"
400 IF A=288 OR A=318 THEN 410 ELSE 240
410 PRINT@489,"PRESS g OR e ";
420 EN$=INKEY$
430 IF EN$="G" THEN RUN ELSE IF EN$="E" THEN END ELSE 420
440 DATA ALLOWS INSERTIONS OF COMMENTS,REM,PUT BACK ALL OF TTHE READ DATA,RESTORE,EXECUTES A PROGRAM,RUN,TURNS ON THE PROGRAM TRACER,TRON
450 DATA LOADS A MACHINE LANGUAGE TAPE,CLOADM,CLEARS THE SCREEN,CLS,TRANSFER CONTROL TO M/L PROGRAM,EXEC
460 DATA PRINT THE AMOUNT OF FREE MEMORY,PRINTMEM,TELL CONTENTS OF MEMORY LOCATION,PEEK,INPUT DATA FROM A CASSETTE,INPUT#-1
470 DATA CALLS A SUBROUTINE,GOSUB,JUMPS TO A LINE NUMBER,GOTO,ASSIGNS VALUE TO A VARIABLE,LET
480 DATA LISTS PROGRAM TO A PRINTER,LLIST,TURNS CASSETTE ON,MOTORON,ERASES EVERYTHING IN MEMORY,NEW,RESERVES GRAPHIC PAGES,PCLEAR
490 DATA PUTS VALUES INTO LOCATIONS,POKE,SELECTS DEGREE OF RESOLUTION,PMODE,PRINTS ITEMS ON A PRINTER,PRINT#-2
