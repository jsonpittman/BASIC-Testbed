10 POKE 32544+54,255:CLS
20 PRINT@108,"CODES"
30 PRINT@168,"1-NUMBER CODES"
40 PRINT@232,"2-NAMES"
50 PRINT@296,"3-PLACES"
60 PRINT@360,"4-ACCOUNTS"
70 PRINT@424,"5-TAXINFO"
80 INPUT NM
90 IF NM=1 THEN NM$="NUMBER"
100 IF NM=2 THEN NM$="NAMES"
110 IF NM=3 THEN NM$="PLACES"
120 IF NM=4 THEN NM$="ACCOUNTS"
130 IF NM=5 THEN CLS:Y1=PEEK(157)*256+PEEK(158):IF Y1=32544 THEN140 ELSE 150 ELSE 160
140 PRINT@11,"L-O-A-D":PRINT:INPUT "ENTER YEAR (LAST 2 DIGITS)";F2$:F1$="TAXINFO"+CHR$(47)+F2$:LOAD F1$,R
150 LOAD "TAX",R
160 IF NM<1 OR NM>5 THEN 80
170 CLS
180 PRINT@168,"1-ENTER NEW INFO
190 PRINT@232,"2-LIST ENTRIES
200 PRINT@296,"3-MODIFY
210 INPUT N
220 CLS:ON N GOTO 230,340,450
230 OPEN"D",#1,NM$,32
240 FIELD #1,10 AS AB$, 22 AS EN$
250 RN=LOF(1)+1
260 INPUT "CODE FOR NEW ENTRY              (6 CHAR MAX)";A$
270 INPUT "NEW ENTRY                       (22 CHAR MAX)";B$
280 LSET AB$=A$
290 LSET EN$=B$
300 PUT #1,RN
310 INPUT "AGAIN";YN$:IFYN$="Y" THEN 250 ELSE 320
320 CLOSE#1
330 GOTO 10
340 OPEN"D",#1,NM$,32:RN=0
350 FIELD #1,10 AS A$, 22 AS B$
360 RN=RN+1
370 GET#1,RN
380 PRINT"RECORD NUMBER ";RN
390 PRINT A$;B$
400 IF INT(RN/5)=RN/5 THEN V$=INKEY$:IF V$="" THEN 400
410 IF LOF(1)<>RN THEN 360
420 CLOSE#1
430 V$=INKEY$:IF V$="" THEN 430
440 GOTO 10
450 OPEN"D",#1,NM$,32
460 FIELD #1, 10 AS AB$, 22 AS EN$
470 INPUT "RECORD  #";RN
480 GET#1,RN
490 PRINT"RECORD NUMBER ";RN
500 PRINT AB$;EN$
510 INPUT "MODIFIED CODE                   (6 CHAR MAX)";A$
520 INPUT "MODIFIED ENTRY                  (22 CHAR MAX)";B$
530 LSET AB$=A$
540 LSET EN$=B$
550 PUT #1,RN
560 PRINT"AGAIN?"
570 INPUT YN$:IF YN$="Y" THEN 470
580 CLOSE#1
590 GOTO 10
