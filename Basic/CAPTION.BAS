0 ' COPYRIGHT 1989 FALSOFT,INC
10 '**************************
20 '** CAPTIVATING CAPTIONS **
30 '**  COPYRIGHT (C) 1989  **
40 '**  BY KEVIN REICHERT   **
50 '**************************
60 '
70 '*****************
80 '**   SET-UP    **
90 '*****************
100 ON BRK GOTO 110: GOTO 120
110 WIDTH 40:ATTR 2,1:POKE 65496,0:PRINT"PROGRAM TERMINATED":END
120 GOSUB 2290 '-=<SPEED UP POKE>=-'
130 GOSUB 1460 '-=<TITLE PAGE>=-'
140 LOCATE 10,9:PRINT"ONE MOMENT PLEASE";:LOCATE 1,22
150 CLEAR 6000:DIM X$(6,37),X(6,37),C$(30),K$(30),A(30),S$(30),OC$(30)
155 POKE 150,1 '-=<9600 BAUD>=-'
160 KE$="This is an example of what your caption might look like. Alter the characters per line by entering various values."
170 R=-1:KK=0:YY=5
180 X$=STRING$(16,32)+"CHOICES"+STRING$(14,32):GOSUB 260
190 X$=STRING$(6,32)+"(1-36)=CHARACTERS PER LINE"+STRING$(5,32):GOSUB 260
200 X$=STRING$(9,32)+"(1)=SMALLEST WIDTH"+STRING$(10,32):GOSUB 260
210 X$=STRING$(9,32)+"(P)=SEND TO PRINTER"+STRING$(9,32):GOSUB 260
220 X$=STRING$(9,32)+"(T)=TYPE NEW CAPTION"+STRING$(8,32):GOSUB 260
230 X$=STRING$(9,32)+"(Q)=QUIT PROGRAM"+STRING$(12,32):GOSUB 260
240 Y(1)=5
250 GOTO 360
260 R=R*-1:KK=KK+1:YY=YY+1
270 FOR T = 1 TO 37
280 IF R=1 THEN X(KK,T)=38-T:X$(KK,T)=LEFT$(X$,T)
290 IF R=-1THEN X(KK,T)=1:X$(KK,T)=RIGHT$(X$,T)
300 Y(KK)=YY
310 NEXT T
320 RETURN
330 '*****************
340 '** GET CAPTION **
350 '*****************
360 LOCATE 10,1:PRINT"INPUT ENTIRE CAPTION"
370 LOCATE 39,1:ATTR5,5:PRINT STRING$(41," ");
380 LOCATE 0,3:ATTR 1,1:PRINT STRING$(239," ");:LOCATE 0,9:ATTR 5,5:PRINT STRING$(39," ");
390 LOCATE 6,14:ATTR 2,1:PRINT"PRESS <ENTER> TO SEE SAMPLE";:LOCATE 19,16:PRINT"OR";:LOCATE 9,18:PRINT"TYPE YOUR OWN CAPTION.";:LOCATE 0,3
400 LINE INPUT C$
410 IF C$="" THEN C$=KE$
420 GOSUB 1300
430 IF LEN(C$)<35 THEN L=LEN(C$)+2:GOSUB 1130 ELSE L=36: GOSUB 1130
440 GOSUB 550
450 TB=19-INT(LEN(S$(1))/2)-1:L=LEN(S$(1)):IF TB<0 THEN TB=0
460 FOR T = 0 TO F+2:LOCATE 0,T:ATTR 0,0:PRINTSTRING$(39," ");:NEXT T
470 FOR T = 0 TO F+1:LOCATE TB,T:ATTR 6,6:PRINTSTRING$(L+2," ");
480 NEXT T
490 LOCATE TB+1,1:ATTR 2,1
500 IF F<5 THEN RN=1 ELSE RN=2
510 ON RN GOTO 680,770
520 '*****************
530 '** CENTER TEXT **
540 '*****************
550 KS=INT(L/2+.5)
560 FOR I=1 TO F
570 A(I)=KS-INT(LEN(C$(I))/2)
580 IF INT(LEN(C$(I))/2) <> LEN(C$(I))/2 THEN C$(I)=C$(I)+" ":GOTO 570
590 S$(I)=" "+STRING$(A(I)," ")+C$(I)+STRING$(A(I)," ")
600 C$(I)=CHR$(179)+" "+STRING$(A(I)," ")+C$(I)+STRING$(A(I)," ")+" "+CHR$(179)
610 NEXT I
620 LOCATE 10,10:PRINT STRING$(24,32);:LOCATE 1,22
630 RETURN
640 '*******************
650 '** PRINT SCREEN  **
660 '** LEFT TO RIGHT **
670 '*******************
680 FOR S=1 TO LEN(S$(1))
690 FOR T=1 TO F
700 LOCATE TB+S,T
710 PRINT MID$(S$(T),S,1);
720 NEXT T,S:GOTO 870
730 '******************
740 '** PRINT SCREEN **
750 '** SIDE TO SIDE **
760 '******************
770 RR=1:J=LEN(S$(1))
780 FOR T=1 TO F: RR=RR*-1
790 IF RR=1 THEN FOR S=1 TO J
800 IF RR=-1 THEN FOR S=J TO 1 STEP -2
810 LOCATE TB+S,T
820 IF RR=1 THEN PRINT MID$(S$(T),S,1); ELSE PRINT MID$(S$(T),S,J-S+1);
830 NEXT S,T
840 '****************
850 '**    MENU    **
860 '****************
870 LOCATE 0,F+3
880 ATTR 5,5:PRINT STRING$(40," ");:LOCATE 17,F+5:ATTR 2,1
890 FOR S=1 TO 6
900 FOR T=1 TO 37
910 LOCATE X(S,T),F+Y(S)
920 PRINT X$(S,T);
930 NEXT T,S
940 P=0:LOCATE 39,F+12:ATTR 5,5:PRINT" ";
950 LOCATE 20,F+12:ATTR 2,1:PRINT"   ";
960 LOCATE 20,F+12:ATTR 2,1
970 LINE INPUT P$
980 IF P$="P" THEN 2040
990 IF P$="T" THEN GOSUB 1310:GOTO 360
1000 IF P$="Q" THEN GOTO 110
1010 IF P$=""  THEN GOTO 940
1020 P=VAL(P$)
1030 IF P<1 OR P>36 THEN GOTO 940
1040 GOSUB 1300
1050 G=0:W=0:FOR Q=1 TO LEN(C$):W=W+1:IFMID$(C$,Q,1)=" "THEN 1060 ELSE NEXTQ:IFW>G THEN G=W:GOTO 1070 ELSE GOTO 1070
1060 IF W>G THEN G=W:W=0:NEXT Q ELSE W=0: NEXT Q
1070 IF P>G THEN 1080 ELSE P=G
1080 L=P:GOSUB 1300:GOSUB 1130:GOTO 440
1090 GOSUB 1300:RETURN
1100 '*****************
1110 '**  SET WIDTH  **
1120 '*****************
1130 LOCATE 10,10: PRINT"ONE MOMENT PLEASE...";:LOCATE 1,22
1140 F=0:TP$=C$
1150 F=F+1:LC=LEN(C$)
1160 IF LEFT$(C$,1)=" " THEN C$=RIGHT$(C$,LC-1):LC=LC-1:GOTO 1160
1170 IF LEN(C$)<L THEN C$(F)=C$:GOTO 1250
1180 FOR A=L TO 1 STEP -1
1190 IF MID$(C$,A,1)=" " THEN 1220
1200 NEXT A:C$(F)=C$
1210 GOTO 1250
1220 C$(F)=LEFT$(C$,A-1)
1230 C$=RIGHT$(C$,LC-A)
1240 GOTO 1150
1250 C$=TP$
1260 IF F<=10 THEN RETURN
1270 L=L+1: GOTO 1130
1280 '****************
1290 '**   BORDER   **
1300 '****************
1310 CLS: ATTR 5,5
1320 PRINT STRING$(40," ");
1330 FOR T = 1 TO 22
1340 LOCATE 39,T
1350 PRINT" ";
1360 NEXT T
1370 LOCATE 0,23: PRINT STRING$(39," ");
1380 LOCATE 1,1
1390 ATTR 2,1
1400 PRINT" ";
1410 LOCATE 1,22
1420 RETURN
1430 '******************
1440 '** TITLE SCREEN **
1450 '******************
1460 WIDTH 80
1470 CMP:PALETTE 4,0:PALETTE 0,0:PALETTE 5,0:PALETTE 1,50
1480 CLS 1:ATTR 2,1:CLS:LOCATE 1,1:ATTR 0,0
1490 FORT=1 TO 22:LOCATE 1,T:PRINTSTRING$(77," ");:NEXT T
1500 LOCATE 32,8:ATTR 3,0:PRINT"ONE MOMENT PLEASE";:ATTR 0,0
1510 T=1:P=7:L=2:K=4:J=4
1520 LOCATE P,L
1530 READ T
1540 IF T=99 THEN L=L+1:LOCATE P,L:GOTO 1630
1550 IF T=88 THEN L=L+1:LOCATE P,L:GOTO 1530
1560 IF T=100 THEN GOTO 1650
1570 IF T=77 THEN P=16:LOCATE P,L:J=5:K=J:GOTO 1530
1580 IF T=0 THEN T=1
1590 FOR A= 1 TO T
1600 ATTR 0,K
1610 PRINT" ";
1620 NEXT A
1630 IF K=J THEN K=0 ELSE K=J
1640 GOTO 1530
1650 '
1660 LOCATE 32,8:ATTR 0,0:PRINT STRING$(19," ");
1670 LOCATE 35,18:ATTR 3,0:PRINT"WRITTEN BY";
1680 LOCATE 33,20:PRINT"KEVIN REICHERT";
1690 LOCATE 1,22
1700 FOR T=1 TO 8
1710 IF INKEY$<>"" THEN 1740
1720 FOR R=19 TO 40:PALETTE 1,R:PALETTE 4,R+2:PALETTE 5,R+4:NEXT R
1730 NEXT T
1740 FOR T = 1 TO 500 : NEXT T
1750 LOCATE 79,23
1760 FOR T=1 TO 24:PRINT:NEXT T
1770 PALETTE 1,50:ATTR 2,1
1780 FOR T=1 TO 24: PRINT STRING$(79," ");: NEXT T
1790 WIDTH 40:PALETTE CMP:PALETTE 0,0:PALETTE 1,50:PALETTE 6,23:PALETTE 5,27:ATTR 2,1: CLS
1800 GOSUB 1300
1810 LOCATE 10,1
1820 PRINT"CAPTIVATING CAPTIONS";
1830 LOCATE 39,1:ATTR5,5
1840 PRINT STRING$(41," ");
1850 LOCATE 2,5:ATTR 2,1 
1860 PRINT"This program is designed to allow";
1870 LOCATE 2,6
1880 PRINT"you to:";
1890 LOCATE 2,8
1900 PRINT"* Alter the width of any caption";
1910 LOCATE 2,10
1920 PRINT"* Center each line of text";
1930 LOCATE 2,12
1940 PRINT"* Print caption with a border";
1950 LOCATE 2,16
1960 PRINT"PRESS <ENTER> TO CONTINUE";
1970 LOCATE1,22
1980 I$=INKEY$:IFI$=""THEN 1980
1990 GOSUB 1300
2000 RETURN
2010 '*****************
2020 '**  PRINT-OUT  **
2030 '*****************
2040 C=KS*2
2050 K$(1)=CHR$(218)+STRING$(C+2,196)+CHR$(191)
2060 K$(2)=CHR$(192)+STRING$(C+2,196)+CHR$(217)
2070 FOR T=F+5 TO F+11: LOCATE 4,T:PRINT STRING$(32," ");:NEXT T
2080 LOCATE 39,F+12:ATTR 5,5:PRINT"  ";:ATTR 2,1
2090 LOCATE 18,F+12:PRINT"  ";
2100 LOCATE 7,F+7
2110 PRINT"<ENTER>=PRINT   <M>=MENU";
2120 LOCATE 1,22:POKE 65496,0
2130 O$=INKEY$:IFO$=""THEN 2130 
2140 IF O$="M" THEN GOSUB 2290:GOTO 870
2150 LOCATE 7,F+7
2160 PRINT STRING$(24,32);
2170 LOCATE 1,22
2180 PRINT#-2, K$(1)
2190 FOR T=1 TO F
2200 PRINT#-2, C$(T)
2210 NEXT T
2220 PRINT#-2, K$(2)
2230 PRINT#-2:PRINT#-2
2240 ATTR 2,1
2250 GOSUB 2290:GOSUB 870: GOTO 2040
2260 '*******************
2270 '** SPEED UP POKE **
2280 '*******************
2290 POKE 65497,0:RETURN
2300 '********************
2310 '** DATA FOR TITLE **
2320 '********************
2330 DATA 5,,5,,5,,5,,5,,,3,,,5,,5,,5,,,3,,,5,99
2340 DATA ,5,,3,,,,3,,3,,5,,3,,3,,,,3,,3,,5,,3,2,2,,,,4,88
2350 DATA ,5,5,,5,3,,5,,4,,,,2,5,3,,5,,3,,,,,,,,2,2,99
2360 DATA ,5,,3,,,,7,,5,,4,,,,2,,3,,3,,5,,3,,2,2,,,3,,99
2370 DATA 5,,,3,,,,7,,3,5,3,,3,,3,,3,,3,5,,,3,,,5,88
2380 DATA ,99,,88,77
2390 DATA 5,,5,,5,,5,,5,,5,,,3,,,5,99
2400 DATA ,5,,3,,,,3,,3,,5,,3,,3,,,2,2,,,,4,88
2410 DATA ,5,5,,5,3,,5,,3,,3,,,,,,,,,5,99
2420 DATA ,5,,3,,,,7,,5,,3,,3,,,,2,2,5,,99
2430 DATA 5,,,3,,,,7,,3,5,,5,,,3,,,5
2440 DATA 100
