0 'AGENDA' by Keiran Kenny,        Sydney, 1989.
1 ' COPYRIGHT 1990  FALSOFT,INC
10 GOTO560
20 DIMDY$(366)
30 IFPEEK(&H15F)=196THENDV=1ELSEDV=-1
40 IFPEEK(&HFFFE)*256+PEEK(&HFFFF)=&H8C1B THENSP=65497:SL=65496ELSESP=65495:SL=65494
50 GOTO160
60 Q=UP:PK=1
70 PRINT@Q,CHR$(62);
80 K$=INKEY$:IFK$=""THEN80
90 IFQ<UP THENQ=UP
100 IFK$=CHR$(10)ANDQ=DN THEN140
110 IFK$=CHR$(10)THENPRINT@Q,CHR$(143);:Q=Q+32:PK=PK+1:PRINT@Q,CHR$(62);
120 IFK$=CHR$(94)ANDQ>65THENPRINT@Q,CHR$(143);:Q=Q-32:PK=PK-1:PRINT@Q,CHR$(62);
130 IFK$=CHR$(13)GOTO150
140 GOTO80
150 RETURN
160 POKESP,0:CLS:PRINT"USE UP/DN ARRROW:ENTER TO SELECT"
170 UP=65:PRINTTAB(2)"LOAD AGENDA"
180 PRINTTAB(2)"ADD ENTRIES"
190 PRINTTAB(2)"SEARCH"
200 PRINTTAB(2)"SAVE AGENDA"
210 PRINTTAB(2)"END PROGRAM"
220 DN=193:GOSUB60
230 PRINTPK:ONPK GOTO240,330,410,470,550
240 POKESL,0:CLS:INPUT"LOAD AGENDA NAME:";F$:IFDV=-1THENPRINT"POSITION TAPE. PRESS PLAY. PRESSANY KEY.":EXEC44539:GOTO260
250 F$=F$+"/DAT"
260 OPEN"I",#DV,F$
270 INPUT#DV,RN
280 FORT=1TORN
290 LINEINPUT#DV,DY$(T)
300 NEXT
310 CLOSE#DV
320 GOTO160
330 CLS:LINEINPUT"ENTER REMINDER DATE: ";DT$
340 CLS:PRINT"ENTER REMINDER FOR "+DT$
350 PRINT@249-LEN(DT$)+35,"^"
360 PRINT@32,"";
370 RN=RN+1
380 LINEINPUTRN$
390 DY$(RN)=DT$+": "+RN$
400 GOTO160
410 CLS:LINEINPUT"SEARCH FOR: ";S$
420 CLS:FORT=1TORN:IFINSTR(1,DY$(T),S$)THENPRINTDY$(T)
430 FORP=1386TO1417:IFPEEK(P)<>96THENPRINTTAB(8)"PRESS ANY KEY.";:EXEC44539:CLS:GOTO440ELSENEXTP
440 NEXTT
450 E$="**END OF FILE "+CHR$(34)+S$+CHR$(34)+"**":PRINTTAB(16-LEN(E$)/2)E$
460 PRINTTAB(2)"**FOR MENU, PRESS ANY KEY**";:EXEC44539:GOTO160
470 POKESL,0:CLS:INPUT"SAVE AGENDA NAME";F$:IFDV=-1THENPRINT"PRESS PLAY AND RECORD. PRESS ANYKEY.":EXEC44539
480 OPEN"O",#DV,F$
490 PRINT#DV,RN
500 FORT=1TORN
510 PRINT#DV,DY$(T)
520 NEXT
530 CLOSE#DV
540 GOTO160
550 POKESL,0:CLS:END
560 PMODE0:PCLEAR1:CLEAR24000:GOTO20
