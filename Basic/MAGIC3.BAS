10 '*****************************
20 '         MAGIC 3 BALL
30 '    (C) 1987 LOGAN R. WARD
40 '       2774 LAKESIDE DR.
50 '       MEMPHIS,TN  38134
60 '*****************************
70 'THIS PROGRAM SIMULATES THE OLD MAGIC EIGHT BALL FROM YEARS PAST. TO GET AN ANSWER, PRESS ANY KEY
80 POKE65497,0:DIM P(15),Q(14),S(14),A$(19),B$(19)
90 A=160:B=96:F=0:G=.5
100 WIDTH40:HSCREEN2
110 'READ DATA
120 FOR P=0 TO 15:READ P(P):PALETTE P,P(P):NEXTP
130 FOR Q=1 TO 14:READ Q(Q):READ S(Q):NEXTQ
140 FOR I=1 TO 7:READ R(I):NEXTI
150 FOR I=1 TO 19:READ A$(I):READ B$(I):NEXTI
160 CLS1:HCOLOR6,0:GOSUB470
170 'DRAW CHARACTERS
180 HDRAW"BM21,8;C4;"+A$:HDRAW"BM21,8;"+B$:HDRAW"BM30,8;"+A$:HDRAW"BM39,8;"+C$
190 HDRAW"BM21,45;C5;"+A$:HDRAW"BM21,45;"+B$:HDRAW"BM39,45;"+C$:HDRAW"BM21,59;"+B$
200 HDRAW"BM21,82;C7;"+A$:HDRAW"BM21,82;"+B$:HDRAW"BM21,111;"+B$:HDRAW"BM39,97;"+D$:HDRAW"BM35,97;"+E$
210 HDRAW"BM29,119;C11;"+A$:HDRAW"BM21,156;C12;"+A$:HDRAW"BM21,156;"+B$:HDRAW"BM21,185;"+B$
220 HDRAW"BM295,8;C13;"+F$:HDRAW"BM276,8;"+B$:HDRAW"BM276,23;"+B$:HDRAW"BM276,37;"+B$
230 HDRAW"BM276,45;C14;"+A$:HDRAW"BM276,45;"+B$:HDRAW"BM276,59;"+B$:HDRAW"BM278,74;"+B$:HDRAW"BM293,45;"+D$:HDRAW"BM295,59;"+D$
240 HDRAW"BM276,82;C4;"+A$:HDRAW"BM276,82;"+B$:HDRAW"BM295,82;"+C$:HDRAW"BM276,96;"+B$
250 HDRAW"BM276,119;C5;"+A$:HDRAW"BM276,148;"+B$:HDRAW"BM276,156;C7;"+A$:HDRAW"BM276,185;"+B$
260 'DRAW BALL
270 FOR I=1 TO 14STEP2:HCIRCLE(A,B),B,Q(I),S(I),F,G
280 IF I+1>7 THEN F=.5:G=1
290 HCIRCLE(A,B),B,Q(I+1),S(I+1),F,G
300 HPAINT(A,R((I+1)/2)),Q(I),Q(I)
310 NEXT I
320 FOR W=17 TO 19:GOSUB400:NEXTW
330 I$=INKEY$:IF I$<>"" THEN GOSUB390
340 FOR I=13 TO 1 STEP-1:X=RND(63):PALETTEQ(I),X:NEXTI
350 ON BRK GOTO700
360 IF SS=1 THEN S=RND(200):SOUNDS,1
370 GOTO330
380 'PRINT ANSWER
390 W=RND(16)
400 HCOLOR9,0:HLINE(110,85)-(210,105),PSET,BF
410 A2=LEN(A$(W)):B2=LEN(B$(W)):A1=(40-A2)/2:B1=(40-B2)/2
420 HCOLOR15,0:HPRINT(A1,11),A$(W):HPRINT(B1,12),B$(W)
430 IF W<18 THEN GOSUB550 ELSE FOR N=1 TO 3000:NEXTN
440 IF W=19 THEN GOSUB670
450 HCOLOR11,0:HLINE(110,85)-(210,105),PSET,BF
460 RETURN
470 A$="D30;R3;U15;L1;D14;L1;U29"
480 B$="R18;D1;L18"
490 C$="D30;R1;U30"
500 D$="D15;R1;U15"
510 E$="R4;D1;L4
520 F$="D30;L3;U15;R1;D14;R1;U29"
530 RETURN
540 'SPEECH ROUTINE
550 POKE65496,0:S$=A$(W)+CHR$(32)+CHR$(32)+B$(W):K=&HFF00:L=&HFF7E
560 POKEK+1,52:POKEK+3,63:POKEK+35,60
570 GOSUB590
580 RETURN
590 FOR I=1 TO LEN(S$)
600 IF PEEK(L) AND 128=0 THEN 600
610 POKEL,ASC(MID$(S$,I,1))
620 NEXT I
630 IF PEEK(L) AND 128=0 THEN 630
640 POKEL,13
650 FOR SD=1 TO 1300:NEXTSD
660 POKE65497,0:RETURN
670 Z$=INKEY$
680 IF Z$="Y" THEN SS=1:RETURN
690 IF Z$="N" THEN SS=0:RETURN ELSE GOTO670
700 POKE65496,0:END
710 DATA 3,25,0,34,36,38,56,20,8,1,20,63,8,40,60,63
720 'DATA 4,20,5,30,7,60,11,96,12,132,13,152,14,172
730 DATA 4,1,4,.75,5,.75,5,.5,7,.5,7,.25,11,.25,11,.25,12,.25,12,.5,13,.5,13,.75,14,.75,14,1
740 DATA 172,152,132,96,60,30,20
750 DATA THINK HARDER,TRY AGAIN,SEEK  ANSWER,LATER,DO NOT BET,ON  IT,OUTLOOK,GREAT,MORE  THAN,LIKELY,YOU  CAN,COUNT  ON IT,OUTLOOK  NOT,VERY  GOOD,SOURCES,SAY NO,ALL  SIGNS,POINT TO YES,FORECAST,AWFUL,LOOKS LIKE,THUMBS DOWN,BETTER NOT,SAY  NOW
760 DATA REPLY CLOUDY,TRY  AGAIN,VERY,UNCERTAIN,WITHOUT,QUESTION,LOOKS LIKE,A  YES,MAGIC,3 BALL,(C) 1987,LOGAN WARD,SOUND,<Y> OR <N>
