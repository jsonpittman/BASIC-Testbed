10 ' COPYRIGHT 1987 MATT LAWSON
20 PCLEAR1:FILES0,0:CLEAR 300,&H3FFF:DSK=256*PEEK(&HC004)+PEEK(&HC005):V1=&HEA
30 CLS:INPUT"REPORT ERRORS TO (S)CREEN OR    (P)RINTER";A$:IF A$="P" OR A$="p" THEN D=-2 ELSE D=0
40 CLS:PRINT"INSERT SOURCE DISK AND PRESS    (ENTER)":GOSUB 220
50 PG=48:POKE V1,2:POKE V1+1,0:POKE V1+4,&H40:POKE V1+5,0:POKE V1+6,0:AD=&H40
60 FOR TK=0+Z TO 16+Z+I:FOR SC=18 TO 1 STEP-1
70 POKE V1,2:GOSUB 140:NEXT SC,TK
80 PRINT"INSERT DESTINATION DISK AND     PRESS (ENTER)":GOSUB220
90 AD=&H40:PG=48
100 FOR TK=0+Z TO 16+Z+I:FOR SC=18 TO 1 STEP-1
110 POKE V1,3:GOSUB140:NEXTSC,TK
120 IF Z=0 THEN Z=17:I=1:GOTO 40
130 PRINT"DONE!  ";ER;" TOTAL ERRORS":END
140 POKE &HFFA2,PG:POKE V1+2,TK:POKE V1+3,SC:POKE V1+4,AD:EXEC DSK:IF PEEK(V1+6)>0 THEN 190
150 AD=AD+1:IF AD<&H60 THEN RETURN
160 AD=&H40:IF PG=59 THEN PG=58:RETURN
170 IF PG=55 THEN PG=59:RETURN
180 PG=PG+1:RETURN
190 PRINT#D,"DISK ERROR #";PEEK(V1+6):IF PEEK(V1)=2 THEN Z$="READ" ELSE Z$="WRITE"
200 ER=ER+1
210 PRINT#D,"     "+Z$+" TRACK";TK;" SECTOR";SC:GOTO 150
220 A$=INKEY$:IF A$=CHR$(13) THEN RETURN ELSE 220
