10 '***************************
20 '    VCR TAPE CATALOG
30 '   BY RANDY MAYFIELD
40 '    COPYRIGHT 1987
50 '***************************
60 '
70 CLEAR10000:PCLEAR1:Z=500:B=0:W=0:P$=".":DIMT$(Z)
80 '
90 '  MAIN MENU
100 '
110 CLS:PRINTSTRING$(32,128);:PRINT@5,"vcr";:PRINT@9,"tape";:PRINT@14,"catalog";:PRINT@22,"menu";:PRINT@26,STRING$(6,128);:PRINT:PRINT"1. CREATE NEW FILE":PRINT"2. WORK IN A FILE":PRINT"    (ADD, DELETE)"
120 PRINT"3. PRINTOUT A FILE":PRINT"    (PAPER, LABELS)":PRINT"4. SAVE FILE":PRINT"5. SCROLL FILE":PRINT"6. SORT FILE":PRINT"7. QUIT":SOUND200,1:PRINT:PRINT"SELECT ONE "
130 R$=INKEY$:IFR$=""THEN130
140 X=VAL(R$):IFX<1ORX>7THENGOSUB1360:GOTO110
150 ON X GOTO190,330,490,740,890,1070,1000
160 '
170 '  CREATE NEW FILE
180 '
190 IFB=0THEN 210 ELSECLS:SOUND1,3:PRINT"A FILE ALREADY EXISTS IN RAM!!  YOU MUST 'SAVE' IT OR ABANDON IT('QUIT') TO ENABLE CREATION OF ANEW FILE!!":PRINT:PRINT"PRESS ANY KEY FOR MAIN MENU."
200 IF INKEY$=""THEN200ELSE110
210 B=1:CLS:SOUND200,1:INPUT"NAME NEW FILE: ";F$:IFLEN(F$)>8THENGOSUB1360:GOTO210
220 CLS:PRINT"new FILE: ";F$:GOTO240
230 CLS:PRINT"add TO FILE: "F$
240 PRINT@32,"ENTER TITLES (NO COMMAS),":PRINT"      TAPE NUMBERS,":PRINT"      AND VCR COUNTER NUMBERS.":PRINT"HIT <ENTER> WHEN FINISHED.":PRINT"ENTRY#: ";B:SOUND200,1:PRINT@192,"";:INPUT"TITLE: ";T$(B):IFT$(B)=""THEN B=B-1:GOTO110
250 SOUND200,1:PRINT@256,"";:INPUT"TAPE NUMBER (###): ";A$:IF LEN(A$)<>3THENGOSUB1360:PRINT@256," ":GOTO250
260 T$(B)=T$(B)+A$
270 SOUND200,1:PRINT@320,"";:INPUT"COUNTER NUMBER (####): ";A$:IFLEN(A$)<>4THENGOSUB1360:PRINT@320," ":GOTO270
280 T$(B)=T$(B)+A$:B=B+1:IFB>Z THENSOUND1,3:PRINT:PRINT"YOU HAVE MADE THE MAXIMUM NUMBEROF ENTRIES...":FORT=1TO2000:NEXTT:B=B-1:GOTO110
290 IFW=1 THEN230ELSE220
300 '
310 '  WORK EXISTING FILE
320 '
330 IFB=Z THENSOUND150,1:GOTO110 ELSEIFB=0 THENGOSUB1280
340 CLS:PRINT"work EXISTING FILE: ";F$:PRINT@32,"1. ADD":PRINT"2. DELETE":PRINT"3. MAIN MENU":PRINT:SOUND200,1:PRINT"SELECT ONE"
350 R$=INKEY$:IFR$=""THEN350
360 W=VAL(R$):IFW<1ORW>3THENGOSUB1360:GOTO340
370 IFW=3 THEN110ELSEIFW=1 THENCLS:B=B+1:GOTO230 ELSEX=0
380 CLS:X=X+1:PRINT"FIND TITLE TO delete":PRINT:PRINT"     UP-ARROW = SCROLL FORWARD":PRINT"   DOWN-ARROW = SCROLL BACK":PRINT"            D = DELETE":PRINT"ANY OTHER KEY = MAIN MENU":SOUND 200,1:GOTO940
390 CLS:PRINT"delete: ";MID$(T$(X),1,L):PRINT"ARE YOU SURE (Y/N)?":SOUND200,1
400 R$=INKEY$:IFR$=""THEN400
410 IFR$="N"THEN380ELSEIFR$="Y"THEN420ELSEGOSUB1360:GOTO390
420 A$=MID$(T$(X),L+1,3):D$=MID$(T$(X),1,L):FOR Y=X TO B-1:T$(Y)=T$(Y+1):NEXT Y:B=B-1
430 CLS:PRINT@32,D$:PRINT"HAS BEEN DELETED":PRINT"FROM TAPE NUMBER ";A$:PRINT:PRINT:SOUND200,1:PRINT"WANT TO DELETE ANOTHER (Y/N)?"
440 R$=INKEY$:IFR$=""THEN440
450 IFR$="N"THEN340ELSEIFR$="Y"THENX=X-1:GOTO380ELSEGOSUB1360:GOTO430
460 '
470 '  PRINT-OUT FILE
480 '
490 POKE150,18  ' THIS POKE FOR DMP-105 PRINTER 2400 BAUD
500 IFB=0THENGOSUB1280
510 CLS:PRINT"print FILE: ";F$:PRINT"1. PRINT ON PAPER":PRINT"2. PRINT ON LABELS":PRINT"3. MAIN MENU":PRINT:SOUND200,1:PRINT"SELECT ONE"
520 R$=INKEY$:IFR$=""THEN520
530 X=VAL(R$):IFX<1ORX>3THENGOSUB1360:GOTO510
540 IFX=3THEN110ELSEPRINT@192,"IS PRINTER ON & READY?":SOUND200,1:PRINT"HIT ANY KEY WHEN READY."
550 IF INKEY$=""THEN550
560 IFX=2THEN610
570 CLS:PRINT@260,"PRINTING:  ";F$:PRINT#-2,TAB(32)"VCR TAPE CATALOG":PRINT#-2,TAB(32)"FILE NAME: ";F$:PRINT#-2:Y=1
580 FORX=1TOB:L=LEN(T$(X))-7:PRINT#-2,TAB(15)MID$(T$(X),1,L):PRINT#-2,TAB(15)"TAPE#: ";MID$(T$(X),L+1,3);:PRINT#-2,"    ";:PRINT#-2,"COUNTER#: ";RIGHT$(T$(X),4):PRINT#-2:Y=Y+1:IFY=18THEN600
590 NEXTX:GOTO510
600 FORT=1TO16:PRINT#-2:NEXTT:Y=1:GOTO590
610 PRINT#-2,CHR$(27)CHR$(20)  ' CONTROL CODES FOR DMP-105 PRINTER CONDENSED CHAR. ENABLE
620 Y=1:CLS:PRINT"labels FOR: ";F$:PRINT:PRINT"ENTER TAPE NUMBER, AND THE      LABEL(S) FOR THAT TAPE WILL BE  PRINTED.":PRINT"HIT <ENTER> WHEN FINISHED.":PRINT:PRINT:SOUND200,1:INPUT"TAPE NUMBER (###): ";R$
630 IFR$=""THEN690ELSEIFLEN(R$)<>3 THENGOSUB1360:GOTO620
640 FORX=1TOB:L=LEN(T$(X))-6:IFMID$(T$(X),L,3)=R$THENA$=MID$(T$(X),1,L-1):GOTO660
650 NEXTX:FORT=1TO7-Y:PRINT#-2:NEXTT:GOTO620
660 A$=A$+P$:IFLEN(A$)>45THENA$=LEFT$(A$,45):GOTO670ELSE660
670 IFY<5THENPRINT#-2,A$;:PRINT#-2,RIGHT$(T$(X),4):Y=Y+1:GOTO650
680 PRINT#-2:PRINT#-2:Y=1:GOTO670
690 PRINT#-2,CHR$(27)CHR$(19)  ' CONTROL CODES FOR DMP-105 PRINTER CONDENSED CHAR. DISABLE
700 GOTO510
710 '
720 '  SAVE FILE
730 '
740 IFB>0THEN760ELSECLS:SOUND1,3:PRINT"NO FILE IN RAM TO SAVE!!":PRINT:PRINT"PRESS ANY KEY FOR MAIN MENU":SOUND200,1
750 IF INKEY$=""THEN750 ELSE110
760 CLS:PRINT"save FILE: ";F$:PRINT"WANT TO RENAME (Y/N)?":SOUND200,1
770 R$=INKEY$:IFR$=""THEN770
780 IFR$="Y"THEN790ELSEIFR$="N"THEN810ELSEGOSUB1360:GOTO760
790 PRINT:SOUND200,1:INPUT"ENTER NEW NAME: ";M$:IFLEN(M$)>8THENGOSUB1360:GOTO760
800 F$=M$:CLS:PRINT"SAVE FILE: ";F$
810 GOSUB1170:OPEN"O",#D,F$
820 FORX=1TOB:PRINT#D,T$(X):NEXTX:CLOSE#D:CLS:PRINTF$" SAVED":PRINT:PRINT"1. RETAIN FILE IN RAM":PRINT"2. ERASE FILE IN RAM":PRINT:PRINT"SELECT ONE":SOUND200,1
830 R$=INKEY$:IFR$=""THEN830
840 X=VAL(R$):IFX<1 OR X>2 THENGOSUB1360
850 IFX=1THEN110ELSE FOR T=1TOB:T$(T)="":NEXTT:B=0:W=0:GOTO110
860 '
870 '  SCROLL FILE
880 '
890 X=1:IFB=0THENGOSUB1280
900 CLS:PRINT"scroll FILE: ";F$:PRINT:PRINT"     UP-ARROW = SCROLL FORWARD":PRINT"   DOWN-ARROW = SCROLL BACK":PRINT"ANY OTHER KEY = MAIN MENU":SOUND200,1:W=0:GOTO940
910 R$=INKEY$:IFR$=""THEN910
920 IFR$=CHR$(94)THENX=X+1:GOTO930ELSEIFR$=CHR$(10)THENX=X-1:GOTO930ELSEIFR$="D" AND W=2 THEN390 ELSE110
930 IF X>B THEN X=1 ELSE IF X<1 THEN X=B
940 IFT$(X)=""THEN950ELSEPRINT@256," ":L=LEN(T$(X))-7:PRINT@224,"TITLE: ";MID$(T$(X),1,L):PRINT@288,"TAPE#: ";MID$(T$(X),L+1,3):PRINT@352,"CNTR#: ";RIGHT$(T$(X),4):GOTO910
950 CLS:SOUND1,3:PRINT"YOU HAVE DELETED ALL TITLES IN":PRINTF$;" !!":PRINT"IT HAS BEEN ABANDONED IN RAM.":PRINT:PRINT"HIT ANY KEY FOR MAIN MENU.":B=0:W=0:F$=""
960 IF INKEY$=""THEN960ELSE110
970 '
980 '  QUIT
990 '
1000 CLS:PRINT"THANK YOU.":IFB>0THENPRINT:PRINT"P.S.  DID YOU SAVE ";F$;" ?":PRINT"PRESS 'S' TO RETURN AND SAVE IT,ANY OTHER KEY TO QUIT.":SOUND200,1
1010 R$=INKEY$:IFR$=""THEN1010
1020 IFR$="S"THEN760ELSECLS:PRINT"THANK YOU":END
1030 '
1040 ' SHELL-METZNER SORT 
1050 '
1060 IFB=0 THENGOSUB1280
1070 CLS:PRINT@70,"SORTING TITLES IN":PRINT@102,"ALPHABETICAL ORDER.":PRINT@201,"PLEASE WAIT...":S1=B
1080 S1=INT(S1/2):IF S1=0THEN1130 ELSE S4=1:S3=B-S1
1090 S5=S4
1100 S2=S5+S1:IF MID$(T$(S5),1) < MID$(T$(S2),1) THEN1120ELSE1110
1110 M$=T$(S5):T$(S5)=T$(S2):T$(S2)=M$:S5=S5-S1:IF S5<1 THEN1120 ELSE1100
1120 S4=S4+1:IF S4>S3 THEN1080ELSE1090
1130 CLS:PRINT@70,"SORT COMPLETED":SOUND150,1:FOR T=1TO2000:NEXTT:GOTO110
1140 '
1150 'DISKTAPE SELECT SUBROUTINE
1160 '
1170 PRINT@32,"1. DISK":PRINT"2. TAPE":PRINT:SOUND200,1:PRINT"SELECT ONE"
1180 R$=INKEY$:IFR$=""THEN1180
1190 D=VAL(R$):IFD<1ORD>2THENGOSUB1360:GOTO1170
1200 PRINT:IFD=2THEN1220
1210 PRINT"DISKETTE LATCHED IN PLACE?":GOTO1230
1220 D=-1:PRINT"POSITION TAPE":IFB=0THENPRINT"PRESS PLAY BUTTON"ELSEPRINT"PRESS RECORD & PLAY BUTTONS"
1230 SOUND200,1:PRINT"HIT ANY KEY WHEN READY"
1240 IF INKEY$=""THEN1240ELSERETURN
1250 '
1260 '  LOAD FILE SUBROUTINE
1270 '
1280 CLS:PRINT"load EXISTING FILE":SOUND200,1:INPUT"ENTER FILENAME: ";F$:IFLEN(F$)>8THENGOSUB1360:GOTO1280
1290 CLS:PRINT"LOAD EXISTING FILE: ";F$:GOSUB1170:OPEN"I",#D,F$:B=1
1300 IF EOF(D)=-1 THEN1320
1310 INPUT#D,T$(B):B=B+1:GOTO1300
1320 CLOSE#D:B=B-1:RETURN
1330 '
1340 'INCORRECT ENTRY SUBROUTINE
1350 '
1360 SOUND1,3:PRINT@416,"INCORRECT ENTRY":PRINT@448,"PRESS ANY KEY TO TRY AGAIN...":SOUND1,3
1370 IF INKEY$=""THEN1370
1380 PRINT@416," ":PRINT@448," ":RETURN
