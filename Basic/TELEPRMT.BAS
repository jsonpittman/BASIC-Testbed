0 'COPYRIGHT 1990 FALSOFT, INC.
10 CLEAR 14000:MX=0:DIMS$(300):SF=26:ST=0:PT=1:RO=PEEK(359):POKE 359,57:SCREEN 0,1
20 CLS:INPUT"tAPE dISK->";D$:IFD$="D"THEND=1ELSED=-1
30 PRINT:INPUT"FILENAME";FL$:IFFL$=""THEN30
40 IFD=-1THENPRINT"REWIND TAPE, PRESS play":INPUTPR$
50 POKE 359,RO:SCREEN 0,0:GOSUB 350:POKE 359,57:SCREEN 0,1
60 PRINT:PRINT"CMD>";
70 MU$=INKEY$:IFMU$=""THEN70
80 IFMU$="?"THENCLS:PRINT"p PAUSE":PRINT"n NEXT LINE":PRINT"s START AND STOP SCROLL":PRINT"b BACKUP TEXT A BIT":PRINT"a ADJUST SPEED":PRINT"q EXIT TO RAT-DOS":PRINT:INPUT "PRESS enter";PR$:GOTO60
90 IFMU$="S"THENCLS:GOTO140
100 IFMU$="B"THENK$=MU$:GOTO330
110 IFMU$="A"THENK$=MU$:GOTO320
120 IFMU$="Q"THEN POKE 359,RO:END
130 GOTO60
140 GOSUB 480
150 FOR X=ST TO MX
160 FORY=1TOLEN(S$(X))
170 PRINTMID$(S$(X),Y,1);
180 K$=INKEY$:IFK$<>""THEN280
190 FORTD=1TOSF:NEXTTD
200 NEXT Y
210 PRINT
220 NEXT X
230 FOR TD=1 TO 1000:NEXT TD
240 IF EF=1 THEN 270
250 IF RF=1 THEN POKE 359,RO:SCREEN 0,0:GOSUB 380:POKE 359,57:SCREEN 0,1:GOTO 140
260 IF RF=2 THEN POKE 359,RO:SCREEN 0,0:GOSUB 440:POKE 359,57:SCREEN 0,1:GOTO 140
270 PRINT:GOTO 60
280 IFK$="S"THENFORTD=1TO1000:NEXTTD:GOTO 60
290 IFK$="P"THEN GOSUB 550:GOTO 190
300 IFK$="N"THEN PRINT:GOTO 220
310 IFK$="Q"THENEND
320 IFK$="A"THENPRINT:PRINT"SF=";SF:INPUT"SPEED";SF:GOTO60
330 IFK$="B"THENPRINT:LINEINPUT"BACK HOW MANY?>";ST$:ST=X-VAL(ST$):IF ST<0 THEN ST=0:GOTO 140:ELSE:GOTO 140
340 GOSUB 480:GOTO190
350 'get first 175 lines
360 OPEN "I",#D,FL$
370 FOR PP=0 TO 174:LINEINPUT#D,S$(PP):NEXT PP:MX=174:RF=RF+1:RETURN
380 'get 2nd 175 lines
390 MX=0
400 IF EOF(D) THEN RF=RF+1:EF=EF+1:MX=MX-1:RETURN
410 LINEINPUT#D,S$(MX)
420 MX=MX+1:IF MX=175 THEN MX=MX-1:RF=RF+1:RETURN
430 GOTO 400
440 'get lines until eof
450 MX=0
460 IF EOF(D) THEN:MX=MX-1:CLOSE#D:RETURN
470 LINEINPUT#D,S$(MX):MX=MX+1:GOTO 460
480 ' five second countdown
490 SCREEN0,0:FORTD=1TO375:NEXTTD
500 SCREEN0,1:FORTD=1TO375:NEXTTD
510 SCREEN0,0:FORTD=1TO375:NEXTTD
520 SCREEN0,1:FORTD=1TO375:NEXTTD
530 SCREEN0,0:FORTD=1TO375:NEXTTD:
540 SCREEN0,1:RETURN
550 'pause scrolling
560 K$=INKEY$:IFK$=""THEN560
570 GOTO 480
