10 CLEAR 1000
20 CLS:PRINT"CHANGE DISK FILETYPE"
30 PRINT"INSTRUCTIONS? (Y/N) ";:LINEINPUT Z$:IF Z$="Y" THEN GOSUB 460
40 LINEINPUT"FILENAME :";F1$
50 LINEINPUT"     EXT :";F2$
60 LINEINPUT"     DRV :";DRV$:X=VAL(DRV$)
70 IF F1$="" AND F2$="" AND DRV$="" THEN 450
80 IF F1$="" THEN DIR X:GOTO40
90 FORZ=3 TO 11
100 DSKI$X,17,Z,A$,B$
110 A$=A$+LEFT$(B$,127)
120 FORK=0 TO 7
130 PT=K*32
140 NA$=MID$(A$,PT+1,13)
150 IF LEFT$(NA$,1)=CHR$(0) THEN 200
160 IF LEFT$(NA$,1)=CHR$(&HFF) THEN PRINT"FILE NOT FOUND!":GOTO40
170 EXT$=MID$(NA$,9,3)
180 X$=RIGHT$(NA$,2):B1=ASC(LEFT$(X$,1)):B2=ASC(RIGHT$(X$,1))
190 IF F1$=LEFT$(NA$,LEN(F1$)) AND F2$=EXT$ THEN GOTO220
200 NEXTK
210 NEXTZ
220 CLS:PRINTCHR$(12):PRINT:PRINTLEFT$(NA$,11)
230 PRINT"THIS IS A ";
240 IF B1=1 AND B2=255 THEN PRINT"ASCII DATA FILE"
250 IF B1=0 AND B2=0 THEN PRINT"BINARY BASIC FILE"
260 IF B1=0 AND B2=255 THEN PRINT"ASCII BASIC FILE"
270 IF B1=2 AND B2=0 THEN PRINT"MACHINE LANG. FILE"
280 PRINT:PRINT"CONVERT THIS TO..."
290 PRINT"1. ASCII DATA"
300 PRINT"2. BINARY BASIC"
310 PRINT"3. ASCII BASIC"
320 PRINT"4. MACHINE LANG."
330 PRINT:LINEINPUT">";Z$
340 IF Z$<"1" OR Z$>"4" THEN 280
350 IF Z$="1" THEN X$=CHR$(1)+CHR$(&HFF)
360 IF Z$="2" THEN X$=CHR$(0)+CHR$(0)
370 IF Z$="3" THEN X$=CHR$(0)+CHR$(&HFF)
380 IF Z$="4" THEN X$=CHR$(2)+CHR$(0)
390 LINEINPUT"SURE? (Y/N)";Z$:IF Z$<>"Y" THEN 450
400 N1$=LEFT$(NA$,11)+X$
410 MID$(A$,PT+1,13)=N1$
420 C$=LEFT$(A$,128):D$=RIGHT$(A$,127)+CHR$(0)
430 DSKO$X,17,Z,C$,D$
440 GOTO 40
450 PRINT"1. EXIT TO DOS":PRINT"2. RESTART":LINEINPUT">";Z$:IF Z$="1" THEN END ELSE GOTO10
460 PRINT:PRINT"THIS PROGRAM WILL ALLOW YOU TO"
470 PRINT"CHANGE THE DIRECTORY TYPE OF"
480 PRINT"XMODEM FILES THAT HAVE BEEN UP-"
490 PRINT"LOADED TO COBBS! WHEN THIS PRO-"
500 PRINT"GRAM ASKS FOR THE FILENAME GIVE"
510 PRINT"ONLY THE FILENAME! YOU WILL BE"
520 PRINT"PROMPTED FOR THE EXTENSION AND"
530 PRINT"DRIVE #. TO EXIT THIS PROGRAM"
540 PRINT"JUST PRESS <ENTER> AT THE FILE-"
550 PRINT"NAME, EXT, AND DRV PROMPTS"
560 PRINT:PRINT
570 LINEINPUT"PRESS <ENTER>...";ZZ$
580 CLS
590 PRINT"TO TAKE A DIR ON A DRIVE, PRESS"
600 PRINT"THE ENTER KEY IN RESPONSE TO "
610 PRINT"THE FILENAME AND THE EXT"
620 PRINT"PROMPTS. THEN PRESS THE # OF"
630 PRINT"THE DRIVE ON WHICH YOU WISH TO"
640 PRINT"SEE A DIRECTORY."
650 PRINT:PRINT
660 RETURN
