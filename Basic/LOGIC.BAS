1 'LOGIC PUZZLE
2 'WRITTEN BY KEVIN SPEIGHT
3 'COPYRIGHT (C) JANUARY 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR5000:DIMEW$(100):CLS:PRINT@73,"logic puzzler":PRINT@200,"BY KEVIN SPEIGHT":PRINT@425,"PRESS ANY KEY":GOSUB520:CLS:PRINT:X=RND(-TIMER)
20 INPUT"HOW MANY PEOPLE WOULD YOU LIKE  TO HAVE IN THIS PUZZLE (3-6)";N:IF N>6 OR N<3 THEN 20
30 PRINT:INPUT"HOW MANY CHARACTERISTICS SHALL  EACH PERSON HAVE (1-4)";C:IF C>4 OR C<1 THEN 30
40 DATAJOHN,NANCY,CLAUDE,IGOR,SUZETTE,RAJIV,ROGER,JULIA,PETER,HELMUT,MARGARET,DON,ALEXA
50 DATACHICAGO,TORONTO,BANGOR,PHOENIX,LAS VEGAS,PARIS,LONDON,MEMPHIS,BONN,MOSCOW,DELHI,SYDNEY,ROME
60 DATAA CAT,A DOG,AN ELEPHANT,A CAMEL,A GOLDFISH,A PIGEON,AN OTTER,A GERBIL,A HORSE,A HAMSTER,A BUDGIE,A SNAKE,A CHICKEN
70 DATABLUE,GREEN,RED,YELLOW,WHITE,ORANGE,BROWN,BLACK,PURPLE,GOLD,SILVER,MAROON,GREY
80 DATAPIZZA,CHICKEN,HAMBURGERS,STEAK,ESCARGOTS,PEA SOUP,POACHED EGGS,CAVIAR,FISH & CHIPS,EGG ROLLS,GRITS,CRUMPETS,SALAD
90 PRINT:PRINT"RANDOMIZING...":D=0:FORYY=1 TO N:FORZZ=1 TO C+1:RESTORE
100 IF DL>0 THEN FOR X=1TO DL:FOR Y=1TO13:READX$:NEXTY:NEXTX
110 Y=RND(13):FORX=1TOY:READ Y$(YY,ZZ):NEXTX:FORX=1TO YY:IF Y$(YY,ZZ)=Y$(X,ZZ) AND X<>YY THEN RESTORE:GOTO100 ELSE NEXT X
120 DL=DL+1:NEXTZZ:DL=0:NEXTYY
130 PRINT:PRINT"THE PEOPLE IN THIS PUZZLE ARE:":FORX=1TON:PRINTY$(X,1),:NEXTX:PRINT:PRINT"FOR THE FIRST CLUE, PRESS A KEY":GOSUB520:CLS:CL=0
140 CL=CL+1:PRINT"CLUE #"CL":"
150 X=RND(N):Y=RND(C)+1:X1=RND(N):Y1=RND(C)+1:IFX=X1 OR(Y=Y1 AND C>1)THEN 150 ELSE Q=RND(8):ON Q GOTO 160,170,180,190,200,220,250,270
160 M$=M$+Y$(X,1):ON Y GOSUB,530,550,570,590:M$=M$+Y$(X,Y):GOSUB440:GOTO290
170 M$=M$+Y$(X,1):ON Y GOSUB,540,560,580,600:M$=M$+Y$(X1,Y):GOSUB440:GOTO290
180 IFC=1THEN150 ELSEM$=M$+"THE PERSON WHO":ON Y GOSUB,530,550,590,590:M$=M$+Y$(X,Y):ON Y1 GOSUB,530,550,590,590:M$=M$+Y$(X,Y1):GOSUB440:GOTO290
190 IFC=1THEN150 ELSEM$=M$+"THE PERSON WHO":ON Y GOSUB,530,550,590,590:M$=M$+Y$(X,Y)+" ISN'T THE ONE WHO":ON Y1 GOSUB,530,550,590,590:Y=Y1:X=X1:GOSUB240:M$=M$+X$:GOSUB440:GOTO290
200 M$=M$+"EITHER "+Y$(X,1)+" OR "+Y$(X1,1):IF RND(2)=1THEN210 ELSE ON Y GOSUB,530,550,570,590:M$=M$+Y$(X,Y):GOSUB440:GOTO290
210 ON Y1 GOSUB,530,550,570,590:M$=M$+Y$(X1,Y1):GOSUB440:GOTO290
220 M$=M$+Y$(X,1)+" EITHER":ON Y GOSUB,530,550,590,590:IFRND(2)=1THEN230 ELSE M$=M$+Y$(X,Y)+" OR ":GOSUB240:M$=M$+X$:GOSUB440:GOTO290
230 GOSUB240:M$=M$+X$+" OR "+Y$(X,Y):GOSUB440:GOTO290
240 F=RND(N):IFF=X THEN240ELSEX$=Y$(F,Y):RETURN
250 M$=M$+"IF "+Y$(X,1):ON Y GOSUB,530,550,570,590:K=RND(2):IF K=1THEN260 ELSEM$=M$+Y$(X,Y)+" THEN "+Y$(X1,1):ON Y1 GOSUB,530,550,570,590:M$=M$+Y$(X1,Y1):GOSUB440:GOTO290
260 GOSUB240:M$=M$+X$+" THEN "+Y$(X1,1):ON Y1 GOSUB,530,550,570,590:X=X1:Y=Y1:GOSUB240:M$=M$+X$:GOSUB440:GOTO290
270 M$=M$+"IF "+Y$(X,1):ON Y GOSUB,530,550,570,590:K=RND(2):IF K=1THEN280ELSE M$=M$+Y$(X,Y)+" THEN "+Y$(X1,1):ON Y1 GOSUB,540,560,580,600:X=X1:Y=Y1:GOSUB240:M$=M$+X$:GOSUB440:GOTO290
280 GOSUB240:M$=M$+X$+" THEN "+Y$(X1,1):ON Y1 GOSUB,540,560,580,600:M$=M$+Y$(X1,Y1):GOSUB440:GOTO290
290 PRINT@320,"OPTIONS:":PRINT:PRINT"(N)EXT CLUE","(Q)UIT","(G)UESS","(L)IST OF CLUES"
300 GOSUB520:IFX$="Q"THENCLS:END ELSEIFX$="N" AND CL<100 THENCLS:GOTO140 ELSEIFX$="G"THEN310 ELSEIFX$="L"THEN380ELSE300
310 CLS:PRINT"HERE'S YOUR CHANCE TO SOLVE THE PUZZLE.  WORKING...":PRINT:FORX=1TO C
320 FOR XD=1TO N:U(XD)=RND(N):FORXX=1TOXD-1:IFU(XX)=U(XD) ANDXD<>1 THEN320 ELSE NEXTXX:NEXTXD
330 FORP=1TO N:PRINT@128,"WHICH PERSON";:M$="":ON X+1 GOSUB,530,550,570,590:PRINTM$:PRINTY$(U(P),X+1);"?":PRINT
340 FORT=1TO N:PRINT"("T") ";Y$(T,1),:NEXTT
350 GOSUB520:XT=VAL(X$):IFXT<1ORXT>N THEN350 ELSEIFXT=U(P)THENPRINT:PRINT:PRINT"RIGHT!":FORXC=1TO500:NEXTXC:CLS:NEXTP:NEXTX ELSE PRINT:PRINT:PRINT"WRONG.":FORXC=1TO1000:NEXTXC:GOTO420
360 CLS:PRINT"YOU SOLVED THE PUZZLE!!!"
370 PRINT:PRINT"PLAY AGAIN (Y/N)?":GOSUB520:IFX$="Y"THEN RUN ELSEPRINT:END
380 CLS:PRINT"PRINT CLUES TO (S)CREEN OR      (P)RINTER":GOSUB520:IFX$="S"THEN390 ELSEIFX$="P"THEN 410 ELSE 380
390 CLS:TL=1
400 FORX=1TO4:M$=EW$(TL):GOSUB450:TL=TL+1:NEXTX:GOSUB520:CLS:IF TL<CL THEN400 ELSE CLS:GOTO290
410 FORX=1TO CL:PRINT#-2,EW$(X):NEXTX:CLS:GOTO290
420 CLS:PRINT"HERE IS THE SOLUTION TO THE     PUZZLE:":PRINT
430 M$="":FORX=1TON:PRINT@64,"";:FORY=1TO(C+1):M$=M$+Y$(X,Y)+" - ":NEXTY:GOSUB450:PRINT:PRINT"PRESS A KEY":GOSUB520:NEXTX:CLS:GOTO370
440 IFCL<101THEN EW$(CL)=M$
450 PRINT:M$=M$+"   ":CX$=CHR$(32):LL=32:JK=INT(LEN(M$)/32):CR$=RIGHT$(M$,JK):IFLEN(M$)<LL THEN510
460 IF MID$(M$,LL,1)=CX$ THEN510
470 FOR CX=LL TO 1STEP-1:IFMID$(M$,CX,1)=CX$ THEN CC=CX:GOTO490
480 NEXT CX:GOTO510
490 PRINT LEFT$(M$,CC-1):M$=MID$(M$,CC+1,LEN(M$)-CC-1)
500 IF LEN(M$)>32 THEN470
510 PRINT M$:M$="":RETURN
520 X$=INKEY$:IFX$=""THEN520ELSERETURN
530 M$=M$+" LIVES IN ":RETURN
540 M$=M$+" DOESN'T LIVE IN ":RETURN
550 M$=M$+" OWNS ":RETURN
560 M$=M$+" DOESN'T OWN ":RETURN
570 M$=M$+"'S FAVORITE COLOR IS ":RETURN
580 M$=M$+"'S FAVORITE COLOR ISN'T ":RETURN
590 M$=M$+" LIKES ":RETURN
600 M$=M$+" DOESN'T LIKE ":RETURN
