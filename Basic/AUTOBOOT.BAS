1 'AUTOBOOT
2 'BY JOEL HEGBERG
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 PCLEAR8:CLEAR5000:LOADM"BOOT2.BIN"
7 CLS:PRINT"INSERT DISK TO HAVE AUTO-BOOT   FUNCTION INSTALLED ON AND PRESS ANY KEY.":EXEC44539
8 DSKI$0,17,2,A$,AA$:IFMID$(A$,67,2)=CHR$(255)+CHR$(255)THEN11:ELSEIFMID$(A$,67,2)=CHR$(175)+CHR$(175)THEN19
9 CLS:PRINT"THIS DISK IS TOO FULL AND CAN   NOT HAVE THE AUTO-BOOT FUNCTION INSTALLED ONTO IT."
10 GOTO20
11 CLS:PRINT"ENTER NAME OF PROGRAM TO BE     AUTO-BOOTED WITH THE DOS COMMANDIS TYPED. (TYPE ONLY THE NAME,  NOT THE EXTENSION.)"
12 LINEINPUT">";NM$:IFLEN(NM$)<1OR LEN(NM$)>8THENSOUND1,5:GOTO11
13 CLS:PRINT"NOW, ENTER THE 3 CHARACTER      EXTENSION OF "NM$".":LINEINPUT">";EX$:IFLEN(EX$)<>3THENSOUND1,5:GOTO11
14 IFLEN(NM$)<8THEN NM$=NM$+CHR$(32):GOTO14
15 N$=NM$+"."+EX$:FORT=1TOLEN(N$):POKE9735+T,ASC(MID$(N$,T,1)):NEXTT
16 P=38:POKE234,3:POKE235,PEEK(2394):POKE236,34:MS=PEEK(&HC004)*256+PEEK(&HC005)
17 FORT=1TO3:POKE237,T:POKE238,P:POKE239,0:EXEC MS:P=P+1:NEXTT
18 MID$(A$,67,2)=CHR$(175)+CHR$(175):DSKO$0,17,2,A$,AA$:GOTO20
19 CLS:PRINT"THIS DISK ALREADY HAS THE AUTO- BOOT FUNCTION INSTALLED. DO YOU WISH TO CHANGE IT? (Y/N)":LINEINPUT">";K$:IFLEFT$(K$,1)="Y"THEN11:ELSE20
20 PRINT:PRINT"DO YOU WANT TO DO ANOTHER DISK? (Y/N)":LINEINPUT">";K$:IFLEFT$(K$,1)="Y"THEN7:ELSEEND
