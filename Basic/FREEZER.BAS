1 CLS0:FORI=1TO 416 :READ A:PRINTCHR$(A+128);:NEXT
3 DATA,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
5 DATA,,,,,,63,48,48,63,,63,48,63,,55,60,59,,60,63,60,,63,60,60,,,,,,
7 DATA,,,,,,63,54,57,63,,63,60,63,,63,60,63,,48,63,48,,60,60,63,,,,,,
9 DATA,,,,,,52,56,52,56,,60,48,60,,60,48,60,,48,60,48,,60,60,60,,,,,,
11 DATA,,,,,,,,,,84,95,88,,95,91,80,95,,,,,,,,,,,,,,
13 DATA,,,,,,,,,,80,95,80,,95,84,91,95,,,,,,,,,,,,,,
15 DATA,,,,,,,,,,84,92,88,,92,80,84,92,,,,,,,,,,,,,,
17 DATA,,,,,,,,124,127,124,,127,112,127,,127,124,124,,,,,,,,,,,,,
19 DATA,,,,,,,,112,127,112,,127,124,127,,127,124,124,,,,,,,,,,,,,
21 DATA,,,,,,,,112,124,112,,124,112,124,,124,124,124,,,,,,,,,,,,,
23 DATA,,47,44,44,,47,44,47,,47,44,44,,47,44,44,,44,44,47,,47,44,44,,47,44,47,,,
25 DATA,,47,44,44,,47,45,34,,47,44,44,,47,44,44,,35,44,32,,47,44,44,,47,45,34,,,
27 DATA,,44,32,32,,44,32,44,,44,44,44,,44,44,44,,44,44,44,,44,44,44,,44,32,44,,,
29 FOR X=1 TO 1200:NEXT
50 REM ITEM TYPES MUST AGREE WITH TYPES LISTED IN LINE 610 TO PRINT
60 REM PRINTOUT FORMATTED FOR NARROW PAPER FOR EASE OF USE
70 REM CODES IN THE PRINT SECTION-LINES 600 TO 660 - ARE FOR GEMINI 10X
80 REM PROGRAM REQUIRES EXTENDED COLOR BASIC
100 REM FREEZER INVENTORY
110 ' BOB GRIFFARD 
120 ' 129 STAGE RD.NEWPORT NEWS VA. 23606
130 ' PHONE (804) 596 8440
140 GOTO740
150 CLEAR3000:DIMT$(105),I$(105),M$(105),P(105),Y(105):M=106:Z$=CHR$(134)
200 ID$=" FREEZER  INVENTORY":GOSUB750:PRINTTAB(3)" FILE CONTAINS";B;" ITEMS":PRINT:PRINT" DO YOU WANT TO:":PRINT:PRINTTAB(5)"<1> ADD ITEMS TO THE LIST":PRINTTAB(5)"<2> VIEW TOTAL FILE"
210 PRINTTAB(5)"<3> VIEW ITEMS BY TYPE":PRINTTAB(5)"<4> DELETE ITEMS FROM FILE"
220 PRINTTAB(5)"<5> SAVE FILE ON DISK":PRINTTAB(5)"<6> LOAD FILE FROM DISK":PRINT TAB(5)"<7> PRINT HARD COPY":PRINT TAB(5)"<8> SEARCH FOR AN ITEM":PRINTTAB(5)"<9> QUIT"
230 INPUT"ENTER YOUR CHOICE <1 TO 9>";A:IF A<1 OR A>9 THEN 200
240 ON A GOTO 250,320,380,450,530,570,600,670,760
250 ID$="     ADD  ITEMS":GOSUB750:PRINT:PRINT" INPUT DATA AS REQUESTED.":PRINT" TO STOP, ENTER end FOR 'TYPE'."
260 PRINT:B=B+1:IF B=M THEN 310
270 INPUT" TYPE";T$(B):IF T$(B)="END"THEN 300
280 INPUT" ITEM";I$(B):INPUT" PACKAGE NO";P(B):INPUT" DATE--MONTH,YEAR";M$(B),Y(B):ID$="      LISTING":GOSUB750:PRINT"TYPE";TAB(8)"ITEM";TAB(21)"PKG";TAB(25)"DATE":PRINTT$(B);TAB(8)I$(B);TAB(21)P(B);TAB(25)M$(B);TAB(29)Y(B)
290 GOTO 260
300 B=B-1:GOTO200
310 PRINT:PRINT"SORRY THE FILE IS FULL":INPUT"PRESS <ENTER> TO RETURN TO MENU";R$:GOTO200
320 CLS:'LIST OF FILE
330 PRINT" CONTENTS AS OF ";D$:PRINT"TYPE";TAB(10)"ITEM";TAB(19)"PKG";TAB(25)"DATE":PRINT:FOR N=1 TO B:PRINTT$(N);TAB(7)I$(N);TAB(19)P(N);TAB(24)M$(N);TAB(27)Y(N):IF N/12=INT(N/12) THEN340 ELSE350
340 INPUT"PRESS<ENTER>TO CONTINUE";R$:CLS:PRINT"TYPE";TAB(10)"ITEM";TAB(19)"PKG";TAB(25)"DATE":PRINT
350 NEXT N:PRINT@448,"<1>=REVIEW LISTING  <2>= MENU";
360 F$=INKEY$:IF F$=""THEN360
370 IF VAL(F$)=1THEN320 ELSE 200
380 ID$="   ITEMS BY TYPE":GOSUB750:PRINT:PRINT:INPUT" ENTER ITEM TYPE TO BE LISTED      ";S$:PRINT"TYPE";TAB(7)"ITEM";TAB(19)"PKG";TAB(25)"DATE":V=0:FOR N=1 TO B:IF T$(N)=S$THEN390 ELSE 410
390 PRINTT$(N);TAB(7)I$(N);TAB(19)P(N);TAB(24)M$(N)TAB(28)Y(N);:V=V+1:IF V=12 THEN 400 ELSE 410
400 INPUT" PRESS enter TO CONTINUE";R$:CLS:V=0
410 NEXT N
420 PRINT@448,"<1>=LIST ANOTHER TYPE  <2>=MENU"
430 F$=INKEY$:IF F$="" THEN430
440 IF VAL(F$)=1 THEN380 ELSE IF VAL(F$)=2 THEN 200 ELSE 420
450 CLS:'DELETE ITEMS
460 ID$="   DELETE  ITEMS":GOSUB750:PRINT:PRINT" PACKAGE NUMBER TO BE DELETED    ":INPUT K:FOR X=1 TO B:IFK=P(X) THEN 480
470 NEXT X:GOTO 490
480 PRINT T$(X); TAB(8)I$(X); TAB(19)"PKG";TAB(21)P(X); " IS DELETED":Y=X:X=B:NEXTX:FOR N=Y TO B:T$(N)=T$(N+1):I$(N)=I$(N+1):P(N)=P(N+1):M$(N)=M$(N+1):Y(N)=Y(N+1):NEXT N:B=B-1:GOTO 500
490 PRINT"NO PACKAGE NUM. "K" WAS FOUND"
500 PRINT@448,"<1>=DELETE ANOTHER ITEM <2>=MENU"
510 F$=INKEY$:IF F$=""THEN510
520 IF VAL(F$)=1 THEN 450 ELSE 200
530 ID$="  SAVE DATA ON DISK":GOSUB750:PRINT:INPUT " DATE OF FILE UPDATE";D$:PRINT:INPUT"DESTINATION DRIVE FOR DATA ";DN:DRIVE(DN):OPEN"O",#1,"FREEZER/DAT":PRINT #1,B
540 FOR N=1 TO B:CLS:PRINT@233,"saving data":PRINT#1,T$(N):PRINT#1,I$(N):PRINT#1,P(N):PRINT#1,M$(N):PRINT#1,Y(N):NEXTN
550 PRINT#1,D$:CLOSE#1
560 ID$="      ALL DONE":GOSUB750:PRINT:PRINT" DATA HAS BEEN SAVED ON DISK.":PRINT:PRINT" PRESS ANY KEY FOR MENU.":EXEC44539:GOTO 200
570 ID$=" LOAD DATA FROM DISK":GOSUB750:PRINT:INPUT"SOURCE DRIVE FOR DATA ";DN:DRIVE(DN):PRINT:INPUT" PRESS enter WHEN READY";R$:OPEN"I",#1,"FREEZER/DAT":IF EOF(1)=-1 THEN 590
580 INPUT #1,B:FOR N=1 TO B:ID$="  LOADING DATA FILE":GOSUB750:PRINT@233,Z$"loading"Z$"data"Z$:INPUT #1,T$(N):INPUT#1,I$(N):INPUT#1,P(N):INPUT#1,M$(N):INPUT#1,Y(N):NEXT N:INPUT #1,D$
590 CLOSE #1:GOTO200
600 ID$="  PRINT BY CATEGORY":GOSUB750:PRINT:PRINT " PRINT OF FREEZER CONTENTS BY    CATEGORY":PRINT:INPUT" INPUT TODAYS DATE";E$:PRINT#-2,CHR$(27)CHR$(87)CHR$(1):PRINT#-2,CHR$(27)CHR$(45)CHR$(1)
610 PRINT#-2,"CONTENTS OF FREEZER":PRINT#-2,"AS OF: ";E$:F$(1)="BEEF":F$(2)="PORK":F$(3)="CHICK":F$(4)="FISH":F$(5)="MISC"
620 FOR J=1 TO 5:PRINT#-2:PRINT#-2,CHR$(27)CHR$(87)CHR$(1):PRINT#-2,CHR$(27)CHR$(45)CHR$(1):PRINT#-2,F$(J):PRINT#-2,CHR$(27)CHR$(87)CHR$(0);:PRINT#-2,CHR$(27)CHR$(45)CHR$(0);
630 FORX=1TO 18:PRINT#-2,CHR$(231);CHR$(232);:NEXTX:PRINT#-2:FOR N=1 TO B
640 IF T$(N)=F$(J) THEN 650 ELSE 660
650 PRINT#-2,I$(N);TAB(23)P(N);TAB(30)M$(N);TAB(33)Y(N)
660 NEXT N:NEXT J:PRINT#-2:PRINT#-2,STRING$(40,45):PRINT:INPUT " WHAT IS THE NEXT PACKAGE NO";U:PRINT#-2," NEXT PACKAGE NO IS ";U:PRINT#-2,STRING$(12,10):PRINT:PRINT"PRESS enter TO RETURN TO MENU":EXEC44539:GOTO 200
670 ID$="  SEARCH FOR ITEM":GOSUB750:PRINT:INPUT" NAME OF ITEM YOU ARE SEARCHING  FOR";S$:P=1:C=0:V=0:FOR N=1 TO B:F=INSTR(P,I$(N),S$):IF F=0 THEN 700
680 PRINT T$(N);TAB(8)I$(N);TAB(25)P(N):C=C+1:V=V+1:IF V=12 THEN 690ELSE700
690 INPUT" PRESS enter TO  CONTINUE";R$:V=0:CLS
700 NEXT N:IF C=0 THEN PRINT:PRINT" NO ITEM CALLED "; S$ ;" FOUND"
710 PRINT@448,"<1>=SEARCH AGAIN <2>= MENU"
720 F$=INKEY$:IF F$=""THEN720
730 IF VAL(F$)=1 THEN670 ELSE IF VAL(F$)=2 THEN 200 ELSE 710
740 PCLEAR1:GOTO150
750 CLS:PRINT@0,STRING$(6,175);:PRINT@6,ID$;:PRINT@26,STRING$(6,175);:PRINT@32,STRING$(32,134);:ID$="":RETURN
760 CLS:PRINT:PRINT"ARE YOU SURE ? y/n":PRINT:INPUT R$:IF R$="Y"THEN ID$="  HAVE A NICE DAY":GOSUB750:END:ELSE GOTO 200
