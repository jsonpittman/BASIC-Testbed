0 '***********************
1 '*   HOME INVENTORY   **
2 '*       (C)1986      **
3 '* BY BILL TOTTINGHAM **
4 '* BOX 331 MILTON, WI **
5 '***********************
10 PCLEAR1
20 CLEAR5000
30 DA=40
40 DIM I$(DA),B$(DA),C$(DA),S$(DA),V$(DA)
50 CLS0
60 PRINT@32,"        HOME INVENTORY"
70 PRINT@128,STRING$(255,32);STRING$(32,32)
80 PRINT@160,"      1) CREATE DISK FILE"
90 PRINT@224,"      2) CREATE TAPE FILE"
100 PRINT@288,"      3) LOAD DISK FILE"
110 PRINT@352,"      4) LOAD TAPE FILE"
120 I$=INKEY$
130 IF I$="1"THEN DN=1:GOTO180
140 IF I$="2"THEN DN=-1:GOTO180
150 IF I$="3"THEN DN=1:GOTO610
160 IF I$="4"THEN DN=-1:GOTO610
170 GOTO120
180 X=1
190 GOSUB840:GOSUB850
200 FORZ=1TO5
210 ON Z GOSUB860,880,900,920,940
220 IF I$(X)=""THEN210
230 NEXT Z
240 IF LEN(C$(X))>111 THEN C$(X)="":GOSUB840:GOSUB850:GOSUB940:GOTO250
250 PRINT@454,"INFORMATION CORRECT?"
260 I$=INKEY$
270 IF I$="N"THEN350
280 IF I$<>"Y"THEN260
290 PRINT@454,"(A)DD (V)IEW (S)AVE "
300 I$=INKEY$
310 IF I$="A"THENX=X+1:GOTO190
320 IF I$="V"THEN NF=X:X=1:GOTO410
330 IF I$="S"THEN NF=X:GOTO560
340 GOTO300
350 PRINT@454,"LINE TO BE CORRECTED?"
360 I$=INKEY$:IF I$=""THEN360
370 I=VAL(I$):IF I=0 THEN 360
380 IF I>5THEN360
390 ON I GOSUB860,880,900,920,940
400 GOSUB840:GOSUB850:GOTO240
410 IF I$(X)=""THEN470
420 CLS:PRINT@32+(32-LEN(I$(X)))/2,I$(X)
430 PRINT@129+(16-LEN(B$(X)))/2,B$(X):PRINT@144+(16-LEN(S$(X)))/2,"#";S$(X)
440 PRINT@191+(32-LEN(V$(X)))/2,"$";V$(X)
450 PRINT@256,C$(X)
460 PRINT@384,"(D)ELETE (A)DD (H)ARDCOPY (S)AVE":PRINT@451,"(Q)UIT   <ARROWS TO SCROLL>"
470 I$=INKEY$:IF I$=""THEN470
480 IF I$=CHR$(94)THENX=X+1:IF X>NF THEN X=NF ELSE GOTO410:GOTO410
490 IF I$=CHR$(10)THEN X=X-1:IF X<1 THENX=1 ELSE GOTO410:GOTO410
500 IF I$="D"THEN I$(X)="":X=X-1:IF X<1THEN X=NF ELSE GOTO410:GOTO410
510 IF I$="H" THEN 1030
520 IF I$="A"THEN X=NF+1:GOTO190
530 IF I$="S"THEN560
540 IF I$="Q" THEN END
550 GOTO470
560 GOSUB960
570 I$=INKEY$:IF I$<>CHR$(13)THEN570
580 GOSUB740
590 I$=INKEY$:IFI$=""THEN590
600 X=1:GOTO410
610 GOSUB 960
620 I$=INKEY$:IF I$<>CHR$(13)THEN620
630 GOSUB650
640 :X=1:GOTO410
650 'READ FILE
660 X=0
670 OPEN"I",#DN,FILE$
680 X=X+1:NF=X
690 IF EOF(DN) THEN720
700 INPUT#DN,I$(X),B$(X),S$(X),V$(X),C$(X)
710 IF EOF(DN)=0THEN680
720 CLOSE
730 RETURN
740 'WRITE FILE
750 OPEN"O",#DN,FILE$
760 FOR X=1TO NF
770 IF I$(X)=""THEN810
780 IF DN=1 THEN WRITE #DN,I$(X),B$(X),S$(X),V$(X),C$(X)
790 IF DN=-1 THEN PRINT #DN,I$(X),B$(X),S$(X),V$(X),C$(X)
800 IF X=NF THEN820
810 NEXT X
820 CLOSE
830 RETURN
840 CLS:PRINT@8,"DATA ENTRY/EDIT":RETURN
850 PRINT@68,"1) ITEM: ";I$(X):PRINT@132,"2) BRAND: ";B$(X):PRINT@196,"3) SERIAL#: ";S$(X):PRINT@260,"4) VALUE: ";V$(X):PRINT@324,"5) COMMENTS: ";C$(X):RETURN
860 PRINT@77,STRING$(LEN(I$(X)),32):PRINT@77,"";:LINEINPUTI$(X)
870 RETURN
880 PRINT@142,STRING$(LEN(B$(X)),32):PRINT@142,"";:LINEINPUTB$(X)
890 RETURN
900 PRINT@208,STRING$(LEN(S$(X)),32):PRINT@208,"";:LINEINPUTS$(X)
910 RETURN
920 PRINT@270,STRING$(LEN(V$(X)),32):PRINT@270,"";:LINEINPUTV$(X)
930 RETURN
940 PRINT@337,STRING$(LEN(C$(X)),32):PRINT@337,"";:LINEINPUTC$(X)
950 RETURN
960 CLS:PRINT@74,"FILE NAME??":PRINT@106,"";:LINEINPUTFILE$
970 IF FILE$=""THEN960
980 IF LEN(FILE$)>8THEN960
990 IF DN=1THENPRINT@264,"PLACE DATA DISK":PRINT@298,"IN DRIVE -0-"
1000 IF DN=-1 THEN PRINT@266,"POSITION TAPE":PRINT@294,"PRESS PLAY & RECORD"
1010 PRINT@460,"<ENTER>"
1020 RETURN
1030 'PRINTER ROUTINE
1040 EO$=CHR$(27)+CHR$(14):'DOUBLEWIDTH ON
1050 ED$=CHR$(27)+CHR$(15):'DOUBLEWIDTH OFF
1060 UO$=CHR$(15):'UNDERLINE
1070 UE$=CHR$(14):'END UNDERLINE
1080 CLS:PRINT@37,"POSITION PRINTER HEAD":PRINT@364,"<ENTER>"
1090 I$=INKEY$:IF I$<>CHR$(13) THEN1090
1100 PRINT#-2,STRING$(5,13)
1110 PRINT#-2,TAB(31);EO$;"INVENTORY";ED$;STRING$(3,13)
1120 PRINT#-2,TAB(0);UO$;TAB(80);UE$
1130 PRINT#-2,TAB(0);UO$;TAB(10);"ITEM";TAB(30);"BRAND";TAB(50);"VALUE";TAB(65);"SERIAL #";TAB(80);UE$
1140 PRINT#-2
1150 FOR X=1TONF
1160 IF I$(X)=""THEN NEXT X
1170 PRINT#-2,TAB(7);I$(X);TAB(25);B$(X);TAB(48);V$(X);TAB(63);S$(X);CHR$(13)
1180 PRINT#-2,TAB(5);"CMTS: ";C$(X);STRING$(2,13)
1190 NEXT X
1200 X=1:GOTO410
