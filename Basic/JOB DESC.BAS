1 ' JOB-DESC
2 ' LARRY M. PAROUBEK
3 ' COPYRIGHT (C) 1986
4 ' ALL RIGHTS RESERVED
10 CLS:GOSUB1600
20 CLEAR1000
100 CLS:TITLE$="-CREATING JOB DESCRIPTION-":PRINT@2,TITLE$
110 PRINT@96," WHAT IS THE JOB TITLE":PRINT:INPUT TT$
150 CLS:PRINT@2,TITLE$
160 PRINT@129,"Overview the position and input the number of responsibilities  of the position.":PRINT@264,"<MAXIMUM OF NINE>":PRINT@384," Think of responsibility items   that must be handled on a       daily, weekly or monthly basis. -positional-";
170 POKE65314,48:I$=INKEY$:IFI$=""THEN170 ELSE IF VAL(I$)<1 OR VAL(I$)>9 THEN170
180 RESPONSIBLE=VAL(I$)
190 FORX=1 TO RE:CLS:PRINT@2,TITLE$:PRINT:PRINT:PRINT" LIST RESPONSIBILITIES           -BE SPECIFIC!-":PRINT:PRINT:LINEINPUTA$(X):NEXTX
200 FORX=1 TO RE:CLS:PRINT@34,"RATE RESPONSIBILITIES 1 -9":PRINT@66,"9 IS HIGH, 1 IS LOW":PRINT@256,"* ";A$(X)
210 PRINT@420,"<INPUT 1 - 9>"
220 I$=INKEY$:IFI$=""THEN220 ELSEIF VAL(I$)<1 OR VAL(I$)>9 THENPRINT@352," OFF SCALE - TRY AGAIN...":GOTO220 ELSE230
230 A(X)=VAL(I$):NEXTX
250 CLS:PRINT@1,"WHAT TYPE OF PERSONALITY DO YOU FEEL YOU NEED?"
260 PRINT@128,"1. INTROVERT - MOST HAPPY BY    THEMSELVES, DOING THEIR OWN     THING...
270 PRINT@256,"2. EXTROVERT - INTERACTION WITH OTHERS GIVES THEIR LIFE MEANING."
280 PRINT@420,"<INPUT 1 OR 2>":POKE65314,48
290 I$=INKEY$:IFI$=""THEN290ELSEIF VAL(I$)<1 OR VAL(I$)>2THEN290 ELSE PERSON=VAL(I$)
300 CLS:PRINT@1,"RATING INDIVIDUAL'S STRENGTH   -WHERE DO YOU NEED THE STRENGTH-"
310 PRINT@96,"1. THINKER : Good with facts andfigures, researching, analysis, accounting, etc.":PRINT"2. SENSOR : Good at initiating  projects, negotiating, converts ideas into action."
320 PRINT"3. INTUITOR : Good at long-term planning, creative writing,     lateral thinking, brainstorming.";:PRINT"4. FEELER : Good at cementing   relationships, counseling,      public relations.":PRINT@481,"<RATE MOST IMPORTANT FIRST>";
330 FORX=1 TO3:POKE65314,48
340 I$=INKEY$:IFI$=""THEN340 ELSEIF VAL(I$)<1 OR VAL(I$)>4 THEN340 ELSE350
350 IF X=1 THEN N1=VAL(I$) ELSE IFX=2 THEN N2=VAL(I$) ELSEIF X=3 THEN N3=VAL(I$)
360 IF X=1 THEN390 ELSEIFX=2THEN370 ELSE380
370 IF N2=N1 THEN340 ELSE390
380 IF N3=N1 OR N3=N2 THEN340 ELSE390
390 TYPE(X)=VAL(I$):FORY=1504 TO1535:POKEY,255:NEXTY:IF X=1 THEN PRINT@481,"<RATE SECOND MOST IMPORTANT>"; ELSEIF X=2 THEN PRINT@481,"<RATE THIRD MOST IMPORTANT>";
400 NEXTX
450 CLS:PRINT@2,TITLE$:PRINT@72," JOB DUTIES":PRINT@416," LIST 5 MAJOR DUTIES THAT YOU    FEEL THE POSITION ENTAILS.";:PRINT@103,"<maximum of 5>":FORX=1 TO5:PRINT@162,"NUMBER ";X:PRINT:LINEINPUT DUTIES$(X):FORY=1248 TO1311:POKEY,143:NEXTY,X
460 CLS:PRINT@2,TITLE$:PRINT@64," ESTIMATE THE NUMBER OF HOURS    PER WEEK THAT THE PARTICULAR    DUTY SHOULD TAKE TO COMPLETE.   input -integers-";:FORX=1 TO5:PRINT@225,DUTIES$(X):PRINT:INPUT DUTIES(X):FORY=1248 TO1311:POKEY,143:POKEY+64,143:NEXTY,X
470 TD=DU(1)+DU(2)+DU(3)+DU(4)+DU(5)
500 CLS:PRINT@2,TITLE$:PRINT@100,"REPORTING ARRANGEMENTS":PRINT@132,"SHOULD REPORT TO WHOM?":PRINT:INPUT SUPER$:CLS:PRINT@2,TITLE$:PRINT@98,"Is person management?":PRINT@134,"<Y>ES OR <N>O":POKE65314,48
510 I$=INKEY$:IFI$=""THEN510 ELSEIFI$="Y"THEN520 ELSEIFI$="N"THEN550 ELSE510
520 PRINT@320,"OVER HOW MANY PEOPLE?":PRINT:INPUT MANAGE
550 CLS:PRINT@2,TITLE$:PRINT@102,"SALARY RANGE":PRINT@130,"<EXAMPLE : 25000 - 30000>":PRINT@194,"INPUT FIRST NUMBER -> NO $'S";:PRINT:INPUTA:PRINT:PRINT"  INPUT SECOND NUMBER -> NO $'S";:PRINT:INPUTB
560 SALARY$="$"+STR$(A)+" -"+STR$(B):PRINT@418,"FLEXIBLE ? : <Y>ES OR <N>O";:POKE65314,48
570 I$=INKEY$:IFI$=""THEN570 ELSEIF I$="Y" THEN FLEX$="FLEXIBLE - YES" ELSEIF I$="N" THEN FLEX$="FLEXIBLE - NO" ELSE570
900 CLS3:P=PEEK(65314):IF P=4 OR P=48 THEN920 ELSE910
910 PRINT@260,"please";CHR$(128);"prepare";CHR$(128);"printer";:POKE65314,48:FORX=1TO200:NEXTX:SOUND1,1:GOTO900
920 FORX=1 TO12:SOUND160,1:NEXTX:PRINT@260,"PRESS ANY KEY TO PROCEED";:POKE65314,40:EXEC44539
1000 'LINES 1000 - 1030 IS SPACE      PROVIDED FOR YOUR OWN           PARTICULAR PRINTER CODES.
1010 'PROGRAM IS WRITTEN FOR          80 CHARACTER PER LINE           PRINTOUT...
1020 'USE THIS AREA FOR YOUR          PRINTER'S BAUD RATE...
1030 'USE THIS AREA FOR ANY           SPECIAL PRINTER CODES...
1100 CLS0:PRINT@426,"working";:POKE65494,0:PRINT#-2,"                 -JOB  DESCRIPTION-"
1110 PRINT#-2:PRINT#-2,"       POSITION : ";TT$:PRINT#-2:PRINT#-2,"       REPORTS TO : ";SUPER$:PRINT#-2:PRINT#-2,"       SALARY RANGE : ";SALARY$:PRINT#-2,"                       ";FLEX$
1120 IF MANAGE >0 THEN1130 ELSE1140
1130 PRINT#-2:PRINT#-2,"       STAFF SIZE : ";MANAGE;" PERSONS"
1140 PRINT#-2:PRINT#-2,"       PARTICULAR DUTIES : "
1150 FORX=1 TO5:PRINT#-2,"                     ";DUTIES$(X):PRINT#-2,"                         ";DUTIES(X);" HRS./WK.":NEXTX
1160 IF TD>40 THEN PRINT#-2,"                   * Job, as listed, is undoable at ";TD;" hours per week total" ELSE1170
1170 PRINT#-2:PRINT#-2,"       POSITIONAL RESPONSIBILITIES : ":X=0
1180 X=X+1:IF X>RE THEN1210 ELSE1190
1190 IF A(X) >7 THEN1200 ELSE1180
1200 PRINT#-2,"             HIGH -> ";A$(X):GOTO1180
1210 X=0
1220 X=X+1:IFX>RE THEN1250 ELSE1230
1230 IF A(X) <=7 AND A(X) >4 THEN1240 ELSE1220
1240 PRINT#-2,"           MEDIUM -> ";A$(X):GOTO1220
1250 X=0
1260 X=X+1:IFX>RE THEN1290 ELSE1270
1270 IF A(X) <=4 AND A(X)>0 THEN1280 ELSE1260
1280 PRINT#-2,"              LOW -> ";A$(X):GOTO1260
1290 X=0:PRINT#-2:IFPERSON=1 THEN1300ELSE1310
1300 PRINT#-2,"       We are looking for someone that is most happy working alone":PRINT#-2,"       and does not need a social environment in which to work.":PRINT#-2:GOTO1320
1310 PRINT#-2,"       We are looking for someone that is a people person":PRINT#-2,"      whose challenge is inter-action with other people.":PRINT#-2
1320 X=X+1:IFX>3 THEN1500
1330 IF TYPE(X)=1 THEN1340 ELSE1360
1340 PRINT#-2,"       ";X;". We want a person that enjoys tackling problems with logic":PRINT#-2,"         is strong on analysis, a methodical worker and good at research.":PRINT#-2,"         Will be good with facts and figures; and, analysis."
1350 PRINT#-2:GOTO1320
1360 IF TYPE(X)=2 THEN1370 ELSE1390
1370 PRINT#-2,"       ";X;". We want someone that is good at getting things done,":PRINT#-2,"         enjoys routine work, has common sense, works hard and is practical.":PRINT#-2,"         Good at initiating projects, negotiating, converts ideas/action."
1380 PRINT#-2:GOTO1320
1390 IF TYPE(X)=3 THEN1400 ELSE1420
1400 PRINT#-2,"       ";X;". We want someone that enjoys playing with ideas and theories,":PRINT#-2,"         can see the overview, is creative and imaginative.":PRINT#-2,"         Good with long-term planning, lateral thinking and is intuitive."
1410 PRINT#-2:GOTO1320
1420 IF TYPE(X)=4 THEN1430 ELSE1320
1430 PRINT#-2,"       ";X;". We want someone that is good at cementing team relationships,":PRINT#-2,"         counseling, arbitrating, public relations and is perceptive.":PRINT#-2,"         Enjoys social contact and can easily talk with anyone."
1440 PRINT#-2:GOTO1320
1500 CLS:PRINT@65,"Do you wish to print another    Job Description ?";:PRINT@260,"<Y>es  or  <N>o";:POKE65314,48
1510 I$=INKEY$:IFI$=""THEN1510 ELSEIFI$="Y"THEN20 ELSEIFI$="N"THEN1520 ELSE1510
1520 CLS:PRINT@100,"Thank you for using":PRINT@164,"Creating a":PRINT@230,"JOB DESCRIPTION":PRINT@330,"by":PRINT@364,"Larry M. Paroubek":POKE65314,48:FORX=1 TO8:PLAY"T16O2V15P16L8AP16L32AP32FP32EDEP32CP32D":NEXTX:PLAY"T8O1L32CDEGBO2DFL1A"
1530 POKE65494,0:CLS:END
1600 CLS:FORX=1024 TO1055STEP2:POKEX,191:POKEX+1,175:POKEX+480,175:POKEX+481,191:NEXTX
1610 FORX=1056TO1472STEP64:POKEX,175:POKEX+32,191:POKEX+31,175:POKEX+63,191:NEXTX
1620 PRINT@99,"CREATING A";:PRINT@200,"job  description";:PRINT@295,"BY";:PRINT@330,"LARRY M. PAROUBEK";:PRINT@362,"COPYRIGHT (C) 1986";:PRINT@394,"ALL RIGHTS RESERVED";
1630 POKE65314,40
1640 PLAY"T3O2V12L8P4AP8AP8AL16DFEL8V6P8AP8AP8AL16DFEO3V16L8P8AP8AP8AL16DFEO1V10L8P8AP8AP8AL16DFEP8O3V18L32AP32O2AP32O1AP16O3AP32O2AP32O1AP8L16DFO2V26L16EE-EE-EE-EE-L8.E"
1650 PRINT@456,"speed poke? Y/N";:POKE65314,48:FORX=1 TO40:NEXTX:PRINT@456,"SPEED POKE? Y/N";:POKE65314,48:FORX=1 TO80:NEXTX
1660 I$=INKEY$:IFI$=""THEN1650 ELSEIFI$="Y"THEN SP=65495 ELSEIF I$="N"THEN SP=65494 ELSE1650
1670 POKESP,0:FORX=1024 TO1151:POKEX,128:NEXTX:FORX=1152 TO1535:A=RND(3)+176:POKEX,A:NEXTX
1680 POKE1220,10:POKE1221,15:POKE1222,2:POKE1225,4:POKE1226,5:POKE1227,19:POKE1228,3:POKE1229,18:POKE1230,9:POKE1231,16:POKE1232,20:POKE1233,9:POKE1234,15:POKE1235,14:POKE1236,19
1690 POKE1510,16:POKE1511,18:POKE1512,5:POKE1513,19:POKE1514,19:POKE1516,1:POKE1517,14:POKE1518,25:POKE1520,11:POKE1521,5:POKE1522,25:POKE1524,97:EXEC44539
1700 RETURN
