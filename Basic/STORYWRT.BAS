0 ' COPYRIGHT 1989  FALSOFT,INC
5 '** CLEAR MAXIMUM STRING SPACE
10 CLEAR 1000
20 PMODE 0,1:PCLEAR 1
30 CLEAR MEM
35 '** 16K OR 64K? N=TOTAL LINES
40 IF PEEK(116)=127 THEN N=100 ELSE N=40
43 '** REDUCE N IF DISK IS USED
46 IF PEEK(188)=14 THEN N=N-8
50 DIM A$(N+1)
55 '** SET UP TITLE SCREEN
60 CLS
70 PRINT"      STORYTIME PROGRAM":PRINT:PRINT"CREATED BY: JOHN FRIEDRICH":PRINT:PRINT
80 INPUT"PRESS ENTER TO BEGIN";A$
85 '** INCREASE LINE COUNT
90 X=X+1
95 '** DISPLAY END OF LAST LINE        AND INPUT NEW LINE
100 CLS
110 PRINT RIGHT$(A$(X-1),32)
120 PRINT@384,"TYPE IN A PARAGRAPH..."
130 IF X=N THEN PRINT@448,"LAST LINE!  FINISH UP!"
140 PRINT@32:LINE INPUT A$(X)
145 '** IF NO LINE TYPED OR ALL         LINES USED, PRINT THEM
150 IF A$(X)="" THEN N=X-1
160 IF X>N-1 THEN 200
165 '** WAIT FOR NEXT PERSON
170 CLS0:PRINT@200,"NEXT PERSON, PLEASE";
180 EXEC 44539
190 GOTO 90
195 '** LAST LINE = 'THE END'
200 A$(N+1)="THE END"
205 '** SCREEN OR PRINTER?
210 CLS
220 PRINT"READY TO PRINT STORY"
230 PRINT:INPUT"SCREEN: 0, OR PRINTER: -2";A
240 IF A<>0 AND A<>-2 THEN 230
245 '** PRINT ALL LINES
250 CLS
260 FOR X=1 TO N+1
270 PRINT#A,A$(X)
275 '** PAUSE FOR READING
280 IF A=0 THEN EXEC 44539
290 NEXT X
295 '** CHECK FOR REPRINTING
300 CLS
310 INPUT"PRINT STORY AGAIN (Y/N)";A$
320 IF A$="Y" THEN 210
325 '** RETURN MEMORY TO NORMAL
330 CLEAR 1000:PCLEAR 4:END
