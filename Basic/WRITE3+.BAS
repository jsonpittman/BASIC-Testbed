1 PCLEAR1:CLEAR12000:DIML$(200)
2 POKE 150,18  ' BAUD RATE= 2400
5 POKE&HFFD9,0:ON BRK GOTO 9000:ON ERR GOTO 10000
6 ' PRINTER CODES:  \0 & \1=UNDERLINE ON & OFF    \2 & \3=BOLD ON & OFF                             \4 & \5=ELONGATE ON & OFF     \B1 & \B0=ITALICS ON & OFF
7 ' ;CT WILL CENTER A LINE OF TEXT.  ;CE WILL CENTER OF LINE OF ELONGATED TEXT.     ;LM=LEFT MARGIN  ;RM=RIGHT MARGIN  ;SP=SPACING  ;LP=LINES/PAGE 
8 ' ;N=NEW PAGE
9 CT$=STRING$(80,CHR$(32))
10 CR=80:FG=1:BG=1:WIDTHCR:PALETTE RGB:PALETTE1,0:CLS2:ATTR7,1:XL=200:FORX=1TO4:X(X)=X+340:NEXT:XX=247
11 PR$=STRING$(CR,"-"):BL$=STRING$(CR-1,32):DF$=":0":DF=0
12 DATA Start New File,Load Old File,Save Current File,Edit/Review File,Print Current File,Resume Document,Kill File,View Directory,Exit to BASIC
13 FORI=1 TO9:READM$(I):NEXT:GOTO100
16 GOSUB33:IFH=CR-4THEN28
17 A$=INKEY$:IFA$=""THEN17
18 A=ASC(A$):RETURN
19 AN$=""
20 GOSUB16:IFA=13 THEN25ELSEIFA=34THEN20ELSEIFA=21THEN50ELSEIFA=93THEN54ELSEIFA=95THEN20ELSEIFA=91THEN20
21 IFA=12THENL$(N)=AN$:RETURN
22 IF A<>8THENAN$=AN$+A$:PRINTA$;:GOTO20
23 IF LEN(AN$)=0THEN19ELSEAN$=LEFT$(AN$,LEN(AN$)-1):PRINTCHR$(8);:GOTO20
25 IF A=13THENL$(N)=AN$+CHR$(126):N=N+1:IFN=>XL THEN80
26 PRINTCHR$(126):AN$="":GOSUB33:GOTO20
28 IFMID$(AN$,H,1)<>" "THENH=H-1:GOTO28
29 L$(N)=LEFT$(AN$,H):LOCATEH,V:PRINTSTRING$(CR-2-H,32):N=N+1':IFN=>XLTHEN80
30 AN$=MID$(AN$,H+1):PRINTAN$;:GOSUB33
31 GOTO20
33 HSTATX$,X,H,V:RETURN
37 LOCATEH,V:RETURN
39 ZZ=INT(CR/2)-INT(LEN(Z$)/2):LOCATEZZ,V:ATTR7,1:PRINTZ$:ATTR3,1:RETURN
46 RP=0:FORX=1TO4:IFPEEK(X(X))=XXTHENRP=1:POKEX(X),255:NEXT:ELSENEXT
48 RETURN
50 GOSUB46:IFRP=1ANDLEN(AN$)>0THENAN$=LEFT$(AN$,LEN$(AN$)-1):PRINTCHR$(8);:H=H+(H-1>-1):LOCATEH,V:GOTO50 ELSE20
54 GOSUB 46:IFRP=1ANDLEN(AN$)<76THENAN$=AN$+" ":PRINT" ";:H=H-(H+1<CR-4):LOCATEH,V:GOTO54 ELSE20
70 IFF$=""THEN100
71 IFINSTR(1,F$,"/")=0 AND INSTR(1,F$,".")=0THENF$=F$+".DAT"
72 OPEN"D",1,F$,1:LO=LOF(1):IFLO=0THENPRINT:PRINT"FILE NOT FOUND.":CLOSE:KILLF$:FORTD=1TO1000:NEXT:UN=1:RETURN ELSE CLOSE:RETURN
100 CLS:H=0:V=0:LOCATEH,V:Z$="Write III --- CoCo III Word Processor":GOSUB39:V=V+1:Z$="By Bill Cook":GOSUB39:PRINTPR$;:ATTR3,1
110 FORI=1TO9:LOCATECR/2-12,I+5:PRINTI;CHR$(8);". ";M$(I):NEXT
115 LOCATECR/2-12,16:PRINT"File in memory : ";F$
116 LOCATECR/2-12,17:PRINT"No. Lines      :";N
117 LOCATECR/2-12,18:PRINT"Space Remaining:";200-N
120 LOCATE CR/2-16,21:ATTR 7,1:PRINT"Select the option of your choice...";:ATTR FG,BG
130 A$=INKEY$:IF A$=""THEN130 ELSEA=VAL(A$)
140 IFA<1ORA>9THEN SOUND100,1:GOTO130ELSECLS:ON A GOTO1000,2000,3000,4000,5000,6000,7000,8000,9000
150 SOUND100,1:GOTO140
200 GOTO 200
1000 ' START NEW DOCUMENT  '
1010 F$="":H=0:V=0:POKE282,0:LOCATEH,V:Z$=M$(1)+"..Press <CLEAR> for Menu":GOSUB39:PRINTPR$
1020 FORI=0TOXL:L$(I)="":NEXT:N=0:GOSUB19:POKE282,255:CLS:GOTO100
1030 FORI=0TON-1:PRINTL$(I):NEXT:PRINTL$(N);:GOSUB33:FORI=NTOXL:L$(I)="":NEXT:GOSUB19:POKE282,255:GOTO100
2000 '  LOAD DOCUMENT  '
2005 POKE&HFFD8,0:H=0:V=0
2010 CLS:Z$=M$(2):GOSUB39:PRINTPR$;
2020 PRINT"Enter Filename/Ext.";:LINEINPUTF$:GOSUB70:IFUN=1THENUN=0:GOTO2010
2030 ATTR7,1:PRINT:PRINT"Loading "+F$:ATTR1,1:AN$=""
2040 OPEN"I",1,F$:X=0
2050 IF EOF(1)=-1THEN2100
2060 INPUT#1,L$(X):X=X+1
2070 GOTO2050
2100 CLOSE:N=X:POKE&HFFD9,0:GOTO100
3000 '  SAVE DOCUMENT  '
3005 POKE&HFFD8,0:H=0:V=0
3010 CLS:Z$=M$(3):GOSUB39:PRINTPR$
3020 PRINT"Enter Filename/Ext.:";:LINEINPUTF$:GOSUB70
3040 ATTR7,1:PRINT:PRINT"Saving "+F$
3050 OPEN"O",1,F$:FORI=0TON:PRINT#1,CHR$(34)L$(I)CHR$(34):NEXT
3060 CLOSE:POKE&HFFD9,0:GOTO100
4000 '  EDIT DISPLAY  '
4001 M1$="   <F>orward  <B>ack  <I>nsert line  <D>elete line  <E>dit line  <M>enu   ":M2$="<F>orward     <B>ack       <I>nsert line":M3$="<Delete line  <E>dit line  <M>enu"
4010 CLS:V=0:Z$=M$(4):GOSUB39:PRINTPR$
4015 PP=0:SC=1:LP=18:LV=0
4020 LOCATE0,2:POKE282,255:A$=INKEY$:FORX=LP*PP TO LP*PP+LP:PRINT" "+L$(X):NEXT:V=22:LOCATE0,21:PRINTPR$;:IFCR=80THENZ$=M1$:GOSUB39
4021 IF CR=40 THEN LOCATE0,22:PRINTM2$;M3$;
4025 IFLV>0THENH=0:V=LV:GOSUB37:GOTO4040
4030 H=0:V=2:GOSUB37
4040 A$=INKEY$:IFA$=""THEN4040 ELSE A=INSTR(CHR$(94)+CHR$(10)+CHR$(8)+CHR$(9)+"IDMEFB",A$):ON A GOTO 4120,4120,4120,4120,4140,4150,4160,4170,4180,4190
4050 SOUND100,1:GOTO4040
4120 IFPEEK(341)=247 ANDV>2THENPOKE341,255:V=V-1:GOSUB37:FORTD=1TO40:NEXT
4130 IF PEEK(342)=247 AND(V<LP+2 AND NO<N-1)THENPOKE342,255:V=V+1:GOSUB37:FORTD=1TO40:NEXT
4131 IFPEEK(344)=247 ANDH<CR-3THENPOKE344,255:H=H+1:GOSUB37:GOTO4200
4132 IF PEEK(343)=247 AND H>0THENPOKE343,255:H=H-1:GOSUB37:GOTO4200
4135 NO=LP*PP+V-2:GOTO4040
4140 NO=LP*PP+V-2:IFN+1>200THENLOCATE0,22:PRINT"Only 200 lines allowed.....":SOUND100,5:FORTD=1TO2000:NEXT:GOTO100
4141 IFN=0THENSOUND100,1:GOTO4040
4142 LOCATE0,22:PRINTTAB(10)"Making room for insert.....":FORI1=N TO NO STEP-1:FORI2=0TO3:IF I2=1THEN4146
4143 I3=PEEK(VARPTR(L$(I1))+I2)
4144 POKE(VARPTR(L$(I1+1))+I2),I3
4146 NEXT I2,I1:L$(NO)=CHR$(32):N=N+1:LV=V:GOTO4020
4150 NO=LP*PP+V-2:IFN=0THENSOUND100,5:GOTO4040
4151 LOCATE0,22:PRINTTAB(10)"Deleting line.....":FORI1=NO+1 TO N
4152 FORI2=0TO3
4153 IFI2=1THEN4157
4154 I3=PEEK(VARPTR(L$(I1))+I2)
4155 POKE(VARPTR(L$(I1-1))+I2),I3
4157 NEXTI2,I1:N=N-1:LV=V:GOTO4020
4160 GOTO100
4170 NO=LP*PP+V-2:LOCATE0,22:ATTR7,1:PRINT"Line Edit Mode: Press <ENTER> to Exit":LOCATE0,23:PRINTBL$;:POKE282,0:IFN=0THENSOUND100,5:GOTO4010
4171 A$=L$(NO):Z1$=" !#$%&'()*=1234567890:-@+;?/>.<,ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz\":LOCATE1,V:ATTR1,1:PRINTA$;:IFH=0THENH=1:GOSUB37 ELSE GOSUB37
4172 Z$=INKEY$:IFZ$=""THEN4172 ELSEIFPEEK(343)=247THENPOKE343,255:H=H-1 ELSEIFPEEK(344)=247THENPOKE344,255:H=H+1
4173 IFH<=1THENH=1 ELSEIFH>=LEN(A$)+1THENH=LEN(A$)+1
4174 IFZ$=CHR$(13)THENLOCATE1,V:ATTR3,1:PRINTA$;:L$(NO)=A$:POKE282,255:LOCATE0,V:GOSUB33:SOUND100,1:LOCATE0,22:IFCR=80THENPRINTM1$:LOCATE0,V:GOTO4040 ELSEPRINTM2$;M3$;:LOCATE0,V:GOTO4040
4175 IFZ$=CHR$(21)ANDLEN(A$)>1THENGOSUB33:Z$=INKEY$:PRINTMID$(A$,H+1);" ";:GOSUB37:A$=LEFT$(A$,H-1)+MID$(A$,H+1):GOTO4172 ELSEIFZ$=CHR$(21)ANDLEN(A$)=1THENGOSUB33:PRINTMID$(A$,H+1);" ";:GOSUB37:A$=MID$(A$,H+1):GOTO4172
4176 IFZ$=CHR$(93)ANDLEN(A$)<CR-2THENGOSUB33:Z$=INKEY$:PRINT" ";MID$(A$,H);:GOSUB37:A$=LEFT$(A$,H-1)+" "+MID$(A$,H):GOTO4172
4177 IFINSTR(Z1$,Z$)=0THEN4179
4178 IF(H>1 ANDH<CR-2)THENA$=LEFT$(A$,H-1)+Z$+MID$(A$,H+1):H=H+1:LOCATE1,V:PRINTA$; ELSE IF(H=1ANDH<CR-2)THENA$=Z$+MID$(A$,H+1):H=H+1:LOCATE1,V:PRINTA$;
4179 GOSUB37:GOTO4172
4180 IF(PP+1)*LP<N THENPP=PP+1:GOTO4020 ELSESOUND100,5:GOTO4040
4190 IFPP*LP>0THENPP=PP-1:GOTO4020 ELSESOUND100,5:GOTO4040
4200 GOTO4040
5000 '   PRINT   '
5001 CLS:PRINT"Screen or Printer?";:LINEINPUTY$:IFLEFT$(Y$,1)="P"ORLEFT$(Y$,1)="p"THENPR=1 ELSEPR=0
5002 PRINT"How many copies";:INPUTNC:FORQ=1TONC:CLS:Z$=M$(5):GOSUB39:PRINTPR$;
5004 LM=10:RM=10:LG=80-LM-RM:LP=55:PG=0:L1=0:SP=1:BF=0:B$="":LF=0:UL=0
5010 FORI=0TO N:IFI=N THENLF=1
5020 A$=B$+L$(I):B$="":IFLEFT$(A$,1)=";"THEN5500
5021 X1=INSTR(A$,"\"):IFX1=0THEN5030
5022 IF MID$(A$,X1+1,1)="0" THEN MID$(A$,X1,2)=CHR$(0)+CHR$(15) ELSE IF MID$(A$,X1+1,1)="1" THEN MID$(A$,X1,2)=CHR$(0)+CHR$(14) ' UNDERLINE ON & OFF
5023 IF MID$(A$,X1+1,1)="4" THEN MID$(A$,X1,2)=CHR$(27)+CHR$(14) ELSE IF MID$(A$,X1+1,1)="5" THEN MID$(A$,X1,2)=CHR$(27)+CHR$(15) ' ELONGATE ON & OFF
5024 IF MID$(A$,X1+1,1)="2" THEN MID$(A$,X1,2)=CHR$(27)+CHR$(31) ELSE IF MID$(A$,X1+1,1)="3" THEN MID$(A$,X1,2)=CHR$(27)+CHR$(32) ' BOLD ON AND OFF
5025 IF MID$(A$,X1+1,1)="B" THEN MID$(A$,X1,1)=CHR$(27) ' ITALICS ON & OFF
5029 GOTO5021
5030 LL=LEN(A$)
5032 IF LL>LG THEN 5040
5033 X=INSTR(A$,CHR$(126)):IFX>0THENMID$(A$,X,1)=CHR$(32):B$=""
5034 GOTO5055
5040 B$=MID$(A$,LG+1):A$=LEFT$(A$,LG)
5041 IFRIGHT$(A$,1)<>" "THENB$=RIGHT$(A$,1)+B$:A$=LEFT$(A$,LEN(A$)-1):GOTO5041
5055 GOSUB5400
5056 X=INSTR(B$,CHR$(126)):IFX>0THENA$=B$:B$="":GOTO5030
5057 IF LEN(B$)>LG THEN A$=B$:B$="":GOTO5030
5060 NEXTI,Q
5070 ATTR 7,1:PRINT"Press any key to return to MENU";:EXEC44539
5399 GOTO100
5400 GOSUB33:LOCATELM,V:POKE&HFFD8,0:PRINTA$
5410 L1=L1+SP:IFSP=2THENPRINT ELSE IFSP=3THENPRINT:PRINT
5411 IFPR=0THEN5490 ELSEPRINT#-2,TAB(LM);A$
5415 IFSP=2THENPRINT#-2 ELSEIFSP=3THENPRINT#-2:PRINT#-2
5419 '  USE LINE 5420 TO PRINT PAGE NUMBERS, OR USE LINE 5425 FOR NO NUMBERS.
5420 ' IF(L1>=LP OR LF=1)THENPRINT#-2:L1=L1+1:IFL1=59THENPRINT#-2,TAB(35);"-";PG+1;"-":PRINT#-2,CHR$(12):L1=1:PG=PG+1:LF=0:ELSE 5420
5425 IF L1=LP THEN PRINT#-2,CHR$(12):L1=1 
5490 POKE&HFFD9,0:RETURN
5500 '  PRINTER COMMANDS  '
5510 X=INSTR(A$,"LM="):IFX>0THENLM=VAL(MID$(A$,X+3,2)):IFLM>20THENLM=20
5520 X=INSTR(A$,"RM="):IFX>0THENRM=VAL(MID$(A$,X+3,2)):IFRM>20THENRM=20
5530 X=INSTR(A$,"LP="):IFX>0THENLP=VAL(MID$(A$,X+3,2)):IFLP>66THENLP=66
5540 X=INSTR(A$,"PG="):IFX>0THENPG=VAL(MID$(A$,X+3,2))
5550 X=INSTR(A$,"SP="):IFX>0THENSP=VAL(MID$(A$,X+3,2)):IFSP>3THENSP=3
5555 X=INSTR(A$,";CL"):IF X=0THEN 5560
5556 IFLEN(A$)-3>=LG THENA$=MID$(A$,4):GOTO5030 ELSE C=(LG-LEN(A$)+4)/2:A$=MID$(CT$,1,C)+MID$(A$,4):GOTO5030
5560 X=INSTR(A$,";CE"):IF X=0THEN5570
5561 IF LEN(A$)-3>=LG/2 THENA$=MID$(A$,4):GOTO5030 
5562 C=(LG/2-LEN(A$)+4)/2:A$=CHR$(27)+CHR$(14)+MID$(CT$,1,C)+MID$(A$,4)+CHR$(27)+CHR$(15):GOTO5030
5570 X=INSTR(A$,";N"):IF X>0 AND PR=1 THEN PRINT#-2, CHR$(12) 
5575 LG=80-LM-RM
5580 GOTO5060
6000 ' RESUME '
6010 CLS:H=0:V=0:Z$=M$(6):GOSUB39:PRINTPR$;
6020 FORI=0TON-1:PRINTL$(I):NEXT:PRINTL$(N);:POKE282,0:GOSUB33:GOSUB20:POKE282,255:GOTO100
7000 ' KILL '
7005 POKE&HFFD8,0
7010 CLS:Z$=M$(7):GOSUB39:PRINTPR$;:DIR:PRINT
7020 PRINT"Enter Filename/Ext:";:LINEINPUTF$:GOSUB70:IFUN=1THENUN=0:GOTO7010
7030 ATTR1,1:PRINT:PRINT"Are you sure?";:INPUTS$:IFLEFT$(S$,1)<>"Y"THEN7010
7040 ATTR7,1:PRINT"KILLING "+F$:KILL F$:POKE&HFFD9,0:GOTO100
8000 ' DIR '
8010 CLS:POKE&HFFD8,0:Z$=M$(8):GOSUB39:PRINTPR$;:DIR:PRINT
8020 ATTR7,1:PRINT"Press any key to return to MENU":EXEC44539:POKE&HFFD9,0:GOTO100
9000 POKE&HFFD8,0:POKE282,255:STOP
10000 PRINT"ERROR #";ERNO;"IN LINE";ERLIN:GOTO9000
