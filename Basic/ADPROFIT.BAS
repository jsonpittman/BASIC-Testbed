1 'ADVERTISING PROFIT PREDICTOR
2 'by Bill Bernico
3 B$=STRING$(32,140)
4 BB$=STRING$(32,131)
5 CLS:INPUT"HOW MANY SUBSCRIBERS DOES THIS  PUBLICATION HAVE";S
6 PRINTB$;:LINEINPUT"WHAT IS THE SELLING PRICE OF    YOUR PRODUCT $";SF$
7 SF=VAL(SF$)
8 PRINT B$;:LINEINPUT"HOW MUCH DOES EACH UNIT COST YOUTO PRODUCE $";CP$
9 CP=VAL(CP$)
10 PRINT B$;:LINE INPUT"WHAT IS THE PRICE OF AN AD IN   THIS PUBLICATION $";AD$
11 AD=VAL(AD$)
12 PRINT B$;:PRINT"YOUR PROFIT PER UNIT IS $";SF-CP
13 PRINT"(LESS THE PRICE OF THE AD)"
14 PRINT B$;
15 PRINT@483,"HIT ANY KEY FOR STATISTICS";
16 FOR X=1504 TO 1535
17 POKE X,PEEK(X)-64:NEXT X
18 IF INKEY$=""THEN 18
19 CLS:PRINT@7,"AVAILABLE OPTIONS
20 FOR X=1024 TO 1055
21 POKE X,PEEK(X)-64:NEXT X
22 PRINT BB$;:PRINT"1. ESTIMATE THE PERCENTAGE         OF BUYERS YOU THINK YOU         CAN SELL YOUR PRODUCT TO
23 PRINT B$;:PRINT"2. ESTIMATE POTENTIAL NET          PROFIT YOU CAN MAKE IF          YOU ADVERTISE YOUR              PRODUCT.
24 PRINT B$;:PRINT"3. ESTIMATE NUMBER OF              POTENTIAL BUYERS FOR            YOUR PRODUCT.
25 PRINT B$;:PRINT@490,"SELECT (1-3)";
26 FOR X=1504 TO 1535
27 POKE X,PEEK(X)-64:NEXT X
28 A$=INKEY$:IF A$=""THEN 28
29 A=VAL(A$):ON A GOTO 31,40,55
30 GOTO 28
31 A=SF-CP:CLS:PRINT"WHAT PER CENTAGE OF THE"S
32 INPUT"READERS DO YOU THINK YOU CAN    SELL";PS
33 PB=(S*PS)/100
34 PRINT B$;:PRINT"IN ORDER TO CAPTURE"PS;"%"
35 PRINT"OF THE MARKET, YOU'LL HAVE TO   SELL";INT(PB);"UNITS. YOUR PROFIT
36 PRINT"WILL BE $";PB*A
37 PRINT"MINUS $"AD"FOR THE AD
38 PRINT"NETTING YOU $"(PB*A)-AD
39 PRINT BB$;:GOTO 67
40 PR$="###.#":CLS
41 LINE INPUT"WHAT PROFIT FIGURE ARE YOU      AIMING FOR $";PF$
42 PF=VAL(PF$)
43 A=SF-CP
44 TP=INT(PF/A)
45 PR=(TP/S)*100
46 PRINT B$;:PRINT"IN ORDER TO MAKE $";PF
47 PRINT"YOU'LL NEED TO SELL"TP"UNITS
48 PRINT"OR";
49 PRINT USING PR$;PR;
50 PRINT"% OF THE READERS.
51 PRINT"DEDUCT $"AD"FOR THE AD
52 PRINT"AND YOU STILL NET $"PF-AD
53 PRINT BB$;
54 GOTO 67
55 PR$="###.#":A=SF-CP:CLS
56 INPUT"HOW MANY UNITS DO YOU THINK YOU CAN REALISTICALLY SELL";NB
57 PRINT B$;
58 PRINT"IF YOU'RE ABLE TO SELL"NB
59 PRINT"UNITS, YOU WILL HAVE SOLD TO
60 PRINT USING PR$;(NB/S)*100;
61 PRINT"% OF THE READERS.
62 PRINT"YOUR PROFIT ON THIS TRANSACTION
63 PRINT"WILL BE $"NB*A
64 PRINT"MINUS $"AD"FOR THE AD
65 PRINT"NET PROFIT IS $"(NB*A)-AD
66 PRINT BB$;
67 PRINT@481,"nEW FIGURES  sAME FIGURES  eND";
68 F$=INKEY$:IF F$=""THEN 68
69 IF F$="N"THEN 5
70 IF F$="S"THEN 19
71 IF F$="E"THEN CLS:END
72 GOTO 68
