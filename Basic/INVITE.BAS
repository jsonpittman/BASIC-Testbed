0 ' COPYRIGHT 1989  FALSOFT,INC
9 CLS:PRINT"    'invite'     BY R.J.MCCORKLE     1-25-87
10 CLEAR3500:DIMN$(33),F(9,33),NJ$(33),F$(9,2),NN$(60)
15 Z=169:B=96:T=32:P=&H421
20 E$=CHR$(27)'PRNTR.ESCAPE CODE
22 FT$(1)="1 PICA":FT$(2)="2 ELITE":FT$(8)="8 UNDERLINE"
25 PRINT@196," A) DMP 105 PRINTER ";:PRINT@260," B) DMP 130 PRINTER ";:PRINT@324," C) EPSON RX-80 PRINTER ";
30 K$=INKEY$:IFK$<"A"ORK$>"C"THEN30
35 ON INSTR(1,"ABC",K$)GOSUB8050,9000 ,8000
40 CLS:PRINT" BAUD":PRINT:PRINT" 1) 9600":PRINT" 2) 4800":PRINT" 3) 2400":PRINT" 4) 1200":PRINT" 5) 600"
45 K$=INKEY$:IFK$=""THEN45ELSEK=VAL(K$):IFK<1ORK>5THEN45ELSEIFK=1THENF=1ELSEIFK=2THENF=7ELSEIFK=3THENF=18ELSEIFK=4THENF=40ELSEF=87
50 POKE149,0:POKE150,F
75 CLS:PRINT"    'INVITE'  MAIN MENU":PRINT
80 PRINT" C) CREATE INVITATION":PRINT:PRINT" R) RETRIEVE FROM TAPE/DISK":PRINT:PRINT" E) EDIT INVITATION":PRINT:PRINT" P) PRINT INVITATION":PRINT:PRINT" S) SAVE ON TAPE/DISK":PRINT:PRINT" V) VIEW INVITATION
82 PRINT:PRINT" Q) QUIT PROGRAM"
85 K$=INKEY$:IF K$="" THEN85
90 ON INSTR(1,"CREPSVQ",K$) GOTO100,200,300,400,500,600,700:SOUND4,4:GOTO85
100 CLS3:PRINT"   CREATE INVITATION":GOSUB900:IFK$="N"THEN75
102 CLS3
103 C=C+1:IF C=34 THEN PRINT"END OF INVITATION SPACE":C=33:GOSUB960:GOTO75
106 LC=0:PRINT"LINE"C;:INPUT N$(C)
109 IF N$(C)="" THEN NJ$(C)="":GOTO103
112 CLS3:PRINTC;:PRINTN$(C):L=LEN(N$(C))
115 PRINT@64,"";:FORF=1 TO 9:PRINTFT$(F):NEXTF
118 PRINT"n NEXT LINE":PRINT"e END":PRINT"R RE-ENTER LINE
121 ONW GOTO 315,805
124 K$=INKEY$:K=VAL(K$):IF K$="" THEN124 
127 IF K$="R" THEN106
130 Q=P+K*32:IF PEEK(Q)=B THEN POKEQ,Z ELSE POKEQ,B 'MARK MENU
133 IF K=1 THENPOKEQ+T,B:POKEQ+64,B
136 IF K=2 THENPOKEQ-T,B:POKEQ+T,B
139 IF K=3 THENPOKEQ-64,B:POKEQ-T,B
142 IF PEEK(P+T)<>Z AND PEEK(P+64)<>Z AND PEEK(P+B)<>Z THEN POKEP+T,Z
145 IF K$<>"E" AND K$<>"N" THEN124
148 IF PEEK(P+192)=Z THEN L=L*2
150 FORF=1TO9:F(F,C)=0:NEXTF
151 IF PEEK(P+T)=Z THEN IFL>38THENLC=L:GOTO183 ELSE FORF=1TO19-L/2:PRINT@458,F;:P$=P$+" ":NEXTF:FORF=1TO40-L:PRINT@458,F;:P1$=P1$+" ":NEXTF:F(1,C)=1
154 IFPEEK(P+T*2)=Z THENLC=L/1.2:IFLC>38THEN183ELSEFORF=1TO23-L/2:PRINT@458,F;:P$=P$+" ":NEXTF:P$=F$(2,1)+P$:FORF=1TO48-L:PRINT@458,F;:P1$=P1$+" ":NEXTF:P2$=F$(2,0):F(2,C)=1
157 IFPEEK(P+T*3)=Z THENLC=L/1.68:IFLC>38THEN183ELSEFORF=1TO32-L/2:PRINT@458,F;:P$=P$+" ":NEXTF:P$=F$(3,1)+P$:FORF=1TO69-L:PRINT@458,F;:P1$=P1$+" ":NEXTF:P2$=F$(3,0):F(3,C)=1
160 IF PEEK(P+T*4)=Z THEN P$=F$(4,1)+P$:P2$=P2$+F$(4,0):F(4,C)=1
163 IF PEEK(P+T*5)=Z THEN P$=P$+F$(5,1):P2$=P2$+F$(5,0):F(5,C)=1
166 IF PEEK(P+T*6)=Z THEN P$=P$+F$(6,1):P1$=F$(6,0)+P1$+F$(6,1):P2$=P2$+F$(6,0):F(6,C)=1
169 IF PEEK(P+T*7)=Z THEN P$=P$+F$(7,1):P2$=P2$+F$(7,0):F(7,C)=1
172 IF PEEK(P+T*8)=Z THEN P$=P$+F$(8,1):P1$=F$(8,0)+P1$+F$(8,1):P2$=P2$+F$(8,0):F(8,C)=1
175 IF PEEK(P+T*9)=Z THEN P$=P$+F$(9,1):P2$=P2$+F$(9,0):F(9,C)=1
178 NJ$(C)=P$+N$(C)+P1$+N$(C)+P2$
181 P$="":P1$="":P2$="":IF N$(C)="" THEN NJ$(C)=""
183 IFLC>38THENPRINT:PRINT"THIS LINE IS TOO LONG":PRINT"ENTER IT AGAIN (TRY USING COMP.)":GOSUB960:CLS3:PRINTN$(C):PRINT:GOTO106
184 ONW GOTO 330,815
187 IF C=33 THEN75 ELSE102
200 CLS3:PRINT"   RETRIEVE FROM TAPE/DISK":GOSUB900:IFK$="N"THEN75
203 GOSUB940:IFK$="D"THEN230
205 PRINT:PRINT"POSITION TAPE":PRINT:PRINT"PRESS play":PRINT:GOSUB990:IFNI$="S"THEN200ELSEGOSUB995
210 OPEN"I",-1,NI$
215 IF EOF(-1) THEN225
220 C=C+1:INPUT#-1,N$(C),F(1,C),F(2,C),F(3,C),F(4,C),F(5,C),F(6,C),F(7,C),F(8,C),F(9,C):GOSUB800:GOTO215
225 CLOSE:CLS7:PRINT"PRESS STOP":PRINT:GOSUB960:GOTO75
230 CLS3:PRINT" INSERT DISK IN DRIVE 0":PRINT:GOSUB980:IFNI$="S"THEN200
235 OPEN"I",1,NI$+"/INV":GOSUB995
240 IF EOF(1) THEN250
245 C=C+1:INPUT#1,N$(C),F(1,C),F(2,C),F(3,C),F(4,C),F(5,C),F(6,C),F(7,C),F(8,C),F(9,C):GOSUB800:GOTO240
250 CLOSE1:PRINT:GOSUB960:GOTO75
300 CLS3:PRINT"     EDIT INVITATION":GOSUB900:IFK$="N"THEN75
305 W=1:FOR C=1 TO 33
310 NJ$(C)="":GOTO112
315 FOR J=1 TO 9:IF F(J,C)=1 THEN POKEP+32*J,Z
320 NEXTJ
325 GOTO124
330 IFK$="E"THENC=33
335 NEXTC:W=0:GOTO75
400 CLS3:PRINT"    PRINT INVITATION":GOSUB900:IFK$="N"THEN75
402 IFC=0THENPRINT:PRINT" THERE IS NO INVITATION IN YET":PRINT:GOSUB960:GOTO75
405 PRINT"ALLIGN PAPER PERFORATION":PRINT"WITH TOP OF PRINT LINE"
410 PRINT:PRINT" N) NAMES  OR  W) WITHOUT NAMES"
415 K$=INKEY$:IFK$=""THEN415 ELSEONINSTR(1,"NW",K$)GOTO425,465:SOUND4,2:GOTO415
425 CLS3:INPUT"ON WHAT LINE NUMBER DO YOU WANT THE NAME TO APPEAR (1-33)";LN:IFLN<1ORLN>33THEN425
430 H=H+1:INPUT"NAME OR xx TO END";NN$(H):IF NN$(H)<>"XX" THEN430
435 NN$(H)="":H=H-1:IF H/2<>INT(H/2) THEN H=H+1
440 CLS3:PRINT@233,"PRINTING";
445 FORJ=1 TO H/2:T$="":T1$="":FORF=1TO19-LEN(NN$(J))/2:T$=T$+CHR$(32):NEXTF
447 FORF=1TO40-LEN(NN$(J))/2-LEN(NN$(J+H/2))/2:T1$=T1$+CHR$(32):NEXTF
448 NJ$(LN)=T$+NN$(J)+T1$+NN$(J+H/2)
450 FOR D=1 TO 33:PRINT#-2,NJ$(D):NEXT D
455 NEXTJ
460 GOTO75
465 CLS3'   PRINT NO NAMES
470 INPUT"HOW MANY PAGES";A:PRINT:PRINT"PRINTING"
475 FORG=1 TO A*2:FORJ=1 TO 33:PRINT#-2,NJ$(J):NEXTJ:NEXTG
480 GOTO75
500 CLS3:PRINT"   SAVE TO TAPE/DISK":GOSUB900:IFK$="N"THEN75
503 GOSUB940:IFK$="D"THEN540
505 PRINT"POSITION TAPE":PRINT:PRINT"PRESS PLAY & RECORD":PRINT
510 GOSUB990:IFNI$="S"THEN500
515 OPEN"O",-1,NI$
520 GOSUB997
525 FORJ=1 TO 33:PRINT#-1,N$(J),F(1,J),F(2,J),F(3,J),F(4,J),F(5,J),F(6,J),F(7,J),F(8,J),F(9,J):NEXTJ
530 CLOSE:CLS0:PRINT"PRESS STOP":PRINT:GOSUB960:GOTO75
540 PRINT"DISK":GOSUB980:IFNI$="S"THEN500
545 OPEN"O",1,NI$+"/INV":FORJ=1 TO 33:PRINT#1,N$(J)CHR$(13);:FORG=1TO9:PRINT#1,F(G,J)CHR$(13);:NEXTG:NEXTJ
550 CLOSE:GOTO75
600 CLS3:PRINT"   VIEW INVITATION":GOSUB900:IFK$="N"THEN75
605 FORJ=1TO33:IF LEN(N$(J))<31 THENPRINTTAB((32-LEN(N$(J)))/2);
610 PRINTN$(J):FORG=1TO200:NEXTG:NEXT
615 GOSUB960:GOTO75
620 CLS3'    DESK CHECK STRING
625 INPUT"LINE #";U
630 FORFJ=1 TO LEN(NJ$(U)):PRINTASC(MID$(NJ$(U),FJ,1));:NEXTFJ
635 GOTO625
700 CLS:PRINT:PRINT" DO YOU WANT TO SAVE THE":PRINT:PRINT"  INVITATION ? ";:GOSUB910:IFK$="N"THENSTOP
710 GOTO500
800 W=2:NJ$(C)="":GOTO112
805 FORJ=1TO9:IFF(J,C)=1THEN POKEP+32*J,Z
810 NEXTJ:GOTO148
815 W=0:RETURN
900 PRINT:PRINT"   CONTINUE ?";
910 PRINT"  Y / N"
915 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN915ELSERETURN
940 PRINT:PRINT" T = TAPE     D = DISK"
950 K$=INKEY$:IFK$<>"T"ANDK$<>"D"THEN950ELSERETURN
960 PRINT"  HIT ANY KEY TO CONTINUE":EXEC44539:RETURN
980 PRINT
990 PRINT:PRINT" ENTER NAME OF INVITATION OR":PRINT"  ENTER 'S' TO STOP":PRINT:INPUTNI$:RETURN
995 C=0:CLS3:PRINT@198," RETRIEVING ";
996 PRINT@265,NI$;:PRINT@320,"";:RETURN
997 CLS3:PRINT@198," RECORDING ";:GOTO996
8000 '  EPSON RX-80
8005 F$(1,1)="":F$(1,0)=""  'PICA
8010 F$(2,1)=E$+"M":F$(2,0)=E$+"P"  'ELITE
8015 F$(3,1)=E$+"F"+CHR$(15):F$(3,0)=CHR$(18):FT$(3)="3 COMPRESSED"
8020 F$(4,1)=E$+"E":F$(4,0)=E$+"F":FT$(4)="4 EMPHASIZED"
8025 F$(5,1)=E$+"G":F$(5,0)=E$+"H":FT$(5)="5 DOUBLESTRIKE"
8030 F$(6,1)=CHR$(14):F$(6,0)=CHR$(20):FT$(6)="6 EXPANDED"
8035 F$(7,1)=E$+"4":F$(7,0)=E$+"5":FT$(7)="7 ITALICS"
8040 F$(8,1)=E$+"-1":F$(8,0)=E$+"-0"' UNDERLINE
8045 F$(9,1)=E$+"S0":F$(9,0)=E$+"T":FT$(9)="9 SUPERSCRIPT"
8047 RETURN
8050 '   DMP 105
8055 F$(1,1)=E$+CHR$(19):F$(1,0)="" '     PICA
8060 F$(2,1)=E$+CHR$(23):F$(2,0)=E$+CHR$(19)'       ELITE
8065 F$(3,1)=E$+CHR$(20):F$(3,0)=E$+CHR$(19):FT$(3)="3 CONDENSED"
8070 F$(4,1)=E$+CHR$(31):F$(4,0)=E$+CHR$(32):FT$(4)="4 BOLD"
8075 F$(5,1)="":F$(5,0)="":FT$(5)=""
8080 F$(6,1)=E$+CHR$(32)+E$+CHR$(14):F$(6,0)=E$+CHR$(15):FT$(6)="6 ELONGATED"
8090 F$(8,1)=CHR$(15):F$(8,0)=CHR$(14)'  UNDERLINE
8095 F$(9,1)="":F$(9,0)="":FT$(9)=""
8097 RETURN
9000 GOSUB8050'     DMP 130
9005 F$(9,1)=E$+CHR$(83)+CHR$(0):F$(9,0)=E$+CHR$(88):FT$(9)="9 SUPERSCRIPT"
9010 F$(7,1)=E$+CHR$(66)+CHR$(1):F$(7,0)=E$+CHR$(66)+CHR$(0):FT$(7)="7 ITALICS"
9015 F$(5,1)=E$+CHR$(18):F$(5,0)=E$+CHR$(19):FT$(5)="5 NLQ-PICA"
9020 RETURN
