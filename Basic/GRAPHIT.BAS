0 '         GRAFIT
1 '         =======
2 '     By Michael G. Sims
3 '     5 Skinner Ct.
4 '     Tomkins Cove, NY 10986
5 '
6 GOSUB 10000
10 DIM TEST(255),L$(97):FOR LE=48TO57:L=L+1:READL$(LE):NEXT:FORLE=65TO90:L=L+1:READL$(LE):NEXT
20 RNUM=0
30 IF TIMER<50 THEN 40 ELSE GOSUB8020
40 CLS:PRINT@0,"GRAFIT":PRINT@8,"FN="FILE$:PRINT@20,"NF="RNUM:PRINT@32,STRING$(32,204);
50 PRINT:PRINTTAB(7);"MENU:"
60 PRINTTAB(7);"<1> ADD DATA"
70 PRINTTAB(7);"<2> SAVE DATA"
80 PRINTTAB(7);"<3> LOAD DATA"
90 PRINTTAB(7);"<4> GRAPH DATA"
100 PRINTTAB(7);"<5> NAME GRAPH"
105 PRINTTAB(7);"<6> PRINT DATA"
106 PRINTTAB(7);"<N> NEW MEMORY"
110 A$=INKEY$:IFA$=""THEN110
120 IF A$="N" THEN TIMER=0:RUN
130 A=VAL(A$):IFA<1 OR A>6 THEN GOSUB 6080:FORDL=1TO1000:NEXTDL:GOTO40
140 ON A GOTO 1000,2000,3000,4000,5000,9000
1000 '
1010 CLS:PRINT@0,"ADD DATA":PRINT@32,STRING$(32,204)
1020 FOR X=RNUM+1 TO 255
1030 PRINT"ITEM"X":";:LINEINPUTTEST$
1040 IF TEST$="" THEN RNUM=X-1:GOTO40
1050 IF ASC(TEST$)>57 THEN GOSUB 6070:GOTO1030
1060 IF LEN(TEST$)>9 THEN GOSUB 6020:GOTO 1030
1070 TEST(X)=VAL(TEST$)
1080 IF TEST(X)<0 THEN GOSUB6000:GOTO 1030
1090 NEXT X
1100 GOSUB 6010:GOTO 40
2000 IFRNUM=0THENGOSUB6060:GOTO40 ELSE CLS:PRINT@0,"SAVE DATA":PRINT@32,STRING$(32,204);
2010 PRINT
2020 PRINT"FILENAME:";:LINEINPUTFILE$
2030 IF FILE$="" THEN 40 ELSE IF LEN(FILE$)>8 THEN GOSUB 6040:GOTO2020
2040 OPEN"D",#1,FILE$+"/GPH",33
2050 FOR RUM=1 TO RNUM
2060 PRINT#1,TEST(RUM):PUT#1,RUM
2070 NEXT RUM
2080 PRINT#1,NAME$:PUT#1:PRINT#1,BOT$:PUT#1:PRINT#1,SIDE$:PUT#1
2090 CLOSE#1:GOTO40
3000 CLS:PRINT@0,"LOAD DATA":PRINT@32,STRING$(32,204);
3010 PRINT
3020 PRINT"FILENAME:";:LINEINPUTFILE$
3030 IF FILE$="" THEN 40 ELSE IF LEN(FILE$)>8 THEN GOSUB6040:GOTO2020
3040 OPEN"D",#1,FILE$+"/GPH",33
3050 FOR RNUM=1 TO LOF(1)-3
3060 GET#1:INPUT#1,TEST(RNUM)
3070 NEXT RNUM
3080 GET#1:INPUT#1,NAME$:GET#1:INPUT#1,BOT$:GET#1:INPUT#1,SIDE$
3090 CLOSE#1:GOTO40
4000 POKE178,3:IF RNUM=0 THEN GOSUB 6060:GOTO40 ELSE IF NAME$="" THEN F1=1:GOSUB 6050:GOTO5000
4005 PRINT:PRINT"LINE OR BAR? (L/B)"
4006 A$=INKEY$:IFA$=""THEN4006 ELSE IF A$="L" THEN PRINT ELSE IF A$="B" THEN PRINT ELSE GOSUB 6080:GOTO4006
4010 R5=0:X=0:PMODE4,1:PCLS:SCREEN1,1
4020 X=123-((7*LEN(NA$))/2):FORM=1TOLEN(NA$):X=X+7:DRAW"BM"+STR$(INT(X))+",10;"+L$(ASC(MID$(NA$,M,1))):NEXT
4030 Y=97-((10*LEN(SI$))/2):FORM=1TOLEN(SI$):Y=Y+9:DRAW"BM2,"+STR$(INT(Y))+";"+L$(ASC(MID$(SI$,M,1))):NEXT
4040 X=123-((7*LEN(BO$))/2):FORM=1TOLEN(BO$):X=X+7:DRAW"BM"+STR$(INT(X))+",189;"+L$(ASC(MID$(BO$,M,1))):NEXT
4050 LINE(0,15)-(255,15),PSET
4060 FOR Y=191 TO 20 STEP -5
4070 LINE(9,Y)-(12,Y),PSET
4080 NEXTY
4090 FORX=0TO255STEP5:LINE(X,179)-(X,176),PSET:NEXTX
4100 T=0
4110 S=255/RNUM
4120 ZZ=TEST(1):FORZQ=1TORNUM:IFTEST(ZQ)>ZZ THEN ZZ=TE(ZQ)
4130 NEXT ZQ
4140 N=151/ZZ
4150 LINE(15,171-(TE(1)*N))-(15,171-(TE(1)*N)),PSET
4155 IF A$="B" THEN POKE178,2
4160 IF A$="L" THEN FOR X=15 TO 255 STEP S ELSE FOR X=15 TO 250 STEP S
4170 R5=R5+1
4175 IF R5=RNUM THEN 4200
4180 IF A$="L" THEN LINE-(X,171-(TE(R5)*N)),PSET ELSE LINE(X,170)-(X+(S-5),171-(TE(R5)*N)),PSET,BF
4190 NEXT X
4200 IFINKEY$=""THEN4200ELSEGOTO40
5000 CLS:PRINT"NAME GRAPH":PRINT@32,STRING$(32,204)
5010 PRINT
5020 PRINT"ENTER TITLE OF GRAPH:"
5030 LINEINPUT">";NAME$
5040 IF NAME$="" THEN NA$=" ":GOTO40 ELSE IF LEN(NAME$)>32 THEN GOSUB6040:GOTO5030
5050 PRINT"BOTTOM LABEL:":LINEINPUT">";BOT$:IFBOT$=""THENBO$=" ":GOTO40 ELSE IF LEN(BOT$)>32 THENGOSUB6040:GOTO5050
5060 PRINT"SIDE LABEL:":LINEINPUT">";SIDE$:IFSIDE$=""THENSI$=" ":GOTO40 ELSE IF LEN(SIDE$)>15 THEN GOSUB 6040:GOTO 5060
5070 IF F1=1 THEN F1=0:GOTO4000 ELSE GOTO40
5080 GOTO5080
6000 PRINT"error:NUMBER TOO SMALL":SOUND 200,3:RETURN
6010 PRINT"error:FILE TOO LARGE":SOUND200,3:FORDL=1TO1000:NEXTDL:RETURN
6020 PRINT"error:NUMBER TOO LARGE":SOUND200,3:RETURN
6030 PRINT"error:NAME TOO SHORT":SOUND200,3:RETURN
6040 PRINT"error:NAME TOO LONG":SOUND200,3:RETURN
6050 PRINT"error:NO TITLE":SOUND200,3:FORDL=1TO1000:NEXTDL:RETURN
6060 PRINT"error:MEMORY EMPTY":SOUND200,3:FORDL=1TO1000:NEXTDL:RETURN
6070 PRINT"error:NO ALPHA DATA ALLOWED":SOUND 200,3:RETURN
6080 PRINT"error:SO SUCH CHOICE":SOUND200,3:RETURN
7000 '
7010 DATABRHU4ERFD4GNLBR2
7020 DATAR2U6NGD6R2
7030 DATABU5ER2FDGL2GD2R4
7040 DATABU5ER2FDGNLFDGL2NHBR3
7050 DATABR3U6G3R4BD3
7060 DATABUFR2EU2HL3U2R4BD6
7070 DATABU3R3FDGL2HU4ER2BD6BR
7080 DATABU6R4DG3D2BR3
7090 DATABRHUER2EUHL2GDFR2FDGNL2BR
7100 DATABRR2EU4HL2GDFR3BD3
7110 DATAU5ER2FD2NL4D3
7120 DATARU6NLR2FDGNL2FDGNL3BR
7130 DATABR4BU5HL2GD4FR2EBD
7140 DATARU6NLR2FD4GNL2BR
7150 DATAU6NR4D3NR3D3R4
7160 DATAU3NR3U3R4BD6
7170 DATABUU4ER3BD4NLD2L3NHR3
7180 DATAU3NU3R4NU3D3
7190 DATAR2U6NL2NR2D6R2
7200 DATABUNUFR2ENU5BD
7210 DATAU3NU3RNE3F3
7220 DATANU6R4
7230 DATAU6F2DUE2D6
7240 DATAU6F4NU4D2
7250 DATABRHU4ER2FD4GNL2BR
7260 DATAU6R3FDGL3D3BR4
7270 DATABRHU4ER2FD4GNL2BUHF2
7280 DATAU6R3FDGL3RF3
7290 DATABUFR2EUHL2HUER2FBD5
7300 DATABU6R4L2D6BR2
7310 DATABUNU5FR2ENU5BD
7320 DATABU6D4F2E2U4BD6
7330 DATANU6E2UDF2NU6
7340 DATAUE4NUG2H2NUF4D
7350 DATABU6DF2E2NUG2D3BR2
7360 DATABU6R4DG4DR4
8000 PMODE4,1:PCLS:SCREEN1,1
8010 READA$:X=X+5:DRAW"BM"+STR$(X)+",120;"+A$:GOTO8010
8020 F5=1:PMODE 4,1:PCLS:SCREEN1,1:POKE178,2:FORX=0TO20STEP2:LINE(X,0)-(X,191),PSET:LINE(256-X,0)-(256-X,191),PSET:LINE(0,X)-(255,X),PSET:LINE(0,191-X)-(255,191-X),PSET:NEXTX:POKE178,3
8021 G$(1)="BM+1,+0;R4E1UL3;BM-3,+0;DU4ER4F"
8022 G$(2)="U2R5FDUHEUH2L4D4":G$(3)="U2R6D2U4H2L2G2D2":G$(4)="U2R4L4U4R6":G$(5)="R6L3U6R3L6":G$(6)="BM+3,+0;U6R3L6"
8023 X=90:FORG=1TO6:X=X+10:DRAW"BM"+STR$(X)+",40;"+G$(G):NEXTG:LINE(85,47)-(170,27),PSET,B
8030 A$(1)="A GENERAL PURPOSE GRAPH":A$(2)="PROGRAM FOR THE TANDY":A$(3)="COLOR COMPUTER":Y=50:A$(4)=" ":A$(5)=" ":A$(6)="BY":A$(7)="MICHAEL SIMS"
8040 FOR A=1 TO 7
8050 X=0
8060 X=123-((LEN(A$(A))*7)/2)
8070 Y=Y+15
8080 FOR M=1 TO LEN(A$(A))
8100 X=X+7
8110 DRAW"BM"+STR$(INT(X))+","+STR$(Y)+";"+L$(ASC(MID$(A$(A),M,1)))
8115 PLAY"L255"
8120 NEXT M,A
8140 FOR Z=1TO1300:IFINKEY$=""THENNEXT ELSE 40
8150 GOTO 40
9000 IFRNUM=0THENGOSUB6060:GOTO40 ELSE CLS:PRINT"PRINT DATA":PRINT@32,STRING$(32,204);
9010 PRINT:PRINT"PRINTER OR SCREEN (P/S)"
9020 A$=INKEY$:IFA$=""THEN9020 ELSE IF A$="P" THEN B=-2 ELSE IF A$="S" THEN B=0 ELSE IF A$=CHR$(13) THEN 40 ELSE GOSUB 6080:GOTO9020
9030 IF B=-2 THEN PRINT"READY PRINTER & HIT <ENTER>"
9040 IFB=-2 THEN IFINKEY$=CHR$(13) THEN 9050 ELSE 9040
9050 PRINT#B,NAME$:PRINT#B,STRING$(LEN(NA$),"-"):FORX=1TORNUM:PRINTTEST(X),:NEXTX
9060 IF INKEY$=""THEN9060
9070 GOTO 40
10000 POKE150,40'set br to 1200
10010 RETURN
10 'GRAPHIT
20 'BY RICK ST. JOHN
30 'COPYRIGHT (C) 1992
40 'BY FALSOFT, INC.
50 'RAINBOW MAGAZINE
100 RGB:WIDTH40:CLS5:POKE65497,0
110 PALETTE9,8:PALETTE1,58
120 ATTR1,4
130 LOCATE7,2:PRINT"St. John Software Presents..."
140 LOCATE16,5:PRINT"-GRAPHIT-"
150 LOCATE12,8:PRINT"Copyright (C) 1988"
160 LOCATE13,10:PRINT"By Rick St. John"
170 ATTR7,4,B
180 LOCATE13,17:PRINT"V - Vector"
190 LOCATE13,19:PRINT"P - Points only";
200 ATTR3,4
210 PRINT
220 ATTR1,4
230 F$=INKEY$:IF F$="V" OR F$="v" THEN V=1 ELSE IF F$="P" OR F$="p" THEN V=0 ELSE 230
240 ATTR7,4,B
250 LOCATE13,17:PRINT"  Grid? (y/n)"
260 ATTR3,4
270 PRINT:PRINT
280 ATTR1,4
290 F$=INKEY$:IF F$="N" OR F$="n" THEN HCOLOR 1,4:HSCREEN 2:CLS:GOTO 390 ELSE IF F$<>"Y" AND F$<>"y" THEN 290
300 HCOLOR 1,4
310 HSCREEN 2
320 CLS
330 FOR X=5 TO 315 STEP 5
340 HLINE(X,0)-(X,191),PSET
350 NEXT X
360 FOR Y=6 TO 186 STEP 5
370 HLINE(0,Y)-(320,Y),PSET
380 NEXT
390 HCOLOR 8,4
400 HLINE(160,0)-(160,191),PSET
410 HLINE(0,96)-(320,96),PSET
420 HCOLOR 3,4
430 FOR X=-160 TO 0
440 GOSUB 650
450 IF Y=999 THEN 470 ELSE D$="BM"+STR$(160+X)+","+STR$(INT(96+Y))
460 DRAW D$:G=X:GOTO 480
470 NEXT X
480 FOR X=G TO 0
490 GOSUB 650
500 IF Y=999 THEN 520
510 IF V=1 THEN HLINE-(160+X,96+Y),PSET ELSE HSET(160+X,96+Y,7)
520 NEXT X
530 FOR X=0 TO 160
540 GOSUB 650
550 IF Y=999 THEN 570
560 IF V=1 THEN HLINE-(160+X,96+Y),PSET ELSE HSET(160+X,96+Y,7)
570 NEXT X
580 POKE 65496,0
590 F$=INKEY$:IFF$=CHR$(13) THEN RUN ELSE 590
600 '**************************
610 '*REPLACE LINE 650 WITH AN*
620 '*EQUATION IN TERMS OF Y&X*
630 '*EX: 650 Y=.025*X^2+X-80 *
640 '**************************
650 Y=90*SIN(300*X)
660 Y=-1*Y
670 IF Y+96<0 OR X+160<0 OR Y+96>191 OR X+160>320 THEN Y=999
680 RETURN
