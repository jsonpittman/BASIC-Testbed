1 'EASY COME EASY GO
2 'WRITTEN BY JAMES BUSH
3 'COPYRIGHT (C) APRIL 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLS:PRINT"      easy come / easy go":PRINT:PRINT:PRINT"A GAME OF WINNING AND LOSING.   (WHAT ELSE DO YOU DO IN GAMES?)":PRINT:PRINT:PRINT"HIGH SPEED POKE (Y/N)?"
15 G$=INKEY$
20 IFG$="Y" THEN POKE65495,0:GOTO 35
25 IFG$="N" THEN POKE65494,0:GOTO35
30 GOTO 15
35 PRINT@480,"DO YOU WANT INSTRUCTIONS (Y/N)?";
40 G$=INKEY$
45 IFG$="Y" THEN 1180
50 IFG$="N" THEN 60
55 GOTO 40
60 CLS:CLEAR2000:M=20:JJ=100
65 DIMN$(15),T$(2),L$(28),F$(8),A$(15),B$(15),A(15),D(53),E(8),H$(8)
70 GOSUB150
75 FORI=1TO15:A(I)=0:NEXTI:S=0
80 GOSUB 785
85 PRINT@480,"PRESS <ENTER> TO START GAME";
90 G$=INKEY$
95 IFG$=CHR$(13) THEN 105
100 GOTO 90
105 GOSUB935
110 GOTO200
115 DRAW"BM24,136;XL$(27);":DRAW"BM27,136;XL$(3);":DRAW"BM34,136;XL$(28);":DRAW"BM42,136;XL$(8);":DRAW"BM49,136;XL$(1);":DRAW"BM56,136;XL$(14);":DRAW"BM63,136;XL$(7);":DRAW"BM70,136;XL$(5);"
120 DRAW"BM84,136;XL$(3);":DRAW"BM91,136;XL$(1);":DRAW"BM98,136;XL$(18);":DRAW"BM105,136;XL$(4);"
125 DRAW"BM24,148;XL$(27);":DRAW"BM27,148;XL$(16);":DRAW"BM34,148;XL$(28);":DRAW"BM42,148;XL$(12);":DRAW"BM49,148;XL$(1);":DRAW"BM56,148;XL$(3);":DRAW"BM63,148;XL$(5);"
130 DRAW"BM77,148;XL$(2);":DRAW"BM84,148;XL$(5);":DRAW"BM93,148;XL$(20);"
135 DRAW"BM27,188;XL$(5);":DRAW"BM34,188;XL$(1);":DRAW"BM41,188;XL$(19);":DRAW"BM49,188;XL$(25);":DRAW"BM63,188;XL$(3);":DRAW"BM70,188;XL$(15);":DRAW"BM77,188;XL$(13);":DRAW"BM84,188;XL$(5);"
140 DRAW"BM98,188;XL$(5);":DRAW"BM105,188;XL$(1);":DRAW"BM112,188;XL$(19);":DRAW"BM120,188;XL$(25);":DRAW"BM134,188;XL$(7);":DRAW"BM141,188;XL$(15);"
145 GOTO305
150 N$(0)="BUU4ER2FD4GL2H":N$(1)="U4E2F2D2L3R3D2":N$(2)="BU5ER2FDGL2GD2R4":N$(3)="BR3L2HBU4ER2FDGLRFDG":N$(4)="BR3U6G3DR4":N$(5)="BR3L2HBU5R4L4D2R3FD2G":N$(6)="BR3L2HU4ER2FBD2BL3R2FDG"
155 N$(7)="BU6R4DG3D2":N$(8)="BR3L2HUEHUER2FDGL2R2FDG":N$(9)="BUFR2EU2L3HUER2FD4":N$(10)="R2LU5LRUBR5R2FD4GL2HU4E":N$(11)="UDR3U6L2R4":N$(12)="BR1HU4ER2FD4GLBUF2":N$(13)="U6BR4G3F3":N$(14)="R2LU6DLRBRBD5"
160 N$(15)="U2E1U2E1F1D2F1D2L4R5E5R1F2L1H1D1G1D1G1L8D3F1D2F1R2E1U2E1U3"
165 T$(1)="R20F2D30R1U30D30G2E2L1G2L20D1R20U1L20H2U30E2"
170 T$(2)="R20F2D30R1U30D30G2E2L1G2L20D1R20U1L21H2U30E2D34R2U34R2D34R2U34R2D34R2U34R2D34R2U34R2D34R2U34R2D34E2U30"
175 L$(1)="U4E2F2D2L3R3D2":L$(2)="U6R3F1D1G1L3R3F1D1G1L3":L$(3)="R3E1G1L3U6R3F1":L$(4)="R2E2G2L2U6R2F2D2":L$(5)="R4L4U3R3L3U3R4":L$(6)="U3R3L3U3R4":L$(7)="R3E1U1L1R1D1G1L3U6R3F1":L$(8)="U6D3R4U3D6":L$(9)="R4L2U6R2L4":L$(10)="U1D1R3U6R2L5"
180 L$(11)="U6D3E3G3F3":L$(12)="R4L4U6":L$(13)="U6F2E2D6":L$(14)="U6D1F4D1U6":L$(15)="U6R4D6L4":L$(16)="U6R3F1D1G1L3":L$(17)="R2E1H1F2H1U4H1L2G1D4F1":L$(18)="U6R3F1D1G1L3F3":L$(19)="R3E1U1H1L2H1U1E1R2"
185 L$(20)="U6R2L4":L$(21)="U6D6R4U6":L$(22)="H2U4D4F2E2U4":L$(23)="U6D6E2F2U6":L$(24)="U1E2F2D1U1H4U1D1F2E2U1":L$(25)="U3E2U1D1G2H2U1":L$(26)="R4L4U1E4U1L4":L$(27)="H3E3":L$(28)="E3H3"
190 AA$="T4O3L8DP32DP32DL4.GL4.BL8DP32DP32DL4.GL2.BP32":AB$="L4GP32L8GF#P32F#P32EP32EL2.DP32":AC$="L8DP32DP32DL4.F#L4AP32L8DP32DP32DL4.F#L2.AP32":AD$="O4L4DL8EP32DP32CP32O3BAL2G"
195 RETURN
200 PMODE4,1:COLOR0,1:PCLS(1):SCREEN1,1
205 IF A(13)=0 THEN DRAW"BM4,0;XT$(2);" ELSE DRAW"BM2,0;XT$(1);":DRAW"BM7,20;XA$(13);"
210 IF A(11)=0 THEN DRAW"BM126,0;XT$(2);" ELSE DRAW"BM125,0;XT$(1);":DRAW"BM130,20;XA$(11);"
215 IF A(12)=0 THEN DRAW"BM162,0;XT$(2);" ELSE DRAW"BM160,0;XT$(1);":DRAW"BM165,20;XA$(12);"
220 IF A(14)=0 THEN DRAW"BM4,44;XT$(2);" ELSE DRAW"BM2,44;XT$(1);":DRAW"BM7,64;XA$(14);"
225 IF A(7)=0 THEN DRAW"BM90,44;XT$(2);" ELSE DRAW"BM90,44;XT$(1);":DRAW"BM95,64;XA$(7);"
230 IF A(8)=0 THEN DRAW"BM126,44;XT$(2);" ELSE DRAW"BM125,44;XT$(1);":DRAW"BM130,64;XA$(8);"
235 IF A(9)=0 THEN DRAW"BM162,44;XT$(2);" ELSE DRAW"BM160,44;XT$(1);":DRAW"BM165,64;XA$(9);"
240 IF A(10)=0 THEN DRAW"BM196,44;XT$(2);" ELSE DRAW"BM195,44;XT$(1);":DRAW"BM200,64;XA$(10);"
245 IF A(15)=0 THEN DRAW"BM4,88;XT$(2);" ELSE DRAW"BM2,88;XT$(1);":DRAW"BM7,108;XA$(15);"
250 IF A(1)=0 THEN DRAW"BM55,88;XT$(2);" ELSE DRAW"BM55,88;XT$(1);":DRAW"BM60,108;XA$(1);"
255 IF A(2)=0 THEN DRAW"BM90,88;XT$(2);" ELSE DRAW"BM90,88;XT$(1);":DRAW"BM95,108;XA$(2);"
260 IF A(3)=0 THEN DRAW"BM126,88;XT$(2);" ELSE DRAW"BM125,88;XT$(1);":DRAW"BM130,108;XA$(3);"
265 IF A(4)=0 THEN DRAW"BM162,88;XT$(2);" ELSE DRAW"BM160,88;XT$(1);":DRAW"BM165,108;XA$(4);"
270 IF A(5)=0 THEN DRAW"BM196,88;XT$(2);" ELSE DRAW"BM195,88;XT$(1);":DRAW"BM200,108;XA$(5);"
275 IF A(6)=0 THEN DRAW"BM230,88;XT$(2);" ELSE DRAW"BM230,88;XT$(1);":DRAW"BM235,108;XA$(6);"
280 IFA$(N-1)=N$(15) THEN FORX=1TO1000:NEXTX:JJ=0:GOTO1065
285 IFA(12)<>0 THEN FORX=1TO1000:NEXTX:GOTO1145
290 IFM<1 THEN FORX=1TO1000:NEXTX:GOTO1065
295 IFM>50000000 THEN FORX=1TO1000:NEXTX:GOTO995
300 GOTO115
305 REM CHECK ACCOUNT
310 DRAW"BM27,160;XL$(1);":DRAW"BM34,160;XL$(3);":DRAW"BM41,160;XL$(3);":DRAW"BM48,160;XL$(15);":DRAW"BM55,160;XL$(21);":DRAW"BM62,160;XL$(14);":DRAW"BM71,160;XL$(20);":DRAW"BM78,156;R4;":DRAW"BM78,158;R4;":DRAW"BM87,160;XL$(19);":DRAW"BM89,161;U8;"
315 DRAW"BM87,160;"
320 U=INT(M)
325 FORV=1TO8
330 U=U*.1
335 IF U < 1 THEN 345
340 NEXTV
345 FORW=1TO(8-V):F$(W)=N$(0):NEXTW
350 IFV=3 THEN VV=6 ELSE IF V=4 THEN VV=5 ELSE IF V=5 THEN VV=4 ELSE IF V=6 THEN VV=3 ELSE IF V=7 THEN VV=2 ELSE IF V=8 THEN VV=1 ELSE IF V=2 THEN VV=7 ELSE IF V=1 THEN VV=8
355 FOR W = VV TO 8 STEP1:U=U*10
360 Z=FIX(U)
365 IFZ=0 THEN F$(W)=N$(0)
370 IFZ=1 THEN F$(W)=N$(14)
375 IFZ=2 THEN F$(W)=N$(2)
380 IFZ=3 THEN F$(W)=N$(3)
385 IFZ=4 THEN F$(W)=N$(4)
390 IFZ=5 THEN F$(W)=N$(5)
395 IFZ=6 THEN F$(W)=N$(6)
400 IFZ=7 THEN F$(W)=N$(7)
405 IFZ=8 THEN F$(W)=N$(8)
410 IFZ=9 THEN F$(W)=N$(9)
415 U=U-Z
420 NEXTW
425 DRAW"BM95,160;XF$(1);":DRAW"BM103,160;XF$(2);":DRAW"BM111,160;XF$(3);":DRAW"BM119,160;XF$(4);":DRAW"BM127,160;XF$(5);":DRAW"BM135,160;XF$(6);":DRAW"BM143,160;XF$(7);":DRAW"BM151,160;XF$(8);"
430 'REM WAIT FOR INPUT
435 H$=INKEY$
440 IFH$="C" OR H$="c" THEN SOUND200,1:GOTO1035
445 IFH$="P" OR H$="p" THEN SOUND200,1:GOTO455
450 GOTO435
455 DRAW"BM27,172;XL$(8);":DRAW"BM34,172;XL$(15);":DRAW"BM41,172;XL$(23);":DRAW"BM55,172;XL$(13);":DRAW"BM62,172;XL$(21);":DRAW"BM69,172;XL$(3);":DRAW"BM76,172;XL$(8);":DRAW"BM87,172;XL$(19);":DRAW"BM89,173;U8;"
460 FORX=1 TO 8:H$(X)=N$(0):E(X)=0:NEXTX
465 P=0
470 FORJ=1TO8
475 G$=INKEY$
480 IFG$="0" THEN E(J)=0:H$(J)=N$(0):SOUND150,1:GOTO740
485 IFG$="1" THEN E(J)=1:H$(J)=N$(14):SOUND150,1:GOTO740
490 IFG$="2" THEN E(J)=2:H$(J)=N$(2):SOUND150,1:GOTO740
495 IFG$="3" THEN E(J)=3:H$(J)=N$(3):SOUND150,1:GOTO740
500 IFG$="4" THEN E(J)=4:H$(J)=N$(4):SOUND150,1:GOTO740
505 IFG$="5" THEN E(J)=5:H$(J)=N$(5):SOUND150,1:GOTO740
510 IFG$="6" THEN E(J)=6:H$(J)=N$(6):SOUND150,1:GOTO740
515 IFG$="7" THEN E(J)=7:H$(J)=N$(7):SOUND150,1:GOTO740
520 IFG$="8" THEN E(J)=8:H$(J)=N$(8):SOUND150,1:GOTO740
525 IFG$="9" THEN E(J)=9:H$(J)=N$(9):SOUND150,1:GOTO740
530 IFG$=CHR$(13) THEN 555
535 IFG$=CHR$(8) AND J-1=0 THEN SOUND 75,1:GOTO475
540 IFG$=CHR$(8) AND J-1>0 THEN SOUND75,1:GOTO105
545 GOTO 475
550 NEXTJ
555 SOUND50,1:P=(E(1)*(10^7))+(E(2)*(10^6))+(E(3)*(10^5))+(E(4)*(10^4))+(E(5)*(10^3))+(E(6)*(10^2))+(E(7)*(10^1))+(E(8)*(10^0))
560 IFJ-1=0 THEN 470
565 IFJ-1=1 THEN P=P*.0000001 ELSE IFJ-1=2 THEN P=P*.000001 ELSE IF J-1=3 THEN P=P*.00001 ELSE IF J-1=4 THEN P=P*.0001 ELSE IF J-1=5 THEN P=P*.001 ELSE IF J-1=6 THEN P=P*.01 ELSE IF J-1=7 THEN P=P*.1 ELSE IF J-1=8 THEN P=P
570 P=P+.1:P=INT(P)
575 IF P>M THEN 105
580 IFB$(11)<>T$(2) AND A(11)<>0 AND P<(M/2) THEN 105
585 'REM GUESS HIGH OR LOW
590 DRAW"BM170,160;U7G3E3F3;":DRAW"BM170,165;D7H3F3E3;":DRAW"BM171,161;U7G3E3F3;":DRAW"BM171,166;D7H3F3E3;"
595 G$=INKEY$
600 IFG$=CHR$(94) THEN L=1:SOUND175,1:DRAW"BM179,160;XL$(8);":DRAW"BM186,160;XL$(9);":DRAW"BM193,160;XL$(7);":DRAW"BM200,160;XL$(8);":DRAW"BM207,160;XL$(5);":DRAW"BM214,160;XL$(18);":GOTO615
605 IFG$=CHR$(10) THEN L=0:SOUND175,1:DRAW"BM179,172;XL$(12);":DRAW"BM186,172;XL$(15);":DRAW"BM193,172;XL$(23);":DRAW"BM200,172;XL$(5);":DRAW"BM207,172;XL$(18);":GOTO615
610 GOTO 595
615 'REM FIND VALUE OF CARDS
620 FORX=1TO500:NEXTX
625 IFA$(N)=N$(1) THEN JJ=14 ELSE IF A$(N)=N$(13) THEN JJ=13 ELSE IF A$(N)=N$(12) THEN JJ=12 ELSE IF A$(N)=N$(11) THEN JJ=11 ELSE IF A$(N)=N$(10) THEN JJ=10 ELSE IF A$(N)=N$(9) THEN JJ=9 ELSE IF A$(N)=N$(8) THEN JJ=8 ELSE IF A$(N)=N$(7) THEN JJ=7
630 IFA$(N)=N$(6) THEN JJ=6 ELSE IF A$(N)=N$(5) THEN JJ=5 ELSE IF A$(N)=N$(4) THEN JJ=4 ELSE IF A$(N)=N$(3) THEN JJ=3 ELSE IF A$(N)=N$(2) THEN JJ=2
635 IFA$(N)=N$(15) THEN JJ=0
640 IFA$(N-1)=N$(1) THEN KK=14 ELSE IFA$(N-1)=N$(13) THEN KK=13 ELSE IFA$(N-1)=N$(12) THEN KK=12 ELSE IFA$(N-1)=N$(11) THEN KK=11 ELSE IFA$(N-1)=N$(10) THEN KK=10 ELSE IFA$(N-1)=N$(9) THEN KK=9 ELSE IF A$(N-1)=N$(8) THEN KK=8
645 IFA$(N-1)=N$(7) THEN KK=7 ELSE IFA$(N-1)=N$(6) THEN KK=6 ELSE IF A$(N-1)=N$(5) THEN KK=5 ELSE IF A$(N-1)=N$(4) THEN KK=4 ELSE IF A$(N-1)=N$(3) THEN KK=3 ELSE IF A$(N-1)=N$(2) THEN KK=2
650 'REM DETERMINE HIGHER OR LOWER
655 IFL=1 AND JJ>KK THEN 685
660 IFL=0 AND JJ<KK THEN 685
665 IFL=1 AND JJ<KK THEN 700
670 IFL=0 AND JJ>KK THEN 700
675 IF JJ=0 THEN 700
680 IFJJ=KK THEN 720
685 DRAW"BM179,188;XL$(23);":DRAW"BM186,188;XL$(9);":DRAW"BM193,188;XL$(14);":DRAW"BM200,188;XL$(14);":DRAW"BM207,188;XL$(5);":DRAW"BM214,188;XL$(18);"
690 SOUND225,2:M=M+P:A(N)=1:FORX=1TO750:NEXTX
695 GOTO105
700 DRAW"BM179,188;XL$(12);":DRAW"BM186,188;XL$(15);":DRAW"BM193,188;XL$(19);":DRAW"BM200,188;XL$(5);":DRAW"BM207,188;XL$(18);"
705 SOUND225,2:M=M-P:A(N)=1:FORX=1TO750:NEXTX
710 IFJJ=0 THEN M=0
715 GOTO105
720 DRAW"BM179,188;XL$(5);":DRAW"BM186,188;XL$(17);":DRAW"BM193,188;XL$(21);":DRAW"BM200,188;XL$(1);":DRAW"BM207,188;XL$(12);"
725 SOUND225,1:FORX=1TO75:NEXTX:SOUND225,1:A(N)=1
730 FORX=1TO750:NEXTX
735 GOTO105
740 IFJ=1 THEN DRAW"BM95,172;XH$(1);"
745 IFJ=2 THEN DRAW"BM103,172;XH$(2);"
750 IFJ=3 THEN DRAW"BM111,172;XH$(3);"
755 IFJ=4 THEN DRAW"BM119,172;XH$(4);"
760 IFJ=5 THEN DRAW"BM127,172;XH$(5);"
765 IFJ=6 THEN DRAW"BM135,172;XH$(6);"
770 IFJ=7 THEN DRAW"BM143,172;XH$(7);"
775 IFJ=8 THEN DRAW"BM151,172;XH$(8);"
780 GOTO 550
785 'REM SELECT CARDS
790 CLS:PRINT@0,"********SELECTING CARDS*********";
795 FORX=1TO53:D(X)=X:NEXTX
800 FORI=1TO15
805 F=RND(100):FORX=1TOF:NEXTX:G=RND(-TIMER):H=(G*100)
810 X=INT(H)
815 IFX>53 THEN 805
820 IFD(X)=0 THEN 805
825 IFX<1 THEN 805
830 IFX=53 THEN 920
835 IFX=53 THEN A$(I)=N$(15)
840 IFX=2 OR X=15 OR X=28 OR X=41 THEN A$(I)=N$(3)
845 IFX=3 OR X=16 OR X=29 OR X=42 THEN A$(I)=N$(4)
850 IFX=4 OR X=17 OR X=30 OR X=43 THEN A$(I)=N$(5)
855 IFX=5 OR X=18 OR X=31 OR X=44 THEN A$(I)=N$(6)
860 IFX=6 OR X=19 OR X=32 OR X=45 THEN A$(I)=N$(7)
865 IFX=7 OR X=20 OR X=33 OR X=46 THEN A$(I)=N$(8)
870 IFX=8 OR X=21 OR X=34 OR X=47 THEN A$(I)=N$(9)
875 IFX=9 OR X=22 OR X=35 OR X=48 THEN A$(I)=N$(10)
880 IFX=10 OR X=23 OR X=36 OR X=49 THEN A$(I)=N$(11)
885 IFX=11 OR X=24 OR X=37 OR X=50 THEN A$(I)=N$(12)
890 IFX=12 OR X=25 OR X=38 OR X=51 THEN A$(I)=N$(13)
895 IFX=13 OR X=26 OR X=39 OR X=52 THEN A$(I)=N$(1)
900 IFX=14 OR X=27 OR X=40 OR X=1 THEN A$(I)=N$(2)
905 D(X)=0
910 NEXTI
915 RETURN
920 J=RND(3):K=RND(3)
925 IFJ=K THEN 835
930 GOTO 810
935 'REM CHECK FOR CURRENT CARD
940 IFS=0 THEN A(1)=1:S=1
945 IFA(1)<>0 AND A(2)=0 THEN N=2
950 IFA(2)<>0 AND A(3)=0 THEN N=3
955 IFA(3)<>0 AND A(4)=0 THEN N=4
960 IFA(4)<>0 AND A(5)=0 THEN N=5
965 IFA(5)<>0 AND A(6)=0 THEN N=6
970 IFA(6)<>0 AND A(7)=0 THEN A$(7)=A$(6):N=8:A(7)=1
975 IFA(8)<>0 AND A(9)=0 THEN N=9
980 IFA(9)<>0 AND A(10)=0 THEN N=10
985 IFA(10)<>0 AND A(11)=0 THEN A$(11)=A$(10):N=12:A(11)=1
990 RETURN
995 CLS:PRINT@0,"********CONGRATULATIONS!********";
1000 PRINT@64,"YOU HAVE BROKEN THE BANK WHICH  IS CURRENTLY SET AT $50,000,000 DOLLARS. DON'T YOU WISH IT WAS  REAL MONEY!"
1005 PLAYAA$+AB$:PLAYAC$+AD$
1010 PRINT@256,"END OF GAME OPTIONS:            <1> START NEW GAME              <2> QUIT";
1015 G$=INKEY$
1020 IFG$="1" THEN RUN
1025 IFG$="2" THEN CLS:PRINT"THANKS FOR PLAYING              'EASY COME / EASY GO'!":POKE65494,0:END
1030 GOTO 1015
1035 'REM CHANGE CARD
1040 IFN<=6 AND A(15)=0 THEN A(15)=1:A$(N-1)=A$(15):GOTO105
1045 IFN>=7 AND N<=10 AND A(14)=0 THEN A(14)=1:A$(N-1)=A$(14):GOTO105
1050 IFN>11 AND A(13)=0 THEN A(13)=1:A$(N-1)=A$(13):GOTO105
1055 DRAW"BM10,184;XL$(14);":DRAW"BM17,184;XL$(15);"
1060 GOTO435
1065 CLS:IFJJ<>0 THEN PRINT"DUE TO CARELESS BETTING AND THE HOPES THAT THE CARDS WOULD PLAY IN YOUR FAVOR, YOU HAVE GONE";:M=0
1070 IFJJ=0 THEN PRINT"DUE TO UNFORSEEN MYSTICISM IN   THE CARDS YOU DREW, THE JOKER   HIMSELF HAS FORCED YOU";:M=0
1075 FORR=96 TO 416 STEP 32:G=R/2: SOUNDG,1
1080 PRINT@R,"***********bankrupt*************";
1085 FORX=1TO50:NEXTX:PRINT@R,"                                ";
1090 NEXTR
1095 FORR=416 TO 96 STEP-32:G=R/2:PRINT@R,"                                ";:FORX=1TO25:NEXTX
1100 PRINT@R,"***********bankrupt*************";
1105 SOUNDG,1
1110 NEXTR
1115 PRINT@448,"PRESS <E> TO END OR"
1120 PRINT@480,"PRESS <ENTER> TO START NEW GAME";
1125 G$=INKEY$
1130 IFG$=CHR$(13) THEN 60
1135 IFG$="E" THEN CLS:PRINT"THANKS FOR PLAYING              'EASY COME / EASY GO'!":POKE65494,0:END
1140 GOTO 1125
1145 CLS:PRINT@0,"***********GAME OVER************";:PRINT@128,"END OF GAME OPTIONS:            <1> CONTINUE ON WITH NEXT GAME  <2> START NEW GAME              <3> QUIT";"
1150 PRINT@64,"YOU FINISHED WITH $"M;
1155 G$=INKEY$
1160 IFG$="1" THEN 75
1165 IFG$="2" THEN 60
1170 IFG$="3" THEN CLS:PRINT"THANKS FOR PLAYING              'EASY COME / EASY GO'!":POKE65494,0:END
1175 GOTO 1155
1180 CLS:PRINT"          instructions          ********in the beginning********YOU START WITH A GAME BOARD OF  12 CARDS PLUS 3 'CHANGE CARDS'. THE 12 CARDS ARE ARRANGED WITH  6 CARDS ON THE BOTTOM ROW, 4 IN THE MIDDLE ROW, AND 2 ON THE"
1185 PRINT"TOP ROW. THERE IS 1 CHANGE CARD SET OFF TO THE LEFT OF EACH ROW.WHEN THE GAME IS FIRST STARTED, THE FIRST CARD ON THE BOTTOM ROWIS TURNED FACE-UP SO AS TO SHOW THE VALUE OF IT. YOU START WITH $20 AND ARE TRYING TO BREAK THE BANK."
1190 PRINT@480,"PRESS <ENTER> TO CONTINUE";
1195 G$=INKEY$
1200 IF G$=CHR$(13) THEN 1210
1205 GOTO 1195
1210 CLS:PRINT"          instructions          **********play options**********AFTER THE GAME IS STARTED YOU   HAVE THESE OPTIONS:                     <C>-CHANGE CARD         THIS OPTION ALLOWS YOU TO USE   THE 'CHANGE CARD' FOR YOUR ROW."
1215 PRINT"YOU MAY USE THIS OPTION ONLY ONETIME PER ROW, SO USE IT WISELY. YOU WOULD USE THIS OPTION IF THECURRENT CARD IS NOT A VERY SAFE BET. FOR EXAMPLE, IF YOUR CARD  WAS A 7, ODDS ON THE NEXT CARD  ARE 50% HIGHER AND 50% LOWER.   CHANGE THAT CARD!"
1220 PRINT@480,"PRESS <ENTER> TOCONTINUE";
1225 G$=INKEY$
1230 IFG$=CHR$(13) THEN 1240
1235 GOTO 1225
1240 CLS:PRINT"          instructions          **********play options**********         <P>-PLACE BET          WHEN YOU GET A FAIRLY SAFE CARD TO BET ON, USE THIS OPTION. IF  YOU HAVE A BAD CARD AND HAVE    ALREADY USED YOUR CHANGE OPTION"
1245 PRINT"THEN YOU MUST MAKE A BET ANYWAY,BUT JUST MAKE A SMALL ONE TO BE SAFE. IF YOU GET A 2 THEN BET   EVERYTHING YOU HAVE BECAUSE THE NEXT CARD CANNOT BE LOWER THAN  2. DITTO FOR THE ACE. NO CARD ISHIGHER THAN THE ACE."
1250 PRINT@480,"PRESS <ENTER> TO CONTINUE";
1255 G$=INKEY$
1260 IFG$=CHR$(13) THEN 1270
1265 GOTO 1255
1270 CLS:PRINT"          instructions          **********play options**********IF YOU MAKE A MISTAKE IN PLACINGYOUR BET, HIT THE BACKSPACE KEY AND THE SCREEN WILL BE REDRAWN  AND YOU CAN PLACE THE BET ALL   OVER AGAIN. AS YOU TYPE IN YOUR"
1275 PRINT"BET, YOU WILL HEAR A BEEP TO LETYOU KNOW THE NUMBER HAS BEEN    ACKNOWLEDGED. HIT <ENTER> AFTER YOU KEY IN THE LAST DIGIT OF THEBET. IF YOUR BET IS 8 DIGITS,   YOU DON'T HAVE TO HIT ENTER."
1280 PRINT@480,"PRESS <ENTER> TO CONTINUE";
1285 G$=INKEY$
1290 IFG$=CHR$(13) THEN 1300
1295 GOTO 1285
1300 CLS:PRINT"          instructions          **********play options**********AFTER YOUR BET IS PLACED, YOU   NEED TO TELL THE COMPUTER IF YOUTHINK THE NEXT CARD WILL BE     HIGHER OR LOWER IN VALUE. THIS  IS DONE USING THE UP AND DOWN"
1305 PRINT"ARROW KEYS, RESPECTIVELY. IF YOUHIT THE UP ARROW KEY, THE WORD  'HIGHER' WILL BE DISPLAYED ON   THE SCREEN. HIT THE DOWN ARROW  KEY AND THE WORD 'LOWER' WILL BEDISPLAYED. A FEW SECONDS LATER  'WINNER' OR 'LOSER' WILL BE DIS-PLAYED."
1310 PRINT@480,"PRESS <ENTER> TO CONTINUE";
1315 G$=INKEY$
1320 IFG$=CHR$(13) THEN 1330
1325 GOTO 1315
1330 CLS:PRINT"          instructions          **********play options**********IF YOU LOST THE BET. THE MONEY  WILL BE DEDUCTED FROM YOUR      ACCOUNT. LIKEWISE IF YOU WIN THEBET, THE MONEY WILL BE ADDED TO YOUR ACCOUNT. NOW FOR THE SCARY"
1335 PRINT"PART, ON THE TOP ROW THERE ARE  ONLY TWO CARDS, MEANING ONLY ONEBET FOR THIS ROW. YOU MUST  BET AT LEAST HALF OF YOUR ACCOUNT   OR THE BET WON'T BE ACKNOWLEDGEDAT ALL. THIS MAKES FOR A QUICK  WAY TO WIN ALOT OR LOSE ALOT,"
1340 PRINT@480,"PRESS <ENTER> TO CONTINUE";
1345 G$=INKEY$
1350 IFG$=CHR$(13) THEN 1360
1355 GOTO 1345
1360 CLS:PRINT"          instructions          **********play options**********THUS THE NAME 'EASY COME / EASY GO'! OF COURSE IN STRIVING TO   BREAK THE BANK YOU WILL HIT MANYDOWNFALLS AND MAYBE EVEN HIT THEJOKER WHICH MAKES YOU BANKRUPT"
1365 PRINT"AUTOMATICALLY. IT WILL TAKE MANYCONSECUTIVE GAMES TO BREAK THE  BANK BUT IT REALLY CAN BE DONE! ONCE THE BANK HAS BEEN BROKEN   YOU WILL BE REWARDED WITH A SONGAND A SARCASTIC REMARK ABOUT THEMONEY YOU WON (WISHING IT WAS   REAL)."
1370 PRINT@480,"PRESS <ENTER> TOCONTINUE";
1375 G$=INKEY$
1380 IFG$=CHR$(13) THEN 1390
1385 GOTO 1375
1390 CLS:PRINT"          instructions          ******end of game options*******AT THE END OF EVERY GAME YOU AREGIVEN THE OPTION TO CONTINUE    WITH YOUR PRESENT ACCOUNT, STARTA NEW GAME, OR QUIT. IF YOU GO  BANKRUPT OR HIT THE JOKER, YOUR"
1395 PRINT"OPTIONS ARE TO START A NEW GAME OR TO QUIT. IF YOU BREAK THE    BANK, YOU WILL GET YOUR VICTORY SONG AND THE OPTIONS TO START A NEW GAME OR QUIT. GOOD LUCK AND HAVE FUN WITH 'EASY COME / EASY GO'. BREAK THAT BANK!"
1400 PRINT@480,"HIT <ENTER> TO GET THINGS GOIN'";
1405 G$=INKEY$
1410 IFG$=CHR$(13) THEN 60
1415 GOTO 1405
