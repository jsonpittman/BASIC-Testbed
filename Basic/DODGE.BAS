10 'dodge' BY JEREMY MOON
20 POKE 65494,0
30 CLS
40 KK=16
50 ME$=CHR$(239)
60 X$=CHR$(255)
70 PRINT"WELCOME TO 'DODGE'":PRINT"THE AIM OF THE GAME IS TO DODGE THE ORANGE SQUARES THAT ARE     CHARGING TOWARDS YOU.":PRINT
80 PRINT"  USE THE LEFT AND RIGHT ARROW    KEYS TO AVOID THE SQUARES."
90 PRINT:PRINT:PRINT
100 PRINT@384,"1=HARD, 3=EASY"
110 INPUT "SKILL LEVEL (1-3)";SK:IF SK=1 THEN POKE 65495,0:SK=0
120 IF SK=2 THEN SK=0
130 SK=SK*30
140 CLS
150 'SCATTER ORANGE BOXES (X$)
160 '
170 X=480+RND(32)-1
180 PRINT@X,X$;
190 GOSUB 250
200 'SCROLL SCREEN UP.
210 PRINT@511,""
220 SCORE=SCORE+1
230 GOSUB 530
240 GOTO 160
250 'KEYBOARD SCAN ROUTINE
260 'IF ARROWS KEYS PRESSED
270 'THEN MOVE MAN.
280 IF PEEK(343)<>255 THEN KK=KK-1ELSEIF PEEK(344)<>255 THEN KK=KK+1
290 IF KK<0 THEN KK=31
300 IF KK>31 THEN KK=0
310 'UPDATE MAN'S POSITION
320 PRINT@KK,ME$
330 'CHECK TO SEE IF MAN IS HIT
340 SET(KK*2,2,3)
350 IF POINT(KK*2,4)=8 THEN GOSUB 370
360 RETURN
370 'YOU HAVE DIED!
380 FOR X=1 TO 72 STEP 8:SOUND X,2:CLS(X/8)
390 NEXT
400 CLS0
410 FOR X=1 TO127:PRINT"you";CHR$(128);:NEXTX
420 CLS0
430 FOR X=1 TO102:PRINT"have";CHR$(128);:NEXT
440 CLS0
450 FOR X=1 TO102:PRINT"died";CHR$(128);:NEXT
460 CLSRND(9)-1
470 PRINT@66,"YOUR FINAL SCORE WAS:";SCORE;
480 PRINT@130,"WOULD YOU LIKE TO PLAY AGAIN?";
490 PRINT@173,"(Y/N)";
500 PRINT@206,"";:INPUT A$
510 IF LEFT$(A$,1)<>"Y" THEN CLS:END
520 RUN
530 FOR TIME=1 TO SK:NEXT:RETURN
