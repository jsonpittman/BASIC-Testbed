0 ' COPYRIGHT 1990 FALSOFT, INC
10 CLEAR 200,&H6000-1:IF PEEK(&H6016)<>204 THEN LOADM"SPIRO"
20 PCLEAR8:DIM M(43),M1(10),M2(10),M3(10),P(43),P1(1),C(2),DR
30 CLS:PRINT"        SPIRO - GRAPHICS"
40 FB=&HFF00:GOSUB 2000
50 M4=0:RA=ATN(1)/45:NN=0
60 PRINT:INPUT"RADIUS OF WHEEL ONE   -";R1:R1=INT(ABS(R1))
70 IF R1>99 THEN R1=90
80 INPUT"RADIUS OF WHEEL TWO   -";R2:R2=INT(ABS(R2))
90 IF R2>R1 THEN PRINT" MUST BE LESS THAN WHEEL ONE":GOTO 80
100 R=R1-R2
110 INPUT"DISTANCE ON WHEEL TWO -";DI:DI=INT(ABS(DI))
120 GOSUB 1500
130 INPUT"SCALE FACTOR [1] -";SC:IF SC=0 THEN SC=1
140 SC=INT(ABS(SC)*100)/100
150 INPUT"ROTATE ENTIRE DISPLAY [0] -";DA:IF DA<0 THEN DA=360+DA
160 IF ABS(DA)>359 THEN DA=0
170 PRINT:PRINT"THIS WILL HAVE"(R1+R2)/J"LOOPS":PRINT"AND WILL TAKE"R2/J"CYCLES":PRINT:PRINT:PRINT"PRESS ANY KEY TO BEGIN"
180 R1(0)=R1:R2(0)=R2:DI(0)=DI:SC(0)=SC:DA(0)=DA
190 EXEC &HADFB:GOTO 600
200 M1=0
210 CLS:PRINT"  CURRENT DATA"
220 PRINT" <R1> <R2> <DI> <SC> <DA> [OK]"
230 GOTO 3000
300 NN=NN+1
310 R1(NN)=R1:R2(NN)=R2:DI(NN)=DI:SC(NN)=SC:DA(NN)=DA
320 PUT(0,0)-(47,35),P,PSET
330 PCOPY 1 TO 5:PCOPY 2 TO 6:PCOPY 3 TO 7:PCOPY 4 TO 8:M3=1
340 GOTO 600
600 SCREEN 1,1
610 POKE &H6000,INT(R*SC+.5)
620 POKE &H6001,INT(DI*SC+.5)
630 DR=R1/R2:V=VARPTR(DR):FOR Q=0 TO 4:POKE &H600D+Q,PEEK(V+Q):NEXT
640 MSB=INT(MX/256):LSB=MX-MSB*256:POKE &H600A,MSB:POKE &H600B,LSB
650 POKE &H600C,INT(SC*16)
660 MSB=INT(DA/256):LSB=DA-MSB*256:POKE &H6006,MSB:POKE &H6007,LSB
670 EXEC &H6016
680 GET(0,0)-(47,35),P,G:PUT(0,0)-(47,35),M,PSET:IF NN<10 THEN 700
690 GOTO 690
700 H=JOYSTK(0)*4:V=JOYSTK(1)*3
710 GET(H,V)-(H+7,V+7),P1,G:PUT(H,V)-(H+7,V+7),C,OR:PUT(H,V)-(H+7,V+7),P1,PSET
720 IF H<47 AND V<9 THEN GET(0,0)-(47,8),M1:PUT(0,0)-(47,8),M1,PRESET:PUT(0,0)-(47,8),M1,PSET:IF PEEK(FB)=254 OR PEEK(FB)=126 THEN 200
730 IF H<47 AND V>8 AND V<17 THEN GET(0,9)-(47,17),M2:PUT(0,9)-(47,17),M2,PRESET:PUT(0,9)-(47,17),M2,PSET:IF PEEK(FB)=254 OR PEEK(FB)=126 THEN 800
740 IF M3=1 AND H<47 AND V>17 AND V<27 THEN GET(0,18)-(47,26),M3:PUT(0,18)-(47,26),M3,PRESET:PUT(0,18)-(47,26),M3,PSET:IF PEEK(FB)=254 OR PEEK(FB)=126 THEN 900
750 GOTO 700
800 CLS:LINEINPUT"FILENAME -- ";N$:VERIFYON:SCREEN1,1
810 N$=N$+"/SPI"
820 PUT(0,0)-(47,35),P,PSET
830 SAVEM N$,&HE00,&H25FF,&HE00
840 GOTO 680
900 PCOPY 5 TO 1:PCOPY 6 TO 2:PCOPY 7 TO 3:PCOPY 8 TO 4
910 GET(0,0)-(47,35),P,G
920 PUT(0,0)-(47,35),M
930 NN=NN-1:M3=0
940 R1=R1(NN):R2=R2(NN):DI=DI(NN):SC=SC(NN):DA=DA(NN)
950 GOTO 700
1500 I=R1:J=R2:IF I=0 OR J=0 THEN RETURN
1510 IF I>=J THEN I=I-J:GOTO1510
1520 IF I=0 THEN 1540
1530 TEMP=I:I=J:J=TEMP:GOTO 1510
1540 MX=(R2/J)*360:R=R1-R2
1550 RETURN
2000 PMODE4,1:PCLS:DRAW"BM0,0;NR2;ND2;BF1;F3":GET(0,0)-(7,7),C,G:PCLS:'CURSER
2010 DRAW"BM6,2;H1;L3;G1;D4;F1;R3;E1;BD1;BR3;NU6;BU3;R5;NU3;D3;BR3;U4;E2;R1;F2;D1;NL4;D3BR3;U6;F5;NU5;D1;BR3;BU1;U4;E1;R4;BD3;NL1;D3;NL4;BR3;U3;NR3;U3;R5;BD6;NL5":'CHANGE
2020 DRAW"BM2,16;NH1;R4;H5;E1;R3;F1;BD5;BR3;U4;E2;R1;F2;D1;NL4;D3;BR3;BU6;M+2,+6;R1;M+2,-6;BD6;BR3;U3;NR3;U3;R5;BD6;L4":'SAVE
2030 DRAW"BM1,25;BU1;U5;BR5;D5;G1;NL4;BR4;U6;F5;NU5;D1;BR3;NR4;U6;R4;F1;D4;BD1;BR3;BU1;U4;E1;R3;F1;D4;G1;L3":'UNDO
2040 GET(0,0)-(47,35),M,G:PCLS
2050 RETURN
3000 FOR T=0 TO NN:TT=T+2
3010 IF R1(T)<10 THEN PRINT@TT*32+2,USING"0#";R1(T);ELSE PRINT@TT*32+2,USING"##";R1(T);
3020 IF R2(T)<10 THEN PRINT@TT*32+7,USING"0#";R2(T);ELSE PRINT@TT*32+7,USING"##";R2(T);
3030 IF DI(T)<10 THEN PRINT@TT*32+12,USING"0#";DI(T);ELSE PRINT@TT*32+12,USING"##";DI(T);
3040 PRINT@TT*32+16,USING"#.##";SC(T);
3050 IF DA(T)>99 THEN PRINT@32*TT+21,USING"###";DA(T);:GOTO 3080
3060 IF DA(T)>9 THEN PRINT@32*TT+21,USING"0##";DA(T);:GOTO 3080
3070 PRINT@32*TT+21,USING"00#";DA(T);
3080 NEXT:TT=(TT+1)*32:T=T-1
3090 IF R1(T)<10 THEN PRINT@TT+2,USING"0#";R1(T);ELSE PRINT@TT+2,USING"##";R1(T);
3100 IF R2(T)<10 THEN PRINT@TT+7,USING"0#";R2(T);ELSE PRINT@TT+7,USING"##";R2(T);
3110 IF DI(T)<10 THEN PRINT@TT+12,USING"0#";DI(T);ELSE PRINT@TT+12,USING"##";DI(T);
3120 PRINT@TT+16,USING"#.##";SC(T);
3130 IF DA(T)>99 THEN PRINT@TT+21,USING"###";DA(T);:GOTO 3160
3140 IF DA(T)>9 THEN PRINT@TT+21,USING"0##";DA(T);:GOTO 3160
3150 PRINT@TT+21,USING"00#";DA(T);
3160 L1=&H400+TT+2
3170 L2=&H400+TT+7
3180 L3=&H400+TT+12
3190 L4=&H400+TT+16
3200 L5=&H400+TT+21
3210 H=INT(JOYSTK(0)/2)
3220 LL=&H400+TT:LR=&H400+TT+31
3230 LO=&H400+TT+H
3240 VA=PEEK(LO):POKE LO,128:POKE LL,41:POKE LR,40
3250 IF ((H>1 AND H<4) OR (H>6 AND H<9) OR (H>11 AND H<14) OR (H=16 OR (H>17 AND H<20)) OR (H>20 AND H<24)) AND (PEEK(FB)=254 OR PEEK(FB)=126) THEN VA=VA+1:IF VA>121 THEN VA=112
3260 POKE LO,VA:POKE LL,126:POKE LR,124:GOSUB 3500
3270 IF H>26 AND (PEEK(FB)=254 OR PEEK(FB)=126) THEN GOSUB 4000
3280 GOTO 3210
3500 IF (H>1 AND H<4) THEN R1=10*(PEEK(L1)-112)+(PEEK(L1+1)-112):GOSUB 1500:RETURN
3510 IF (H>6 AND H<9) THEN R2=10*(PEEK(L2)-112)+(PEEK(L2+1)-112):GOSUB 1500:RETURN
3520 IF (H>11 AND H<14) THEN DI=10*(PEEK(L3)-112)+(PEEK(L3+1)-112):RETURN
3530 IF (H>15 AND H<20) THEN SC=(PEEK(L4)-112)+(PEEK(L4+2)-112)/10+(PEEK(L4+3)-112)/100:RETURN
3540 IF (H>20 AND H<24) THEN DA=100*(PEEK(L5)-112)+10*(PEEK(L5+1)-112)+(PEEK(L5+2)-112)
3550 IF DA>359 THEN DA=DA-360:GOTO 3550
3560 RETURN
4000 IF (R1=R1(NN)) AND (R2=R2(NN)) AND (DI=DI(NN)) AND (SC=SC(NN)) AND (DA=DA(NN)) THEN M1=0:GOTO 3210
4010 M1=1:GOTO 300
