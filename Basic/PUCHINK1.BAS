1 'THE ASSEMBLY LINE
2 'WRITTEN BY WILLIAM NEE
3 'COPYRIGHT (C) JULY 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR100,&H7FFF
20 L=1400
30 DIM S(L),X(L),Y(L)
40 X=RND(-TIMER)
50 PMODE4,1:PCLS:SCREEN1,1
60 LINE(0,191)-(255,191),PSET
70 FOR N=0 TO L:S(N)=0
80 FOR S=0 TO N
90 IF S(S)=2 THEN 300
100 IF S(S)=3 THEN 170
110 IF S(S)=0 THEN X(S)=127:Y(S)=0:S(S)=1:PSET(X(S),Y(S)):GOTO170
120 Y(S)=Y(S)+1
130 DX=2*RND(2)-3
140 PRESET(X(S),Y(S)-1):IF Y(S)>120 THEN S(S)=2
150 X(S)=X(S)+DX
160 PSET(X(S),Y(S))
170 NEXT S
180 NEXT N
190 T=0:FOR S=0 TO L
200 IF S(S)=3 THEN 280
210 T=1
220 IF S(S)=2 THEN 400
230 Y(S)=Y(S)+1
240 DX=2*RND(2)-3
250 PRESET(X(S),Y(S)-1):IF Y(S)>120 THEN S(S)=2
260 X(S)=X(S)+DX
270 PSET(X(S),Y(S))
280 NEXT:IF T<>0 THEN 190
290 GOTO 290
300 IF PPOINT(X(S),Y(S)+1)<>0 THEN S(S)=3:GOTO 170
310 PRESET(X(S),Y(S))
320 Y(S)=Y(S)+1
330 PSET(X(S),Y(S))
340 GOTO 170
400 IF PPOINT(X(S),Y(S)+1)<>0 THEN S(S)=3:GOTO 280
410 PRESET(X(S),Y(S))
420 Y(S)=Y(S)+1
430 PSET(X(S),Y(S))
440 GOTO 280
