1 'LONG DIVISION DRILL
2 'COPYRIGHT RICHARD D. GORDLEY
3 'BOX 28, CASTLETON, IL 61426
4 ' SEPT. 24, 1985
10 CLS0:PRINT@96,"chillicothe";:PRINT@108,"alternative";:PRINT@120,"school";:PRINT@172,"presents";:PRINT@235,"dynamic";
14 CLEAR500
20 PRINT@301,"daily";:PRINT@367,"drills";:PRINT@425,"long";:PRINT@430,"division";
25 XX=RND(-TIMER)
30 FORX=1TO2500:NEXT
40 CLS:PRINT"PLEASE FOLLOW INSTRUCTIONS ON   THE SCREEN exactly TO ENJOY     THIS DRILL!":GOSUB1000
50 CLS:PRINT"HOW MANY DIGITS DO YOU WISH IN  THE DIVISOR?  <1-3>"
51 QL=37:RT$="V15O3L4T5GO4CEL2GL4EL2G":NU$=STRING$(14,143):NL$=STRING$(31,143)
52 WR$="V31O1T255CDCDCDCDCDCDCDCDCDCDCDCDCD":WR$=WR$+WR$+WR$+WR$
60 A$=INKEY$:IFA$=""THEN60
70 IFA$<"1" ORA$>"3" THEN50
80 GOSUB1050
95 CLS:PRINT@70,STRING$(LEN(DI$),45);:PRINT@100-LEN(D$),D;:PRINT@101,"/";:PRINT@102,DI$;
100 PRINT@37,"?      ";
101 QL=37
105 PRINT@16,"STEP 1";:PRINT@48,"divide";
110 PRINT@306,"WHERE WILL";:PRINT@338,"FIRST DIGIT";:PRINT@370,"OF QUOTIENT";:PRINT@402,"GO?";
120 PRINT@448,"USE ARROW KEYS TO MOVE THE '?'";:PRINT@480,"PRESS <enter> WHEN DONE.";
121 FORX=1TO200:NEXT:PRINT@476,CHR$(143);:FORX=1TO200:NEXT
122 A$=INKEY$:IFA$=""THEN120
130 PRINT@476,"?"
140 IFA$=CHR$(8) THENQL=QL-1:PRINT@QL,"? ";:GOTO122
150 IFA$=CHR$(9) THENPRINT@QL," ?";:QL=QL+1:GOTO122
160 IFA$<>CHR$(13)THEN120
170 IFQL<PL THENPLAYWR$:GOSUB1150:PRINT@306,"DIGIT TOO";:PRINT@338,"FAR left!";:PRINT@370,"TRY AGAIN.";:GOSUB1000:GOSUB1150:GOTO100
180 IFQL>PL THENPLAYWR$:GOSUB1150:PRINT@306,"DIGIT TOO";:PRINT@338,"FAR right!";:PRINT@370,"TRY AGAIN.";:GOSUB1000:GOSUB1150:GOTO100
190 PLAYRT$
191 GOSUB1150:FORX=1TO3:PRINT@306,"VERY GOOD!";:FORY=1TO200:NEXT:PRINT@306,"very good";:FORY=1TO200:NEXT:NEXT
200 GOSUB1150:GOSUB1000:GOSUB1150
210 PRINT@QL,CHR$(191);:PRINT@306,"NOW press";:PRINT@338,"A NUMBER TO";:PRINT@370,"USE AS FIRST";:PRINT@402,"DIGIT OF";:PRINT@434,"QUOTIENT.";
215 A$=""
220 A$=INKEY$:IFA$="" THEN220
230 A=VAL(A$):IFA<1 ORA>9 THEN220
232 PRINT@QL,A$:SOUND100,3
234 ML=153:GOSUB240:GOTO350
240 PRINT@21,"2";:PRINT@48,"multiply back";:GOSUB1150
250 PRINT@ML-LEN(D$),D;:PRINT@ML+30,"X";A;:PRINT@ML+65-LEN(D$),STRING$(LEN(D$),45);:PRINT@ML+93,"      ";
260 GOSUB1450
270 GOSUB1150
275 MA$=STR$(MA)
280 PRINT@ML+97-LEN(MA$),MA;
290 PR=A*D:IFMA<>PR THENPLAYWR$:FORX=1TO3:PRINT@306,"MULTIPLICATION";:PRINT@338,"ERROR!";:FORY=1TO300:NEXT:PRINT@306,"multiplication";:PRINT@338,"error";:FORY=1TO300:NEXT:NEXT:PRINT@402,"TRY AGAIN";:GOSUB1000:GOTO240
300 PLAYRT$:FORX=1TO3:PRINT@306,"EXCELLENT!";:FORY=1TO200:NEXT:PRINT@306,"excellent";:FORY=1TO200:NEXT:NEXT
305 GOSUB1150
310 RETURN
350 GOSUB1210
360 GOSUB1570
365 IFQL=38 THENPRINT@133,PR;:PRINT@166,"-"
370 IFQL=39  ANDLEN(PR$)=1 THENPRINT@134,PR;:PRINT@166,"--";
380 IFLEN(D$)=1 ANDLEN(PR$)=2 THENPRINT@133,PR;:PRINT@166,"--";
390 IFQL=39 ANDLEN(D$)=2 THENPRINT@133,PR;:PRINT@166,"--";
400 IFQL=40  ANDLEN(PR$)=2 THENPRINT@134,PR;:PRINT@166,"---";
410 IFLEN(D$)=2 ANDLEN(PR$)=3 THENPRINT@133,PR;:PRINT@166,"---";
420 IFQL=40 ANDLEN(PR$)=3 THENPRINT@133,PR;:PRINT@166,"---"
430 IFQL=41 ANDLEN(PR$)=3 THENPRINT@134,PR;:PRINT@166,"----";
440 IFQL=41 AND LEN(PR$)=4 THENPRINT@133,PR;:PRINT@166,"----";
445 ER=133
450 PRINT@133,"-";:GOSUB1200
460 IFPR>VAL(LEFT$(DI$,QL-37)) THENGOSUB1220:GOTO210
470 MN=VAL(LEFT$(DI$,QL-37))
480 PRINT@21,"3";:PRINT@48,"subtract      ";
490 GOSUB1460
495 DF$=STR$(DF):DF$=RIGHT$(DF$,LEN(DF$)-1)
500 IF DF<>(MN-PR) THENGOSUB1250:GOTO490
505 PRINT@QL+5*32-LEN(DF$),DF;
510 IFDF=>D THENPRINT@210,CHR$(95):GOSUB1300:PRINT@133,"    ";:PRINT@165,"    ";:PRINT@197,"    ";:GOTO210
520 GOSUB1150:PLAYRT$:FORX=1TO3:PRINT@306,"WELL DONE!";:FORY=1TO200:NEXT:PRINT@306,"well done";:FORY=1TO300:NEXT:NEXT:TL=QL:QL=QL+1
525 PRINT@QL+5*32-LEN(DF$)-1,DF;
530 BL=96:BD=1024+QL+64:GOSUB1350
540 PRINT@21,"1";:PRINT@48,"divide       ";:
550 GOSUB1550
555 PRINT@QL,CHR$(143);
557 PB=197:PN=203:GOSUB1500
558 MN=CU
560 PRINT@203,CHR$(95);:FORX=1TO200:NEXT
570 PRINT@203,CHR$(143);:FORX=1TO200:NEXT
580 A$=INKEY$:IFA$="" THEN560
590 IFA$<"0" OR A$>"9" THEN560
594 A=VAL(A$)
600 PRINT@QL,A$;:SOUND100,3
610 GOSUB240
611 ER=229
618 GOSUB1570
620 PB=197:GOSUB1580
630 PR$="-"+PR$:PRINT@SL-LEN(PR$),PR$;
640 PRINT@SL+32-LEN(PR$),STRING$(LEN(PR$),45);
650 IFPR>MN THENGOSUB1220:GOSUB1210:GOTO540
660 PRINT@21,"3";:PRINT@48,"subtract      ";
670 GOSUB1460
680 IFDF<>MN-PR THENGOSUB1250:GOTO670
690 IFDF=>D THENGOSUB1300:GOSUB1210:PRINT@229,"      ";:PRINT@261,"      ";:GOTO540
700 GOSUB1150:PLAYRT$:FORX=1TO3:PRINT@306,"RIGHT!";:FORY=1TO200:NEXT:PRINT@306,"right";:FORY=1TO200:NEXT:NEXT:TL=QL:QL=QL+1
710 DF$=STR$(DF):DF$=RIGHT$(DF$,LEN(DF$)-1):PRINT@QL+8*32-LEN(DF$)-1,DF
720 PB=37:PN=45:GOSUB1500
730 IFCU=INT(DI/D) THEN2000
735 BL=192:BD=1024+QL+64:GOSUB1350
740 GOSUB1150:PRINT@21,"1";:PRINT@48,"divide       ";
750 GOSUB1550
755 PRINT@QL,CHR$(143);
757 PB=294:PN=301:GOSUB1500
758 MN=CU
760 PRINT@301,CHR$(95);:FORX=1TO200:NEXT
770 PRINT@301,CHR$(143);:FORX=1TO200:NEXT
780 A$=INKEY$:IFA$=""THEN760
790 IFA$<"0" ORA$>"9" THEN760
794 A=VAL(A$)
800 PRINT@QL,A$;:SOUND100,3
810 GOSUB240
811 ER=325
818 GOSUB1570
820 PB=294:GOSUB1580
830 PR$="-"+PR$:PRINT@SL-LEN(PR$),PR$;
840 PRINT@SL+32-LEN(PR$),STRING$(LEN(PR$),45);
850 IFPR>MN THENGOSUB1220:GOSUB1210:GOTO740
860 PRINT@21,"3";:PRINT@48,"subtract      ";
870 GOSUB1460
880 IFDF<>MN-PR THENGOSUB1250:GOTO870
890 IFDF=>D THENGOSUB1300:GOSUB1210:PRINT@326,"      ";:PRINT@358,"      ";:GOTO740
900 GOSUB1150:PLAYRT$:FORX=1TO3:PRINT@306,"CORRECT!";:FORY=1TO200:NEXT:PRINT@306,"correct";:FORY=1TO200:NEXT:NEXT:TL=QL:QL=QL+1
910 DF$=STR$(DF):DF$=RIGHT$(DF$,LEN(DF$)-1):PRINT@QL+11*32-LEN(DF$)-1,DF
920 PB=37:PN=45:GOSUB1500
930 IFCU=INT(DI/D) THEN2000
999 END
1000 PRINT@448,"PRESS ANY KEY TO CONTINUE";
1005 A$=INKEY$:IFA$=""THEN1005
1010 RETURN
1050 IFA$="1" THEND=RND(9) ELSEIFA$="2" THEND=RND(89)+10 ELSEIF A$="3" THEND=RND(899)+100
1060 IFA$="1" THENDI=RND(899)+100 ELSEIFA$="2" THENDI=RND(8999)+1000 ELSEIFA$="3" THENDI=RND(89999)+10000
1075 D1$=STR$(D):D$=RIGHT$(D1$,LEN(D1$)-1):D2$=STR$(DI):DI$=RIGHT$(D2$,LEN(D2$)-1)
1080 DC$=LEFT$(DI$,LEN(D$)):DC=VAL(DC$)
1090 IFLEN(D$)=1 AND D<=DC THENPL=38:RETURN
1095 IF LEN(D$)=1 THENPL=39:RETURN
1100 IFLEN(D$)=2 AND D<=DC THENPL=39:RETURN
1105 IFLEN(D$)=2 THENPL=40:RETURN
1110 IFLEN(D$)=3 ANDD<=DC THENPL=40:RETURN
1115 IFLEN(D$)=3 THENPL=41
1120 RETURN
1150 FORX=306TO434STEP32:PRINT@X,NU$;:NEXT:PRINT@448,NL$:PRINT@480,NL$;:RETURN
1160 RETURN
1200 PRINT@320,STRING$(185,143);:RETURN
1210 PRINT@149,"      ";:PRINT@149+32,"      ";:PRINT@149+64,"      ";:PRINT@149+96,"      ";:RETURN
1220 PLAYWR$:GOSUB1150:FORX=1TO3:PRINT@448,"TRIAL QUOTIENT TOO LARGE        SUBTRACTION IMPOSSIBLE";:FORY=1TO 500:NEXT:PRINT@448,"trial quotient too large        subtraction impossible";:FORY=1TO500:NEXT:NEXT:GOSUB1150
1230 PRINT@ER,"     ";:PRINT@ER+32,"     ";:RETURN
1250 PLAYWR$:GOSUB1150:FORX=1TO3:PRINT@306,"SUBTRACTION";:PRINT@338,"ERROR!";:FORY=1TO300:NEXT:PRINT@306,"subtraction";:PRINT@338,"error";:FORY=1TO300:NEXT:NEXT:GOSUB1150:RETURN
1300 GOSUB1150:PLAY WR$:FORX=1TO5:PRINT@306,"REMAINDER";:PRINT@338,"LARGER THAN";:PRINT@370,"DIVISOR!";:FORY=1TO400:NEXT
1310 PRINT@306,"remainder";:PRINT@338,"larger than";:PRINT@370,"divisor";:FORY=1TO400:NEXT:NEXT:GOSUB1150:PRINT@21,"1";:PRINT@48,"divide      ";:RETURN
1350 PE=PEEK(BD):PRINT@21,"4";:PRINT@48,"bring down    ";:POKEBD,PE-64
1360 GOSUB1150:PRINT@306,"USE ARROW";:PRINT@338,"TO BRING DOWN";:PRINT@370,"NEXT NUMBER";:PRINT@402,"FROM";:PRINT@434,"DIVIDEND.";
1370 A$=INKEY$:IFA$="" THEN1370
1380 IFA$<>CHR$(10) THEN1370
1390 POKEBD,PE:FORX=1TO3:POKEBD+BL,PE-64:FORY=1TO100:NEXT:POKEBD+BL,PE:FORY=1TO100:NEXT:NEXT
1400 RETURN
1450 PRINT@306,"MULTIPLY";:PRINT@338,"AND <enter>";:PRINT@370,"YOUR ANSWER.";:PRINT@402,"";:INPUTMA:RETURN
1460 PRINT@306,"SUBTRACT";:PRINT@338,"AND <enter>";:PRINT@370,"YOUR ANSWER.";:PRINT@402,"";:INPUTDF:RETURN
1500 CU$="":FORX=1024+PB TO 1024+PN
1510 IFPEEK(X)>111 ANDPEEK(X)<122 THENCU$=CU$+CHR$(PEEK(X)-64)
1520 NEXT
1530 CU=VAL(CU$)
1540 RETURN
1550 GOSUB1150:PRINT@306,"NOW DIVIDE";:PRINT@338,"THIS NUMBER";:PRINT@370,"BY DIVISOR";:PRINT@402,"AND press";:PRINT@434,"ANSWER.";
1560 RETURN
1570 PR$=STR$(PR):PR$=RIGHT$(PR$,LEN(PR$)-1):RETURN
1580 PB=PB+1024
1590 PP=PEEK(PB):IFPP<112 ORPP>121THENPB=PB+1:GOTO1590
1600 PP=PEEK(PB):IFPP>111 ANDPP<122 THENPB=PB+1:GOTO1600
1610 SL=PB-1024+32
1620 RETURN
1999 CLS:DF=1
2000 GOSUB1150:IFDF<>0 THENPRINT@306,"DIVISION DONE.";:PRINT@338,"PRESS <enter>";:PRINT@370,"TO BRING UP";:PRINT@402,"REMAINDER.";:SOUND150,1:A$=INKEY$:IFA$<>CHR$(13)THEN2000 ELSEPRINT@44,"R";DF;
2010 PRINT@16,STRING$(10,32);:PRINT@48,STRING$(10,32);:FORX=128TO448 STEP32:PRINT@X,STRING$(32,32);:NEXT
2020 PRINT@0," problem successfully completed!";
2030 POKE65314,9
2040 PLAY"T2V15L6O4CL16DCO3A#L8AAAGFGL4AL8FO4L6CL16DCO3A#L8AA#AGFGL3F"
2045 'PUT A "GOSUB" HERE TO SEND STUDENT TO A GAME ROUTINE USED  AS A REWARD
2050 PRINT:PRINT"WOULD YOU LIKE TO DO ANOTHER    PROBLEM (Y/N)?"
2060 A$=INKEY$:IFA$="" THEN2060
2070 IFA$<>"Y" ANDA$<>"N" THEN2060
2080 IFA$="Y" THENRUN50
2090 PRINT:PRINT"LONG DIVISION DRILL COMPLETED."
2100 END
