10 REM ************************
20 REM **     COCO SAVER     **
30 REM ** PROGRAMMED BY:     **
40 REM ** PAUL RUBY, JR.     **
50 REM **                    **
60 REM ** COPYRIGHT (C) 1987 **
70 REM ************************
80 CLS
90 PRINT@75,"COCO-SAVER";
100 PRINT@129,"PROGRAMMED BY: PAUL RUBY, JR.";
110 PRINT@199,"COPYRIGHT (C) 1987"
120 PRINT@490,"PRESS ANY KEY";
130 IFINKEY$=""THEN130
140 CLS:PRINT "INITIAL AMOUNT OF":INPUT"SAVINGS";ST
150 PRINT
160 PRINT"MAKE <W>EEKLY OR <M>ONTHLY":INPUT"DEPOSITS (W/M)";DP$
170 IF DP$<>"W"ANDDP$<>"M"THEN 160
180 IF DP$="W"THENDP=7
190 IF DP$="M"THENDP=30
200 INPUT"AMOUNT OF DEPOSIT";AD
210 INPUT"# OF YEARS TO SAVE";YR
220 INPUT"% EARNINGS (5% IS .05)";PC
230 AM=ST:CT=1:CLS
240 PRINT@480,"STARTING AT";:PRINTUSING"#######.##";ST:PRINT"DEPOSITS OF";:PRINTUSING"#######.##";AD:PRINT"A.P.R. OF";PC:PRINT"SAVE FOR ";STR$(YR);" YEARS"
250 IF DP$="M"THENPRINT"MONTHLY DEPOSITS"ELSE IF DP$="W"THENPRINT"WEEKLY DEPOSITS"
260 FOR A=1 TO YR
270 FOR B=1 TO 365
280 PRINT@480,"YR";A,:PRINTUSING"#######.##";AM;
290 IF CT=DP THEN AM=AM+AD:CT=1
300 AM=AM+(AM*(PC/365))
310 CT=CT+1
320 NEXT
330 PRINT
340 NEXT
