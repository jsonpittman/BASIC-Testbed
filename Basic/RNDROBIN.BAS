10 'title screen (1-5-87 9 AM)
11 CLS:PRINT@106,"RNDROBIN.BAS"
12 PRINT:PRINTTAB(4)"BY RICHARD STEINBRUECK"
13 PRINTTAB(6)"1109 LAKEMONT DRIVE"
14 PRINTTAB(6)"VALRICO, FL  33594"
15 PRINTTAB(8)"813/681-1526"
16 PRINT:PRINTTAB(7)"COPYRIGHT 1987":PRINTTAB(5)"ALL RIGHTS RESERVED"
17 FORX=1TO1000:NEXT:CLS
18 CLEAR2000
19 '
20 'listing of competing teams (max length of each name is 19 char.)  NOTE: starting with the second team, list all the teams in order and then place the first team's name last.
21 DATA TEAM B,TEAM C,TEAM D,TEAM E,TEAM F,TEAM G,TEAM H,TEAM I,TEAM J,TEAM K,TEAM L,TEAM A
22 T=12'number of teams (range from 5 to 12)
23 TP$="1987 ROUND-ROBIN VOLLEYBALL TOURNAMENT"'title phrase printed at top of sheets (cannot be over 40 characters long)
24 DT$="May 22, 1987"'date
25 H=2:M=00'starting time of tournament
26 L=15'length of each round in minutes
27 B=5'length of break between matches in minutes
29 DIMTS$(T),T$(T),A$(T),N$(T)'T rounds of competition with t teams
30 'miscellaneous calculations & equations
40 LM=0:RM=40'margins set for double-wide printing
50 TP=INT((40-LEN(TP$))/2):DT=INT((40-LEN(DT$))/2)
60 NT=1
70 GOTO1000
80 '
100 'Citizen 120D printer codes (lines 100-140)
110 'printer code for competition schedule printing (lines 120-130)
120 'PRINT#-2,CHR$(27);CHR$(126);CHR$(49);CHR$(1)'double height
130 'PRINT#-2,CHR$(27);CHR$(33);CHR$(40)'emphasized double wide pica (40 columns)
135 RETURN
137 'printer code for win/loss summary sheet printing (line 140)
140 'PRINT#-2,CHR$(27);CHR$(33);CHR$(8)'pica (80 column),emphasized
150 RETURN
160 '
200 'Hour & Minute Subroutine
205 IF M > 59 THEN M = M-60:H=H+1
210 IF H > 12 THEN H = H-12
215 RETURN
220 'start & end time of round
225 FOR X = 1 TO T
230 M$=STR$(M):IF M<10 THEN M$="0"+RIGHT$(STR$(M),1)
235 TS$(X)=STR$(H)+":"+M$
240 M=M+L'length of each round in L minutes
245 GOSUB 205
250 M$=STR$(M):IF M<10 THEN M$="0"+RIGHT$(STR$(M),1)
255 T$(X)=STR$(H)+":"+M$
260 M=M+B'break between matches in B minutes
265 GOSUB 205
270 NEXT X
275 '
300 'team competition assignments
305 CLS:PRINT@160,"  DO YOU WISH THE PRINTER TO    PAUSE AFTER IT PRINTS EACH ROUNDSO YOU CAN ADJUST THE PAPER TO  GET ONE ROUND PER SHEET <Y/N>";:INPUTPP$:IFPP$="Y"THENPP=1
307 FORA=1 TO T 'round number
310 G=INT(T/2)'# of matches played at the same time
315 IFG<>T/2THENP=1'set P=1 if odd number of teams
320 F=0:IF (A/2=INT(A/2) AND P=0) THENF=1:G=G-1
325 IF A = T THEN 355
330 U=T
335 'reading and assigning team names
340 FORX=A+1 TO U
345 IF DA=0 THEN READ A$(X) ELSEA$(X)=N$(NT):NT=NT+1
350 NEXT X
355 FOR X = 1 TO A
360 IF DA=0 THEN READ A$(X) ELSE A$(X)=N$(NT):NT=NT+1
365 NEXT X
370 NT=1:RESTORE
375 '
400 'printing of competition schedule by round
405 GOSUB 120'printer codes
415 GOSUB 506'heading subroutine
420 FOR X= 0 TO G-1
425 B$=A$(X+1):C$=A$(U-X)
430 GOSUB 545
435 NEXT X
440 PRINT#-2
445 IFPP=1THEN CLS:PRINT@160,"PROGRAM PAUSING. <ENTER> TO     CONTINUE PRINTING";:INPUTQ$
450 NEXT A
455 PRINT#-2
460 PRINT#-2
465 GOTO1000
470 '
500 'schedule sheet heading print subroutine
503 '
506 CLS:PRINT"PRINTING SHEET HEADING"
509 PRINT#-2,TAB(TP)TP$
510 TM$="ROUND #"+STR$(A)+"  "+TS$(A)+" --"+T$(A):TM=INT((40-LEN(TM$))/2)
512 PRINT#-2,TAB(TM)TM$
515 PRINT #-2,""
518 'PRINT #-2,TAB(INT((LM+RM)/2)-16)STRING$(32,"=")
521 PRINT#-2,TAB(4)STRING$(32,"=")
524 PRINT #-2,""
527 IF P=1THENPRINT #-2,A$(T-G);" has a bye (Rest period)"'if an odd number of teams compete, each round one has a bye (rest period)
530 IF (P=0 AND F=1) THEN PRINT #-2,A$(T/2);" & ";A$(T/2+1);" have byes (rest period)":F=0'if an even number of teams compete, every other round two teams have a bye (rest period)
533 PRINT #-2,""
536 RETURN
539 '
542 'subroutine for printing competition assignments
545 CLS:PRINT"PRINTING ASSIGNMENTS FOR ROUND";A
548 PRINT #-2,"Half-Court ";2*X+1;TAB(25)"Half-Court ";2*X+2
551 PRINT #-2,B$;TAB(20)"VS";TAB(25)C$
554 PRINT #-2
557 PRINT #-2,"SCORES: Game 1 ..... - ....."
560 PRINT #-2
563 PRINT #-2,"        Game 2 ..... - ....."
566 PRINT #-2
569 PRINT #-2,"        Game 3 ..... - ....."
572 PRINT #-2
575 PRINT #-2
578 RETURN
581 '
600 'Summary sheet print routine
610 '
620 GOSUB 140'printer code
630 TM=INT((56-3*T)/2)'compute top and bottom margin
640 FOR X = 1 TO TM:PRINT#-2:NEXT X
650 LM=24-2*T:RM=LM+32+4*T:MS=LM+RM
660 TP=INT((MS-LEN(TP$))/2)
670 DT=INT((MS-LEN(DT$))/2)
680 WL=INT((MS-22)/2)
690 PRINT#-2,TAB(TP)TP$'from line 22
700 PRINT#-2,TAB(DT)DT$
710 PRINT#-2
720 PRINT#-2,TAB(WL)"WIN/LOSS SUMMARY SHEET"
730 GOSUB850
740 PRINT#-2,TAB(LM)"TEAM";TAB(LM+16+2*T)"ROUND #";TAB(LM+20+4*T)"TOTAL";TAB(LM+26+4*T)"PLACE"
750 FORY=1TOT:PRINT#-2,TAB(4*Y+16+LM)Y;:NEXTY:PRINT#-2
760 GOSUB850
770 IF DA=0 THEN FOR X=2 TO T:READN$(X):NEXTX:READN$(1)
775 FOR X=1 TO T
780 GOSUB840
790 A$=N$(NT):NT=NT+1
800 PRINT#-2,TAB(LM)A$;:GOSUB840
810 GOSUB850
820 NEXTX
830 GOTO860
840 FORY=0TOT:PRINT#-2,TAB(4*Y+19+LM)":";:NEXTY:PRINT#-2,TAB(25+4*T+LM)":";TAB(31+4*T+LM)":":RETURN
850 PRINT#-2,TAB(LM)STRING$(RM-LM,"."):RETURN
860 GOSUB850
870 NT=1:RESTORE
880 FORX=1TOTM:PRINT#-2:NEXTX
885 NT=1:RESTORE
890 '
1000 'main menu
1002 CLS:PRINT@42,"RNDROBIN.BAS";TAB(37)"BY RICHARD STEINBRUECK";TAB(32)STRING$(32,"*");TAB(45)"MENU"
1010 PRINT@224," <C>OMPETITION ASSIGNMENT SHEETS <S>CORING SUMMARY SHEETS        <I>NSTRUCTIONS":PRINT:INPUT" WHAT IS YOUR CHOICE";Q$:IFQ$="C"THEN225ELSEIFQ$="S"THEN600
1100 'instructions
1110 CO$="to continue, press <enter>"
1120 RU$="INSTRUCTIONS":RR$="          ":RS$=STRING$(32,"#"):R$=RR$+RU$+RR$+RS$
1130 CLS:PRINTR$
1140 PRINT"  RNDROBIN.BAS IS A PRINTING    PROGRAM DESIGNED TO PRINT OUT   COMPETITION ASSIGNMENTS FOR FROM5 TO 12 PARTICIPANTS OR TEAMS INA ROUND ROBIN  TOURNAMENT."
1150 PRINT"  IN A ROUND ROBIN TROURNAMENT  EACH OF THE PARTICIPANTS OR     TEAMS COMPETES AGAINST EACH     OF THE OTHER PARTICIPANTS OR    TEAMS.
1160 PRINT@480,CO$;:INPUTQ$:CLS:PRINTR$
1170 PRINT"  THE WINNER IS DETERMINED BY   THE BEST WIN/LOSS RECORD. IN THEEVENT OF A TIE, THE TEAM WHO WONTHE HEAD-TO-HEAD MATCH IS THE   WINNER OF THE TIE.
1180 PRINT"  IF RNDROBIN.BAS IS RUN AS IS, IT WILL PRINT OUT A TOURNAMENT  FOR 12 TEAMS.  MODIFIY IT FOR   YOUR TOURNAMENT BY EDITING LINES21-28 AND REPLACING MY VALUES   WITH YOUR OWN."
1190 PRINT@480,CO$;:INPUTQ$:CLS:PRINTR$
1200 PRINT"  YOU WILL NEED TO KNOW THE     NAMES OF THE COMPETITORS, THE   TIME OF THE FIRST MATCH, THE    LENGTH OF THE MATCHES AND THE   LENGTH OF THE BREAK BETWEEN EACHMATCH."
1210 PRINT"  ALSO PUT IN THE NAME AND DATE OF YOUR TOURNAMENT. RNDROBIN.BASIS DESIGNED TO PRINT ON AN 80   COLUMN PRINTER, EITHER DAISY    WHEEL OR DOT MATRIX."
1220 PRINT@480,CO$;:INPUTQ$:CLS:PRINTR$
1230 PRINT"  RNDROBIN.BAS USES REM STATE-  MENTS EXTENSIVELY TO HELP YOU   IN YOUR MODIFICATIONS.  ALSO,   LINE NUMBERS ARE ARRANGED SO    THAT MAJOR ROUTINES BEGIN AT    100, 200, 300, ETC."
1240 PRINT"  IF YOU HAVE ANY QUESTIONS,    FEEL FREE TO CONTACT ME.  IF YOUWRITE, PLEASE INCLUDE A SELF-   ADDRESSED, STAMPED ENVELOPE."
1250 PRINT:PRINT"   ENJOY YOUR TOURNAMENT!"
1260 PRINT@480,CO$;:INPUTQ$:CLS
1270 GOTO1000
1280 'RNDROBIN.BAS, COPYRIGHT 1987 BY RICHARD STEINBRUECK, 1109 LAKEMONT DRIVE, VALRICO, FL  33594, 813/681-1526
