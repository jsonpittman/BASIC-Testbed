1 'READDOC
2 'WRITTEN BY STUART WYSS-GALLIFENT
3 'COPYRIGHT (C) MAY 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 WIDTH 80:RGB:ATTR 1,0:PALETTE 0,0:PALETTE 9,63:CLS
20 LOCATE 28,1:ATTR 1,0:PRINT" *  READDOC  v 1.1  *";
30 LOCATE 22,2:ATTR 5,0:PRINT"        ASCII  file reader       ";
40 LOCATE 16,3:PRINT"  by Stuart T. Wyss-Gallifent  (June 20,1990) ";
50 LOCATE 16,5:ATTR 7,0:PRINT"Here is a brief summary of available commands:":LOCATE 16,6:PRINT STRING$(46,"-");
60 FOR HE=1 TO 8:READ HE$:LOCATE 4,2*HE+5:ATTR 1,0:PRINT LEFT$(HE$,12);:ATTR 7,0:PRINTRIGHT$(HE$,LEN(HE$)-12);:NEXT HE
70 LOCATE 15,22:ATTR 5,0:PRINT"Press P now to dump this summary to your printer"
80 DATA L  =  load  -gives a directory and asks for a filename to load
90 DATA down arrow  -displays next line.  Hold for continuous display
100 DATA N  =  next  -displays the next page (23 lines)
110 DATA S  =  search-asks for and searches for the first occurence of a string
120 DATA P  =  print -either print the current screen or up to the current screen
130 DATA T  =  top   -jumps back the the beginning of the file
140 DATA B  =  bottom-displays entire file without stopping
150 DATA E  =  exit  -exits the program
160 POKE 150,18:' PRINTER BAUD POKE    SET TO 2400
170 ' PLACE ANY NECESSARY CONTROL CODES FOR YOUR PRINTER HERE (PROGRAM ASSUMES 80 CHARS/LINE AND 66 LINES/PAGE
180 CLEAR 5000
190 ON BRK GOTO 300:ON ERR GOTO 300
200 ' INITIALIZE MENU BAR AND BLANK BAR
210 Q$=STRING$(34,32)+"Load down Next Search Print Top Bottom Exit":MID$(Q$,15,6)="line:"
220 R$=STRING$(78,32)
230 POKE 282,0:' LOWERCASE ON
240 LN=0:GOTO 310
250 ' PRINT BLANK
260 POKE 65497,0:LOCATE 1,23:ATTR 1,2:PRINT R$;:ATTR 1,0:POKE 65496,0:RETURN
270 ' PRINT MENU
280 POKE 65497,0:IF VAL(MID$(Q$,20,5))<>LN THEN MID$(Q$,20,5)="     ":MID$(Q$,20,LEN(STR$(LN)))=STR$(LN)
290 LOCATE 1,23:ATTR 1,2:PRINT Q$;:ATTR 1,0:POKE 65496,0:RETURN
300 '
310 I$=INKEY$:CF=0
320 IF LPEEK(446202)<>116 THEN GOSUB 270
330 IF I$<>"" THEN IF ASC(I$)>96 THEN I$=CHR$(ASC(I$)-32)
340 IF I$="E" THEN GOSUB 1190
350 IF I$="L" THEN GOSUB 680
360 IF I$="P" THEN GOSUB 920
370 IF NA$="" THEN 310
380 IF I$="S" THEN GOSUB 820
390 IF I$<>"N" THEN 410 ELSE LOCATE 1,23:PRINT STRING$(78,32);:H=0
400 GOSUB 450:H=H+1:IF H<23 THEN 400
410 IF I$="T" THEN GOSUB 1150
420 IF I$="B" THEN GOSUB 450:GOTO 420
430 IF PEEK(342)=247 THEN LOCATE 1,23:PRINT STRING$(78,32);:GOSUB 450
440 GOTO 310
450 ' LOAD A LINE
460 IF EOF(1)=-1 THEN 1170
470 LINEINPUT #1,A$
480 IF AN$="n" THEN 630
490 ' FILTER ANSI
500 POKE 65497,0:B=1:F2=0
510 IF B<1 THEN B=1
520 IF CF=1 AND INSTR(1,A$,LEFT$(ST$,1))=0 THEN A$=CHR$(32)
530 L1=INSTR(B,A$,CHR$(91)):IF L1=0 THEN 630 ELSE L0=L1
540 IF L1+1>LEN(A$) THEN L3=109:GOTO 580
550 L2=ASC(MID$(A$,L1+1,1)):IF L2<47 OR L2>57 THEN B=L1+1:GOTO 530
560 IF L1+2>LEN(A$) THEN L3=109:GOTO 580
570 L3=ASC(MID$(A$,L1+2,1))
580 IF L3=109 OR L3=72 OR L3=65 OR L3=66 OR L3=67 OR L3=68 OR L3=74 THEN A$=LEFT$(A$,L0-1)+MID$(A$,L1+3,255):B=L0-1:GOTO 510
590 IF F2=1 THEN L1=L1+1:GOTO 560
600 IF L3>46 AND L3<58 THEN L1=L1+1:F2=1:GOTO 560
610 IF L3<>59 THEN B=L1+3:GOTO 530
620 L1=L1+1:F2=1:GOTO 560
630 POKE 65496,0:IF CF=1 THEN RETURN ELSE IF YN$="n" THEN IF LEN(A$)=0 THEN 460
640 IF LEN(A$)>79 THEN LN=LN+INT(LEN(A$)/80):H=H+INT(LEN(A$)/80)
650 LN=LN+1:MID$(Q$,20,5)="     ":MID$(Q$,20,LEN(STR$(LN)))=STR$(LN)
660 LOCATE 0,23:PRINT A$
670 RETURN
680 ' LOAD
690 CLOSE #1
700 CLS:PRINT"Filename Ext  Attr":PRINT"------------------":DIR:POKE 282,255
710 GOSUB 250:LOCATE 1,23:ATTR 1,2:INPUT"Enter name of file and extention to view";NA$:ATTR 1,0:YN$=""
720 POKE 282,0:CLS
730 IF LEN(NA$)>12 THEN 700
740 IF INSTR(1,NA$,".")=0 AND INSTR(1,NA$,"/")=0 THEN 700
750 MID$(Q$,2,12)=NA$+STRING$(12-LEN(NA$),32)
760 LN=0:OPEN"I",#1,NA$:IF YN$<>"" THEN 790
770 GOSUB 250:LOCATE 0,23:ATTR 1,2:INPUT" Print blank lines (y/N)";YN$:ATTR 1,0:CLS:IF YN$<>"y" AND YN$<>"n" THEN YN$="n"
780 GOSUB 250:LOCATE 0,23:ATTR 1,2:INPUT" Filter ANSI commands (y/N)";AN$:ATTR 1,0:CLS:IF AN$<>"y" AND AN$<>"n" THEN AN$="n"
790 H=0
800 GOSUB 450:H=H+1:IF H<23 THEN 800
810 RETURN
820 ' SEARCH
830 GOSUB 250:LOCATE 0,23:ATTR 1,2:PRINT" String (ENTER='";ST$;"') ";:LINEINPUT SP$:IF SP$<>"" THEN ST$=SP$
840 ATTR 1,0:CLS
850 IF EOF(1)=-1 THEN 1170
860 CF=1:GOSUB 470:CF=0
870 LN=LN+1:IF LEN(A$)>80 THEN LN=LN+INT(LEN(A$)/80)
880 GOSUB 270
890 IF INSTR(1,A$,ST$)=0 THEN 850 ELSE ATTR 1,0:LOCATE 0,23:PRINT A$:H=0
900 GOSUB 450:H=H+1:IF H<21 THEN 900
910 RETURN
920 ' PRINT
930 LOCATE 0,23:ATTR 1,2:PRINT" 1) this page only  2) up to this page  "+STRING$(39,32);:LOCATE 65,23:PRINT"PAGES:"STR$(INT(LN/60+.99999));:ATTR 1,0
940 J$=INKEY$:IF J$="" THEN 940
950 ON VAL(J$) GOTO 970,1040
960 RETURN
970 ' PRINT THIS PAGE: SCANS MEMORY AND DUMPS TO PRINTER
980 K2=0:POKE 65497,0
990 J=442368
1000 PP=LPEEK(J):IF PP<32 OR PP>126 THEN PP=32
1010 PP$=PP$+CHR$(PP):K2=K2+1:IF K2=80 THEN POKE 65496,0:PRINT #-2,PP$:PP$="":K2=0:POKE 65497,0
1020 J=J+2:IF J<446048 THEN 1000
1030 POKE 65496,0:RETURN
1040 ' PRINT UP TO THIS PAGE
1050 UT=LN:CLOSE #1:L2=0:L3=0
1060 OPEN"I",#1,NA$
1070 IF EOF(1)=-1 THEN 1170
1080 CF=1:GOSUB 470:CF=0
1090 IF YN$="n" THEN IF LEN(A$)=0 THEN 1080
1100 L2=L2+1:L3=L3+1:IF LEN(A$)>80 THEN L2=L2+INT(LEN(A$)/80):L3=L3+INT(LEN(A$)/80)
1110 IF L2>UT THEN RETURN
1120 PRINT #-2,A$
1130 IF L3=>60 THEN PRINT #-2,STRING$(66-L3,13);:L3=0
1140 GOTO 1070
1150 ' TOP
1160 CLS:CLOSE:GOSUB 760:RETURN
1170 ' EOF
1180 LOCATE 0,23:ATTR 1,2:PRINT"                  ---END OF FILE--- press any key to continue                  ";:CLOSE #1:ATTR 1,0:EXEC 44539:GOTO 310
1190 ' END
1200 GOSUB 250:LOCATE 0,23:ATTR 1,2:PRINT" Are you sure (y/n) ?";:ATTR 1,0
1210 J$=INKEY$:IF J$="" THEN 1210 ELSE IF J$<>"y" THEN RETURN
1220 CLOSE:POKE 282,255:POKE 113,0:EXEC 40999
1230 ' END OF PROGRAM
