0 'DISCRIMINATION 1.0; COPYRIGHT (C) 1986 BRUCE BELL
1 'note: pressing 'Q' anytime during play, will end the game before all pieces are moved.
10 CLEAR1000:DIMF$(31),L$(25),F(31),P(31),U(31),A(11):HS=999
15 GOSUB2205
20 POKE65494,0:CLS:PRINT"DISCRIMINATION 1.0","COPYRIGHT (C) 1986 BRUCE BELL   EXTENDED COLOR BASIC 32K":PRINT:PRINT
25 PRINT@160,STRING$(13,60)" MENU "STRING$(13,62),,TAB(6)"1. BEAT THE CLOCK"TAB(38)"2. MEMORY MATCH"TAB(38)"3. LOOK & FIND"TAB(38)"4. END PROGRAM":PRINT:PRINT" ENTER YOUR SELECTION (1-4)";:S=0
30 FORP=1024TO1535:IFS<1ORS>4THENS=VAL(INKEY$):POKEP,(PEEK(P)+64)AND127:NEXT:GOTO30
35 IFS=4THENCLS:PRINT"BYE BYE":ENDELSEPRINTS:PRINT@418,"LEVEL OF DIFFICULTY (1-4)";:INPUTLD$:LD=VAL(LD$):IFLD>4THEN30ELSEIFLD<1THEN20
40 POKE65495,0:ONS GOTO105,505,1005
100 '*****beat the clock ******
105 CLS8:PRINT@96,"AS QUICKLY AS POSSIBLE, PLACE   THE BLACK FIGURES ON THE LOWER  SCREEN IN THE MATCHING HOLES","ABOVE. USE THE JOYSTICK TO MOVE THE CURSOR AND PRESS THE FIRE   BUTTON TO PICK UP OR PLACE A","FIGURE.",,,,"PLAY BEGINS IN ONE MOMENT..."
110 PMODE4,1:PCLS:DRAW"BM180,0C5D191"
115 FORK=0TO31:U(K)=0:P(K)=0:F(K)=0:NEXT
120 GOSUB2025:'draw lower screen
125 GOSUB2050:'draw upper screen
130 W$="BEAT":DRAW"BM200,8C6":GOSUB2185:W$="THE":DRAW"BM205,18C8":GOSUB2185:W$="CLOCK":DRAW"BM195,28C7":GOSUB2185:'title
135 GOSUB2160:'draw copr notice
140 GOSUB2085:'countdown
145 X=5:Y=104:K=1:C=3:N=0:TIMER=0:'initialize game
150 GOSUB2105:'joystick input
155 IFQ$="Q"THEN2135ELSEIFK=2THEN165:'fb pressed or quit?
160 K=2:C=1:D=94:XG=X:YG=Y:PG=P:Y=Y-94:SOUND100,1:GOTO175:'adjust for upper board
165 IFF(PG)=P(P)THENGET(XG,YG)-(XG+20,YG+20),A,G:COLOR0,0:LINE(XG,YG)-(XG+20,YG+20),PSET,BF:PUT(X,Y)-(X+20,Y+20),A,AND:F(PG)=-1:N=N+1:SOUND200,1ELSESOUND50,1:'match?
170 K=1:C=3:D=0:Y=Y+94:'adjust for lower board
175 IFN=LD*8THEN2135ELSEGOSUB2120:GOTO155:'figures completed?
500 '*****memory match *******
505 CLS6:PRINT@96,"A FIGURE HIDES BEHIND EACH NUM- BER. PRESSING THE FIRE BUTTON   UNCOVERS THE FIGURE. AS QUICKLY AS POSSIBLE USE THE JOYSTICK TO MATCH THE FIGURES","PLAY BEGINS IN ONE MOMENT..."
510 FORK=0TO31:U(K)=0:P(K)=0:F(K)=0:NEXT:PG=-1
515 FORK=0TOLD*4-1
520 F=RND(32)-1:IFF(F)=1THEN520ELSEF(F)=1:FORX=1TO2
525 P=RND(LD*8)-1:IFU(P)=1THEN525ELSEP(P)=F:U(P)=1:NEXTX,K
530 PMODE4,1:PCLS5:COLOR0,5:LINE(180,0)-(255,191),PSET,BF:POKE178,2:LINE(0,98)-(180,191),PSET,BF:SCREEN1,1
535 GOSUB2070:'print discrimination
540 N=1:FORY=22TO88STEP22:FORJ=0TO2*LD-1:X=1+88/LD*J:DRAW"BM=X;,=Y;C5":W$=STR$(N):GOSUB2185:N=N+1:NEXTJ,Y:'print numbers in squares
545 W$="MEMORY":DRAW"BM188,18C6":GOSUB2185:W$="MATCH":DRAW"BM194,28":GOSUB2185
550 GOSUB2160:'draw copr notice
555 GOSUB2085:'countdown
560 X=5:Y=10:C=3:K=1:N=0:D=94:TIMER=0:'initialze game
565 GOSUB2105:'joystick input
570 IFQ$="Q"THEN2135ELSEIFP=PG ORP(P)=-1THENSOUND1,1:GOTO565ELSEIFK=2THEN580
575 K=2:XG=X:YG=Y:PG=P:GOSUB2010:SOUND100,1:GOTO590
580 K=1:GOSUB2010:IFP(P)=P(PG)THENN=N+1:W$="MATCHES"+STR$(N):COLOR0,5:LINE(30,120)-(150,135),PSET,BF:DRAW"BM40,130C5":GOSUB2185:FORT=1TO3:PLAY"L100;1;2;3;4;5;6;7;8;9;10;11;12":NEXT:P(P)=-1:P(PG)=-1:GOTO590:'match!
585 SOUND10,2:COLOR0,5:LINE(XG,YG)-(XG+20,YG+20),PSET,BF:LINE(X,Y)-(X+20,Y+20),PSET,BF:W$=STR$(PG+1):H=XG-5:V=YG+12:DRAW"BM=H;,=V;C5":GOSUB2185:W$=STR$(P+1):H=X-5:V=Y+12:DRAW"BM=H;,=V;":GOSUB2185:PG=-1:'no match
590 IFN=LD*4THEN2135ELSE565
1000 '*****look and find ******
1005 CLS4:PRINT@128,"USE THE JOYSTICK TO FIND THE","FIGURE SHOWN IN THE BOTTOM HALF OF THE SCREEN. PRESS THE FIRE   BUTTON WHEN YOU'VE FOUND IT.","PLAY BEGINS IN ONE MOMENT..."
1010 FORK=0TO31:U(K)=0:P(K)=-1:NEXT
1015 PMODE4,1:COLOR0,5:PCLS:LINE(180,0)-(255,191),PSET,BF:DRAW"BM0,100R180"
1020 GOSUB2050:'draw upper screen
1025 W$="LOOK":DRAW"BM200,8C6":GOSUB2185:W$="AND":DRAW"BM205,18C8":GOSUB2185:W$="FIND":DRAW"BM200,28C7":GOSUB2185:'title
1030 GOSUB2160:'draw copr notice
1035 W$="FIND THE     ABOVE":DRAW"BM10,130C5":GOSUB2185
1040 GOSUB2085:'countdown
1045 X=5:Y=10:D=94:C=1:N=0:TIMER=0:'initialize game
1050 F=RND(LD*8)-1:IFU(F)=1THEN1050ELSEPMODE4,1:SCREEN1,1:COLOR5,0:LINE(90,118)-(110,138),PSET,BF:DRAW"BM100,128C0XF$(F);":SOUND200,1:PMODE3,1
1055 GOSUB2105:'joystick input
1060 IFQ$="Q"THEN2135ELSECOLOR0,5:LINE(5,170)-(105,180),PSET,BF:IFF<>P(P)THENW$="LOOK AGAIN":DRAW"BM5,180C7":GOSUB2185:SOUND5,2:GOTO1055:'did not find
1065 W$="CORRECT":DRAW"BM5,180C6":GOSUB2185:N=N+1:U(F)=1:FORT=1TO3:PLAY"L100;1;2;3;4;5;6;7;8;9":NEXT:'found it!
1070 IFN=LD*8THEN2135ELSE1050
1075 GOTO1075
2000 '!!!!!!!subroutines!!!!!!!
2005 'draw figure in memory match
2010 PMODE4,1:SCREEN1,1:COLOR5,0:LINE(X,Y)-(X+20,Y+20),PSET,BF:H=X+10:V=Y+10:DRAW"BM=H;,=V;C0XF$(P(P));":PMODE3,1
2015 RETURN
2020 'draw lower screen subr
2025 K=0:FORY=114TO180STEP22:FORJ=0TO2*LD-1
2030 P=RND(LD*8)-1:IFU(P)=1THEN2030ELSEF(K)=P:U(P)=1:K=K+1:X=14+88/LD*J:DRAW"BM=X;,=Y;C5XF$(P);":NEXTJ,Y
2035 COLOR5,0:LINE(0,8)-(180,100),PSET,BF:PAINT(1,191),5,5
2040 RETURN
2045 'draw upper screen subr
2050 K=0:FORY=20TO86STEP22:FORJ=0TO2*LD-1
2055 P=RND(LD*8)-1:IFU(P)=2THEN2055ELSEP(K)=P:U(P)=2:K=K+1:X=14+88/LD*J:DRAW"BM=X;,=Y;C0XF$(P);":NEXTJ,Y
2060 DRAW"BM0,100R180":POKE178,1:PAINT(1,9),,0
2065 COLOR5,5:LINE(0,0)-(180,7),PSET,BF
2070 SCREEN1,1:PMODE3,1:W$="DISCRIMINATION":DRAW"BM20,6C7":GOSUB2185
2075 RETURN
2080 'countdown subr
2085 W$="GET":DRAW"BM184,100C6":GOSUB2185:W$="READY":DRAW"BM184,110":GOSUB2185:SOUND50,1:FORJ=1TO700:NEXT:COLOR5,5:LINE(184,102)-(255,110),PSET,BF:W$="SET":DRAW"BM184,110C6":GOSUB2185:SOUND100,1:FORJ=1TO700:NEXT
2090 COLOR5,5:LINE(184,90)-(255,110),PSET,BF:W$="GO":DRAW"BM184,100C7":GOSUB2185:SOUND150,2
2095 RETURN
2100 'joystick subr
2105 IFTIMER>54000THENQ$="Q"ELSEQ$=INKEY$
2110 IFQ$="Q"THENRETURNELSEJH=JOYSTK(0):JV=JOYSTK(1):POKE178,C:DRAW"BM=X;,=Y;R20D20L20U20"
2115 X=INT(JH*LD/32)*88/LD+5:Y=INT(JV/16)*22+104-D:P=(X-5+(Y+D-104)*8)/(88/LD):P=INT(P+.5)
2120 PK=PEEK(65280):IFPK<>126ANDPK<>254THENZ=1ELSEIFZ THENZ=0:RETURN
2125 DRAW"BM=X;,=Y;C5R20D20L20U20":GOTO2105
2130 'compute score/replay subr
2135 SC=TIMER/60:COLOR5,5:IFQ$<>"Q"THENIFSC<HS THENHS=SC
2140 COLOR5,5:LINE(182,44)-(255,74),PSET,BF:GOSUB2170
2145 COLOR5,5:LINE(184,90)-(255,110),PSET,BF:DRAW"BM184,100C7XL$(15);C6":W$="LAY":GOSUB2185:DRAW"BM205,110C8":W$="OR":GOSUB2185:DRAW"BM210,120C7XL$(16);C6":W$="UIT":GOSUB2185
2150 Q$=INKEY$:IFQ$="P"THENSCREEN0,0:GOTO40 ELSEIFQ$="Q"THEN10ELSE2150
2155 'draw score/copr subr
2160 DRAW"BM252,32C6L68BF2C8R64BG2C7L60"
2165 W$="C 1986":DRAW"BM188,178C8":GOSUB2185:PMODE4,1:SCREEN1,1:CIRCLE(191,175),07:PMODE3,1:W$="B BELL":DRAW"BM188,188":GOSUB2185
2170 DRAW"BM182,54C7":W$="LEVEL"+STR$(LD):GOSUB2185:DRAW"BM182,64C8":W$="TIME"+STR$(INT(SC+.5)):GOSUB2185:DRAW"BM182,74C6":W$="BEST"+STR$(INT(HS+.5)):GOSUB2185:RETURN
2175 RETURN
2180 'print hi-res character
2185 FORK=1TOLEN(W$):L=ASC(MID$(W$,K,1)):IFL>64ANDL<91THENL$=L$(L-65)ELSEIFL=46THENL$="URDLBR6"ELSEIFL<48ORL>57THENL$="BR6"ELSEL$=N$(L-48)
2190 DRAWL$:NEXTK
2195 RETURN
2200 'define figures
2205 FORK=0TO31:READF$(K):NEXT:DATABD8E4R4H4E4L4H4G4L4F4G4R4F4,"BD8R8M-8,-16M-8,+16R8",BD8R8U16L16D16R8,BD6R8U12L16D12R16,BH2U6R4D6R6D4L6D6L4U6L6U4R6
2210 DATABU2E6F2G6F6G2H6G6H2E6H6E2F6,BL8E8F8G8H8,BU2E6F2G6D8L4U8H6E2F6
2215 DATAE8L16F16L16E8,BU8F6D2G6H6U2E6,BU8F8D8L16U8E8,BR2U8L4D16R4U8,F8U16G16U16F8,E8D16L16U16F8,BR2D8L4U12L6U4R16D4L6D8,BU2R8D4L16U4R8
2220 DATABR2R6U4L6U4L4D4L6D4R6D8R4U8,BU2R8U4L16D4R8BD4R8D4L16U4R8,BR2E6L4G12R4E6,"E8M-8,+16M-8,-16F8",BD8R8H6U10L4D10G6R8,BE2U6R4D16L4U6L4D6L4U16R4D6R4,BD8R8U4L6U4R6U4L16D4R6D4L6D4R8,BD8R8U16L4D12L12D4R8,BD8R8U16L4G4H4L4D16R8
2225 DATABR8H8L4G4F4G4F4R4E8,BD8E6U4H6G6D4F6,U8R8G16R8U8,BL2H6R4F12L4H6,BD8E8U8L4G4H4L4D8F8,"BD8M+8,-16L4G4H4L4M+8,+16",U8L8F16L8U8
2230 'define hi-res char set
2235 N$(0)="NU6R4U6L4R6D6BR4":N$(1)="R6UL2NL4U5L2DR2BR6BD5":N$(2)="R6UNL2BU2U3L6R4D3L4D3BR10":N$(3)="R6U3NL4U3L6R4D6BR6":N$(4)="BU3NU3R4ND3U3R2D6BR4"
2240 N$(5)="UR2DL2R6U3L6U3R2ND3R4BR4BD6":N$(6)="NU6R6U3LND3L5U3R6DL2BR6BD5":N$(7)="BU6R4D6RU6RD6BR4":N$(8)="U3NR4U3R4D6RU6RD6L6BR10":N$(9)="BU3NR4U3R4D6RU6RD6BR4"
2245 L$(0)="U6R2ND6R4D3NL6D3BR4":L$(1)=L$(0)+"BL4L6BR10":L$(2)="NR6U6R2ND6R4D2BF4":L$(3)="U6R2ND6R2F2D2G2BR6":L$(4)="NR6U3NR4U3R2ND6R4BD6BR4":L$(5)="U3NR4U3R2ND6R4BD6BR4":L$(6)="NR6U6R2ND6R4BD4D2BR4"
2250 L$(7)="U6R2D3ND3R4U3D6BR4":L$(8)="R2U6L2R6L2D6R2BR4":L$(9)="R2U6L2R6L2D6BR6":L$(10)="U6R2D2NM+4,-2ND4F4BR4":L$(11)="U6R2D6R4BR4":L$(12)="U6R2ND6F2E2D6BR4"
2255 L$(13)="U6R2D3R2D3R2NU6BR4":L$(14)="U6R6L4D6R4NU6BR4":L$(15)="U6R2ND6R4D3L4BD3BR8":L$(16)="U6R2ND6R4D6L6R4BU2M+4,+2BR4":L$(17)="U6R2ND6R4D3L4R2M+2,+3BR4":L$(18)="R6U3L2ND3L4U3R2ND3R4BD6BR4"
2260 L$(19)="BU6R2ND6R2ND6R2BD6BR4":L$(20)="U6R2D6R4NU6BR4":L$(21)="BR2H2U4R2D4F2E2U4BD6BR4":L$(22)="BU2U4R2D6E2F2NU6BR4":L$(23)="UE2H2UR2DF2G2DBR4UH2E2UBD6BR4":L$(24)="UR2DL2R6U3NU3L6U3R2D3BR8BD3":L$(25)="R6UL2DL4U2M+6,-2U2L6DR2
2265 RETURN
