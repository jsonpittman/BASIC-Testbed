0 ' STOCK TRACKER GRAPH PROGRAM
1 'COPYRIGHT(C), 1986 MARK EVANS
2 PCLEAR4:PMODE4,1:PCLS:CLEAR1200:DIMS(5,130),D$(130):CLS0:INPUT"ENTER FILENAME";DN$:PRINT@96,"PRESS PLAY AND <ENTER> TO LOAD  DATA FROM TAPE.":LINEINPUTR$:R$="":DN$=MID$(DN$,1,8)
3 OPEN"I",-1,DN$:INPUT#-1,NE:FORQ=1TO5:INPUT#-1,S$(Q):NEXT:FORW=1TONE:INPUT#-1,D$(W),S(1,W),S(2,W),S(3,W),S(4,W),S(5,W):NEXT:CLOSE(-1):POKE65495,0:FORW=1TO5:T(W)=0:NEXT:PRINT@448,"PLEASE STANDBY"
4 FORQ=1TO5:HP(Q)=0:LP(Q)=5000:FORW=1TONE:T(Q)=T(Q)+S(Q,W):IFS(Q,W)>HP(Q) THENHP(Q)=S(Q,W)
5 IFS(Q,W)<LP(Q)THENLP(Q)=S(Q,W)
6 NEXT:AP(Q)=T(Q)/NE:NEXT:POKE65494,0
7 MA=0:MT=0:CLS0:PRINT"ENTER NUMBER TO BE GRAPHED":FORQ=1TO5:PRINTUSING"# ";Q;:PRINTS$(Q):NEXT
8 R$=INKEY$:S=VAL(R$):IFS>0ANDS<6THEN9ELSE8
9 SC=180/(HP(S)-LP(S)):MP(S)=(HP(S)+LP(S))/2:DS=256/NE:SCREEN1,1:LINE-(0,96+(MP(S)-S(S,1))*SC),PRESET
10 FORD=1TONE:IFD<=30THENMT=MT+S(S,D):MA=MT/D
11 IFD>30THENMT=MT+S(S,D)-S(S,D-30):MA=MT/30
12 PSET(DS*D,96+(MP(S)-MA)*SC):LINE-(DS*D,96+(MP(S)-S(S,D))*SC),PSET:PSET(DS*D,192):PSET(DS*D,191):NEXT
13 SCREEN1,1:R$=INKEY$:IF R$=""THEN13
14 CLS:PRINT"ENTER SELECTION:":PRINT"1 DAY GRID":PRINTUSING"2 AVERAGE PRICE ($####.###)";AP(S):PRINT"3 HIGHLIGHT A DAY":PRINT"4 CLEAR SCREEN":PRINT"5 GRAPH ANOTHER STOCK":PRINT"6 VIEW GRAPH":PRINT"7 HORIZONTAL PRICE LINES":PRINT"8 END PROGRAM"
15 R$=INKEY$:IFR$="4"THENPCLS:GOTO13ELSEIFR$="6"THEN13ELSEIFR$="2"THEN25ELSEIFR$="5"THENGOTO7ELSEIFR$="1"THEN21ELSEIFR$="3"THEN16ELSEIFR$="8"THENENDELSEIFR$="7"AND(HP(S)-LP(S))>1THENGOTO23ELSEIFR$="7"THENPRINT"INTERVAL TOO SMALL":GOTO15ELSEGOTO15
16 CLS:PRINT"USE THE UP AND DOWN ARROW TO    SCROLL THROUGH THE DATES. PRESS ENTER TO HIGHLIGHT THE DATE     BEING VIEWED.":QW=1
17 R$=INKEY$:IFR$=CHR$(10)THENQW=QW-1ELSEIFR$="^"THENQW=QW+1
18 IF QW<1THENSOUND1,1:QW=1ELSEIF QW>NE THENSOUND255,1:QW=NE
19 IFR$=CHR$(13)THENLINE(DS*QW,0)-(DS*QW,196),PSET:GOTO13
20 PRINT@164,USING"%        %";D$(QW):GOTO17
21 CLS0:INPUT"ENTER INVERVAL FOR GRIDS, ENTER <1> FOR A GRID FOR EACH DAY";GI:IF GI>NE THENPRINT"INTERVAL TOO GREAT, ENTER AGAIN";ELSEIF GI<1THENPRINT"INTERVAL MUST BE GREATER THEN 0":FORWQ=1TO3000:NEXT:GOTO21
22 FORX=1TO NE*DS STEP GI*DS:LINE(X,0)-(X,255),PSET:NEXT:GOTO13
23 I=INT((HP(S)-LP(S))/20):GOSUB24:CLS:PRINTUSING"$##.#INCREMENTS";I:FORXY=INT(LP(S)+1)TOINT(HP(S))STEPI:LINE(0,96+(MP(S)-XY)*SC)-(255,96+(MP(S)-XY)*SC),PSET:NEXT:GOTO13
24 IFI<1THENI=1:RETURNELSERETURN
25 POKE65495,0:YP=96+(MP(S)-AP(S))*SC:FORP=0TO255:IFPPOINT(P,YP)=5THENC=0ELSEC=5
26 PSET(P,YP,C):NEXT:POKE65494,0:GOTO13
1 CLEAR1000:CLS
2 FLAG=1:PMODE4,1:PCLS:SCREEN1,0
5 CLS:DIM K$(100),E1(192),E2(192),A(4,4):C$="COMPUTATIONS ARE IN PROGRESS...":GOTO32
10 Y=SIN(X)
12 RETURN
20 Y=COS(X)
22 RETURN
32 K$(32)="BR8":'SPACE
33 K$(33)="BR3UBU2U3BD6BR4":'!
34 K$(34)="BRBU3EU2BR3D2GBD3BR4":'"
35 K$(35)="BRBUR5BU3L5BED5BR3U5BD5BR2":'#
36 K$(36)="BRBUFR3EUHL3HUER3FBHBL2NUD7BU2BR5":'$
37 K$(37)="BRBU4EFGHBR5G5BR4HEFGBR3":'%
39 K$(39)="BR3BU3EU2BR4BD6":'APOST
40 K$(40)="BR4H2U2E2BD6BR4":'(
41 K$(41)="BR4BU6F2D2G2BR4":')
42 K$(42)="BR2BU2E2NH2NU2NE2NF2D2BD2BR4":'*
43 K$(43)="BR2BU3R2NU2NR2D2BDBR4":'+
44 K$(44)="BR3EUBD2BR4":',
45 K$(45)="BR2BU3R4BD3BR2":'-
46 K$(46)="BR2R1BR4":'.
47 K$(47)="BRE5BD5BR2":'/
48 K$(48)="BR2HU4ER3FD4GNL3BR3":'0
49 K$(49)="BR3R2BLU6GBD5BR5":'1
50 K$(50)="BRBU5ER3FG4DR4BR3":'2
51 K$(51)="BRBU5ER3FG2F2G1L3HBFBR6":'3
52 K$(52)="BR5U6G4R5BD2BR2":'4
53 K$(53)="BRBUFR3EU2HL4U2R5BD6BR2":'5
54 K$(54)="BRBUNUFR3EUHL3GU3ER3BD6BR3":'6
55 K$(55)="BRBU6R5G5DBR7":'7
56 K$(56)="BR2HUEHUER3FDGNL3FDGL3BR6":'8
57 K$(57)="BR2BU3HUER3FD2NL3D2GL3BR6":'9
58 K$(58)="BR4BUUBU2UBD5BR4":':
59 K$(59)="BR3EUBU2UBD5BR4":';
60 K$(60)="BR2BU3NE3F3BR3":'<
61 K$(61)="BRBU4R4BD2L4BD2BR7":'=
62 K$(62)="BR2BU6F3G3BR6":'>
63 K$(63)="BR2BU5ER2FG2BD2DBR4":'?
65 K$(65)="BRU5ER3FD3NL5D2BR2":'A
66 K$(66)="BRU6R4FDGNL4FDGNL4BR3":'B
67 K$(67)="BR2HU4ER3FBD4GNL3BR3":'C
68 K$(68)="BRU6R3F2D2G2NL3BR4":'D
69 K$(69)="BRNR5U3NR3U3R5BD6BR2":'E
70 K$(70)="BRU3NR3U3R5BD6BR2":'F
71 K$(71)="BR2HU4ER3FBD2NL2D2GNL3BR3":'G
72 K$(72)="BRU3NU3R5NU3D3BR2":'H
73 K$(73)="BR4NLNRU6NLNRBD6BR4":'I
74 K$(74)="BR2NHR3EU5NLRBD6BR":'J
75 K$(75)="BR2U2NU4ENE3F3BR2":'K
76 K$(76)="BRNU6R5BR2":'L
77 K$(77)="BRU6F2RE2D6BR2":'M
78 K$(78)="BRU6F5NU5DBR2":'N
79 K$(79)="BR2HU4ER3FD4GNL3BR3":'O
80 K$(80)="BRU6R4FDGL4BD3BR7":'P
81 K$(81)="BR3H2U2E2RF2D2GNHNFGLBR5":'Q
82 K$(82)="BRU5ER3FDGL2NL2F3BR2":'R
83 K$(83)="BRBUFR3EUHL3HUER3FBD5BR2":'S
84 K$(84)="BR4U6NL2R2BD6BR2":'T
85 K$(85)="BRBU6D5FR3EU5BD6BR2":'U
86 K$(86)="BRBU6D3F3E2U4BD6BR2":'V
87 K$(87)="BRNU6E2RF2NU6BR2":'W
88 K$(88)="BRUE2H2UBR5DG2NLF2DBR2":'X
89 K$(89)="BRBU6F3ND3E2UBD6BR2":'Y
90 K$(90)="BRBU6R5DG4DR5BR2":'Z
94 K$(94)="BR2BU4E2F2BD4BR2":'^
95 IF FLAG=0 GOTO5820 ELSE GOTO 5600
1000 '**PRINT ON HIRES SCREEN
1100 P=ZP:GOSUB1240
1120 ZF$="S"+STR$(ZF)+"BM"+STR$(ZX)+","+STR$(ZY):DRAW ZF$
1140 FOR ZJ=1 TO LEN(ZM$)
1160 ZK=ASC(MID$(ZM$,ZJ,1))
1180 DRAW K$(ZK):P=P+1:IF INT(P/32)=P/32 THEN ZX=0:ZY=ZY+12:ZF$="BM"+STR$(ZX)+","+STR$(ZY):DRAW ZF$
1200 NEXT ZJ:RETURN
1240 '**CONVERT PRINT@ TO HIRES COORD
1340 ZX=(P-INT(P/32)*32)*8:ZY=INT(P/32)*12+8:RETURN
1400 '**HI RES TEXT INPUT
1560 GOSUB1000:'PROMPT
1580 GOSUB1240:'FIND COORD FOR INPUT
1600 ZZ$=INKEY$:ZZ$="":ZA$=""
1620 ZZ$=INKEY$:IF ZZ$<>""THEN1700
1660 LINE(ZX,ZY-7)-(ZX+6,ZY),PSET,BF
1680 COLOR 1,0:LINE(ZX,ZY-7)-(ZX+6,ZY),PSET,BF:COLOR 0,1:GOTO1620
1700 IFZZ$>CHR$(13)THEN1760
1720 IFZZ$=CHR$(13)ANDLEN(ZA$)<>0THEN1900
1740 IFZZ$=CHR$(8)THEN1840
1760 IFLEN(ZA$)=XM THEN1620
1780 IFXT<>2THEN1820
1800 IFZZ$<CHR$(43)ORZZ$>CHR$(57)THEN1620ELSE1820
1820 ZA$=ZA$+ZZ$:ZI=ASC(ZZ$):DRAW"BL6"+K$(ZI):ZX=ZX+8:GOTO1620
1840 IFLEN(ZA$)<1THEN1620
1860 ZA$=LEFT$(ZA$,LEN(ZA$)-1)
1880 ZX=ZX-8:COLOR1,0:LINE(ZX,ZY-7)-(ZX+8,ZY),PSET,BF:COLOR0,1:GOTO1620
1900 IFXT=2THENZA=VAL(ZA$)
1920 RETURN
2700 '** FIND MAX MIN OVER INTERVAL
2740 POKE65495,0:D=(R-L)/192:YU=-1E+16:YL=1E+16:X=L:FORK=0TO192:X=L+K*D:IFEQ=1THENGOSUB10:E1(K)=Y:GOTO2840
2820 GOSUB20:E2(K)=Y
2840 IFY<YL THENYL=Y:XL=X
2860 IFY>YU THENYU=Y:XU=X
2880 NEXTK
2900 X=R:IFEQ=1THENGOSUB10ELSEGOSUB20
2920 IFEQ=1THENE1(192)=Y ELSEE2(192)=Y
2940 IFY<YL THENYL=Y:XL=X
2960 IFY>YU THENYU=Y:XU=X
2970 IFYL>0THENYL=0
2975 IFYU<0THENYU=0
2980 POKE65494,0:RETURN
3020 '**DRAW GRAPH
3080 '**DRAW BORDERS
3120 PMODE4,1:COLOR0,1:PCLS:SCREEN1,1:POKE178,2:LINE(63,1)-(255,145),PSET,B
3180 '**HORIZONTAL GRIDS
3220 FORY=1TO145STEP36:LINE(64,Y)-(255,Y),PSET:NEXTY
3320 '**VERTICAL GRIDS
3360 FORX=63TO255STEP96:LINE(X,1)-(X,7),PSET:LINE(X,139)-(X,151),PSET:NEXTX:FORX=111TO207STEP96:LINE(X,1)-(X,7),PSET:LINE(X,139)-(X,164),PSET:NEXTX:POKE178,0
3540 '**Y-AXIS
3580 IFSGN(L)=SGN(R)THEN3700
3600 X=63-L/(R-L)*192:X=INT(X):FORK=X-1TOX+1:IFK>255THEN3660
3610 LINE(K,0)-(K,145),PSET:NEXTK
3660 '**X-AXIS
3700 AX=0:IF SGN(YU)=SGN(YL) THEN 3780
3720 Y=145+YL/(YU-YL)*145+1:Y=INT(Y):AX=Y:FORK=Y-1 TO Y+1:LINE(63,K)-(255,K),PSET:NEXTK
3780 '**PRINT Y-COORD
3820 ZX=0:ZY=8:FORK=0TO4:N=YU-(YU-YL)/4*K:GOSUB5320
3860 ZM$=STR$(N):ZM$=RIGHT$(ZM$,LEN(ZM$)-1):IF ABS(N)<1 AND N<>0 THEN ZM$="0"+ZM$
3880 IFN<0THENZM$="-"+ZM$
3900 IFLEN(ZM$)<7THENZM$=" "+ZM$:GOTO3900
3924 P=0:GOSUB1120:ZY=ZY+34:NEXTK
3960 '**PRINT X-COORD
4000 N=L:GOSUB4240
4020 ZX=42:ZY=163:GOSUB1120
4040 N=L+(R-L)/2:P=0:GOSUB4240
4060 ZX=140:ZY=163:GOSUB1120
4080 N=L+(R-L):GOSUB5320:ZM$=STR$(N):ZM$=RIGHT$(ZM$,LEN(ZM$)-1)
4100 IF ABS(N)<1 AND N<>0 THEN ZM$="0"+ZM$
4120 IF N<0 THEN ZM$="-"+ZM$
4140 IF LEN(ZM$)<6 THEN ZM$=" "+ZM$:GOTO4140
4160 ZX=210:ZY=163:GOSUB1120
4180 N=L+(R-L)/4:GOSUB4240
4200 ZX=92:ZY=175:GOSUB1120
4220 N=L+(R-L)/4*3:GOSUB4240:ZX=188:ZY=175:GOSUB1120:GOTO4420
4240 GOSUB5320:ZM$=STR$(N):ZM$=RIGHT$(ZM$,LEN(ZM$)-1)
4260 IFABS(N)<1ANDN<>0THENZM$="0"+ZM$
4280 IFN<0THENZM$="-"+ZM$
4300 IFLEN(ZM$)<6THENZM$=ZM$+" "
4320 IFLEN(ZM$)<6THENZM$=" "+ZM$:GOTO4300
4340 RETURN
4380 '**PLOT GRAPH
4420 IFM=2THENZM$="GRAPH OF EQUATION"+STR$(EQ):ZP=480:GOSUB1000
4480 X1=63:FORK=0TO192:IFEQ=1THENY=E1(K):POKE178,1:ELSEY=E2(K):POKE178,2
4485 IF Y=0 THEN POKE 178,0
4500 Y1=145*(Y-YU)/(YL-YU)+1:IFY1<0THENY1=0
4510 LINE(X1,Y1)-(X1-1,Y1-1),PSET,BF
4520 X1=X1+1:NEXTK:RETURN
4650 '**ENTER X-INTERVAL
4660 GOSUB4780:ZM$="MINIMUM VALUE OF X? ":XM=8:XT=2:GOSUB1400:L=ZA:GOSUB4780:ZM$="MAXIMUM VALUE OF X? ":GOSUB1400:R=ZA:IFABS(R-L)<.001THENGOSUB4780:ZM$="INTERVAL MUST BE > .001":ZP=480:GOSUB1000:ZT=2:GOSUB4900:GOTO4660
4670 IFR<L THENTEMP=L:L=R:R=TEMP:RETURN
4700 '**CLEAR TO END OF SCREEN
4710 POKE65495,0:P=ZP:GOSUB1240:COLOR1,0:LINE(ZX,ZY-8)-(255,191),PSET,BF:COLOR0,1:POKE65494,0:RETURN
4740 '**PRINT CENTERED
4750 ZP=32*(ZL-1)+16-LEN(ZM$)/2:GOSUB1000:RETURN
4780 '**CLEAR PROMPT LINE
4820 COLOR1,0:LINE(0,180)-(255,191),PSET,BF:COLOR0,1:RETURN
4900 '**TIME DELAY
4940 TIMER=0
4960 IF TIMER<ZT*60THEN4960
4980 RETURN
5020 '**ENTER KEY S/R
5060 ZM$="PRESS THE ENTER KEY TO CONTINUE.":ZP=480:GOSUB1000
5080 ZZ$=INKEY$:IFZZ$=""THEN5080ELSEIFZZ$<>CHR$(13)THEN5080
5100 RETURN
5140 '**ALTERNATE ENTER KEY S/R
5160 XX=0:X1=INT(X1):Y1=INT(Y1):IFX1<63ORX1>255ORY1<0ORY1>146THENXX=1
5162 ZM$="ENTER":ZP=507:GOSUB1000
5163 IFXX=1THEN5180
5165 X2=X1-2:IFX2<63THENX2=63
5167 X3=X1+2:IFX3>255THENX3=255
5169 Y2=Y1-2:IFY2<0THENY2=0
5171 Y3=Y1+2:IFY3>146THENY3=146
5173 GET (X2,Y2)-(X3,Y3),A
5180 ZZ$=INKEY$:IFZZ$=CHR$(13)THEN5200
5185 IFXX=1THEN5180
5190 CIRCLE(X1,Y1),2,0:ZT=.5:GOSUB4900:CIRCLE(X1,Y1),2,1:ZT=.5:GOSUB4900:GOTO5180
5200 IFXX=1THEN5220
5210 PUT (X2,Y2)-(X3,Y3),A
5220 GOSUB4780:RETURN
5320 '**ROUND OFF S/R
5360 IFABS(N)>=1000000THEN5460
5380 IFABS(N)>=1000ANDABS(N)<1000000THENN=INT(N+.5):GOTO5460
5400 IFABS(N)>=100ANDABS(N)<1000THENN=INT(N*10+.5)/10:GOTO5460
5420 IFABS(N)>=10ANDABS(N)<100THENN=INT(N*100+.5)/100:GOTO5460
5440 IFABS(N)<10THENN=INT(N*1000+.5)/1000:GOTO5460
5460 RETURN
5500 '***** MAIN PROGRAM *****
5560 '**OPENING PAGE
5600 ZM$="GRAPHS AND ROOTS":ZF=8:ZP=64:GOSUB1000
5620 ZM$="  OF FUNCTIONS  ":ZP=128:GOSUB1000:ZF=4
5640 ZM$="BY LOUIS R. TOSCANO":ZL=8:GOSUB4740
5660 ZM$="P.O. BOX 2400":ZL=10:GOSUB4740
5680 ZM$="VINEYARD HAVEN, MA 02568":ZL=12:GOSUB4740
5700 ZM$="TEL: (617)-693-1328":ZL=13:GOSUB4740
5720 ZM$="COPYRIGHT 1984":ZL=15:GOSUB4740:ZT=4:GOSUB4900
5730 COLOR0,1:PCLS:ZM$="DO YOU WANT INSTRUCTIONS (Y/N)?":ZP=256:XT=1:XM=1:GOSUB1400
5740 IF ZA$="Y"THEN GOSUB20000
5780 '**MENU AND DRIVER
5820 CLS:PRINT@4,"WHAT DO YOU WISH TO DO?"
5840 PRINT@64,"(1) DRAW THE GRAPH OF THE           PRESENT EQUATION AT LINE 10";
5860 PRINT@160,"(2) DRAW THE GRAPH OF THE           PRESENT EQUATIONS AT LINES      10 AND 20 SIMULTANEOUSLY"
5920 PRINT@288,"(3) WRITE NEW EQUATIONS AT LINES    10 AND/OR 20"
5940 PRINT@392,"CHOOSE 1 - 3: ";
5942 LINEINPUT MENU$:M=VAL(MENU$)
5945 IF M<>1 AND M<>2 AND M<>3 THEN PRINT@384,STRING$(32,32);:GOTO5940
6020 PCLS:ON M GOSUB 7060,7060,30000
6040 CLS:GOTO5820
7020 '**GRAPH OF TWO EQUATIONS
7060 PMODE 4,1:COLOR0,1:PCLS:SCREEN 1,0
7160 ZP=0:ZM$="CHOOSE THE DOMAIN (X-INTERVAL)":GOSUB1000
7180 ZM$="MINIMUM VALUE OF X? ":ZP=64:XM=8:XT=2:GOSUB1400:L=ZA
7200 ZP=128:ZM$="MAXIMUM VALUE OF X? ":GOSUB1400:R=ZA
7220 IF R<L THEN TEMP=L:L=R:R=TEMP
7240 ZP=256:ZM$="COMPUTATIONS WILL TAKE ABOUT   "+STR$(15*M)+" SECONDS. PLEASE STAND BY.":GOSUB1000
7260 EQ=1:GOSUB2700:IFM=1THEN7340
7280 YL(1)=YL:YU(1)=YU:EQ=2:GOSUB2700:YL(2)=YL:YU(2)=YU
7300 YL=YL(1):IF YL(2)<YL THEN YL=YL(2)
7320 YU=YU(1):IF YU(2)>YU THEN YU=YU(2)
7340 EQ=1:GOSUB3020:IFM=1THEN7380
7360 GOSUB4780:EQ=2:GOSUB4380
7380 GOSUB4780:ZM$="<D><V><R><S><A>":IFM=2THENZM$=ZM$+"<I>
7382 ZM$=ZM$+"<M> <H>ELP ":ZP=480:XM=1:XT=1:GOSUB1400
7385 IF ZA$<>"D"ANDZA$<>"V"ANDZA$<>"R"ANDZA$<>"S"ANDZA$<>"A"ANDZA$<>"I"ANDZA$<>"M"ANDZA$<>"H"THEN7380
7387 IFM=1ANDZA$="I"THEN7380
7390 IF ZA$="D"THEN7400 ELSE IF ZA$="V"THEN7450 ELSE IF ZA$="R"THEN7500 ELSE IF ZA$="H"THEN 7550 ELSE IF ZA$="I" THEN 7600 ELSE IF ZA$="A"THEN7650 ELSE IF ZA$="S"THEN7800 ELSE RETURN
7400 GOSUB4650
7435 GOSUB4780:ZM$=C$:ZP=480:GOSUB1000:GOTO7260
7450 IFM=1THEN7460
7452 GOSUB4780:ZM$="EQUATION (1) OR (2)? ":ZP=480:XT=2:XM=1:GOSUB1400:EQ=ZA:IFEQ<>1ANDEQ<>2THEN7452
7460 GOSUB4780:ZM$="X = ":ZP=480:XM=8:XT=2:GOSUB1400:X=ZA:IF EQ=1 THEN GOSUB10 ELSE GOSUB20
7465 N=Y:GOSUB5320:ZM$="Y = "+STR$(N):ZP=494:GOSUB1000:X1=63+(X-L)/(R-L)*192:Y1=145*(Y-YU)/(YL-YU)+1:GOSUB5140:GOTO7380
7500 IFM=1THEN7510
7505 GOSUB4780:ZM$="EQUATION (1) OR (2)? ":ZP=480:XT=2:XM=1:GOSUB1400:EQ=ZA:IFEQ<>1ANDEQ<>2THEN7505
7510 GOSUB4780:ZM$="ENTER ESTIMATE. X = ":ZP=480:XT=2:XM=8:GOSUB1400
7515 GOSUB4780:ZM$=C$:ZP=480:GOSUB1000
7520 GOSUB9000:GOTO7380
7550 SCREEN 0,1:CLS
7555 PRINT@0,"<D>OMAIN - CHANGE THE DOMAIN OF THE GRAPH(S)"
7557 PRINT@64,"<V>ALUE - FIND THE Y-COORDINATE OF THE GRAPH(S) FOR A SPECIFIED VALUE  OF X"
7560 PRINT@160,"<R>OOTS - FIND THE ROOTS OF THE FUNCTION(S)"
7561 PRINT@224,"<S>LOPE - FIND THE SLOPE OF THE FUNCTION(S)"
7562 PRINT@288,"<A>REA - FIND THE AREA BETWEEN  THE CURVE AND X-AXIS OR BETWEEN TWO CURVES"
7563 ZM$="<I>NTERSECTION - FIND THE INTER-SECTION OF THE GRAPHS":IF M=2 THEN PRINT@384,ZM$
7564 IF M=1 THEN PRINT@384,"<M>ENU - RETURN TO THE MENU" ELSE PRINT@448,"<M>ENU - RETURN TO THE MENU"
7570 PRINT@482,"PRESS <enter> TO CONTINUE";
7580 ZZ$=INKEY$:IF ZZ$<>CHR$(13) THEN 7580
7590 PMODE 4,1:SCREEN 1,1:GOTO7380
7600 GOSUB4780:ZM$="ENTER ESTIMATE. X = ":ZP=480:XT=2:XM=8:GOSUB1400
7610 GOSUB4780:ZM$=C$:ZP=480:GOSUB1000:GOSUB9200
7620 GOTO7380
7650 GOSUB4780:ZM$="MINIMUM VALUE OF X? ":ZP=480:XM=8:XT=2:GOSUB1400:L1=ZA
7660 GOSUB4780:ZM$="MAXIMUM VALUE OF X? ":ZP=480:GOSUB1400:R1=ZA:IF ABS(R1-L1)<.001 THEN GOSUB4780:ZM$="INTERVAL MUST BE > .001":ZP=480:GOSUB1000:ZT=2:GOSUB4900:GOTO7650
7670 IF R1<L1 THEN TEMP=L1:L1=R1:R1=TEMP
7680 IF L1<L OR R1>R THEN GOSUB4780:ZM$="INTERVAL OUTSIDE DOMAIN OF GRAPH":GOSUB1000:ZT=2:GOSUB4900:GOTO7650
7780 GOSUB4780:ZM$=C$:ZP=480:GOSUB1000:IFM=1THENGOSUB10000ELSEGOSUB12000
7790 N=AREA:GOSUB5320:GOSUB4780:ZM$="AREA = "+STR$(N):GOSUB1000:ZM$="ENTER":ZP=507:GOSUB1000
7795 ZZ$=INKEY$:IFZZ$<>CHR$(13)THEN7795ELSE7340
7800 IFM=1THEN7820
7810 GOSUB4780:ZM$="EQUATION (1) OR (2)? ":ZP=480:XM=1:XT=2:GOSUB1400:EQ=ZA:IFEQ<>1ANDEQ<>2THEN7810
7820 GOSUB4780:ZM$="X = ":ZP=480:XM=8:XT=2:GOSUB1400:GOSUB4780
7830 X=ZA:H=X*(1E-6):IFH=0THENH=1E-6
7835 IFEQ=1THENGOSUB10ELSEGOSUB20
7840 Y1=Y:X=H+X:IFEQ=1THENGOSUB10ELSEGOSUB20
7841 S1=Y/H-Y1/H
7842 X=ZA-H:IFEQ=1THENGOSUB10ELSEGOSUB20
7844 S2=Y1/H-Y/H:N=(S1+S2)/2:GOSUB5320:SLOPE=N
7860 ZM$="SLOPE = "+STR$(SLOPE):GOSUB1000:ZM$="ENTER":ZP=507:GOSUB1000
7870 ZZ$=INKEY$:IFZZ$<>CHR$(13)THEN7870ELSE7380
9000 '**FIND ROOT
9020 POKE65495,0:ROOT=ZA
9030 X=ROOT
9040 IF X=0 THEN X=.001
9050 FOR J=1 TO 50:IF EQ=1 THEN GOSUB10 ELSE GOSUB20
9060 IF ABS(X)<.0001 AND ABS(Y)<.0001 THEN 9160
9070 Y1=Y:X1=X:X=X+.000001*X
9080 IF EQ=1 THEN GOSUB10 ELSE GOSUB20
9090 Y2=Y:IF Y2-Y1=0 THEN X=X+.0001:GOTO9080
9100 SLOPE=(Y2-Y1)/(X-X1):X=X1:Y=Y1
9110 ROOT=X-Y/SLOPE
9120 IF ABS((ROOT-X)/X)<.00005 THEN 9160
9130 X=ROOT:NEXT J
9140 GOSUB4780:ZM$="NO ROOT FOUND":IF MM=2 THEN ZM$=ZM$+" FOR EQUAT."+STR$(EQ)
9150 ZP=480:GOSUB1000:GOSUB5160:GOTO9180
9160 IF ABS(ROOT) <.00001 THEN ROOT=0
9170 GOSUB4780:N=ROOT:GOSUB5320:ZM$="ROOT AT X = "+STR$(N):ZP=480:GOSUB1000:X1=63+(N-L)/(R-L)*192:Y1=145*(-YU)/(YL-YU)+1:GOSUB5140
9180 POKE65494,0:RETURN
9200 '**FIND INTERSECTION
9220 POKE65495,0:ROOT=ZA:X=ROOT:IF X=0 THEN X=.001
9230 FOR J=1 TO 75:GOSUB10:Y1=Y
9240 GOSUB20:Y1=Y1-Y
9250 X1=X:X=X+.0001*X
9260 GOSUB10:Y2=Y:GOSUB20:Y2=Y2-Y
9270 IF ABS(X1)<.000001 AND ABS(Y1)<.000001 THEN 9360
9280 IF Y2-Y1<>0 THEN 9310
9290 GOSUB10:Y1=Y:GOSUB20:Y2=Y:IF ABS(Y2-Y1)<.00001 THEN 9360
9300 GOTO 9350
9310 SLOPE=(Y2-Y1)/(X-X1):X=X1:Y=Y1
9320 ROOT=X-Y/SLOPE
9330 IF ABS((ROOT-X)/X)<.00005 THEN 9360
9340 X=ROOT:NEXT J
9350 GOSUB4780:ZM$="NO INTERSECTION FOUND.":ZP=480:GOSUB1000:5140:POKE65494,0:RETURN
9360 IF ABS(ROOT)<.00001 THEN ROOT=0
9370 X=ROOT:GOSUB10:IF ABS(Y)<.000001 THEN Y=0
9380 GOSUB4780:X1=63+(X-L)/(R-L)*192:Y1=145*(Y-YU)/(YL-YU)+1:N=X:GOSUB5320:ZM$="X="+STR$(N):N=Y:GOSUB5320:ZM$=ZM$+"  Y="+STR$(N):ZP=480:GOSUB1000:GOSUB5140:POKE65494,0:RETURN
10000 '**DEFINITE INTEGRAL
10100 POKE65495,0:AREA=0:DX=(R1-L1)/200:X=L1:GOSUB10:X1=63+(X-L)/(R-L)*192:AREA=AREA+Y
10110 Y1=145*(Y-YU)/(YL-YU)+1:IF AX<>0 THEN Y2=AX ELSE Y2=145
10115 LINE (X1,Y1)-(X1,Y2),PSET
10120 FOR ZK=1 TO 199:X=X+DX:GOSUB10:X1=63+(X-L)/(R-L)*192
10140 IF INT(ZK/2)<>ZK/2 THEN AREA=AREA+4*Y ELSE AREA=AREA+2*Y
10142 Y1=145*(Y-YU)/(YL-YU)+1:IFY<0THEN POKE178,1 ELSE POKE 178,2
10143 LINE(X1,Y1)-(X1,Y2),PSET
10150 NEXTZK
10160 X=R1:GOSUB10:X1=63+(X-L)/(R-L)*192:AREA=AREA+Y
10170 Y1=145*(Y-YU)/(YL-YU)+1:LINE(X1,Y1)-(X1,Y2),PSET
10190 AREA=DX/3*AREA:RETURN
12000 '**AREA BETWEEN CURVES
12100 AREA=0:DX=(R1-L1)/200:X=L1:GOSUB10:X1=63+(X-L)/(R-L)*192:Y1=145*(Y-YU)/(YL-YU)+1:YTEMP=Y
12110 GOSUB20:Y2=145*(Y-YU)/(YL-YU)+1:Y=ABS(YTEMP-Y):AREA=AREA+Y
12120 LINE(X1,Y1)-(X1,Y2),PSET
12130 FORZK=1TO199:X=X+DX:GOSUB10:X1=63+(X-L)/(R-L)*192:Y1=145*(Y-YU)/(YL-YU)+1:YTEMP=Y
12135 GOSUB20:Y2=145*(Y-YU)/(YL-YU)+1:Y=ABS(YTEMP-Y)
12140 IF INT(ZK/2)<>ZK/2 THEN AREA=AREA+4*Y ELSE AREA=AREA+2*Y
12145 LINE(X1,Y1)-(X1,Y2),PSET:NEXTZK
12150 X=R1:GOSUB10:X1=63+(X-L)/(R-L)*192:Y1=145*(Y-YU)/(YL-YU)+1:TEMP=Y:GOSUB20:Y2=145*(Y-YU)/(YL-YU)+1:Y=ABS(YTEMP-Y):AREA=AREA+Y:LINE(X1,Y1)-(X1,Y2),PSET
12160 AREA=DX/3*AREA:POKE65494,0:RETURN
20000 '**INSTRUCTIONS
20100 COLOR0,1:PCLS:SCREEN1,0:ZM$="THIS PROGRAM WILL DRAW THE GRAPHOF ONE OR TWO FUNCTIONS ON THE  CARTESIAN COORDINATE PLANE.":ZP=0:GOSUB1000
20105 ZM$="IT CAN ALSO BE USED TO FIND THE ROOTS OF AN EQUATION, TO SOLVE ASYSTEM OF TWO EQUATIONS WITH TWOUNKNOWNS, TO FIND THE MAXIMUM   OR MINIMUM VALUES OF A FUNCTION,TO SHOW THE RELATIONSHIP BETWEENTHE GRAPH OF A FUNCTION AND ITS":ZP=96:GOSUB1000
20107 ZM$="DERIVATIVE, TO FIND AREAS UNDER OR BETWEEN CURVES (THE DEFINITE INTEGRAL) AND TO SHOW THE EFFECTOF TRANSLATIONS, REFLECTIONS,   ETC. ON THE GRAPH OF A FUNCTION.":ZP=320:GOSUB1000:GOSUB5020:PCLS
20110 ZM$="TO GRAPH ONE EQUATION, YOU MUST WRITE THAT EQUATION AT LINE 10  IN THE FORM:  Y = F(X)":ZP=0:GOSUB1000
20115 ZM$="EXAMPLE:  10 Y = 2*X - SIN(X)":ZP=128:GOSUB1000
20117 ZM$="DO NOT USE ANY OTHER VARIABLE   EXCEPT X AND Y.":ZP=192:GOSUB1000
20118 ZM$="THEN TYPE "+CHR$(34)+"RUN 5"+CHR$(34)+" TO RESUME THE":ZP=288:GOSUB1000:ZM$="PROGRAM.":ZP=320:GOSUB1000:GOSUB5020
20120 PCLS:ZM$="YOU WILL BE ASKED TO SPECIFY THEDOMAIN (X-INTERVAL) OVER WHICH  YOU WANT THE GRAPH TO BE DRAWN.":ZP=0:GOSUB1000
20125 ZM$="AFTER ABOUT 15 SECONDS, THE     GRAPH OF THE EQUATION AT LINE 10WILL BE DISPLAYED ON THE SCREEN.":ZP=128:GOSUB1000:GOSUB5020
20130 PCLS: ZM$="AFTER THE GRAPH HAS BEEN DRAWN, YOU WILL BE PROMPTED AT THE     BOTTOM OF THE SCREEN WITH":ZP=0:GOSUB1000
20134 ZM$="<D><V><R><S><A><M>  <H>ELP.":ZP=128:GOSUB1000
20140 ZM$="ENTER <D> IF YOU WISH TO RE-DRAWTHE SAME GRAPH OVER A DIFFERENT <D>OMAIN (X-INTERVAL).":ZP=192:GOSUB1000
20170 ZM$="ENTER <V> IF YOU WISH TO FIND   THE <V>ALUE OF THE FUNCTION (Y- COORDINATE) FOR ANY VALUE OF X.":ZP=320:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20200 ZM$="ENTER <R> IF YOU WISH TO FIND   THE ROOTS (ZEROES) OF THE       FUNCTION (WHERE THE GRAPH       CROSSES THE X-AXIS).":ZP=192:GOSUB1000
20210 ZM$="THE PROGRAM USES NEWTON'S METHODOF ITERATION TO FIND THE ROOTS.":ZP=352:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20220 ZM$="NEWTON'S METHOD REQUIRES THAT   YOU ENTER AN INITIAL ESTIMATE OFTHE ROOT. YOU CAN GET SUCH AN   ESTIMATE BY LOOKING AT WHERE THEGRAPH CROSSES THE X-AXIS. THE   ESTIMATE DOES NOT HAVE TO BE TOOEXACT.":ZP=192:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20230 ZM$="IF THE GRAPH HAS MULTIPLE ROOTS,YOU CAN REPEAT THE PROCEDURE.":ZP=192:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20232 ZM$="ENTER <S> IF YOU WISH TO FIND   THE SLOPE OF THE FUNCTION FOR   ANY VALUE OF X (IE. EVALUATE ITSDERIVATIVE). THE PROGRAM WILL   NUMERICALLY COMPUTE A GOOD      APPROXIMATION TO THE SLOPE.":ZP=192:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20235 ZM$="ENTER <A> IF YOU WISH TO FIND   THE AREA BETWEEN THE CURVE AND  THE X-AXIS (THE DEFINITE INTE-  GRAL). YOU WILL BE PROMTED FOR  THE LIMITS OF INTEGRATION. THE  PROGRAM WILL PARTITION THE":ZP=192:GOSUB1000
20237 ZM$="INTERVAL INTO 200 SUB-INTERVALS AND USE SIMPSON'S RULE TO FIND  THE AREA.":ZP=384:GOSUB1000:GOSUB5020:ZP=192:GOSUB4700
20240 ZM$="ENTER <M> IF YOU WISH TO RETURN TO THE MENU OR <H> FOR HELP IF  YOU CAN'T RECALL WHAT           <D><V><R><S><A><M> MEANS.":ZP=192:GOSUB1000:GOSUB5020LIST20250
20250 PCLS:ZM$="IF YOU WISH TO GRAPH TWO        EQUATIONS AT THE SAME TIME, YOU MUST WRITE EQUATION (1) AT LINE 10 AND EQUATION (2) AT LINE 20, BOTH IN THE FORM Y = F(X).":ZP=0:GOSUB1000
20260 ZM$="EXAMPLE:  10 Y = X^2 - 3*X + 5            20 Y = 2*X - 3":ZP=192:GOSUB1000
20270 ZM$="USE ONLY X AND Y FOR BOTH       EQUATIONS.":ZP=288:GOSUB1000:GOSUB5020
20280 PCLS:ZM$="AFTER ABOUT 30 SECONDS, THE     GRAPHS WILL BE DISPLAYED ON THE SCREEN. YOU WILL AGAIN BE       PROMPTED WITH:":ZP=0:GOSUB1000
20290 ZM$="<D><V><R><S><A><I><M> <H>ELP":ZP=160:GOSUB1000
20300 ZM$="<D>,<V>,<R>,<A>,<M>, AND <H>    HAVE THE SAME MEANING AS BEFORE.YOU CAN EVALUATE EITHER FUNCTION<V>, FIND THE ROOTS OF EITHER   FUNCTION <R>, FIND THE SLOPE OF EITHER FUNCTION <S>, OR FIND THE":ZP=224:GOSUB1000
20305 ZM$="AREA BETWEEN THE TWO CURVES <A>.":ZP=416:GOSUB1000:GOSUB5020:ZP=224:GOSUB4700
20310 ZM$="HOWEVER, YOU ALSO HAVE THE      THE OPTION OF FINDING THE INTER-SECTION OF THE TWO GRAPHS BY    PRESSING <I>.":ZP=224:GOSUB1000:GOSUB5020:ZP=224:GOSUB4700
20320 ZM$="YOU MUST ENTER AN INITIAL       ESTIMATE OF THE INTERSECTION    WHICH YOU CAN GET BY LOOKING AT THE GRAPH. THE PROGRAM WILL THENFIND THE EXACT VALUES FOR X AND":ZP=224:GOSUB1000
20330 ZM$="Y. IF THERE IS MORE THAN ONE    INTERSECTION, YOU CAN REPEAT THEPROCESS.":ZP=384:GOSUB1000:GOSUB5020
20340 PCLS:ZM$="TO FIND THE RELATIVE MAXIMUM OR MINIMUM VALUES OF A FUNCTION    WITHIN AN INTERVAL, GRAPH THE   FUNCTION (LINE 10) AND ITS      DERIVATIVE (LINE 20). THE MAXI-":ZP=0:GOSUB1000
20350 ZM$="MUMS OR MINIMUMS OCCUR WHERE THEGRAPH OF THE DERIVATIVE CROSSES THE X-AXIS.":ZP=160:GOSUB1000:GOSUB5020
20360 PCLS:ZM$="THUS, FIRST FIND THE ROOTS OF   THE DERIVATIVE (EQUATION 2) AND THEN EVALUATE THE FUNCTION      (EQUATION 1) AT THE ROOTS OF THEDERIVATIVE.":ZP=0:GOSUB1000:GOSUB5020
20370 PCLS:ZM$="TO GRAPH A PIECE-WISE FUNCTION, SUCH AS:":ZP=0:GOSUB1000
20380 ZM$="Y = 2*X        FOR X < -1       Y = 3*X - 2    FOR -1<=X<=2     Y = 5       FOR X > 2":ZP=96:GOSUB1000
20390 ZM$="WRITE AT LINE 10:":ZP=224:GOSUB1000
20400 ZM$="10 IF X<-1 THEN Y=2*X ELSE         IF X>=-1 AND X<=2 THEN Y=3*X    ELSE Y=5":ZP=288:GOSUB1000:GOSUB5020
20410 PCLS:ZM$="YOU CAN USE A SIMILAR TECHNIQUE TO GRAPH DISCONTINUOUS FUNC-    TIONS. FOR EXAMPLE, Y=1/X IS    DISCONTINUOUS AT X=0. TO GRAPH  THIS FUNCTION, WRITE AT LINE 10:":ZP=0:GOSUB1000
20420 ZM$="10 IF X<-.1 OR X>.1 THEN Y=X       ELSE Y=0":ZP=160:GOSUB1000
20430 ZM$="ALTHOUGH Y DOES NOT EQUAL 0 FOR VALUES OF X BETWEEN -.1 AND .1, THESE POINTS WILL BE PLOTTED ON THE X-AXIS AND WILL NOT BE      VISIBLE.":ZP=256:GOSUB1000:GOSUB5020
25000 RETURN
30000 'WRITE EQUATIONS INSTRUCTIONS
30100 CLS:PRINT@0,"TO GRAPH ONE EQUATION, YOU MUST WRITE THAT EQUATION AT LINE 10  IN THE FORM:  Y = F(X)          EXAMPLE:   10 Y = 4*X - SIN(2*X)";
30110 PRINT@160,"TO GRAPH TWO EQUATIONS, WRITE   EQUATION (1) AT LINE 10 AND     EQUATION (2) AT LINE 20.        EXAMPLE:  10 Y = X^2 - 5*X + 3            20 Y = 2*X - 5"
30120 PRINT@320,"USE ONLY THE VARIABLES X AND Y.":PRINT@384,"THEN TYPE 'RUN 5' TO RETURN TO  THE MENU."
0 ' COPYRIGHT 1989  FALSOFT,INC
10 POKE65497,0:X=-300:Y=-90
20 HSCREEN4:PALETTE0,0:HCLS0:PALETTE1,63:HCOLOR1
30 GOSUB 150
40 FORX=-300 TO 300 STEP S
50 GOSUB 120
60 HLINE(320+X*Z,96-(90*Z))-(320+X*Z,96-(90*Z)),PSET
70 FORY=-90 TO 90 STEP T
80 GOSUB 120
90 HLINE-(320+(X*Z),96+(Y*Z)),PSET
100 NEXTY,X
110 GOTO 130
120 Z=COS(X*Y*.0001):RETURN
130 ON BRK GOTO 140:GOTO 130
140 END
150 S=6:T=6:RETURN
0 'COPYRIGHT 1990 FALSOFT, INC.
5 ON BRK GOTO 5
6 ON ERR GOTO 5'IF ERROR, THEN RUN INSTEAD OF CRASHING
10 'VERSION 3.7-4/23/89
15 'UltraGraph!
16 '(c) 1988 by Matthew Hunt
19 IF TA=0 THEN HBUFF 1,1800:HBUFF 2,1800:HBUFF 3,1800:TA=1
20 GOTO 40
30 GOTO 5
40 CLEAR 1000:TA=1:FI=1:WIDTH 80:ON BRK GOTO 30:GOSUB 1260:ATTR 3,0:Q=23:Y$="<<< Press any key to Continue >>>":GOSUB 1230:Q=0:TIMER=0
41 A$=INKEY$:IF A$="" AND TIMER/60<10 THEN LOCATE 76,23:PRINT STR$(10-INT(TIMER/60));:GOTO 41
42 TA=1
50 POKE65497,0
60 R=1:D=1:Q=23:Y$=STRING$(78," "):GOSUB 1230:Q=0:Y$="**** UltraGraph! ****":GOSUB 1230:Y$=" -=< By Matthew Hunt >=-"+CHR$(13):GOSUB 1230:ATTR 3,2
70 RGB:PALETTE 0,0:PRINT"1. LINES ":PRINT "2. PARABOLAS":PRINT"3. CIRCLES & ELLIPSES":PRINT "4. HYPERBOLAS":PRINT"5. COS(X)":PRINT"6. SIN(X)":PRINT"7. TAN(X)"
75 LOCATE 39,12:PRINT"8.  Define function";:LOCATE 39,13:PRINT"9.  End program.";:LOCATE 39,14:PRINT"A.  Execute defined function";:LOCATE 0,19
80 PRINT STRING$(78,"-"):ATTR 3,0:PRINT CHR$(8);:ATTR 3,2:LOCATE 79,19
89 LOCATE 0,23:ATTR 2,4:TIMER=0:TA=1
90 A$=INKEY$
91 LOCATE 0,23:ATTR 3,2:PRINT STRING$(78," ");:ATTR 2,4:LOCATE Q1,23:PRINT " Hit number of choice or wait "+STR$(30-INT(TI))" seconds";:TI=TIMER/60:IF TI>30 THEN GOTO 30000 ELSEQ1=Q1+F:IF Q1>35 THEN F=-1 ELSE IF Q1<1 THEN F=1
92 IF A$="" THEN 90
93 IF A$="A" THEN JL=6:TYPE$="UserDefined":GOSUB 50070:IF Y$="UNDEFINED" THEN PRINT"MUST DEFINE FUNCTION FIRST!!!":FOR X=0TO1000:NEXT:GOTO 5 ELSE UD=1:GOTO 290
94 UD=0
100 IF VAL(A$)<1OR VAL (A$)>9 THEN 90
110 G=VAL(A$)
115 IF VAL(A$)=9 THEN CLS:PRINT"OK.":POKE65496,0:END
116 IF G=8 THEN 50000
120 IFG=1THENTYPE$="LINE"ELSEIFG=2THENTYPE$="PARABOLA"ELSEIFG=3THENTYPE$="CIRCLE"ELSEIFG=4THENTYPE$="HYPERBOLA"ELSEIFG=5THENTYPE$="COSINE"ELSEIFG=6THENTYPE$="SINE"ELSEIFG=7THENTYPE$="TANGENT"
130 JL=G:IF JL>4THENJL=JL-2
140 S=0:IF G>2 AND G<5THEN S=1:IF G=4 THEN B=-1 ELSE B=1
150 IF S=1THEN970
155 IF G>4 THEN GOSUB 20000
160 IF JL=1 THEN GOSUB 1240:Q=12:Y$="<<<< Lines >>>>":GOSUB 1230:Q=Q+1:LOCATE 0,Q:PRINT"An equation for a LINE is in the form [Y=MX+B], where M is the SLOPE (how steep it is), and B is the Y-INTERCEPT, or where the line crosses the Y(VERTICAL)AXIS."
170 IF JL=1 THEN GOSUB1220:GOSUB1240:Q=12:Y$="<<<LINE EQUATION INPUT>>>":GOSUB1230:Y$="Y=M*X+B":GOSUB1230:Y$="SLOPE(M)":GOSUB1230:INPUT M:Y$="Y="+STR$(M)+"*X+B":Q=13:GOSUB1230:Y$="Y-INTERCEPT(B)":GOSUB1230:INPUT B:FM$="Y="+STR$(M)+"*X+"+STR$(B)
180 IF JL=1 THEN Q=13:Y$=FM$:GOSUB 1230
190 IF JL=1 THEN SE=M:HI=B:GOTO 290
210 IF JL<>2 THEN 290
220 GOSUB1240:Y$=" <Parabolas and power functions>"+CHR$(13):GOSUB 1230:PRINT"Parabolas are in the form AX^2*BX+C(^=to the power of).  C is the Y-intercept,  or where the parabola intersects the y-axis.  The program automatically gives   the intercepts.
230 GOSUB1220:GOSUB1240:Q=12:Y$="<Parabolas and power functions>":GOSUB 1230:FM$="Y=A*X^??+B*X+C":Y$=FM$:GOSUB1230
240 Y$="Power to raise X to(??)[MAX=10,MIN=-10]":GOSUB 1230:INPUT PO:IF PO>10 OR PO<-10 THEN Q=Q-1:GOTO 240:FM$="Y=A*"+STR$(PO)+"+B*X+C":Y$=FM$:Q=13:GOSUB 1230
250 Y$="Number to multiply X^?? BY(A)[1000 MAX, -1000 MIN]":GOSUB 1230:INPUT NU:IF NU>1000 OR NU<-1000 THEN Q=Q-1:GOTO 250
260 Y$="Number to multiply the 2nd X by(B)":GOSUB 1230:INPUT SE:IF SE>10000 OR SE<-10000 THEN Q=Q-1:GOTO 260
270 Y$="Number to add on the end(c)":GOSUB 1230:INPUT HI:IF HI>10000 OR HI<-10000 THEN Q=Q-1:GOTO 270
280 FM$="Y="+STR$(NU)+"*X^"+STR$(PO)+"+"+STR$(SE)+"*X+"+STR$(HI):Q=13
290 GOSUB 1240:Q=15:Y$="Erase previous screen?(y/n)":GOSUB 1230
291 A$=INKEY$:IF A$="y" OR A$="Y" THEN CL=1:POKE &HE6E4,&HE7 ELSE IF A$="N" OR A$="n" THEN CL=0:POKE &HE6E4,&HE6 ELSE GOTO 291
292 Y$="Color of graph(1-3)?":GOSUB 1230
293 A$=INKEY$:IF A$="" THEN 293 ELSE IF VAL(A$)<1 AND VAL(A$)>4 THEN GOTO 293 ELSE CO=VAL(A$)
295 GOSUB 1240:Q=12:Y$="Range of numbers[MAX=200=]?":GOSUB 1230:INPUT RA$:IF RA$=""THEN RA=5 ELSE IF VAL(RA$)=0 OR VAL(RA$)<0 OR VAL(RA$)>200 THEN Q=Q-1:GOTO 290 ELSE RA=VAL(RA$)/2
300 IFS=1THENLY=-RA:LX=-RA:GOTO350
310 Y$="LOWEST X-VALUE TO GRAPH?":GOSUB 1230:INPUT LX$:IF VAL(LX$)>10000 OR VAL(LX$)<-10000 THEN Q=Q-1:PRINT"OUT OF RANGE. REDO.":GOTO 310 ELSE IF LX$="" THEN LX=-RA ELSE LX=VAL(LX$)
320 IF LX$="" THEN LX=-RA ELSE LX=VAL(LX$)
330 Y$="LOWEST Y-VALUE":GOSUB 1230:INPUT LY$:IF VAL(LY$)>10000 OR VAL(LY$)<-1000 THEN PRINT"OUT OF RANGE.  REDO":Q=Q-1:GOTO 330 ELSE IF LY$="" THEN LY=-RA ELSE LY=VAL(LY$)
340 IF LY$="" THEN LY=-RA ELSE LY=VAL(LY$)
350 MX=LX+RA*2:MY=LY+RA*2
360 IF JL=1 THEN SP=8:GOTO 390
370 PRINT"SPEED(1-4)-each # doubles speed of previous# but halves resolution.(1-slowest)"
380 A$=INKEY$:IFA$=""THEN380ELSEIFVAL(A$)=0THEN380ELSEIFVAL(A$)>4THEN380ELSESP=2^(VAL(A$)-1)
385 '
386 '
390 D=1/(436/SP/(RA*2))
395 IF UD=1 THEN GOSUB 50070:FM$="Y="+Y$
400 HSCREEN4:HCOLOR 1:HLINE(102,184)-(538,191),PRESET,BF:PALETTE 1,63:PALETTE 2,54:HLINE(102,191)-(538,183),PSET,B:HGET(103,190)-(537,184),3:C$="Graphing:"+FM$:GOSUB 9999:TIMER=0
401 IF PEEK(&HE6E4)=&HE6 THEN HLINE(1,9)-(99,183),PRESET,BF:HLINE(541,9)-(638,183),PRESET,BF
402 IF S=1 THEN 510
410 R=6:X$="INTERCEPTS:":GOSUB1200
420 IFTYPE$<>"HYPERBOLA"ORTYPE$<>"CIRCLE"THENR=8:X=0:ON JL GOSUB 1090,1070,1110,1130,1150:X$="Y:"+STR$(Y):GOSUB1200
450 IFJL>2 THEN IN$="X:":X$="X:????":GOSUB1200
460 IFJL>2 AND PO=2 THENGOTO510ELSEX$="X:":G=SE^2-4*NU*HI:IFG<0ORNU=0THENX$=X$+"NONE":GOSUB1200ELSEIFG=0THENF=SE/(2*NU):X$=X$+STR$(F):GOSUB1200ELSEF=(-SE+G^.5)/(2*NU):B=(-SE-G^.5)/(2*NU):X$=X$+LEFT$(STR$(F),4)+","+LEFT$(STR$(B),4):GOSUB1200
470 MK=R+2:R=1
480 PALETTE 1,63
490 '
500 ' DRAW SCREEN
510 T1=(-LX)*436/(RA*2)+102:T2=(2*RA+LY)*174/(RA*2)+9:IF T1>102 AND T1<538 THEN HLINE(T1,9)-(T1,183),PSET
520 IF T2>9 AND T2<183 THEN HLINE(102,T2)-(538,T2),PSET
530 HLINE(0,0)-(639,191),PSET,B:HPRINT(35,0),"UltraGraph":HLINE(0,9)-(639,9),PSET:HLINE(0,183)-(639,183),PSET
540 'PRINT X AND Y VALUES
550 HLINE(100,0)-(100,191),PSET:HLINE(540,0)-(540,191),PSET
560 HCOLOR 2:HPRINT(3,0),"x":HPRINT(9,0),"y":HPRINT(71,0),"stats:":HCOLOR 1:HLINE(50,9)-(50,183),PSET:HCOLOR 2
570 IF TYPE$="ELLIPSE" OR TYPE$="HYPERBOLA" OR TYPE$="CIRCLE" THEN GOTO 637 ELSE AM=RA
580 IF RA<11 THEN ST=RA/10 ELSE ST=(RA*2)/20
590 SN=599:X=LX:ON JL GOSUB 1090,1070,1110,1130,1150,1165:MAX=Y:MIN=Y
599 SN=610:N=2:TE=LX
600 FOR X=TE TO MX STEP ST:ON JL GOSUB 1090,1070,1110,1130,1150,1165:IF Y>MAX THENMAX=Y
610 IF Y<MIN THEN MIN=Y
620 A$=LEFT$(STR$(Y),6):HPRINT(0,N),LEFT$(STR$(X),6):IFND=1THENA$=" DNE"
630 HPRINT(6,N),A$:N=N+1:IF ERNO>-1 THEN IF TE<MX THEN TE=X+ST:GOTO 600 ELSE ELSE NEXT X
637 X$="TYPE:":R=2:GOSUB 1200:X$=TYPE$:GOSUB 1200
638 IF S<>1 AND DE<>1 THEN R=R+8 ELSE IF DE=1 THEN R=R+7
639 HCOLOR 1:IF SP=4 THEN SP=3 ELSE IF SP=8 THEN SP=4
640 HC=3:X$=" RANGE:"+STR$(RA*2):GOSUB1200:X$="X:"+STR$(LX)+" to"+STR$(MX):GOSUB 1200:X$="Y:"+STR$(LY)+" to"+STR$(MY):GOSUB 1200:X$="SPEED:"+STR$(SP):GOSUB 1200
645 IF DE=1 THEN HCOLOR 2:R=R+1:X$="Demo mode:":GOSUB 1200:X$="To exit hit":GOSUB 1200:X$="<Break>":HCOLOR 1:GOSUB 1200
650 G=436/RA/2:FOR X=320TO102STEP-G:IF T2>9 AND T2<183 THEN HLINE(X,T2-2)-(X,T2+2),PSET:NEXT X:FOR X=320TO539STEPG:HLINE(X,T2-2)-(X,T2+2),PSET:NEXTX
660 G=174/RA/2:FORY=96TO9STEP-G:IF T1>102 AND T1<538 THEN HLINE(T1-2,Y)-(T1+2,Y),PSET:NEXTY:FORY=96TO183STEPG:HLINE(T1-2,Y)-(T1+2,Y),PSET:NEXTY
670 K=174/(RA*2):J=436/(RA*2)
680 M1=103-(RA+LX)*J:M2=(RA+LY)*K+9
685 HGET(102,182)-(538,190),2:HGET(102,1)-(538,8),1
690 IF S=1 THEN GOTO 850
700 'START GRAPHING
710 SN=715:MK=19:R=19:X=-RA:ON JL GOSUB 1090,1070,1110,1130,1150:H=(X+RA)*J+102:R=R+1:MK=R:X$="MIN:"+STR$(MIN):GOSUB1200:X$="MAX:"+STR$(MAX):GOSUB1200:I=(-Y+RA)*K+9:IFI<9THENI=9ELSEIFI>183THENI=183'SET UP FOR INITIAL POINT
715 HCOLOR CO:SN=740:TE=LX
720 FOR X=TE TO MX STEP D
730 ON JL GOSUB 1090,1070,1110,1130,1150,1165
740 IFND=1THENIF ERNO>0 THEN H=(X+RA)*J+M1:TE=X+D:GOTO 720 ELSE NEXT X
750 IF Y=0 THEN IF JL>2 THEN IN$=IN$+LEFT$(STR$(X),3):X$=IN$:R=MK-3:GOSUB1200
760 IF Y>MAX THEN MAX=Y:X$="MAX:"+STR$(Y):R=MK+1:GOSUB1200:HCOLOR CO
770 IF MIN>Y THEN X$="MIN:"+STR$(Y):R=MK:MIN=Y:GOSUB1200:HCOLOR CO
780 T=(X+RA)*J+M1:Y=(-Y+RA)*K+M2
790 IFY<9 THEN IF I=183 THEN I=9:Y=9:NEXT X ELSE Y=0:HLINE(H,I)-(T,Y),PSET:HPUT(102,1)-(538,8),1,PSET:I=9:Y=9:H=T:NEXT X
800 IFY>183 THEN IF I=9 THEN I=183:Y=183:H=T:NEXT X ELSE Y=191:HLINE(H,I)-(T,Y),PSET:HPUT(102,182)-(538,190),2,PSET:I=183:Y=183:H=T:NEXT X
810 HLINE(H,I)-(T,Y),PSET:H=T:I=Y
820 IF X<MX THEN NEXT X
829 IF DE=1 THEN ON LN GOSUB 30010,30020,30030,40000:GOSUB 40500:GOTO 390  ELSE C$="Graph completed("+MID$(STR$(TIMER/60),2,4)+" secs)-hit any key":GOSUB 9999
830 A$=INKEY$:IFA$=""THEN830
831 C$="Print Screen to printer(Y/N)?":GOSUB 9999
832 IF A$="N" OR A$="n" THEN GOTO 5 ELSE IF A$="y" OR A$="Y" THEN GOTO 833 ELSE A$=INKEY$:GOTO 832
833 C$=FM$:GOSUB 9999:GOTO 1180
840 'GRAPH HYPERBOLAS,ELLIPSES,AND CIRCLES
850 FORX=LX TO MX STEP D
860 F=C*D2*(EQ-B*(X^2/D1)):IFF<0THENH=96:E=96:G=(X+RA)*J+M1:NEXTX
870 IF F>0THENY=F^.5 ELSE IF X<RA-D THEN NEXT X
880 IF F<9THENF=9ELSEIFF>183THENF=183
890 T=(X+RA)*J+M1:F=(Y+RA)*K+9:Y=(-Y+RA)*K+M2
900 IFF<9THENF=0ELSEIFF>183THENF=192
910 IFY<9THENG=T:E=9:H=F:V=1ELSEIFY>183THENG=T:E=183:H=F:V=1ELSEV=0
920 IFV=1THEN950
930 IF X>RA THEN GOTO830
940 HLINE(G,E)-(T,Y),PSET:HLINE(G,H)-(T,F),PSET:G=T:E=Y:H=F
950 IF X<RA-D THEN NEXT X
960 GOTO 829
970 IF B=-1 THEN L$="-" ELSE L$="+"
980 GOSUB 1240:Y$=" CIRCLES/HYPERBOLAS/ELLIPSES"+CHR$(13):Q=12:GOSUB 1230
990 PRINT"Hyperbolas, circles, and ellipses are all quite similar.  Everyone knows what   a circle is.  An ellipse is just a sort of streched-out circle, and a hyperbola looks like an ellipse cut in two and fliped."
1000 Q=17:Y$="Enter in the form [x/v1]^2+(- for hyperbola)[y/v2]^2=v3":GOSUB 1230:GOSUB 1220
1010 GOSUB 1240:Y$="<<Circles,ellipses, & hyperbolas>>":Q=12:GOSUB 1230:Y$="[X/V1]"+L$+"[Y/V2]=V3":GOSUB 1230:Y$="Value for v1":GOSUB 1230:INPUT D1
1020 Y$="[X/"+STR$(D1)+"]"+L$+"[Y/v2]=v3":Q=13:GOSUB 1230:Y$="Value for v2?":GOSUB 1230:INPUT D2:FM$="[X/"+STR$(D1)+"]"+L$+"[Y/"+STR$(D2)+"]=":Y$=FM$+"v3":Q=13:GOSUB 1230:Y$="Value for v3?":GOSUB 1230:INPUT EQ:FM$="[X/"+STR$(D1)+"]"+L$+"[Y/"+STR$(D2)+"]="
1030 FM$=FM$+STR$(EQ):IF EQ=0 OR D1=0 OR D2=0 THEN Y$="Sorry, I am unable to divide by zero!  Please enter again.":GOSUB 1230:GOSUB 1220:GOTO 970
1040 C=1
1050 IF G=4 THEN IF D1>D2 THEN B=1:C=-1
1060 GOTO 290
1070 IFPO>-1ANDPO<1ANDX<0THENH=319:I=96:ND=1:RETURNELSEY=NU*X^PO+SE*X+HI:ND=0
1080 RETURN
1090 Y=M*X+B
1100 RETURN
1110 IF V3>-1 AND V3<1 AND X<0 THEN ND=1 ELSE ND=0:Y=V1*(COS(V2*X)^V3)
1120 RETURN
1130 IF V3>-1 AND V3<1 AND X<0 THEN ND=1 ELSE ND=0:Y=V1*(SIN(V2*X)^V3)
1140 RETURN
1150 IF V3>-1 AND V3<1 AND X<0 THEN ND=1 ELSE Y=V1*(TAN(V2*X)^V3):ND=0
1160 RETURN
1165 ON ERR GOTO 1167:Y=RND(RND(RND(RND(RND(X)))))*RND(X)
1166 ON ERR GOTO 5:ND=0:RETURN
1167 ND=1:IF SN=599 THEN GOTO 599 ELSE IF SN=610 THEN GOTO 610 ELSE IF SN=715 THEN GOTO 715 ELSE IF SN=740 THEN GOTO 740
1170 'SCREEN PRINT ROUTINE
1180 POKE150,42:PRINT#-2,CHR$(27)CHR$(14):PRINT#-2,CHR$(18)
1190 FOR X=0TO639STEP7:FORY=191TO0STEP-1:N=128:FORK=0TO6:IF HPOINT(X+K,Y)<>0 THEN N=N+2^K
1191 NEXT K:PRINT#-2,CHR$(N);:NEXT Y:PRINT#-2:NEXT X
1195 C$="All done! Press any key to continue.":GOSUB 9999
1196 IF A$="" THEN GOTO 1196 ELSE GOTO 5
1200 IF LEN(X$)>11 THEN X$=LEFT$(X$,11)
1210 HLINE(541,R*8)-(638,(R+1)*8-1),PRESET,BF:HCOLOR 3:HPRINT(74-.5*LEN(X$),R),X$:HCOLOR 1:R=R+1:RETURN
1220 Q=19:Y$="<< PRESS ANY KEY TO CONTINUE >>":ATTR 3,2,B:GOSUB 1230:ATTR 3,2:EXEC 44539:Q=Q-1:Y$=STRING$(78,"-"):GOSUB 1230:RETURN
1230 IF Q<10 THEN Q=10:GOTO 1230 ELSE LOCATE 40-(LEN(Y$)/2),Q:PRINT Y$;:Q=Q+1:RETURN
1240 FOR CL=12 TO 18:LOCATE 0,CL:PRINT:NEXT CL:LOCATE 0,19:PRINT STRING$(78,"-");:RETURN
1250 HLINE(102,190)-(538,182),PRESET,BF:HPRINT(40-(LEN(B$)/2),23),B$:RETURN
1260 GOTO 60000
1270 '
9999 HCOLOR 1:HPUT(103,190)-(537,184),3,PSET:GR=40-(LEN(C$)/2):IF 68-GR>LEN(C$) THEN C$=C$+STRING$(68-GR-LEN(C$)," "):GR=LEN(C$)
10000 FOR X=1 TO GR:A$=INKEY$:IF A$<>"" THEN HPUT(537,190)-(103,184),3,PSET:HPRINT(67-GR,23),C$:RETURN ELSE HPUT(537,190)-(103,184),3,PSET:HPRINT(67-X,23),LEFT$(C$,X):NEXT X:RETURN
20000 Q=12:GOSUB 1240:Y$=" <<< Trigonometric functions >>>"+CHR$(13):GOSUB 1230
20010 PRINT"Trigonometry is based largely on the SINE,COSINE,and TANGENT functions.Sine and Cosine are derived from the lengths of the legs of a right triangle inscribed   (drawn in) a circle of radius 1.  As the angles of the legs change, so does";
20020 PRINT" the corresponding value of SINE(x) or COSINE(x).  TANGENT(x) is SINE(x)/COS(x). The functions are useful in Geometry and engineering ; if you know 2 angles and the length of 1 side, the measures of the other 2 sides can be determined.";
20030 GOSUB 1220:GOSUB 1240:Q=12:Y$=" <<< Trig input >>>"+CHR$(13):GOSUB 1230:Y$="V1*("+TYPE$+"(V2*X))^V3":GOSUB 1230
20040 Y$="Value for v1":GOSUB 1230:INPUT V1:IF V1>10000 OR V1<-10000 THEN Q=Q-1:PRINT"OUT OF RANGE. REDO":GOTO 20040
20050 Y$="Value for v2":GOSUB 1230:INPUT V2:IF V2>10 OR V2<-10 THEN Q=Q-1:PRINT"OUT OF RANGE. REDO":GOTO 20050
20060 Y$="Value for v3":GOSUB 1230:INPUT V3:IF V3>10000 OR V3<-10000 THEN Q=Q-1:PRINT"OUT OF RANGE. REDO":GOTO 20050
20070 FM$=STR$(V1)+"*("+LEFT$(TYPE$,3)+"("+STR$(V2)+"*X)^"+STR$(V3)+")":Q=12:GOSUB 1240:RETURN
30000 CO=3:JL=2:TYPE$="PARABOLA":FM$="Y=X^2":RA=5:LY=-5:MY=5:LX=-5:MX=5:DE=1:PO=2:NU=1:LN=1:SP=4:GOTO 390
30010 CO=2:JL=3:V1=1:V2=1:V3=1:TYPE$="COSINE":FM$="Y=COS(X)":LN=2:SP=8:RETURN
30020 CO=1:JL=5:TYPE$="Tangent":FM$="Y=TAN(X)":LN=3:SP=2:RETURN
30030 CO=3:TYPE$="Tangent":FM$="Y=2[TAN(2X)]^2":V1=2:V2=2:V3=2:LN=4:RETURN
40000 GOTO 5
40500 TIMER=0:HPRINT(73,23),"- 5 -"
40510 IF TIMER/60=INT(TIMER/60) THEN HLINE(540,184)-(638,190),PRESET,BF:HPRINT(73,23),"-"+STR$(INT(6-TIMER/60))+" -"
40520 IF TIMER/60>4 THEN RETURN ELSE GOTO 40510
50000 POKE65496,0:Q=12:GOSUB 1240:Y$=" <<< User-Defined function >>>":GOSUB 1230:PRINT
50010 PRINT"User-defined functions can use any function avaliable to the color computer.    The program does not screen for errors in syntax - such as too many parenthesis-but will accept the undefined, such as 1/x at x=0.  After entering function,";
50020 PRINT"the computer will write an ASCII file and MERGE.  Then type RUN and A to execute    function.  *NOTE-USE CAPITAL LETTER X!":GOSUB 1220
50030 Q=12:GOSUB 1240:Y$="  <<< Define function >>>":GOSUB 1230:PRINT:PRINT"Enter function and hit <enter> when done.":PRINT"Y=";:INPUT A$:PRINT"SAVING...":OPEN"O",1,"TEMP/BAS":PRINT#1,"1165 ON ERR GOTO 1167:Y="+A$:B$="50070 Y$="+CHR$(34)+A$+CHR$(34)
50040 B$=B$+":RETURN":PRINT#1,B$
50050 CLOSE#1:PRINT"MERGING...type RUN and select A to graph function when done.":MERGE "TEMP/BAS"
50060 ERNO=5:PRINT ERNO
50070 Y$="RND(RND(RND(RND(RND(X)))))*RND(X)":RETURN
60000 WIDTH 80:POKE 65497,0:FOR R=0 TO 15:READ A:POKE &HFFB0+R,A:NEXT R:READ CA:C1=(CA AND 56)/8:C2=CA AND 7:IF CA<64 THEN ATTR C1,C2:CLS ELSE IF CA<128 THEN ATTR C1,C2,U ELSE ATTR C1,C2,U,B
60001 LOCATE 20,12:PRINT"Title screen developed with Hi-Res Design":LOCATE 25,14:PRINT"Intrested? Contact me through":LOCATE 0,15:PRINT"Delphi:MATTHEWHUNT            Compuserve:73167,3212          Echo-mail:Matt Hunt"
60002 LOCATE 20,18:PRINT"Or write:":LOCATE 30,20:PRINT"Matthew Hunt":LOCATE 30,21:PRINT"84 Smith St.":LOCATE 30,22:PRINT"Charleston, SC 29401"
60010 DATA 64,118,73,100,127,91,109,102,64,82,64,127,64,82,64,75,24
60100 READ A:IF A=9999 THEN RETURN ELSE IF A>300 THEN LO=A+442000 ELSE LPOKE LO,A:LO=LO+2
60105 GOTO 60100
60170 DATA 691,27
60200 DATA 841,27,853,27
60210 DATA 915,30,30
60220 DATA 30,30,933,25,25,25,949,28,28,965,26,26
60230 DATA 26,979,29,987,29,999,27,1015,27
60250 DATA 1073,30,1083,30,1091,25,25,1099,25,1107,28,1111,28
60260 DATA 1121,26,26,1131,26,1137,29,1145,29,1157,27
60270 DATA 1171,27,27,27,1183,27,1189,27,1193,25,1203,82,82,82,1213,28,28
60280 DATA 1221,29,1231,30,1251,25,25,25,25,1265,28,28
60290 DATA 1273,28,1281,26,26,26,26,26,1295,29,29,29,29,29,1317,27
60300 DATA 27,27,27,1335,27,1343,27,1349,27,1353,25,1365,82
60310 DATA 1371,28,1379,29,1383,29,1389,30,1401,30,30,1409,25,1413,25
60320 DATA 1423,28,1429,100,28,28,1439,26,1453,29,1461,29
60330 DATA 1477,27,1493,27,1503,99,99,99,83,80,81,81,81
60340 DATA 81,80,80,82,1531,100,1537,29,1541,32,1545,29,1551,30,30,30,30,30,1567,25
60350 DATA 1575,25,1581,28,1589,96,96,100,1597,26,1611,29
60360 DATA 1619,29,1639,27,1651,27
60390 DATA 1801,27
60400 DATA 1825,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27
60410 DATA 27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27,27
60420 DATA 27,27,27,27,27,27,27,27,27,27,27,27,27,27
60470 DATA 9999
