5 'PROGRAM: LETTERS/TWO, GIVES     TWO COLUMN PRINTING, WITH       HEADER SPACE
10 CLEAR15000
11 CO=80  'PAGE WIDTH FOR TOTAL TEXT
12 NL=45  'LINES PER FIRST PAGE           (LINE COUNTER)
13 TM=10  'WIDTH OF TOP MARGIN            (VARY FOR LETTERHEAD)
14 SP=0   'SINGLE PAGE FLAG               (CONTINUOUS/SINGLE PG)
15 LE=0   'LENGTH OF DATA ENTRY
16 'CN   = COUNTER FOR CHARACTERS  (RD$=INPUT CHARACTER READ)
17 'LF   = LINEFEED
18 'PL$  = PRINT LINE STRING
19 'ST   = FLAG FOR FINISH OF WORK
20 DIMPL$(116):AL=0:CN=1:Z$=CHR$(128):PG=0  '(PG=PAGE COUNTER)
21 BL$=STRING$(32,32)  '1 LINE OF BLANKS ON SCREEN
22 IN=INT((CO/80)/2)  'DETERMINES PAGE CENTERLINE
25 '----------MAIN MENU
30 CLS:PRINT"   TWO COLUMN PROGRAM LISTER":PRINTSTRING$(32,131);
40 PRINTZ$"note"Z$" THE FILE TO BE USED        MUST BE IN 'ASCII' FORMAT."
45 PRINT:PRINTZ$"filename"Z$" ";
50 LINEINPUTDN$:IFLEFT$(RIGHT$(DN$,4),1)<>"/"THEN DN$=DN$+"/BAS"
60 IF LEN(DN$)>12 OR LEN(DN$)<5 THEN30
65 PRINT:PRINTZ$"source"Z$"drive"Z$" ";:INPUTDD:DRIVEDD
70 PRINT:PRINTZ$"city"Z$"and"Z$"date"Z$" "  'INPUT, NEXT LINE, MAY BE UP TO 79 CHARACTERS, CENTERED ON PAGE
80 LINEINPUT   " ";NM$:LE=LEN(NM$):IFLE>CO THEN70
90 PRINT:PN=INT((CO-LE)/2)
100 PRINTZ$"stop"Z$"for"Z$"each"Z$"page"Z$" Y/N "
110 I$=INKEY$:IFLEFT$(I$,1)="Y" ORLEFT$(I$,1)="y" THENSP=1 ELSEIFLEFT$(I$,1)<>"N" ANDLEFT$(I$,1)<>"n" THEN110 ELSESP=0
119 '----------LOAD THE DATAFILE TO BE PRINTED
120 CLS:PRINT"    LOAD THE FILE FROM DISK":PRINTSTRING$(32,131);
125 PRINT:PRINTZ$"insert"Z$"data"Z$"disk"Z$"in"Z$"drive"Z$;DD
128 PRINT:PRINT:PRINTZ$"press"Z$"enter"Z$"to"Z$"continue"Z$;:INPUTAN$
130 OPEN"D",#1,DN$,1
140 FIELD#1,1 AS RD$
150 LF=LOF(1)
160 '----------SET UP NEW PAGE FOR PRINTING
190 CLS:PRINT"      PREPARE FOR NEW PAGE":PRINTSTRING$(32,131);:PG=PG+1
200 PRINT:      PRINT" PLEASE WAIT.":PRINT:PRINT" PRINT LINES ARE BEING COUNTED.":PRINT:PRINT" PRINTING WILL START BEFORE THIS NUMBER SHOWS"NL*2+1
205 PRINT@331,CHR$(129);STRING$(5,131);CHR$(130):PRINT@363,CHR$(133);:PRINT@369,CHR$(138):PRINT@395,CHR$(132);STRING$(5,140);CHR$(136)
210 FOR F=1 TO NL*2:PL$(F)=BL$:NEXT F
220 FORF1=1TONL*2:PL$(F1)=STRING$(37,32)  '37 SHOWS COLUMN WIDTH (INCLUDES CR)
230 FORF2=1TO37:CN=CN+1:IFCN>LF THENST=1:GOTO300  '37 SHOWS COLUMN WIDTH (INCLUDES CR)
240 GET#1,CN
250 IFRD$=CHR$(13) THEN280
260 MID$(PL$(F1),F2,1)=RD$
270 NEXTF2
280 PRINT@364,F1;:NEXTF1
300 IFSP=1 THENGOSUB400 ELSEIFAL=0 THENGOSUB400 ELSEPRINT#-2:PRINT#-2
310 PRINT@480,       Z$"now"Z$"printing"Z$:FORF=1 TONL:PRINT#-2,TAB(IN);PL$(F);  "    ";PL$(F+NL)
320 NEXT
325 PRINT#-2:PRINT#-2,TAB(36)"Page"PG
330 IFST=1THENPRINT#-2,CHR$(12):GOTO450
340 PRINT#-2,CHR$(12):NL=56:GOTO190  '56 SHOWS NUMBER OF LINES, PAGE 2 AND AFTER
390 '----------READY A NEW PAGE FOR PRINTING
400 AL=1:CLS:PRINT"       INSERT NEW PAGE":PRINTSTRING$(32,131);:PRINTZ$"position"Z$"new"Z$"page"Z$"in"Z$"printer"Z$
405 PRINT:PRINT:PRINTZ$"press"Z$"enter"Z$"to"Z$"continue"Z$" ";:LINEINPUTI$
410 PRINT#-2,STRING$(TM,13):PRINT#-2,TAB(PN);NM$:PRINT#-2:RETURN
420 '----------JOB IS FINISHED
450 CLOSE:CLS:PRINT"      PRINTING IS FINISHED":PRINTSTRING$(32,131);:PRINT" THE TWO COLUMN PRINTING IS      FINISHED."
455 PRINT:PRINT:PRINT" PRESS 'Y' FOR ANOTHER PRINTING  OR PRESS 'N' TO STOP.":PG=0
460 I$=INKEY$:IFLEFT$(I$,1)="Y" ORLEFT$(I$,1)="y" THENRUN ELSEIFLEFT$(I$,1)="N" ORLEFT$(I$,1)="n" THENEND ELSE460
550 'FIRST SPACE ALWAYS LOST. SO     INDENT ONE SPACE TO START.
560 'USE NO PRINT CODES IN TEXT.     CARRIAGE RETURN AFTER LINE.
570 'DO NOT PLACE HEADER SPACES      IN FILE COPY. PROGRAM = 3,      OR 10 IF USING LETTERHEAD.
580 'IF DATE IS TO BE PRINTED BY     PROGRAM, TOP CENTER, DO NOT     PLACE DATE IN DATA COPY.
590 'USE VIP/WRITER LINELENGTH       OF 37, NON JUSTIFIED.
1990 'NOTE THAT YOU MUST COUNT        ANY 37-CHARACTER LINE AS        INCLUDING THE CARRIAGE          RETURN (36 CHARACTERS +CR)
2000 'LETTERS/TWO,  870424. WAS ORIGINALLY '2-COLUMN/BAS' IN THEMAY 1985 RAINBOW, PAGE 44, BY   STEPHEN LAI. PROGRAM MODIFIED BYC E BROWN FOR NEWSLETTERS AND   CORRESPONDENCE USE.
