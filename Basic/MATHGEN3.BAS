1 REM**************************
2 REM*       MONEY MATH       *
3 REM*   WORKSHEET GENERATOR  *
4 REM*    BY FRED B. SCERBO   *
5 REM*   COPYRIGHT (C) 1990   *
6 REM*     60 HARDING AVE.    *
7 REM*  NORTH ADAMS,MA 01247  *
8 REM**************************
10 DIM U(21,5)
15 NB$="####.##"
20 CLS0:SCREEN0,1:LC$=CHR$(175)+CHR$(170):RC$=CHR$(165)+CHR$(175):EB$=CHR$(175)+CHR$(175):GOSUB25:GOTO30
25 CLS6:PRINTSTRING$(32,172);:POKE1056,32:PRINT@33,"money";:POKE1062,32:PRINT@39,"math";:POKE1067,32:PRINT@44,"worksheet";:POKE1077,32:PRINT@54,"generator";:POKE1087,32:PRINT@64,STRING$(32,163);CHR$(175);:RETURN
30 FORI=0TO8:PRINT@127+(I*32),EB$;:NEXT:PRINT@133,CHR$(175);CHR$(174);STRING$(18,172);CHR$(173);CHR$(175);
35 PRINT@165,LC$;" (A) ADD TWO      ";RC$;:PRINT@197,LC$;" (B) ADD THREE    ";RC$;:PRINT@229,LC$;" (C) ADD FOUR     ";RC$;:PRINT@261,LC$;" (D) ADD FIVE     ";RC$;:PRINT@293,LC$;" (E)XIT PROGRAM   ";RC$;
40 PRINT@325,CHR$(175);CHR$(171);STRING$(18,163);CHR$(167);CHR$(175);
45 PRINT@384,STRING$(32,172);:POKE1440,32:POKE1441,32:POKE1442,32:POKE1443,32:POKE1444,32:PRINT@421,"press";:POKE1450,32:PRINT@427,"desired";:POKE1458,32:PRINT@435,"category";:POKE1467,32:POKE1468,32
50 POKE1469,32:POKE1470,32:POKE1471,32:PRINT@448,STRING$(32,163);STRING$(31,128);:POKE1535,128
55 X$=INKEY$:KE=RND(TIMER):IFX$="A"THEN160ELSEIFX$="B"THEN270ELSEIFX$="C"THEN370ELSEIFX$="D"THEN475ELSEIFX$="E"THEN590ELSE55
60 GOTO20
65 PRINT#-2,CHR$(14);:REM       INSERT ANY VALUE FOR ENLARGED   PRINT ON YOUR PRINTER
70 PRINT#-2,"NAME";STRING$(24,&H5F);"DATE";STRING$(7,95)
75 PRINT#-2," "
80 PRINT#-2,"SHEET IDENTIFICATION NO.";SN(NC):PRINT#-2," "
85 RETURN
90 PRINT#-2,"--------------ANSWER SHEET--------------":PRINT#-2," "
95 PRINT#-2,"SHEET IDENTIFICATION NO.";SN(NC):PRINT#-2," "
100 RETURN
105 FORI=1TO3:PRINT#-2,"   -------   ";:NEXTI:PRINT#-2,"":RETURN
110 GOSUB25:FORI=127TO191STEP32:PRINT@I,EB$;:NEXT:PRINT@192,STRING$(32,172);
115 QK$(NC)="A":PRINT@133," ADDITION  WORKSHEETS ";:PRINT@224,"        INPUT NUMBER SPAN":PRINTSTRING$(32,32);:PRINT@256,"   (4 DIGITS-MAX.9999)";:INPUTWW:IF WW>9999THEN115ELSEIF WW=0THENRUN
120 SN(NC)=TIMER
125 WW=WW*100:RETURN
130 FORS=1TO3:PRINT#-2,USING"##.";S+K;:PRINT#-2,"          ";:NEXTS:PRINT#-2,"":RETURN
135 FORI=1TO3:U(I+K,W)=RND(WW)/100:PRINT#-2,"   ";:PRINT#-2,USING NB$;U(I+K,W);:PRINT#-2,"   ";:U(I+K,0)=U(I+K,0)+U(I+K,W):NEXTI:PRINT#-2,"":RETURN
140 FORI=1TO3:U(I+K,W)=RND(WW)/100:PRINT#-2,"  +";:PRINT#-2,USING NB$;U(I+K,W);:PRINT#-2,"   ";:U(I+K,0)=U(I+K,0)+U(I+K,W):NEXTI:PRINT#-2,"":RETURN
145 FORI=1TO3:PRINT#-2,"   ";:PRINT#-2,USING NB$;U(I+K,W);:PRINT#-2,"   ";:NEXTI:PRINT#-2,"":RETURN
150 FORI=1TO3:PRINT#-2,"  +";:PRINT#-2,USING NB$;U(I+K,W);:PRINT#-2,"   ";:NEXTI:PRINT#-2,"":RETURN
155 FORI=1TO3:PRINT#-2,"   ";:PRINT#-2,USING NB$;U(I+K,0);:PRINT#-2,"   ";:NEXTI:PRINT#-2,"":RETURN
160 REM ADD TWO
165 GOSUB110
170 GOSUB65
175 FORK=0TO18STEP3
180 GOSUB130
185 W=1:GOSUB135
190 W=2:GOSUB140
195 GOSUB105
200 FORI=1TO4:PRINT#-2," ":NEXTI,K
205 FORI=1TO6:PRINT#-2," ":NEXT
210 GOSUB580
215 GOSUB90
220 FORK=0TO18STEP3
225 GOSUB130
230 W=1:GOSUB145
235 W=2:GOSUB150
240 GOSUB105
245 GOSUB155
250 FORI=1TO3:PRINT#-2," ":NEXTI,K
255 FORI=1TO5:PRINT#-2," ":NEXT
260 RUN
265 REM ADD THREE
270 GOSUB110
275 GOSUB65
280 FORK=0TO15STEP3
285 GOSUB130
290 FORW=1TO2:GOSUB135:NEXT
295 W=3:GOSUB140
300 GOSUB105
305 FORI=1TO4:PRINT#-2," ":NEXTI,K
310 GOSUB580
315 FORI=1TO8:PRINT#-2," ":NEXT
320 GOSUB90
325 FORK=0TO15STEP3
330 GOSUB130
335 FORW=1TO2:GOSUB145:NEXT
340 W=3:GOSUB150
345 GOSUB105
350 GOSUB155
355 FORI=1TO3:PRINT#-2," ":NEXTI,K
360 FORI=1TO8:PRINT#-2," ":NEXT
365 RUN
370 REM ADD FOUR
375 GOSUB110
380 GOSUB65
385 FORK=0TO12STEP3
390 GOSUB130
395 FORW=1TO3:GOSUB135:NEXT
400 W=4:GOSUB140
405 GOSUB105
410 FORI=1TO5:PRINT#-2," ":NEXTI,K
415 GOSUB580
420 FORI=1TO8:PRINT#-2," ":NEXT
425 GOSUB90
430 FORK=0TO12STEP3
435 GOSUB130
440 FORW=1TO3:GOSUB145:NEXT
445 W=4:GOSUB150
450 GOSUB105
455 GOSUB155
460 FORI=1TO4:PRINT#-2," ":NEXTI,K
465 FORI=1TO8:PRINT#-2," ":NEXT
470 RUN
475 REM ADD FIVE
480 GOSUB110
485 GOSUB65
490 FORK=0TO12STEP3
495 GOSUB130
500 FORW=1TO4:GOSUB135:NEXT
505 W=5:GOSUB140
510 GOSUB105
515 FORI=1TO4:PRINT#-2," ":NEXTI,K
520 GOSUB580
525 FORI=1TO7:PRINT#-2," ":NEXT
530 GOSUB90
535 FORK=0TO12STEP3
540 GOSUB130
545 FORW=1TO4:GOSUB145:NEXT
550 W=5:GOSUB150
555 GOSUB105
560 GOSUB155
565 FORI=1TO3:PRINT#-2," ":NEXTI,K
570 FORI=1TO7:PRINT#-2," ":NEXT
575 RUN
580 CLS5:PRINT@192,STRING$(32,172);"now";:POKE1251,32:PRINT@228,"printing";:POKE1260,32:PRINT@237,"coded";:POKE1266,32:PRINT@243,"answer";:POKE1273,32:PRINT@250,"sheets";STRING$(32,163);
585 RETURN
590 CLS0:SCREEN0,0:END
