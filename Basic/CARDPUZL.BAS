10 'CARDPUZL - BY WALTER PULLEN - 21 JUNE 1990'
20 N=4:V$="JQKA":S$="CDHS":D=1
30 NN=N*N:DIM V(N,N),S(N,N),A(NN),D(1,N,N*4):J=1
40 I=I+1:IF I>N THEN IF J>=N THEN 100 ELSE I=1:J=J+1
50 V(I,J)=1:S(I,J)=0
60 S(I,J)=S(I,J)+1:IF S(I,J)>N THEN S(I,J)=1:V(I,J)=V(I,J)+1:IF V(I,J)>N THEN S(I,J)=0:V(I,J)=0:GOTO 140
70 A=V(I,J)*N+S(I,J)-N:IF A(A) THEN 60 ELSE X=0:Y=0
80 X=X+1:IF X<>I AND (V(X,J)=V(I,J) OR S(X,J)=S(I,J)) THEN 60 ELSE IF X<N THEN 80
90 Y=Y+1:IF Y<>J AND (V(I,Y)=V(I,J) OR S(I,Y)=S(I,J)) THEN 60 ELSE IF Y<N THEN 90 ELSE A(A)=1:T=T+1:GOTO 40
100 PRINT "TESTING":FOR Y=1 TO N:FOR X=1 TO 4*N:D(0,Y,X)=0:D(1,Y,X)=0:NEXT X,Y
110 FOR Y=1 TO N:FOR X=1 TO N:D(0,V(X,Y),X+Y)=D(0,V(X,Y),X+Y)+1:D(1,S(X,Y),X+Y)=D(1,S(X,Y),X+Y)+1:D(0,V(X,Y),3*N-X+1+Y)=D(0,V(X,Y),3*N-X+1+Y)+1:D(1,S(X,Y),3*N-X+1+Y)=D(1,S(X,Y),3*N-X+1+Y)+1:NEXT X,Y
120 FOR Y=1 TO N:A=0:B=0:FOR X=1 TO 4*N:A=A-(D(0,Y,X)>1):B=B-(D(1,Y,X)>1):NEXT X:IF A>D OR B>D THEN 140 ELSE NEXT Y
130 U=U+1:FOR Y=1 TO N:FOR X=1 TO N:PRINT " ";MID$(V$,V(X,Y),1);MID$(S$,S(X,Y),1);:NEXT X:IF Y=1 THEN PRINT,"SOLUTION:";U:NEXT Y ELSE PRINT:NEXT Y
140 I=I-1:IF I<1 THEN I=N:J=J-1:IF J<1 THEN PRINT "DONE":END
150 A(V(I,J)*N+S(I,J)-N)=0:GOTO 60
