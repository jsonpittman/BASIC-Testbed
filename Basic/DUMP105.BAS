2 '      DMP-105 DUMPER
3 '       BY DRUE KING
4 '       
10 DIMD(15):CL=1
20 FOR X=0 TO 15:READ A:D(X)=A:NEXT X
30 CLS:PRINT@9,"PMODE4 DUMP"
40 :PRINT:PRINT" 1) (3 1/8) X (3 4/8)":PRINT" 2) (6 3/8) X (7 1/8)":PRINT:INPUT"SIZE";SZ
50 IF SZ<>1 AND SZ<>2 THEN 40
60 PRINT:PRINT" 1) NORMAL ":PRINT" 2) reverse":PRINT:INPUT" OUTPUT";OP
70 IF OP<>1 AND OP<>2 THEN 60
80 PRINT@240,"1) 600";:PRINT@272,"2) 2400";:PRINT@336,"BAUD RATE";:INPUT PT:IF PT<>1 AND PT<>2 THEN 80 ELSE IFPT=1THEN POKE150,88 ELSE POKE150,18
90 PRINT:INPUT"GRAPHICS PAGE START ADD.";AD
100 AD=AD+31:KE=AD-1
110 PRINT:PRINT"  ALIGN PAPER TO TOP OF RIBBON":PRINT"         AND HIT ENTER";
120 A$=INKEY$:IFA$=""THEN120
130 PRINT#-2,CHR$(18);:SCREEN1,1
140 IF SZ=1 THEN PRINT#-2,CHR$(27);CHR$(19):GOTO270  ELSE PRINT#-2,CHR$(27);CHR$(14):GOTO150
150 FOR SP=1 TO 20:PRINT#-2,CHR$(13);:NEXT
160 GOSUB350:FOR AD=KE+1 TO KE+6113 STEP32:P=PEEK(AD):J=D((P AND 15)):IF OP=1 THEN J=255-J 
170 IF J<128 THEN J=J+128
180 PRINT#-2,CHR$(J);:NEXT
190 GOSUB350
200 PRINT#-2,CHR$(27);CHR$(90);CHR$(1);:FOR GN=KE+1 TO KE+6113 STEP32:P=PEEK(GN):P=D((P AND 15)):IF P>63 AND OP=1 THEN PRINT#-2,CHR$(128);:NEXT:GOTO220ELSEIF OP=1 THENPRINT#-2,CHR$(192);:NEXT:GOTO220
210 IF P>63 THENPRINT#-2,CHR$(192);:NEXT ELSE PRINT#-2,CHR$(128);:NEXT
220 PRINT#-2,CHR$(13);:GOSUB350:FOR BA=KE+1 TO KE+6113 STEP 32:P=(PEEK(BA) AND 240)/16:J=D(P):IF OP=1 THEN J=255-J 
230 IF J<128 THEN J=J+128
240 PRINT#-2,CHR$(J);:NEXT
250 GOSUB350:GOSUB330:PRINT#-2,CHR$(13);
260 KE=KE-1:CL=CL+1:IF CL=33 THEN SCREEN0,1:CLS:PRINT@0,"DONE":STOP ELSE GOTO160
270 FORSP=1TO37:PRINT#-2,CHR$(13);:NEXT
280 PRINT#-2,CHR$(27);CHR$(16);CHR$(0);CHR$(143);
290 FOR AD=KE+1 TO KE+6113 STEP32:J=PEEK(AD):IF OP=1 THEN J=255-J 
300 IF J<128 THEN J=J+128
310 PRINT#-2,CHR$(J);:NEXT
320 PRINT#-2,CHR$(27);CHR$(16);CHR$(0);CHR$(143);:GOSUB 330:PRINT#-2,CHR$(13);:KE=KE-1:CL=CL+1:IF CL=33 THEN SCREEN0,1:CLS:PRINT@0,"DONE":STOP ELSE GOTO 280
330 PRINT#-2,CHR$(27);CHR$(90);CHR$(1);:FOR GN=KE+1 TO KE+6113 STEP32:P=PEEK(GN):IF P>=128 AND OP=1 THEN PRINT#-2,CHR$(128);:NEXT:RETURN  ELSE IF OP=1 THEN PRINT#-2,CHR$(192);:NEXT:RETURN
340 IF P>=128 THEN PRINT#-2,CHR$(192);:NEXT:RETURN ELSE PRINT#-2,CHR$(128);:NEXT:RETURN
350 PRINT#-2,CHR$(27);CHR$(16);CHR$(0);CHR$(47);:RETURN
360 DATA 0,3,12,15,48,51,60,63,192,195,204,207,240,243,252,255
