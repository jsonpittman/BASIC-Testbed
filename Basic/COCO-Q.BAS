0 ' COPYRIGHT 1989 FALSOFT, INC
1 CLEAR2000:POKE65497,0:POKE44014,71:POKE44015,81:POKE41598,255:POKE&HF80F,0:POKE&HF84F,0:POKE&HF89C,0:POKE140,180:EXEC43350:DIM H(49),A(5,31),B(5,31),K(33),L(33),G(49):ONBRKGOTO89
2 RGB:PALETTE1,25:PALETTE2,9:PALETTE4,54:PALETTE8,63:BP=25
3 'CMP:PALETTE1,59:PALETTE2,11:PALETTE4,36:PALETTE8,63:BP=59
4 WIDTH40:CLS2:ATTR3,1:POKE65434,BP:GOSUB78:GOSUB80:GOSUB78:POKE59063,57:HSCREEN2:POKE59063,32:HCLS1:HCOLOR2,0:GOSUB69
5 A$="R8D8L8U8BR12":B$=A$+A$+A$:FORT=1TO7:C$=C$+A$:NEXT:HDRAW"C3BM126,15S8"+B$:HDRAW"BM126,39"+B$:HDRAW"BM78,63"+C$:HDRAW"BM78,87"+C$:HDRAW"BM78,111"+C$:HDRAW"BM126,135"+B$:HDRAW"BM126,159"+B$
6 FORT=1TO33:HPAINT(K(T)+4,L(T)-4),2,3:NEXTT:HPAINT(158,95),8,3:V=1:U=0:IFDS=1THENDS=0:RETURN
7 IFTM=0 THENHCOLOR8,0:GOSUB73
8 IFDD=0THENPOKE59108,230:HSCREEN2:POKE59108,231:POKE65434,BP:GOTO48ELSE U=0:X=154:Y=99:CC$="R4U4L4D4":HCOLOR8,0:GOSUB70
9 P$=INKEY$:IFP$=" "THENV=1:MI=0:GOTO6
10 IFPEEK(343)=191THENTM=0:DD=1:DS=1:HCOLOR1,0:GOSUB70:GOSUB74:GOSUB75:GOSUB76:HCOLOR8,0:GOSUB73:GOSUB6:GOTO48
11 IFPEEK(344)=247THEN X=X+24:GOSUB67 ELSEIFPEEK(343)=247THEN X=X-24:GOSUB67 ELSEIFPEEK(341)=247THEN Y=Y-24:GOSUB67 ELSEIFPEEK(342)=247THEN Y=Y+24:GOSUB67
12 IFY=195THENY=171ELSEIFY=3THENY=27ELSEIFX=58THENX=82ELSEIFX=250THENX=226ELSEIFX<130ANDY=51THENX=130ELSEIFX>178ANDY=51THENX=178ELSEIFX=106ANDY=27THENX=130
13 IFX=202ANDY=27THENX=178ELSEIFX=<130ANDY=147THENX=130ELSEIFX>178ANDY=147THENX=178ELSEIFX=106ANDY=171THENX=130ELSEIFX=202ANDY=171THENX=178
14 IFHPOINT(X+2,Y-3)=2 THENHDRAW"S8C8BM"+STR$(X)+","+STR$(Y)+CC$ ELSE HDRAW"C2BM"+STR$(X)+","+STR$(Y)+CC$
15 FORT=1TO25:NEXTT:IFHPOINT(X+2,Y-3)=2THENHDRAW"C2BM"+STR$(X)+","+STR$(Y)+CC$ELSEHDRAW"C8BM"+STR$(X)+","+STR$(Y)+CC$
16 IFP$=CHR$(13)ANDHPOINT(X+2,Y-3)=2THENX1=X:Y1=Y:IFHPOINT(X1,Y1)=2THENGOSUB68
17 IF P$=CHR$(13) AND HPOINT(X1,Y1)=2 AND HPOINT(X,Y)=8THENGOSUB19
18 GOTO9
19 IFX=X1 THEN21ELSEIFY=Y1 THEN25ELSERETURN
20 ' Down move.
21 IF X=X1 AND Y1<Y THEN 23 ELSE IF Y1-Y=48 AND HPOINT(X1,Y1)=2AND HPOINT(X,Y+24)=2 AND HPOINT(X,Y)=8 THEN GOSUB60:HPAINT(X1,Y1),8,3:GOSUB67:HPAINT(X1,Y+24),8,3:GOSUB67:HPAINT(X,Y),2,3:GOSUB67:U=U+1:IFDG=0THENGOSUB32
22 ' Up move.
23 IF X=X1 AND Y1>Y THEN 25 ELSE IF Y-Y1=48 ANDHPOINT(X1,Y1)=2 AND HPOINT(X,Y1+24)=2 AND HPOINT(X,Y)=8 THEN GOSUB60:HPAINT(X1,Y1),8,3:GOSUB67:HPAINT(X1,Y1+24),8,3:GOSUB67:HPAINT(X,Y),2,3:GOSUB67:U=U+1:IFDG=0THENGOSUB32
24 ' Right move.
25 IF X1>X AND Y1=Y THEN 27 ELSE IF X-X1=48 ANDHPOINT(X1,Y1)=2 AND HPOINT(X1+24,Y)=2 AND HPOINT(X,Y)=8 THEN GOSUB60:HPAINT (X1,Y1),8,3:GOSUB67:HPAINT(X1+24,Y),8,3:GOSUB67:HPAINT(X,Y),2,3:GOSUB67:U=U+1:IFDG=0THENGOSUB32
26 ' Left move.
27 IFX1-X=48 ANDY1=Y AND HPOINT(X1,Y1)=2 ANDHPOINT(X1-24,Y)=2 AND HPOINT(X,Y)=8 THEN GOSUB60:HPAINT(X1,Y1),8,3:GOSUB67:HPAINT(X+24,Y),8,3:GOSUB67:HPAINT(X,Y),2,3:GOSUB67:U=U+1:IFDG=0THENGOSUB32
28 IFU=31 THEN U=0:FORT=1TO5:SOUND100,1:SOUND150,1:SOUND180,1:NEXTT ELSE IFU=31 THEN RETURN
29 IF V<>R THEN RETURN ELSE IFMI<10 THEN MI$=RIGHT$(STR$(MI),1):MI$="0"+MI$ ELSE MI$=RIGHT$(STR$(MI),2)
30 HCOLOR1,0:GOSUB70:HCOLOR2,0:GOSUB72:FORT=1TO3000:NEXTT:HCOLOR1,0:GOSUB72:MI=0:MI$="":GOSUB66:HCOLOR8,0:GOSUB70:RETURN
31 ' Check if there are any jumps left.
32 IFU<6 ORU=31THENRETURN
33 A=0:FORT=28TO172STEP24:FORF=82TO226STEP24:A=A+1:H(A)=HPOINT(F,T):NEXTF,T
34 FORT=1TO9:IFH(G(T))=8 THEN35 ELSEIFH(G(T)+1)=2 ANDH(G(T)+2)=8 THENRETURN ELSEIFH(G(T)-1)=2 ANDH(G(T)-2)=8 THENRETURN ELSEIFH(G(T)+7)=2 ANDH(G(T)+14)=8 THENRETURN ELSEIFH(G(T)-7)=2 ANDH(G(T)-14)=8 THENRETURN
35 NEXTT
36 FORT=10TO19:IFH(G(T))=8 THEN37 ELSEIFH(G(T)+1)=2 ANDH(G(T)+2)=8 THENRETURN
37 NEXTT
38 FORT=20TO29:IFH(G(T))=8 THEN39 ELSEIFH(G(T)-1)=2 ANDH(G(T)-2)=8 THENRETURN
39 NEXTT
40 FORT=30TO39:IFH(G(T))=8 THEN41 ELSEIFH(G(T)+7)=2 ANDH(G(T)+14)=8 THENRETURN
41 NEXTT
42 FORT=40TO49:IFH(G(T))=8 THEN43 ELSEIFH(G(T)-7)=2 ANDH(G(T)-14)=8 THENRETURN
43 NEXTT
44 X=154:Y=99:HCOLOR1,0:GOSUB70:GOSUB66:HCOLOR2,0:L=32-U:IFL<10THENL$=RIGHT$(STR$(L),1):L$="0"+L$:GOSUB71 ELSE L$=RIGHT$(STR$(L),2):GOSUB71
45 FORT=1TO3000:NEXTT:GOSUB67:HCOLOR1,0:GOSUB71:HCOLOR8,0:GOSUB70
46 P$=INKEY$:IFP$=""THEN46 ELSEIFP$=" "THENGOSUB67:GOTO6 ELSE IF PEEK(343)=191 THENGOSUB67:GOTO10 ELSE46
47 ' Title screen inkey functions.
48 DG=0:TM=0:P$=INKEY$:IFP$=" "THENHCOLOR1,0:GOSUB69:HCOLOR4,0:GOSUB76:DD=1:GOTO6
49 IFP$>"0"ANDP$<"6"THEN54
50 HCOLOR7,0:GOSUB69
51 IFP$="T"THENDG=1:HCOLOR1,0:GOSUB69:GOTO57
52 HCOLOR2,0:GOSUB69:GOTO48
53 ' Demonstration games.
54 DG=1:U=0:DS=1:HCOLOR1,0:GOSUB69:HCOLOR8,0:GOSUB75:GOSUB6:GOSUB66:P=VAL(P$):FORD=1TOZ(P):X1=K(A(P,D)):Y1=L(A(P,D)):X=K(B(P,D)):Y=L(B(P,D))
55 FORT=1TO50:NEXTT:GOSUB19:NEXTD:GOSUB66:FORT=1TO3000:NEXT:DS=1:GOSUB6:GOSUB66:DG=0:HCOLOR1,0:GOSUB75:GOTO48
56 ' Teaching mode.
57 V=1:HCOLOR2,0:GOSUB77
58 N$=INKEY$:N=VAL(N$):IFN$=""ORN<1 ORN>5 THEN58ELSE R=Z(N)+1
59 HCOLOR1,0:GOSUB77:GOSUB73:GOSUB69:HCOLOR2,0:GOSUB74:HCOLOR4,0:GOSUB76:DS=1:GOSUB6:DD=1:TM=1:GOTO8
60 IF TM=0 THENRETURN
61 ' Check if correct move.
62 HCOLOR2,0:IFX1=K(A(N,V)) AND Y1=L(A(N,V)) AND X=K(B(N,V)) AND Y=L(B(N,V)) THEN V=V+1:W=0:RETURN
63 MI=MI+1:W=W+1:X1=0:IFW=3 THENHCOLOR8,0:HLINE(K(A(N,V))+3,L(A(N,V))-5)-(K(A(N,V))+5,L(A(N,V))-3),PSET,B:HCOLOR2,0:HLINE(K(B(N,V))+3,L(B(N,V))-5)-(K(B(N,V))+5,L(B(N,V))-3),PSET,B:X=K(A(N,V)):Y=L(A(N,V)):W=0
64 GOSUB66:GOTO9
65 ' Gosubs, Gosubs, Gosubs.
66 POKE140,5:FORG=1TO2:FORK=1TO10:EXEC:NEXTK:FORL=1TO20:NEXTL,G:RETURN
67 POKE140,200:FORT=1TO4:EXEC:NEXTT:RETURN
68 POKE140,9:FORT=1TO4:EXEC:NEXT:RETURN
69 HPRINT(26,3),"PRESS SPACEBAR":HPRINT(26,5),"TO PLAY A GAME":HPRINT(1,3),"PLEASE  MAKE":HPRINT(0,5),"YOUR SELECTION":HPRINT(1,18),"PRESS [T] FOR":HPRINT(1,20),"TEACHING MODE":HPRINT(26,18),"NUMBERS: 1 - 5":HPRINT(26,20),"FOR DEMO GAMES":RETURN
70 HPRINT(3,23),"PRESS F1 AT ANY TIME FOR MAIN MENU":RETURN
71 HPRINT(4,23),"NO MORE MOVES.":HPRINT(19,23),L$:HPRINT(22,23),"PEGS REMAINING":RETURN
72 V=1:HPRINT(3,23),"GAME IS DONE. YOU MADE":HPRINT(26,23),MI$:HPRINT(29,23),"MISTAKES":RETURN
73 HPRINT(17,0),"COCO-Q":RETURN
74 HPRINT(2,0),"LET'S LEARN HOW TO PLAY GAME NUMBER":HPRINT(37,0),STR$(N):RETURN
75 HPRINT(9,23),"NOW PLAYING DEMO GAME ":HPRINT(31,23),P$:RETURN
76 HPRINT(0,3),"USE ARROW KEYS":HPRINT(0,5),"TO MOVE CURSER":HPRINT(26,3),"PRESS: [ENTER]":HPRINT(26,5),"ON JUMPING PEG":HPRINT(1,18),"PRESS [ENTER]":HPRINT(1,20),"ON EMPTY HOLE":HPRINT(26,18),"PRESS SPACEBAR":HPRINT(26,20),"FOR A NEW GAME":RETURN
77 HPRINT(8,23),"PRESS 1-5 TO SELECT GAME":RETURN
78 LOCATE8,12:PRINT"PLEASE WAIT LOADING DATA";:RETURN
79 ' Read data for demo & teaching games
80 FORM=1TO5:Z(M)=VAL(MID$("3128302924",M*2-1,2)):FORD=1TOZ(M):READA(M,D),B(M,D):NEXTD,M:FORM=1TO49:READG(M):NEXTM
81 C=0:FORT=130TO178STEP24:C=C+1:K(C)=T:L(C)=27:K(C+3)=T:L(C+3)=51:K(C+27)=T:L(C+27)=147:K(C+30)=T:L(C+30)=171:NEXT
82 C=6:FORT=82TO226STEP24:C=C+1:K(C)=T:L(C)=75:K(C+7)=T:L(C+7)=99:K(C+14)=T:L(C+14)=123:NEXT:RETURN
83 DATA 19,17,16,18,28,16,25,23,33,25,18,30,31,33,33,25,26,24,29,17,6,18,13,11,10,12,27,13,13,11,18,6,8,10,1,9,16,4,3,1,1,9,10,8,7,9,6,4,21,7,22,24,24,10,10,8,7
84 DATA 9,4,16,15,17,15,17,4,16,17,15,14,16,7,9,16,4,1,9,10,8,3,1,6,4,1,9,8,10,11,9,13,11,18,6,27,13,26,12,13,11,6,18,25,11,33,25,24,26,31,33,28,30,33,25,26,24
85 DATA 23,25,21,23,19,17,30,18,17,19,20,18,27,25,18,30,33,25,24,26,31,33,28,30,33,25,26,24,6,18,13,11,18,6,3,11,10,12,1,3,4,6,3,11,12,10,16,28,21,23,28,16,7,21
86 DATA 8,22,21,23,24,22,9,23,22,24,5,17,12,10,17,5,2,10,3,11,10,12,13,11,18,6,27,13,26,12,13,11,6,18,25,11,33,25,24,26,31,33,28,30,33,25,26,24,8,10,1,9,10,8,16,28,21,23,28,16,7,21,8,22,21,23,23,9
87 DATA 15,17,18,16,5,17,12,10,3,11,10,12,8,10,1,9,10,8,7,9,16,18,29,17,28,16,21,23,16,28,31,23,25,11,12,10,10,24,27,25,24,26,33,25,26,12,13,11
88 DATA 17,18,19,24,25,26,31,32,33,3,10,15,16,22,23,29,30,38,45,5,12,20,21,27,28,34,35,40,47,3,4,5,10,11,12,15,16,20,21,29,30,34,35,38,39,40,45,46,47
89 WIDTH80:CLS3:ATTR3,2:POKE41598,3:POKE65496,0:STOP
