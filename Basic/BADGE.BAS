1 'BADGE MASTER
2 'WRITTEN BY STEPHEN DUFF
3 'COPYRIGHT (C) NOVEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 '***************************
20 'BADGE MASTER V1.0
30 'BOY SCOUTS OF CANADA
40 'CUB PACK BADGES.
50 '(C)copyright February 1990
60 'Stephen Duff
70 '1123 62 St.
80 'Edmonton Alta. Can. T6L 2H7
90 '***************************
100 CLEAR MEM/2:CLS
110 DF$=STRING$(50,"N")
120 DIM BA$(50),CUB$(50),CA$(50),CP$(50),CS$(50),CC$(50),CB$(50),CN$(50)
130 'BADGES DATA
140 DATA ARTIST,ATHLETE,CARPENTER,COLLECTOR
150 DATA COMPUTER,CYCLIST,FIRST AIDER,FISHERMAN
160 DATA GARDENER,GUIDE,HANDICRAFT,HANDYMAN
170 DATA HOUSE ORDERLY,LANGUAGE,LAW AWARENESS,MUSICIANS
180 DATA OBSERVER,PERSONAL FITNESS,PET KEEPER,READER,RELIGION,SKATER
190 DATA SKIER,SWIMMER,TEAM PLAYER,TROUBADOUR
200 DATA WINTER CUBBING,WOODMAN,WORLD CONSERVATION
210 DATA WORLD CUBBING,END
220 X=X+1
230 READ BA$(X):IF BA$(X)="END"GOTO250 ELSE220
240 CLS
250 'MAIN MENU
260 DATA LOAD DATA,SAVE DATA,PRINT,DISPLAY BADGE LIST,UPDATE CUBS,CLEAR DATA,EXIT,END
270 M=M+1
280 READ MN$(M):IF MN$(M)="END"GOTO290 ELSE270
290 GOSUB410:GOSUB530
300 CLS:PRINTTAB(4);"CUB SCOUT'S BADGE MASTER";
310 PRINT:PRINTTAB(11);"MAIN MENU";
320 FOR Z=1TOM-1
330 PRINT@(32*Z)+72,Z;MN$(Z)
340 NEXT
350 PRINT:PRINTTAB(9);:INPUT "SELECT OPTION";I
360 IF I<1 OR I>M-1 THEN SOUND100,3:GOTO300
370 ON I GOSUB 1930,2060,1150,2290,390,2230,2160
380 GOTO300
390 'MAIN UPDATES
400 IF FL=1GOTO440:CM=0
410 DATA ADD CUB,ADD/DISPLAY BADGES,CORRECT DATA,DISPLAY BADGE LIST,MAIN MENU,END
420 CM=CM+1
430 READ CM$(CM):IF CM$(CM)="END"THENFL=1:RETURN:ELSE420
440 CLS:PRINTTAB(10);"UPDATE MENU"
450 FORZ=1TOCM-1:PRINT@(32*Z)+39,Z;CM$(Z):NEXT
460 PRINT:PRINTTAB(8);:INPUT "SELECT OPTION";I
470 IF I<1 OR I>CM-1 THEN SOUND100,3:GOTO440
480 ON I GOSUB 500,670,860,2290,300
490 GOTO440
500 'ADD CUBS
510 CLS:PRINTTAB(7);"ADDING NEW CUB DATA"
520 IFF2=1GOTO550:AC=0
530 DATA LAST NAME,FIRST NAME,ADDRESS,PCODE,PHONE#,SIX,END
540 AC=AC+1:READ AC$(AC):IFAC$(AC)="END"THENF2=1:RETURN:ELSE540
550 FORZ=1TOAC-1:PRINT@(32*Z)+34,Z;AC$(Z):NEXT
560 N=N+1
570 PRINT@452,"ADDING CUB #";N
580 PRINT@80,"";
590 INPUTCUB$(N):PRINT@112,"";:IF CUB$(N)=""THENN=N-1:GOTO390
600 INPUTCN$(N):PRINT@144,"";
610 INPUTCA$(N):PRINT@176,"";
620 INPUTCC$(N):PRINT@208,"";
630 INPUTCP$(N):PRINT@240,"";
640 INPUTCS$(N):PRINT@420,"";
650 INPUT"CORRECT ([Y]/N)";A$
660 IFA$="N"THENGOTO580ELSE510
670 'UPDATE CUB AND BADGES
680 CLS:IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO390
690 PRINTTAB(10);"UPDATE CUB"
700 PRINT@37,"";:INPUT"LAST NAME";N$
710 IFN$=""THEN390
720 PRINT@68,"";:INPUT"FIRST NAME";N1$
730 FORZ=1TON:IFN$=CUB$(Z)ANDN1$=CN$(Z)THEN750ELSENEXT
740 PRINT@235,"NOT FOUND":FORZ=1TO500:NEXT:GOTO670
750 CLS:PRINTTAB(2);CN$(Z);" ";CUB$(Z)
760 IFCB$(Z)=""THENCB$(Z)=DF$
770 IF INSTR(CB$(Z),"Y")THEN780 ELSEPRINTTAB(2);"NO BADGES ON FILE":GOTO820
780 PRINTTAB(2);"BADGES ON FILE :"
790 FORP=1TOX:IF MID$(CB$(Z),P,1)="Y"THENPRINTP;MID$(BA$(P),1,10),
800 NEXTP
810 GOSUB2380
820 PRINT@450,"";:INPUT"ADD WHICH BADGE";AB$
830 IFAB$=""THEN670
840 FORP=1TOX:IFAB$=BA$(P)THEN MID$(CB$(Z),P,1)="Y":GOTO750:ELSENEXTP
850 PRINT@450,"NOT FOUND CHECK SPELLING":FORP=1TO500:NEXT:PRINT@450,STRING$(25," ");:GOTO820
860 'CHANGE DATA
870 CLS:IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO390
880 PRINTTAB(10);"CHANGE DATA"
890 PRINT@37,"";:INPUT"LAST NAME";N$
900 IFN$=""THEN390
910 PRINT@68,"";:INPUT"FIRST NAME";N1$
920 FORZ=1TON:IF N$=CUB$(Z)ANDN1$=CN$(Z)THEN940 ELSENEXT
930 PRINT@235,"NOT FOUND":FORZ=1TO500:NEXT:GOTO860
940 CLS:PRINTTAB(5);CN$(Z);" ";CUB$(Z),"  CUB#:";Z
950 PRINT:PRINTTAB(5);CA$(Z):PRINTTAB(5);CC$(Z):PRINTTAB(5);CP$(Z):PRINTTAB(5)CS$(Z)
960 PRINT:PRINTTAB(4);:INPUT" CHANGE (Y/N)";A$
970 IFA$="Y"THEN980ELSE1060
980 CLS:PRINTTAB(5);"CHANGING CUB #";Z
990 PRINT:INPUT" LAST NAME";CUB$(Z)
1000 INPUT" FIRST NAME";CN$(Z)
1010 INPUT" ADDRESS";CA$(Z)
1020 INPUT" POSTAL CODE";CC$(Z)
1030 INPUT" PHONE #";CP$(Z)
1040 INPUT" SIX";CS$(Z)
1050 GOTO940
1060 'REMOVE BADGES
1070 CLS:PRINTTAB(5);CN$(Z);" ";CUB$(Z)
1080 IFINSTR(CB$(Z),"Y")THEN1090ELSECLS:PRINT@232,"NO BADGES ON FILE":FORZ=1TO500:NEXT:GOTO390
1090 FORP=1TOX:IFMID$(CB$(Z),P,1)="Y"THENPRINTP;MID$(BA$(P),1,10),
1100 NEXTP
1110 PRINT@484,"";:INPUT"REMOVE WHICH BADGE#";A
1120 IFA<1THEN390
1130 MID$(CB$(Z),A,1)="N"
1140 GOTO1060
1150 'PRINT ROUTINES
1160 'CONTROL CODES FOR DMP105
1170 IFF3=1THEN1220:PM=0
1180 POKE150,18 'SETS BAUD TO 2400
1190 DATA BADGE LIST,BADGE REQUEST FORM,CUB'S DATA,CUB'S BADGES,MAIN MENU,END
1200 PM=PM+1
1210 READ PM$(PM):IFPM$(PM)="END"THEN1220ELSE1200
1220 F3=1:CLS:PRINTTAB(10);"PRINT REPORTS"
1230 FORP=1TOPM-1:PRINT@(32*P)+39,P;PM$(P):NEXT
1240 PRINT:PRINTTAB(8);:INPUT"SELECT OPTION";I
1250 IFI<1ORI>PM-1THENSOUND100,3:GOTO1220
1260 ON I GOTO 1270,1740,1340,1440,300
1270 'PRINT BADGE LIST
1280 CLS:PRINTTAB(7);"CURRENT BADGE LIST"
1290 GOSUB2380
1300 PRINT#-2,CHR$(13);CHR$(27);CHR$(14)
1310 PRINT#-2,TAB(11);"CURRENT BADGE LIST";CHR$(27);CHR$(15);CHR$(13)
1320 FORP=1TOX-1 STEP2:PRINT#-2,TAB(20);P;BA$(P);TAB(45);P+1;BA$(P+1):NEXT
1330 PRINT#-2,STRING$(75-P,CHR$(13)):GOTO1220
1340 'PRINT CUB PACK
1350 CLS:PRINTTAB(7);"PRINT CUB PACK LIST"
1360 IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO1220
1370 GOSUB2380
1380 PRINT#-2,CHR$(27);CHR$(14);CHR$(13)
1390 PRINT#-2,TAB(13);"REGISTERED CUBS";CHR$(13);CHR$(27);CHR$(15)
1400 FORP=1TON
1410 PRINT#-2,TAB(1);P;CN$(P);" ";CUB$(P);TAB(25);CA$(P);TAB(45);CC$(P);TAB(55);CP$(P);" ";CS$(P);"   ";TAB(72);"SIX"
1420 NEXTP
1430 GOTO1150
1440 'PRINT CUB'S BADGES
1450 CLS:IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO1220
1460 CLS:PRINTTAB(7);"PRINT CUB'S BADGES"
1470 PRINT@226,"TYPE <ALL> FOR COMPLETE LIST"
1480 PRINT@37,"";:INPUT"LAST NAME";N$
1490 IFN$=""THEN1220
1500 IFN$="ALL"THEN1600
1510 PRINT@68,"";:INPUT"FIRST NAME";N1$
1520 FORZ=1TON:IFN$=CUB$(Z)ANDN1$=CN$(Z)THEN1540ELSENEXT
1530 PRINT@267,"NOT FOUND":FORZ=1TO500:NEXT:GOTO1440
1540 IF INSTR(CB$(Z),"Y")THEN1560ELSEPRINTTAB(4);"NO BADGES ON FILE":FORZ=1TO500:NEXT:GOTO1460
1550 GOSUB2380
1560 PRINT#-2,CHR$(27);CHR$(14);CN$(Z);" ";CUB$(Z);CHR$(27);CHR$(15);CHR$(13)
1570 FORP=1TOX:IFMID$(CB$(Z),P,1)="Y"THENPRINT#-2,P;BA$(P)
1580 NEXTP
1590 PRINT#-2,STRING$(80,"-"):GOTO1440
1600 'PRINT ALL
1610 GOSUB2380
1620 FORP=1TON
1630 IF INSTR(CB$(P),"Y")THEN1670
1640 PRINT#-2,CHR$(27);CHR$(14);P;CN$(P);" ";CUB$(P);CHR$(27);CHR$(15);"  NO BADGES ON FILE"
1650 PRINT#-2,STRING$(80,"-"):NEXTP
1660 GOTO1440
1670 PRINT#-2,CHR$(27);CHR$(14);P;CN$(P);" ";CUB$(P);CHR$(27);CHR$(15)
1680 FOR P1=1TOX
1690 IF MID$(CB$(P),P1,1)="Y"THENPRINT#-2,P1;BA$(P1)
1700 NEXTP1
1710 PRINT#-2,STRING$(80,"-")
1720 NEXTP
1730 GOTO1440
1740 'BADGE REQUEST FORM
1750 UL$=STRING$(20," ")
1760 CLS:PRINTTAB(9);"BADGE REQUEST FORM"
1770 PRINT:PRINTTAB(9);:INPUT"# OF COPIES";P2
1780 GOSUB2380
1790 FORP1=1TOP2
1800 PRINT#-2,CHR$(27);CHR$(14);TAB(10);"CUB'S REQUEST FOR BADGES";STRING$(3,CHR$(13))
1810 PRINT#-2,"DATE:";CHR$(15);UL$;CHR$(14);CHR$(13)
1820 PRINT#-2,CHR$(13);"CUB NAME:";CHR$(15);UL$;CHR$(14);CHR$(13):PRINT#-2,CHR$(27);CHR$(15);"Dear Akela,":PRINT#-2,"I have completed my requirements for the following badge(s)."
1830 PRINT#-2,"Please test me on these requirements and supply the necessary badge(s)."
1840 PRINT#-2,"For any home requirements I am supplying a confirmation from my parent/gaurdian."
1850 PRINT#-2,STRING$(80,"-")
1860 PRINT#-2,CHR$(13);CHR$(27);CHR$(14)
1870 FOR P=1TOX-1
1880 PRINT#-2,TAB(10);"[]";P;BA$(P)
1890 NEXT P
1900 PRINT#-2,CHR$(27);CHR$(15);STRING$(22,CHR$(13))
1910 NEXTP1
1920 GOTO1150
1930 'LOAD DATA
1940 CLS:IFN<>0THENPRINT@234,"DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO300
1950 PRINTTAB(7);"LOAD DATA FROM DISK"
1960 PRINT@226,"TYPE <DIR> FOR DIRECTORY LIST"
1970 PRINT:PRINTTAB(7);:INPUT"DATA FILE NAME";N$
1980 IFN$=""THEN300
1990 IFN$="DIR"THENCLS:DIR:GOSUB2380:GOTO1930
2000 OPEN"I",#1,N$+"/DAT"
2010 N=N+1
2020 INPUT#1,CUB$(N),CN$(N),CA$(N),CP$(N),CS$(N),CC$(N),CB$(N)
2030 IF EOF(1)=-1 THEN2050
2040 GOTO2010
2050 CLOSE#1:GOTO300
2060 'SAVE DATA
2070 CLS:IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO300
2080 PRINTTAB(7);"SAVE DATA TO DISK"
2090 PRINT:PRINT:PRINTTAB(2);"PACK NUMBER OR NAME"
2100 PRINTTAB(2);:INPUT"(8 CHARS OR LESS)";PN$
2110 IFPN$=""THEN300
2120 OPEN"O",#1,PN$+"/DAT"
2130 FORP=1TON:WRITE#1,CUB$(P),CN$(P),CA$(P),CP$(P),CS$(P),CC$(P),CB$(P):NEXTP
2140 CLOSE#1
2150 GOTO300
2160 'EXIT
2170 IFN=0THENEND
2180 CLS:PRINTTAB(09);"EXITING PROGRAM"
2190 PRINT:PRINT" DO YOU HAVE TO SAVE YOUR DATA"
2200 INPUT" (Y/N) <ENTER> FOR MAIN MENU";A$
2210 IFA$=""THEN300
2220 IFA$="Y"THEN2060ELSEEND
2230 'CLEAR DATA
2240 CLS:IFN=0THENPRINT@232,"NO DATA IN MEMORY":FORZ=1TO500:NEXT:GOTO300
2250 PRINTTAB(9);"CLEARING MEMORY"
2260 PRINT:PRINT" DO YOU HAVE TO SAVE YOUR DATA"
2270 INPUT" (Y/N) <ENTER> FOR MAIN MENU";A$
2280 IFA$="Y"THEN2060 ELSEIFA$="N"THENRUN ELSEGOTO300
2290 'BADGE DISPLAY
2300 CLS
2310 PRINTTAB(09);"CURRENT BADGES":PRINT
2320 FORY=1TOX-1
2330 PRINTY;MID$(BA$(Y),1,10),
2340 IFY=20 GOSUB2380:CLS:PRINT:NEXT
2350 NEXT
2360 GOSUB2380
2370 RETURN
2380 PRINT@484,"PRESS <ENTER> TO CONTINUE";
2390 IF INKEY$=CHR$(13)THENPRINT@484,STRING$(25," ");:RETURN:ELSE2390
