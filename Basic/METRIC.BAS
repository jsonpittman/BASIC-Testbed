10 REM"COOKING WITH METRIC UNITS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1990"
30 CLS5:XX=RND(-TIMER):X=X+1:REM"X IS THE COUNTER"
40 IF X=11 THEN 320
50 PRINT@3,"cooking with metric units";
60 FOR T=1056 TO 1087:POKE T,204:NEXT T
70 FOR T=1120 TO 1151:POKE T,195:NEXT T
80 PRINT@64,"VOLUME (1 CUP)       WEIGHTS"
90 PRINT@128,"";
100 PRINT"BUTTER              80Z.  224G."
110 PRINT"GRANULATED SUGAR    8OZ.  224G."
120 PRINT"FLOUR(SIFTED)       4OZ.  112G."
130 PRINT"FLOUR(UNSIFTED)     6OZ.  160G."
140 PRINT"POWDERED SUGAR      4OZ.  114G."
150 A=(2+RND(6))/2
160 B=RND(5):IF B=1 THEN B$="BARRY" ELSE IF B=2 THEN B$="MARY" ELSE IF B=3 THEN B$="SONIA" ELSE IF B=4 THEN B$="SHARI" ELSE B$="ANNE"
170 R=RND(5)
180 IF R=1 THEN A$="BUTTER":Z=8:G=224
190 IF R=2 THEN A$="GRANULATED SUGAR":Z=8:G=224
200 IF R=3 THEN A$="POWDERED SUGAR":Z=4:G=114
210 IF R=4 THEN A$="UNSIFTED FLOUR":Z=6:G=160
220 IF R=5 THEN A$="SIFTED FLOUR":Z=4:G=112
230 PRINT@320,"#";X;". ";B$" NEEDS"A;"CUPS OF ":PRINTA$;" THIS TIME."
240 PRINT@416,"HOW MANY OUNCES IS THIS";:INPUT Y
250 IF Y=A*Z THEN PLAY"L8CDEGGG":J=J+5
260 IF Y<>A*Z THEN SOUND 10,2:PRINT@441,A*Z
270 PRINT"HOW MANY GRAMS IS THIS";:INPUT YY
280 IF YY=A*G THEN PLAY"CDEGGG":J=J+5
290 IF YY<>A*G THEN SOUND 10,2:PRINT@473,A*G
300 PRINT@490,"press enter";:EN$=INKEY$
310 IF EN$=CHR$(13) THEN 30 ELSE 300
320 CLS:PLAY"GFEDCCCC":PRINT"  YOUR FINAL SCORE WAS";J;"%":PRINT
330 PRINT"PRESS enter TO PLAY AGAIN OR eND";
340 EN$=INKEY$
350 IF EN$="E" THEN 360 ELSE IF EN$=CHR$(13) THEN RUN ELSE 340
360 CLS:END
