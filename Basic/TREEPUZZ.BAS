0 CLS:PRINT@69,"CHRISTMAS TREE PUZZLE":PRINT@200,"BY MIKE MOORE"
10 PMODE1:PCLS:FORX=0TO5
20 FORY=0TO5:LINE(30*X,30*Y)-(30*X+30,30*Y+30),PSET,B
30 NEXTY,X
35 SCREEN1,1
40 DIMP(5,5),T(9),C(4)
50 DRAW"BM15,177C2U6L6E6L6E6L4E4R4F4L4F6L6F6L6D6L4
60 PAINT(17,170),2,2:GET(2,152)-(28,178),T,G
70 PUT(152,2)-(178,28),T,PSET
80 FORI=0TO5:FORJ=0TO5:P(I,J)=1:NEXTJ,I
90 P(0,5)=2:P(5,0)=2
100 GOSUB730
110 S$="*PD^"+CHR$(10)+CHR$(9)+CHR$(8)+"RQ"
115 R=75:D=75
120 GOSUB720
130 ONINSTR(S$,INKEY$)GOTO130,140,150,160,170,180,190,200,210:GOTO130
140 X=(R-15)/30:Y=(D-15)/30:IFP(X,Y)<>1THENSOUND200,2:GOTO130
143 PUT(R-13,D-13)-(R+13,D+13),T,PSET
145 P=P+1:IFP<10THENP(X,Y)=2:GOSUB730:GOTO115ELSE500
150 X=(R-15)/30:Y=(D-15)/30:IFP(X,Y)<>2THENSOUND200,2:GOTO130
151 IF (X=5ANDY=0)OR(X=0ANDY=5)THENSOUND250,3:GOTO130ELSEP=P-1
153 P(X,Y)=1:FORX=0TO5:FORY=0TO5
155 IFP(X,Y)<2THENP(X,Y)=1:C=30*X+2:D=30*Y+2:LINE(C,D)-(C+26,D+26),PRESET,BF
157 NEXTY,X:GOSUB730:GOTO115
160 GOSUB720:IFD>15THEND=D-30ELSED=165
165 GOTO120
170 GOSUB720:IFD<165THEND=D+30ELSED=15
175 GOTO120
180 GOSUB720:IFR<165THENR=R+30ELSER=15
185 GOTO120
190 GOSUB720:IFR>15THENR=R-30ELSER=165
195 GOTO120
200 GOSUB220:IFA THENRUNELSESCREEN1,1:GOTO130
210 GOSUB220:IFA THENENDELSESCREEN1,1:GOTO130
220 CLS:PRINT@233,"SURE(Y/N)"
230 A=INSTR("*NY",INKEY$)-2:IFA<0THEN230ELSERETURN
500 PLAY"T20ABCDABCDABCD":CLS:PRINT@233,"WELL DONE!!"
510 FORI=0TO500:NEXT:IFINKEY$<>""THENEND
520 SCREEN1,1:FORI=0TO999:NEXT:GOTO500
720 PUT(R-2,D)-(R+6,D),T,NOT:PUT(R+2,D-4)-(R+2,D+4),T,NOT:RETURN
730 FORJ=0TO5
740 FORI=0TO5
750 IFP(I,J)=2GOSUB800
760 NEXTI,J:RETURN
770 P(X,Y)=0:C=30*X+2:D=30*Y+2
780 LINE(C,D)-(C+26,D+26),PSET,BF
790 RETURN
800 IFI=5THEN850
810 Y=J:FORX=I+1TO5
820 IFP(X,Y)<2THEN840
830 FORX=0TO5:ONP(X,Y)GOSUB770
840 NEXT
850 IFJ=5THENRETURN
860 FORU=J+1TO5
870 FORA=0TO5
880 IFP(A,U)<2THEN960
890 AB=ABS(A-I):IFAB=0ORAB=U-J THEN SU=1:SA=SGN(A-I):GOTO920
900 IFAB=2*(U-J)THEN SU=1:SA=2*SGN(A-I):GOTO920
910 IFU-J<>AB*2THEN960ELSE SU=2:SA=SGN(A-I)
920 X=I:Y=J
930 X=X+SA:Y=Y+SU:IFX>=0ANDX<6ANDY<6THENONP(X,Y)GOSUB770:GOTO930
940 X=I:Y=J
950 X=X-SA:Y=Y-SU:IFX>=0ANDX<6ANDY>=0THENONP(X,Y)GOSUB770:GOTO950
960 NEXTA,U:RETURN
970 SCREEN1,1:X=2:Y=5:U=0:GOSUB980
972 P=10:Y=4:GOSUB980:X=4:GOSUB980
974 Y=1:X=1:GOSUB980:Y=3:GOSUB980
976 X=0:GOSUB980:GOSUB730:GOTO115
980 P(X,Y)=2:PUT(30*X+2,30*Y+2)-(30*X+28,30*Y+28),T,PSET:A=X:X=Y:Y=A:U=1-U
990 IFU THEN980ELSERETURN
