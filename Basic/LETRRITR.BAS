10 GOTO 30    * LETRWRTR/BAS       14 JAN 84  * ASHALLOCK
30 PCLEAR1:POKE150,41:CLS3 '***    BAUDRATE SET TO 1200 DMP110*
40 CLEAR10000:DIML$(200)
50 DIMC(180),L(25),M(60),P(60),Q(60),R(96),Z(96)
90 GOTO2440
100 IF C>0 THEN30ELSE110
110 CLS3:PRINT@33,"** "BK$"start-up"BK$"routine"BK$" **";:PRINT@96,STRING$(32,182);:POKE1066,45
120 PRINT@162," WHEN ENTERING TEXT, ENTER ";:PRINTSTRING$(5,175);:PRINT@194,"   A PERIOD ( . ) TO EXIT  ";
130 P=0:PRINT@258,;:INPUT" ENTER LEFT MARGIN ";L:L=L-1:IFL<0 THEN L=0:PRINT@257,STRING$(5,175);
140 PRINT@258," RIGHT MARGIN MUST BE      ";:PRINTSTRING$(5,175)" EQUAL TO OR LESS THAN 96 ";
150 PRINT@354," ";:INPUT"ENTER RIGHT MARGIN ";R
160 IFR=0 THEN90ELSEIFR<L THEN150ELSE170
170 IFR>(K+L+2) THENR=0:SOUND150,5:GOTO140 ELSE180
180 R=R-1:Z=R-L:M=44:GOTO210
200 IFC=0 THEN90ELSE210
210 C=C+1:IFC=>M THEN1360
220 POKE282,0:CLS3:PRINT@128,STRING$(32,182);:PRINT@160,TAB(4);CHR$(188)"5";:PRINTTAB(9);CHR$(188)"10";:PRINTTAB(14);CHR$(188)"15";:PRINTTAB(19);CHR$(188)"20";
230 PRINTTAB(24);CHR$(188)"25";TAB(29);CHR$(188)"30";:PRINT@0,"tab"BK$"guide"BK$"and"BK$"end"BK$"of"BK$"text"BK$"marker";
240 EN=96-Z:PRINT@192,"";:PRINTTAB(Z);STRING$(EN,191);:PRINTSTRING$(32,183);
250 PRINT@357," MARGINS ARE"L+1"AND"R+1;:PRINT@417," LINE LENGTH IS"R+1-L"CHARACTERS ";
260 PRINT@480,STRING$(4,128);:PRINT@484,"enter"BK$"a"BK$"period"BK$"."BK$"to"BK$"end";:PRINTSTRING$(4,128);:POKE1535,128:POKE1523,46
270 PRINT@71," LINE NUMBER "C" ";:PRINT@192,;:LINEINPUTL$(C)
280 IFL$(C)="."THEN C=C-1:GOTO90
290 IF LEN(L$(C))>Z THEN300ELSE310
300 L$(C)="":PRINT@320," TOO LONG- TRY AGAIN ":SOUND100,10:SOUND50,10:FORD=1TO1000:NEXT:GOTO220
310 IFLEN(L$(C))=0 THENL$(C)=" "
320 GOTO210
400 IFC=0 THEN90ELSEPOKE282,0
410 CLS3:PRINT@33,"** edit"BK$"routine **";:PRINT@96,;
420 Q=0:P=0
430 Q=Q+1:P=P+1
440 PRINTQ;TAB(4);L$(Q)
450 IFQ=C THEN480 ELSE460
460 IFP=5 THEN480
470 GOTO430
480 P=0
490 YY=0:PRINT"enter"BK$"line"BK$"number"BK$"or"BK$"enter";:INPUTYY
500 IF YY>C THEN490 ELSE510
510 IFYY<=0 THEN520 ELSE540
520 IFQ=C ANDP=0 THEN90ELSE530
530 CLS3:GOTO470
540 EE=0:EE=YY:YY=0:CLS3
550 PRINT@97," OLD LINE: ";:PRINTL$(EE):L$(EF)=L$(EE):L$(EE)=""
560 PRINT@257,;::POKE282,0:PRINT" NEW LINE: ";:PRINTSTRING$(Z,".");:PRINT@268,;:LINEINPUTL$(EE)
570 IFL$(EE)="" THENL$(EE)=L$(LF):GOTO410
580 IFL$(EE)="."THEN L$(EE)="":EE=EE-1:C=C-1
590 IF LEN(L$(EE))>Z THENPRINT" TOO LONG - TRY AGAIN":L$(EE)="":GOTO560
600 GOTO410
690 PRINT@480,BK$"touch"BK$"any"BK$"other"BK$"key"BK$"to"BK$"return"BK$;:POKE1535,128:RETURN
700 POKE282,255:CLOSE
710 CLS4:PRINTSTRING$(64,182);:PRINT@97," DID YOU SAVE YOUR LAST PAGE? ";:PRINT@160,STRING$(64,182);:PRINT@257," TERMINATE ?...(y"BK$"to"BK$"confirm)"BK$;
720 POKE1296,40:POKE1309,41:GOSUB690
730 Q$=INKEY$:IFQ$=""THEN730
740 IFQ$="Y" ORQ$="y"THEN790
750 IFQ$<>"Y" ORQ$<>"y"THEN90
790 CLS3:PRINT"THE LETTER WRITER PROGRAM IS","TERMINATED   (C)1984 ASHALLOCK","TYPE "CHR$(34)"CONT"CHR$(34)" TO CONTINUE WITHOUT LOSS OF DATA ";:END
800 IFC=0 THEN2440ELSE810
810 CLS4:PRINT@39,"** save"BK$"routine **";:GOSUB2700:FORD=1TO1000:NEXT
820 IF DV=-1 THENPRINT@98," * * * ready"BK$"cassette * * * ";ELSEPRINT@98," * * * touch"BK$"S"BK$"to"BK$"save * * *";:GOTO920
830 PRINT@227," [space] TURNS MOTOR OFF ";
840 PRINT@291," [enter] TURNS MOTOR ON ";
850 PRINT@358," TOUCH [s] TO SAVE ";:GOSUB690
860 I$=INKEY$:IFI$=""THEN860
870 IFI$=" "THEN910
880 IFI$=CHR$(13)THEN900
890 IFI$="S" ORI$="s"THEN920 ELSE90
900 PRINT@424," MOTOR IS on  ";:AUDIOON:MOTORON:GOTO860
910 PRINT@424," MOTOR IS off ";:AUDIOOFF:MOTOROFF:GOTO860
920 CLS3:PRINT@97," NAME MUST BE BETWEEN 1 AND 8 ";:PRINT@129," CHARACTERS LONG only ";
930 NE$="":PRINT@225," ENTER NAME YOU WISH TO SAVE ";:PRINT@257,;:LINEINPUT" THIS FILE AS: ";NE$
940 IF LEN(NE$)=0 THEN2440
950 IF LEN(NE$)>8 THEN810ELSE960
960 PRINT@321," SAVING TO "TD$" AS ";NE$" ";STRING$(5,175);
970 IF DV=-1 THENMOTORON:FORD=1TO300:NEXT:MOTOROFF
980 OPEN"O",#DV,NE$
990 PRINT#DV,C,L,R,Z,M
1000 FOR RR=1 TOC
1010 PRINT#DV,L$(RR)
1020 NEXT
1030 CLOSE
1040 PRINT@321," FILE IS SAVED AS "NE$" ";STRING$(5,175);
1050 IF DV=-1 THENMOTORON:FORD=1TO100:NEXT:MOTOROFF
1060 GOTO2440
1070 GOTO2440
1100 IF C>0 THEN30ELSE1110
1110 CLS3:PRINT@38,"**  load"BK$"routine  **";:GOSUB2700:FORD=1TO1000:NEXT
1120 IF DV=-1 THENPRINT@98," * * * ready"BK$"cassette * * * ";ELSEIF DV=1THEN1220
1130 PRINT@227," [space] TURNS MOTOR OFF ";
1140 PRINT@291," [enter] TURNS MOTOR ON ";
1150 PRINT@358," TOUCH [l] TO LOAD ";:GOSUB690
1160 I$=INKEY$:IFI$=""THEN1160
1170 IFI$=" "THEN 1210
1180 IFI$=CHR$(13)THEN1200
1190 IFI$="L" ORI$="l"THEN1220 ELSE90
1200 PRINT@424," MOTOR IS on  ";:AUDIOON:MOTORON:GOTO1160
1210 PRINT@424," MOTOR IS off ";:AUDIOOFF:MOTOROFF:GOTO1160
1220 CLS3:GOSUB2700:PRINT@1," SYSTEM SET TO "TD$" ";
1230 '* SAVE ROUTINE
1240 NE$="":PRINT@225,;:INPUT" ENTER NAME OF FILE ";NE$
1250 IF LEN(NE$)=0 THEN90 ELSEIFNE$="DIR" THEN2600 ELSE1260
1260 IF LEN(NE$)>8 THEN1240 ELSE1270
1270 OPEN"I",#DV,NE$
1280 INPUT#DV,C,L,R,Z,M
1290 PRINT:PRINT" LEFT MARGIN SET AT ";L+1
1300 PRINT:PRINT" RIGHT MARGIN SET AT ";R+1
1310 PRINT:PRINT" PAGE LENGTH SET AT ";M
1320 FOR RR=1TO C
1330 IF EOF(DV) THEN90
1340 LINEINPUT#DV,L$(RR)
1350 NEXT:CLOSE:GOTO2440
1360 CLS3:PRINT@33," YOU HAVE REACHED THE LIMIT ";:PRINT@65," OF YOUR PAGE LENGTH ";
1370 PRINT@129," DO YOU WANT TO INCREASE THE ";:PRINT@161," LENGTH ( y OR ANY OTHER KEY)";
1380 Y$=INKEY$:IFY$=""THEN1380ELSE1390
1390 IFY$="Y"OR Y$="y"THEN1400ELSE1430
1400 PRINT@225,;:MM=0:INPUT" ENTER NEW PAGE LENGTH ";MM
1410 IFMM <M THEN1400 ELSE1420
1420 M=MM:GOSUB690
1430 IFINKEY$=""THEN1430 ELSE90
1490 '* PRINT FANFOLD ENVELOPE    ALL PRINTER CODES FOR DMP110
1500 CLS4:PRINT@130," READY PRINTER PAPER ";:GOSUB1620:PRINT@480," ANY KEY WHEN READY ";:EXEC&HA171:PRINT#-2,STRING$(18,10):PRINT#-2,CHR$(27);CHR$(17);
1510 PRINT#-2,"Arthur S. Hallock":PRINT#-2,"Route 1, Box 198HHH":PRINT#-2,"Deming, NM  88030"
1520 PRINT#-2,STRING$(12,10)
1530 CLS4:PRINT@130," ENTER ADDRESSEE: ":PRINT@224," NAME: ";:PRINT@256," ADDR: ";:PRINT@288," CTST: ";:PRINT@320," ZIP : ";:PRINT@224,;
1540 POKE282,0:LINEINPUT" NAME: ";A$
1550 LINEINPUT" ADDR: ";B$
1560 LINEINPUT" CTST: ";C$
1570 LINEINPUT" ZIP : ";D$:POKE282,255
1580 PRINT#-2,TAB(45)A$:PRINT#-2,TAB(45)B$:PRINT#-2,TAB(45)C$
1590 PRINT#-2,CHR$(27);CHR$(14);:PRINT#-2,TAB(48)D$:PRINT#-2,CHR$(27);CHR$(15);
1600 PRINT#-2,CHR$(27);CHR$(19);:PRINT#-2,CHR$(12):GOTO90
1610 '* PRINTER TEST ROUTINE
1620 IF(PEEK(&HFF22)AND1)THEN1630ELSERETURN
1630 SOUND200,3:PRINT@483,"*** PRINTER NOT READY *** ";:FORD=1TO1000:NEXT:GOTO90
1700 IFC=0 THEN90ELSE1720
1710 '* CHECK PRINTER OPTIONS
1720 CLS3:PRINT@33,"**  hardcopy"BK$"routine  **";
1730 IF(PEEK(&HFF22)AND1)THENSOUND150,5:PRINT@129," printer IS not TURNED on ";:PRINT@225," PLEASE CHECK, THEN TOUCH ";:PRINT@257,;:PRINT" [SPACE] ENTER FOR MENU   ";:PRINT@289,;:LINEINPUT" ENTER TO CONTINUE  ";K$:IFK$=" "THEN90ELSE1720
1740 PRINT@97," DO YOU NEED A FORM FEED ? ";:PRINT@129,"  (y OR TOUCH ANY KEY ) ";
1750 Y$=INKEY$:IFY$=""THEN1750
1760 IFY$="Y"OR Y$="y"THEN 1770ELSE1780
1770 PRINT#-2,CHR$(12)
1780 GOSUB1900:CLS3
1790 CP=0:PRINT@352,STRING$(2,128)"enter"CHR$(128)"zero"CHR$(128)"to"CHR$(128)"return"CHR$(128)"to"CHR$(128)"menu"STRING$(2,128);:PRINT@417,"";
1800 INPUT" NUMBER OF COPIES NEEDED ";CP:IFCP=0 THEN90
1810 FORZZ=1TO CP
1820 FORE=1TO C
1830 PRINT#-2,TAB(L);L$(E)
1840 IF E=44 ORE=95 THENPRINT#-2,CHR$(12):ELSE1860
1850 IFST$="Y"THENPRINT@481," TOUCH [ENTER] TO CONTINUE ";ELSE1860
1860 NEXT
1870 E=0:NEXT
1880 E=0:PRINT#-2,CHR$(12);:GOTO90
1900 CLS3:PRINT@33,"** LETTERHEAD AND  ";:PRINT@67," SALUTATION ROUTINE **";
1910 PRINT@129," SELECT LETTERHEAD ";
1920 PRINT@195,"[a]RTHUR S. HALLOCK ";
1922 PRINT@227,"[t]EXT ONLY         ";
1930 I$=INKEY$:IFI$=""THEN 1930
1940 IFI$="A" ORI$="a" THEN1960
1942 IFI$="T" ORI$="t" THEN2180
1950 IFI$="" THENRETURNELSE1940
1960 CLS3:PRINT@33," LETTERHEAD FOR ... ASHALLOCK ";
1970 POKE282,0
1980 PRINT@97," ";:LINEINPUT"DATE ";D$
1990 PRINT@225," SALUTATION OR HEADING ";:PRINT@289,"";:LINEINPUTSA$
2000 POKE282,255
2010 LH$="Arthur S. Hallock"
2020 AD$="Route 1, Box 198HHH     Deming, New Mexico  88030"
2030 GOTO2040
2040 PRINT#-2:PRINT#-2
2050 PRINT#-2,CHR$(27);CHR$(66);CHR$(27);CHR$(14);
2060 PRINT#-2,TAB(8);LH$
2070 PRINT#-2,CHR$(27);CHR$(15);
2080 PRINT#-2,CHR$(27);CHR$(29);
2090 PRINT#-2,TAB(44);D$
2100 PRINT#-2,CHR$(27);CHR$(66)
2110 PRINT#-2,TAB(12)AD$
2120 PRINT#-2,CHR$(27);CHR$(29);
2130 PRINT#-2,STRING$(95,167)
2140 PRINT#-2:PRINT#-2
2150 PRINT#-2,CHR$(27);CHR$(29)
2160 PRINT#-2,TAB(L+1);SA$:PRINT#-2
2170 RETURN
2180 CLS:PRINT@33,"** TEXT ENTRY ROUTINE **"
2190 POKE282,0
2200 PRINT@128," ";:INPUT"DATE ";D$
2210 PRINT@192," ";:INPUT"PAGE NUMBER ";P$
2220 PRINT@256," ";:LINEINPUT"HEADER (96 CHARACTERS, MAXIMUM)";HE$
2230 PRINT:PRINT" ";:INPUT"NUMBER OF LINES OF SPACE FROM   THE POP OF PAGE ";N
2240 PRINT:PRINT" ";:LINEINPUT"TITLE OF ARTICLE OR PAGE       ";TT$
2250 TT=LEN(TT$):TU=96-TT:TA=TU/6
2260 POKE282,255
2270 PRINT#-2,STRING$(N,10)
2280 PRINT#-2,CHR$(27);CHR$(20);
2290 PRINT#-2,D$;TAB(80)"Page "P$
2300 PRINT#-2,CHR$(27);CHR$(29)
2310 PRINT#-2,HE$
2320 PRINT#-2,STRING$(96,167)
2330 PRINT#-2:PRINT#-2
2340 PRINT#-2,CHR$(27);CHR$(14);
2350 PRINT#-2,TAB(TA);TT$
2360 PRINT#-2,CHR$(27);CHR$(29);CHR$(27);CHR$(15);CHR$(27);CHR$(17);
2370 RETURN
2440 K=95:POKE282,255:BK$=CHR$(128):CLS3
2450 PRINTBK$"letter"BK$"writer"BK$"program"BK$"in"BK$"basic"BK$;
2460 PRINT@71,"** "BK$"main"BK$"menu"BK$" **";
2470 PRINT@129,"c";:PRINT@132," COMMENCE  A  NEW  LETTER ";
2480 PRINT@161,"a";:PRINT@164," ADD  TO  EXISTING LETTER ";
2490 PRINT@193,"e";:PRINT@196," EDIT, INQUIRE, VIEW PAGE ";
2500 PRINT@225,"l";:PRINT@228," LOAD PAGE - TAPE OR DISK ";
2510 PRINT@257,"h";:PRINT@260," HARDCOPY ON LINE PRINTER ";
2520 PRINT@289,"s";:PRINT@292," SAVE PAGE - TAPE OR DISK ";
2530 PRINT@321,"x";:PRINT@324," EXIT - TERMINATE PROGRAM ";
2540 PRINT@353,"p";:PRINT@356," PRINT : FANFOLD ENVELOPE ";
2550 PRINT@416,"  NOTE: "BK$"save"BK$"page"BK$"if"BK$"you"BK$"have"BK$BK$BK$BK$"made"BK$"any"BK$"changes"BK$"since"BK$"entry"BK$BK$;
2560 PRINT@486," touch MENU choice ";
2570 M$=INKEY$:IFM$=""THEN2570
2580 ON INSTR("PWACNELHSQXTpwacnelhsqxt",M$)GOTO1500,100,200,100,100,400,1100,1700,800,700,700,700,1500,100,200,100,100,400,1100,1700,800,700,700,700
2590 GOTO2570
2600 DIR
2610 IFINKEY$=""THEN2610 ELSE1220
2700 TD$="":IFPEEK(188)=14 THENTD$="DISK" ELSETD$="TAPE"
2710 IF TD$="TAPE" THENDV=-1 ELSE IFTD$="DISK" THENDV=1
2720 RETURN
9999 SV$="LETRWRTR":FORC=1TO3:CLS3:PRINT@96," SAVING "SV$" TO TAPE:"C;:CSAVESV$:MOTORON:FORD=1TO3000:NEXT:NEXT:FORD=1TO5000:NEXT:MOTOROFF:CLS3:PRINT@96," "SV$" IS SAVED"C-1"TIMES";:PRINT@416,;:END
