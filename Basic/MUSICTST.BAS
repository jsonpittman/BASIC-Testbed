1 'MUSIC TEST
2 'WRITTEN BY PERRY FRIESEN
3 'COPYRIGHT (C) APRIL 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 HBUFF 1,300
20 POKE65497,0
30 A$="C4;BM34,105;D2F2R2E2U51E2R2F2D4GDGDGDGDGDGDGDGDGDGDGDGDGD2GD4FDFDFRFRFR4EREREUEUEU4HUHUHLHL4GLGLGDGD4F
40 B$="C4;BM35,132;LH2U4EUERERER2FRFRF3DFD6GD2GD2GDGDGDGDGLDLDLDBU27BR14RDLUBD10RDLU
50 C$="C7;D2FU4ED5FU7RD7RU7ED8RU8RD7RU7FD6EU4D4EU30
60 HSCREEN 2:HCLS2:POKE&HFF9A,11
70 A=50:NC=0:TT=10:C=0:TM=300
80 '***DRAW PLAY SCREEN***
90 HCOLOR8:HPRINT(7,0),"** M U S I C   T E S T **":HCOLOR4
100 FOR I=1TO5
110 A=A+10:HLINE(30,A)-(290,A),PSET
120 NEXT I
130 A=A+10
140 FOR I=1TO5
150 A=A+10:HLINE(30,A)-(290,A),PSET
160 NEXT I
170 HLINE(30,60)-(30,160),PSET:HLINE(287,60)-(287,160),PSET:HLINE(290,60)-(290,160),PSET
180 HDRAW A$+B$:POKE65496,0
190 '***MAIN LOOP***
200 C=C+1:TIMER=0:N=RND(25):V=N*5+45
210 FORI=1TO25
220 READ NN,P$,II$
230 IFNN=N THEN250
240 NEXT I
250 RESTORE:HCOLOR4,2:HLINE(30,180)-(290,192),PRESET,BF:HGET(160,V-30)-(175,V+5),1:HDRAW "BM160,"+STR$(V)+";"+C$
260 PLAYP$
270 I$=INKEY$:IFTIMER>TM THEN380ELSEIFI$=""THEN270
280 HCOLOR4:IFI$<"A"ORI$>"G"THENHPRINT(10,24),"That's not a note!":GOTO390
290 IF I$=II$ THENGOSUB420ELSE380
300 HPUT(160,V-30)-(175,V+5),1,PSET:IFC<>TT THEN200
310 NC$=STR$(NC):NW$=STR$(TT-NC):SC$=STR$(NC/TT*100)
320 HCOLOR0:HPRINT(2,2),"Number correct:"+NC$
330 HPRINT(22,2),"Number wrong:"+NW$
340 HPRINT(12,4),"Your score:"+SC$+"%"
350 HPRINT(2,21),"** PRESS <CLEAR> FOR ANOTHER TEST **"
360 I$=INKEY$:IFI$=""THEN360
370 IFI$=CHR$(12)THEN20ELSEEND
380 HCOLOR4:HPRINT(12,24),"Right answer is "+II$
390 PLAY"L8O3FDFD":GOTO300
400 DATA 1,O4A,A,2,O4G,G,3,O4F,F,4,O4E,E,5,O4D,D,6,O4C,C,7,O3B,B,8,O3A,A,9,O3G,G,10,O3F,F,11,O3E,E,12,O3D,D,13,O3C,C,14,O2B,B,15,O2A,A
410 DATA 16,O2G,G,17,O2F,F,18,O2E,E,19,O2D,D,20,O2C,C,21,O1B,B,22,O1A,A,23,O1G,G,24,O1F,F,25,O1E,E
420 HCOLOR4:HPRINT(12,24),I$+" is correct":NC=NC+1:PLAY"L10O3CEGO4CO3GO4L4C":RETURN
