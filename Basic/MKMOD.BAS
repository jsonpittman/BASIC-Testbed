100 'MKMOD.BAS
105 '
110 CLEAR 1000: DIM S$(2)
115 ON ERR GOTO 435: WIDTH 32
120 PRINT "MKMOD V1": PRINT
125 READ F$: PRINT "MAKING "+F$
130 BC=0
135 READ B$
140 IF B$="*" THEN 160
145 IF B$="@" THEN 155
150 BC=BC+1: GOTO 135
155 READ NZ: BC=BC+NZ: GOTO 135
160 OPEN "D",#1,F$+".MOD",1
165 FIELD #1,1 AS A$
170 LSET A$=CHR$(0)
175 PUT #1
180 LSET A$=CHR$(BC/256)
185 PUT #1
190 LSET A$=CHR$(BC AND 255)
195 PUT #1
200 LSET A$=CHR$(0)
205 PUT #1
210 PUT #1
215 RESTORE
220 READ F$
225 FOR I=1 TO BC
230 READ B$
235 IF B$<>"@" THEN 270
240 LSET A$=CHR$(0)
245 READ NZ
250 FOR J=1 TO NZ
255 PUT #1
260 NEXT J
265 I=I+NZ-1: GOTO 280
270 LSET A$=CHR$(VAL("&H"+B$))
275 PUT #1
280 NEXT I
285 LSET A$=CHR$(255)
290 PUT #1
295 LSET A$=CHR$(0)
300 FOR I=1 TO 4: PUT #1: NEXT I
305 CLOSE #1
310 '
315 'UPDATE DIRECTORY
320 '
325 F$=F$+STRING$(8-LEN(F$),32)
330 F$=F$+"MOD"
335 FOR S=3 TO 11
340 DSKI$ 0,17,S,S$(0),S$(1)
345 FOR I=0 TO 1
350 GOSUB 375
355 NEXT I,S
360 F$=LEFT$(F$,8)
365 PRINT F$+" CREATED"
370 END
375 FOR E=0 TO 3
380 F2$=MID$(S$(I),32*E+1,11)
385 IF F$=F2$ THEN GOSUB 395
390 NEXT E: RETURN
395 C2$=CHR$(2): C0$=CHR$(0)
400 MID$(S$(I),32*E+12,1)=C2$
405 MID$(S$(I),32*E+13,1)=C0$
410 DSKO$ 0,17,S,S$(0),S$(1)
415 E=4: I=2: S=12: RETURN
420 '
425 'ERROR HANDLER
430 '
435 IF ERNO=3 THEN 450
440 PRINT "FILE ERROR IN"; ERLIN
445 UNLOAD: END
450 PRINT "OUT OF DATA"
