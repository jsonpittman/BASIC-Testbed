1 'TRIANGLE
2 'BY TOM BESHARA
3 'COPYRIGHT (C) 1992
4 'FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 HCOLOR 0,14
20 HSCREEN2
30 F=20:P=176:R=300:T=176:L=160:I=12'THE X,Y COORDINATES OF A, B, C
40 HSET(F,P)'=POINT A
50 HSET(R,T)'=POINT B
60 HSET(L,I)'=POINT C
70 X=RND(300)
80 Y=RND(176)
90 HSET(X,Y)'=FIRST RANDOM POINTT R
100 Q=RND(6)'=THE DICE
110 IF Q=1 OR Q=2 THEN GOTO 160'=POINT A
120 IF Q=3 OR Q=4 THEN GOTO 170'=POINT B
130 IF Q=5 OR Q=6 THEN GOTO 180'=POINT C
140 GOTO 100
150 '***CONVERT THE X,Y COORDINATES OF A, B, C TO A COMMON VARIABLE
160 D=F:S=P:GOTO190
170 D=R:S=T:GOTO190
180 D=L:S=I
190 IFX<D THEN XX=D-X:GOTO210
200 IFX>D THEN XX=X-D:GOTO230
210 XX=XX/2
220 X=X+XX:GOTO 250
230 XX=XX/2
240 X=D+XX
250 X=INT(X)
260 IF Y<S THEN YY=S-Y:GOTO280
270 IF Y>S THEN YY=Y-S:GOTO300
280 YY=YY/2
290 Y=Y+YY:GOTO 320
300 YY=YY/2
310 Y=S+YY
320 Y=INT(Y)
330 HSET(X,Y)'=NEW MEASURING POINT R
340 GOTO 100
