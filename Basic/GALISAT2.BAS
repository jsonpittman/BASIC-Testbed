0 ' COPYRIGHT 1989 FALSOFT, INC
2 ' ############################
4 ' #                          #
6 ' # GALISAT 2                #
8 ' #                          #
10 '# COCO 2 VER. BY DAVID     #
12 '# FRANCIS. DEFAULT PLOT:   #
14 '# EAST=RIGHT, WEST=LEFT.   #
16 '# CHG. L TO -1 IN LINE     #
18 '# 6002 TO FLIP.            #
20 '#                          #
22 '############################
24 CLS:PRINTSTRING$(32,128):PRINT@11,"galisat";:POKE1043,50:PRINT@73,"COCO 2 VERSION":PRINTTAB(8)"BY DAVID FRANCIS":PRINT:PRINT"  PLOTS POSITIONS OF JUPITER'S    GALILEAN SATELLITES
28 GOSUB6000 ' INITIALIZE
30 GOSUB2000 ' GET DATE
32 POKE65495,0
34 GOSUB5000 ' SETUP PLOT SCREEN
35 GOSUB1000 ' MAIN LOOP
36 ' ===========================
38 ' MAIN PROGRAM LOOP
40 ' ===========================
1000 DL=INT((N-INT(N))*20+.5)/20:MT=(358.476+.9856003*N)*P:MJ=(225.328+.0830853*N)*P:JJ=221.647+.9025179*N
1002 VT=1.92*SIN(MT)+.02*SIN(2*MT):VJ=5.55*SIN(MJ)+.17*SIN(2*MJ): K=(JJ+VT-VJ)*P:DT=SQR(28.07-10.406*COS(K))
1004 Z=SIN(K)/DT:I=ATN(Z/SQR(1-Z*Z)):I=I/P:F=(N-DT/173):F1=I-VJ
1006 U1=84.5506+203.405863*F+F1:U2=41.5015+101.2916323*F+F1:U3=109.9770+50.2345169*F+F1:U4=176.3586+21.4879802*F+F1
1008 X1=5.906*SIN(U1*P+PI):X1=139+L*INT(X1*3.5+.5):X2=9.397*SIN(U2*P+PI):X2=139+L*INT(X2*3.5+.5)
1010 X3=14.989*SIN(U3*P+PI):X3=139+L*INT(X3*3.5+.5):X4=26.364*SIN(U4*P+PI):X4=139+L*INT(X4*3.5+.5)
1012 IFINKEY$=CHR$(12) THENPOKE65494,0:RUN
1016 PSET(X1,V):PSET(X2,V):PSET(X3,V):PSET(X4,V)
1018 V=V+1:N=N+.05:IFV=191 THEN1020 ELSE1000
1020 IFINKEY$=CHR$(12) THENPOKE65494,0:RUN ELSE1020
1022 ' =========================
1024 ' GET DATE & CONVERT IT TO
1026 ' JULIAN DAY NO.
1028 ' =========================
2000 PRINT:INPUT" ENTER YEAR (YYYY)";Y:INPUT" ENTER MONTH  (MM)";M:INPUT" ENTER DAY    (DD)";D
2002 GOSUB3000 ' DATE VALID?
2004 D=INT(D):G=1
2006 D1=INT(D):F=D-D1-.5
2008 J=-INT(7*(INT((M+9)/12)+Y)/4)
2010 IF G=0 THEN2018
2012 S=SGN(M-9):A=ABS(M-9)
2014 J1=INT(Y+S*INT(A/7))
2016 J1=-INT((INT(J1/100)+1)*3/4)
2018 J=J+INT(275*M/9)+D1+G*J1
2020 J=J+1721027+2*G+367*Y
2022 IF F>=0 THEN2026
2024 F=F+1:J=J-1
2026 N=J-2415020+F
2028 RETURN
2030 ' =========================
2032 ' CHECK FOR VALID DATE
2034 ' =========================
3000 IFY<0 OR Y>99999 THENPRINT:PRINT" YEAR MUST BE 0 - 99999.":GOTO3008
3002 IFD<1 OR D>31 THENPRINT:PRINT" DAY IS INVALID.":GOTO3008
3004 IF M<1 OR M>12 THENPRINT:PRINT" MONTH IS INVALID.":GOTO3008
3006 RETURN
3008 PRINT:PRINT" PRESS [ENTER] TO RE-ENTER DATA
3010 I$=INKEY$
3012 I$=INKEY$:IFI$="" THEN3012
3014 IFI$<>CHR$(13) THEN3012
3016 POKE65494,0:RUN
4002 ' =========================
4004 ' SETUP PLOT SCREEN
4006 ' =========================
5000 PMODE4,1:PCLS:SCREEN1,1
5001 IFL=1 THENDRAW"BM249,180"+EAST$:DRAW"BM2,180"+WEST$ ELSEDRAW"BM2,180"+EAST$:DRAW"BM249,180"+WEST$
5002 LINE(136,0)-(136,191),PSET:LINE(142,0)-(142,191),PSET
5004 FORZZ=2 TO 191 STEP20:LINE(0,ZZ)-(15,ZZ),PSET:LINE(241,ZZ)-(256,ZZ),PSET:NEXT
5012 RETURN
5014 ' =========================
5016 ' INITIALIZE
5018 ' =========================
6000 PI=3.14159265:P=PI/180:V=0:V1=180
6002 L=1
6006 EAST$="U6R4L4D3R2L2D3R4":WEST$="U6D6E2U1D1F2U6
6008 RETURN
