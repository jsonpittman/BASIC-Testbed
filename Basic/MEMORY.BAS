10 CLS:GOTO30
20 K$=INKEY$:IFK$=""THEN20ELSERETURN
30 PRINT@40,"<<<MEMORY>>>"
40 PRINT@96,"BY KEIRAN KENNY, THE HAGUE, 1987"
50 PRINT@192,"SET DIFFICIULTY LEVEL:"
60 PRINT@260,"";:INPUT"NO. OF ROWS (2-10):";NR
70 IFNR<2ORNR>10THENPRINT@256,"":GOTO60
80 PRINT@324,"";:INPUT"NO. OF LETTERS (2-6):";NL
90 IFNL<2ORNL>6THENPRINT@324,"":GOTO80
100 PRINT:PRINTTAB(6)"PRESS ANY KEY.":GOSUB20
110 CLS
120 P=34
130 FORN=1TONR
140 FORT=1TONL
150 R=64+RND(26)
160 A$=CHR$(R)
170 PRINT@P,A$;
180 B$=B$+A$
190 P=P+1
200 NEXT
210 C$(N)=B$:B$=""
220 P=P+32-NL
230 NEXT
240 PL=PL+NR*NL
250 K$=INKEY$
260 P=32:PP=P*(NR+2)+2
270 PRINT@PP,"WHEN READY, PRESS ANY KEY.":GOSUB20
280 N=RND(NR)
290 IP=P*N:PRINT@IP,""
300 PRINT@PP,"<ENTER> THE MISSING ROW."
310 PRINT@IP,"";:INPUTD$
320 PRINT@IP+NL+3,"";:IFD$=C$(N)THENPRINT"RIGHT!":RT=RT+NR*NL ELSEPRINT"WRONG! IT WAS "CHR$(34)C$(N)CHR$(34)
330 PRINT@PP,"SCORE:"RT;CHR$(8)"! POSSIBLE:"PL;CHR$(8)"."
340 SC=SC+1:IFSC/5=INT(SC/5)THENPRINT@PP+64,"CHANGE DIFFICULTY LEVEL? Y/N"ELSE390
350 GOSUB20
360 IFK$="Y"THENCLS:GOTO50
370 IFK$="N"THENCLS:GOTO120
380 GOTO350
390 PRINT@PP+70,"PRESS ANY KEY.":GOSUB20
400 CLS:GOTO120
