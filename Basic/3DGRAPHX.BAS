0 'COPYRIGHT 1989  FALSOFT,INC
1 GOTO 224
2 W=0:CLS(3):PRINT@39,"*** 3-D MENU ***";
3 PLAY"V25"
4 FORSX=1TO20:PLAY"V-O4L255T255CE-G":NEXTSX
5 PLAY"V15"
6 PRINT@131,"1 - TEST SCREEN          ";:PRINT@163,"2 - FOREGROUND CIRCLE    ";:PRINT@195,"3 - JUMPING CIRCLE       ";:PRINT@227,"4 - ATOM SMASHER         ";
7 PRINT@259,"5 - YELLING FACE         ";:PRINT@291,"6 - STARBURST            ";:PRINT@323,"7 - DIMENSIONAL SINELINES";:PRINT@355,"8 - DO IT YOURSELF       ";
8 PRINT@417,">>PRESS NUMBER FOR SELECTION<<";:PRINT@450,"**RETURN TO MENU WITH [M]**";:PRINT@480,CHR$(143+32);
9 W$=INKEY$
10 W=VAL(W$)
11 IF W=>1 AND W=<8 THEN GOTO13
12 GOTO9
13 FORWN=1TO20:CLS(7):PRINT@198,"PUT ON 3-D GLASSES";:PRINT@301,"now";:PLAY"T255L255O4FF#G":FORPP=1TO100:NEXTPP
14 CLS(8):PLAY"O1DD#E":FORPP=1TO50:NEXTPP,WN
15 ON W GOTO16,28,50,73,94,138,185,202
16 CLS(4):PRINT @38,"3-D- TEST SCREEN";:PRINT@130,"ADJUST THE FOLLOWING SCREEN     TO MATCH THE COLORS OF YOUR     3-D GLASSES. THE RIGHT BLUE     SQUARE SHOULD APPEAR TO BE IN   FRONT OF THE RED BACKGROUND --  OPPOSITE ON THE LEFT SIDE."
17 PRINT@384,"PRESS ANY KEY WHEN READY        >[M] RETURNS YOU TO THE MENU"
18 '**TEST SCREEN***
19 PMODE3,1:PCLS(3)
20 COLOR5:LINE(125,0)-(125,196),PSET
21 PAINT(200,10),4,5
22 COLOR4:LINE(30,50)-(125,120),PSET,BF
23 COLOR3:LINE-(220,50),PSET,BF
24 EXEC44539
25 W$=INKEY$:IF W$="M" THEN GOTO2
26 PMODE3,1:SCREEN1,1:GOTO25
27 '**CIRCLE & LINES***
28 PMODE3,1:PCLS(5):SCREEN1,1
29 FORX=10TO250STEP5
30 COLOR3
31 LINE(125,10)-(X,30),PSET
32 COLOR4
33 LINE-(X,170),PSET
34 COLOR3
35 LINE-(125,10),PSET
36 NEXTX
37 CIRCLE(125,100),71,2
38 PLAY"O2L255T255CAFDFAG"
39 CIRCLE(125,100),70,2:PAINT(125,100),3,2
40 FORX=1TO60STEP8:CIRCLE(125,100),X,4:SOUND X,1:NEXTX
41 DRAW"BM70,70;S6C5;R25D15L10D5R10D15L25U7R10U4L6U13R6U4L10U7"
42 PAINT(75,75),4,5
43 DRAW"BM137,71;S5;R23F5D31G5L23U40BF9R8F2D23G2L8U26"
44 PAINT(142,72),4,5
45 DRAW"BM115,90;S5;R9D5L9U5"
46 PAINT(117,92),4,5
47 PLAY"O5L255T255BB-AA-GG-FE"
48 W$=INKEY$:IF W$="M" THEN GOTO2 ELSE48
49 '** JUMPING CIRCLE**
50 G=10
51 FORP=1TO8STEP2
52 PMODE1,P:PCLS(4):SCREEN1,1
53 FORX=1TO115STEP5
54 SOUND X,1
55 Y=Y+1
56 COLOR(2+Y)
57 LINE(10+X,10+(X/1.4))-(240-X,180-(X/1.4)),PSET,BF
58 COLOR5:LINE(10+X,10+(X/1.4))-(240-X,180-(X/1.4)),PSET,B
59 IFY=>3THEN Y=0
60 NEXTX
61 COLOR5:LINE(0,0)-(255,195),PSET:LINE(255,0)-(0,195),PSET
62 CIRCLE(125,95),G,2:PAINT(125,95),2,2
63 CIRCLE(125,95),G-1,4:PAINT(125,95),4,4
64 G=G+35
65 NEXTP
66 FORSH=1TO8STEP2
67 PMODE1,SH:SCREEN1,1
68 FORPZ=1TO95:NEXTPZ:PLAY"O5T255L255B-"
69 NEXTSH
70 W$=INKEY$:IF W$="M" THEN GOTO2 ELSE71
71 GOTO66
72 '**ATOM SMASHER**
73 FORP=1TO8STEP2:PMODE1,P:PCLS(4):NEXTP
74 PMODE1,1:SCREEN1,1
75 SOUND 1,1
76 COLOR2:LINE(255,0)-(0,195),PSET:LINE(0,0)-(255,195),PSET
77 FORS=1TO125
78 GOSUB88
79 CIRCLE(125,95),S,3
80 NEXTS
81 FORP=1TO8STEP2
82 PMODE1,P:SCREEN1,1
83 W$=INKEY$:IF W$="M" THEN GOTO2
84 FORPP=1TO80:NEXTPP:PLAY"O1L255T255CC#D"
85 NEXTP
86 PLAY"L255T255O5B"
87 GOTO81
88 S$="S"+STR$(S)+";"
89 IF S=20THEN PMODE1,3:SCREEN1,1:SOUND S,1
90 IF S=50THEN PMODE1,5:SCREEN1,1:SOUND S,1
91 IF S=80THEN PMODE1,7:SCREEN1,1:SOUND S,1
92 RETURN
93 '**YELLING FACE**
94 PMODE1:PCLS(2):SCREEN1,1
95 CIRCLE(195,70),20,4,1.3,.4,.3
96 CIRCLE(55,70),20,4,1.3,.1,.9
97 CIRCLE(125,85),80,4
98 PAINT(125,85),4,4
99 CIRCLE(90,50),20,3
100 CIRCLE(160,50),20,3
101 PAINT(90,50),3,3
102 PAINT(160,60),3,3
103 FORX=1TO7
104 CIRCLE(160,60),X,2
105 CIRCLE(90,60),X,2
106 NEXTX
107 DRAW"BM120,80;C3;S4;D10R15U10"
108 CIRCLE(127,95),15,3,.4
109 PAINT(127,95),3,3
110 CIRCLE(127,135),45,2,.4
111 PAINT(127,135),2,2
112 PAINT(10,10),5,4
113 PLAY"V1":FORSX=1TO30:PLAY"L255T255O2V+FF#GG#":NEXTSX:PLAY"V15"
114 DRAW"BM127,135"
115 FORS=1TO20STEP3
116 S$="S"+STR$(S)+";"
117 SOUND S,1
118 DRAW S$+"C3;BM-4,+1;L10U2R6U2L6U4R6U2L6U2R10D6L2D1R2D5"
119 FORPP=1TO150:NEXTPP
120 NEXTS
121 DRAW"BM127,135"
122 FORS=1TO20STEP3
123 SOUND S+20,1
124 S$="S"+STR$(S)+";"
125 DRAW S$+"C3;BM+2,+4;R8E2U8H2L8D12BU4BR3U4R3D4L3"
126 FORPP=1TO150:NEXTPP
127 NEXTS
128 DRAW"BM15,100;C5;R6D4L3D1R3D5L6"
129 PAINT(16,100),3,3
130 DRAW"BM230,115;C5;L6D6R6U6"
131 PAINT(230,115),3,3
132 PLAY"O1"
133 FORSX=1TO4:PLAY"L255T255O+CC#GG#B-BDD#AA#EF":NEXTSX
134 FOREY=4TO6STEP2:PAINT(90,60),EY,3:PAINT(160,60),EY,3
135 NEXTEY
136 W$=INKEY$:IF W$="M" THEN GOTO2 ELSE 134
137 '**STARBURST**
138 PMODE1,1:PCLS(3):SCREEN1,1
139 FORX=5TO110STEP2
140 QQ=X
141 IFX=>100THEN QQ=105
142 PSET(QQ+15,QQ,4)
143 PLAY"O5L255T255B"
144 PSET(X+12,X-5,3)
145 NEXTX
146 FORC=5TO3STEP-1
147 C$="C"+STR$(C)+";"
148 DRAW C$+"BM120,100;S4;NE40NF40NG40NH40"
149 PLAY"O1L255T255CC#"
150 NEXTC
151 A=60:B=60:C=30:D=15:E=20:F=15
152 FORN=1TO120STEP2
153 X=C+E*SIN(N/A*3.14)
154 XX=X*5
155 M=N*3
156 GOSUB182
157 Y=D-F*Z
158 YY=Y*6
159 W$=INKEY$:IF W$="M" THEN GOTO2
160 COLOR2:LINE(XX-30,YY)-(270-XX,180-YY),PSET
161 COLOR4:LINE(XX-30,YY)-(270-XX,180-YY),PSET
162 NEXTN
163 FORX=3TO108STEP3
164 CIRCLE(120,90),X,2
165 PLAY"L255T255O1EF"
166 CIRCLE(120,90),X-3,3
167 NEXTX
168 FORX=110TO3STEP-2
169 CIRCLE(120,90),X-2,4
170 CIRCLE(120,90),X,3
171 NEXTX
172 FORFC=1TO15
173 CIRCLE(120,90),FC,4
174 PLAY"L255T255O1E-D"
175 NEXTFC
176 FORFF=15TO1STEP-1
177 CIRCLE(120,90),FF,3
178 PLAY"L255T255O1CC#"
179 NEXTFF
180 W$=INKEY$:IF W$="M" THEN GOTO2
181 GOTO138
182 Z=SIN((90-M)/57.296)
183 RETURN
184 '**DIMENSIONAL SINELINES**
185 PMODE3,1:PCLS(4):SCREEN1,1:PLAY"O5V30":FORSX=1TO4:PLAY"L100T1O-V<BGEC":NEXTSX:PLAY"V15"
186 PMODE3,1:PCLS(5):SCREEN1,1
187 B=0:S=0:D=0:Q=0:X=0:I=0
188 B=B+I
189 S=S+.1
190 D=D+Q
191 IF B>250 THEN PCLS:I=-2
192 IF D>180 THEN Q=-2
193 IF D<5 THEN Q=2
194 IF B<5 THEN I=2
195 X=(SIN(S)*129)+129
196 COLOR2:LINE(X,D+1)-(D,B+1),PSET:COLOR3
197 LINE(X,D)-(D,B),PSET:COLOR4
198 LINE(X,D+1)-(D,B+1),PSET
199 W$=INKEY$:IF W$="M" THEN GOTO2
200 GOTO188
201 '**DO IT YOURSELF**
202 CLS(7)
203 PRINT@37,"** DO IT YOURSELF **";
204 PLAY"L4T2O4DCFL8GO5CO4L4AL8GO5CO4L4AL3F"
205 PRINT@128," JOYSTICKS CONTROL THE ENDS OF   THE DRAWING LINE.               > PRESS [B] FOR BLUE LINE       >       [R] FOR RED LINE        >       [G] FOR GREEN LINE      >       [C] FOR CIRCLES"
206 PRINT@320," >       [CLEAR] TO ERASE        >       [M] FOR MAIN MENU"
207 PRINT@448,"  ** PRESS ANY KEY TO BEGIN **"
208 EXEC44539
209 PLAY"O1V15L6T2":FORSX=1TO4:PLAY"T>V+O+CFEDB-":NEXTSX:PLAY"V15L255T255"
210 PMODE3,1:PCLS(3):SCREEN1,1
211 CP=4
212 J=JOYSTK(0)*4:K=JOYSTK(1)*3
213 L=JOYSTK(2)*4:M=JOYSTK(3)*3
214 COLOR CP+1:LINE(J,K)-(L,M),PSET
215 COLOR CP:LINE(J,K)-(L,M),PSET
216 CP$=INKEY$
217 IF CP$="R" THEN CP=4:PLAY"O5B"
218 IF CP$="G" THEN CP=2:PLAY"O5G"
219 IF CP$="B" THEN CP=3:PLAY"O5F"
220 IF CP$="C" THEN PLAY"O5AB-B":CIRCLE(J,K),10,5:PAINT(J,K),CP,5:CIRCLE(L,M),10,5:PAINT(L,M),CP,5
221 IF CP$=CHR$(12) THEN PLAY"O5C":GOTO210
222 IF CP$="M" THEN GOTO2
223 GOTO212
224 PCLEAR8:GOTO2
