0 '***** ONE STARRY NIGHT *****
1 '
2 '(C) 1987 BY JIM MCDOWELL
3 '         JML SOFTWARE DESIGN
4 '
5 '****************************
6 HSCREEN2
7 PALETTE 9,16 : PALETTE 10,32 : PALETTE 12,63
8 HCLS8
9 GOSUB 27
10 FOR I=1 TO 50
11 IF RND(5)>1 THEN C=4 ELSE C=11
12 HSET(RND(320)-1,RND(192)-1,C)
13 NEXTI
14 FOR I=1TORND(2000)+500:NEXT
15 ON RND(4) GOTO 17,17,17,20
16 '***** TWINKLE STARS *****
17 PALETTE11,1:PALETTE11,63
18 GOTO 14
19 '***** SHOOTING STAR *****
20 X=RND(220)+50 : Y=RND(96)+40
21 R=RND(50)+99 : S=RND(0) : E=S+.2
22 HCIRCLE (X,Y),R,4,.9,S,E
23 HCIRCLE(X,Y),R,8,.9,S,E
24 GOSUB 27
25 GOTO 15
26 '***** DRAW MOON *****
27 HCIRCLE(96,56),30,4,.9
28 HCIRCLE(80,50),5,4
29 HPAINT(80,50),9,4
30 HCIRCLE(95,60),7,4
31 HPAINT(95,60),9,4
32 HCIRCLE(96,35),10,4,.9,.93,.6
33 HPAINT(96,35),10,4
34 HCIRCLE(113,56),9,4
35 HPAINT(113,56),10,4
36 HCIRCLE(104,74),6,4
37 HPAINT(104,74),9,4
38 HCIRCLE(80,63),5,4
39 HPAINT(70,50),12,4
40 RETURN
10 'ROTATING STAR.   BY JIM PRUYNE JR.
20 '201 W. SUMMIT, NORMAL IL.
30 'WITH ENCOURAGEMENT FROM MELISSA RODRIGO & RICHARD NEWTON
40 POKE 65497,0
50 ON ERR GOTO 410
60 ON BRK GOTO 410
70 PALETTE RGB
80 HSCREEN 0
90 INPUT"POINTS";X
100 INPUT"OUTLINE (Y/N)";O$:O$=LEFT$(O$,1)
110 INPUT"ROTATE <F>AST OR <S>LOW";D$:D$=LEFT$(D$,1):IF D$="S" THEN D$="P16" ELSE D$=""
120 INPUT"INNER STAR (Y/N)";C$:C$=LEFT$(C$,1):IF C$="Y" THEN CS=64 ELSE CS=70
130 INPUT"ACCUARACY (1-15)";AC
140 DIMA(2,X)
150 PI=3.14159265
160 S=2*PI/X
170 F=0
180 PALETTE 0,0
190 HSCREEN 2
200 CD=2/X/AC*PI
210 FOR F1=1 TO AC
220 HCOLOR F1
230 F=F+CD
240 FOR BB=80 TO 15 STEP -CS
250 FOR R=0 TO 2*PI STEP S
260 A(1,R/S)=COS(R+F)*BB+160
270 A(2,R/S)=SIN(R+F)*BB+96
280 NEXT
290 FOR R=0 TO X-1
300 W=R+INT(X/2)+1
310 IF W>X-1 THEN W=W-X
320 HLINE(A(1,R),A(2,R))-(A(1,W),A(2,W)),PSET
330 NEXT
340 IF O$<>"Y" THEN 380 ELSE FOR R=0 TO X-2
350 HLINE(A(1,R),A(2,R))-(A(1,R+1),A(2,R+1)),PSET
360 NEXT
370 HLINE(A(1,0),A(2,0))-(A(1,X-1),A(2,X-1)),PSET
380 NEXT:PALETTE F1,63:NEXT
390 FOR F=1 TO AC:PALETTE F,63:PLAY D$:PALETTE F,0:IF INKEY$<>"" THEN 400 ELSE NEXT:GOTO 390
400 PALETTE F,23:A$=INKEY$:IF A$=" " THEN 390 ELSE IF A$="" THEN 400 ELSE CLEAR:RUN
410 POKE 65496,0:PALETTE RGB:STOP
