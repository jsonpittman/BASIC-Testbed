10 REM romans xv march mcmlxxxvi
20 DATA1,I,5,V,10,X,50,L,100,C,500,D,1000,M
30 FORI=0TO6:READV(I),D$(I):NEXT
40 GOTO800 'menu
50 PRINT"  (ENTER AN @ FOR MAIN MENU)":PRINT:RETURN
60 REM arabic-->roman
70 CLS:PRINTTAB(8)"MAXIMUM=3999"
80 PRINT:GOSUB50
90 LINEINPUT"ARABIC NUMERAL: ";N$:IFN$=""THENSOUND100,1:GOTO90
100 IFN$="@"THENRETURN
110 F=1:LL=LEN(N$)
120 FORI=1TO LL:J$=MID$(N$,I,1)
130 IFJ$<"0"OR J$>"9"THENF=0:I=LL
140 NEXT:IFF=0 THENPRINT"DIGITS ONLY,PLEASE.":SOUND100,1:GOTO90
150 N=VAL(N$):IFN<1 OR N>3999 THENPRINT"OUT OF RANGE.":SOUND100,1:GOTO90
160 GOSUB190 'get-r$
170 PRINTR$:GOTO90
180 ' make-roman
190 R$=""
200 B=1000:O=6
210 IFN>=B THENN=N-B:R$=R$+D$(O):GOTO210
220 IFN=0THENRETURN
230 B=B/10:O=O-2:IFB>N GOTO230
240 C=0
250 IFN>=B THENN=N-B:C=C+1:GOTO250
260 IFC=9THENR$=R$+D$(O)+D$(O+2):GOTO290ELSEIFC>=5THENR$=R$+D$(O+1):C=C-5:IFC=0THEN290
270 IFC=4THENR$=R$+D$(O)+D$(O+1):GOTO290
280 R$=R$+STRING$(C,D$(O))
290 IFN>0THEN230
300 RETURN
310 REM roman-->arabic
320 CLS:PRINTTAB(7)"MAXIMUM=MMMCMXCIX":PRINT
330 GOSUB50
340 LINEINPUT"ROMAN NUMERAL: ";RN$
350 IFRN$=""THENSOUND100,1:GOTO340
360 IFRN$="@"THENRETURN
370 F=1:GOSUB420:IFF=0THEN340 'eval-rn$
380 GOSUB550:IFF=0THEN340 'validate-rn$
390 PRINTRN
400 GOTO340
410 ' eval-rn$
420 RN=0:V1=0
430 FORI=LEN(RN$)TO 1 STEP-1
440 C$=MID$(RN$,I,1)
450 FORC=0TO6
460 IFC$=D$(C)THENV2=V(C):GOTO500
470 NEXTC
480 PRINT"'";C$;"'";" IS NOT A ROMAN NUMERAL.":F=0:RETURN
490 'eval-chars
500 IFV2<V1 THENRN=RN-V2 ELSERN=RN+V2
510 V1=V2
520 NEXTI
530 RETURN
540 'validate-rn$
550 IFRN<1 OR RN>3999 THENGOSUB580:RETURN
560 N=RN:GOSUB190:IFRN$<>R$THEN:GOSUB580:RETURN
570 RETURN
580 SOUND100,1:PRINT"THIS IS NOT STANDARD FORM.":F=0:RETURN
590 REM DRILL
600 CLS:PRINT@12,"DRILL":PRINT:GOSUB50
610 N=RND(-TIMER)
620 A=0
630 ANS=RND(3999):REM ans range
640 A=(A=0):IFA=0 THEN720
650 N=ANS:GOSUB190
660 PRINT"ROMAN FOR"ANS"?"
670 LINEINPUTRN$
680 IFRN$="@"THENRETURN
690 IFR$<>RN$ THENSOUND100,1:GOTO660
700 PLAY"V15T12O3 L2CL4CCL2DL4GGO4L2C"
710 GOTO630
720 N=ANS:GOSUB190
730 PRINT"ARABIC FOR "R$"?"
740 LINEINPUTRN$
750 IFRN$="@"THENRETURN
760 N=VAL(RN$):IFN<>ANS THENSOUND100,1:GOTO730
770 PLAY"V15T8O3 L4D+FL2F+FD+"
780 GOTO630
790 'menu
800 CLS0:PRINT@41,"ROMAN NUMERALS";
810 PRINT@32*5,"1. ARABIC TO ROMAN";
820 PRINT@32*7,"2. ROMAN TO ARABIC";
830 PRINT@32*9,"3. DRILL";
840 PRINT@32*11,"4. END";
850 PRINT@32*15,"PRESS A NUMBER";
860 K$=INKEY$:IFK$=""THEN860
870 ONVAL(K$)GOSUB70,320,600,880:GOTO800
880 CLS:PRINT@32*8+6,"ROMAN NUMERALS ENDED":PRINT@32*13,"":END:RETURN
