1 'PHASER RANGE
2 'BY STEVEN PULS
3 'COPYRIGHT (C) 1992
10 POKE 65495,0
20 L=3000:RN=1
30 A$="NU5ND5NL5NR5"
40 CLS0:INPUT"LEVEL (1 EASY - 20 HARD)";LV:IF LV<1 OR LV>20 THEN 40 ELSE R=21-LV
50 PMODE4:SCREEN1,1:PCLS0
60 PCLS0:FOR RD=1 TO 10
80 TM=RND(500)+L
90 PLAY"T255L255V31O3CDEFGABO4CDEFGB":H=0:V=0:X=160:Y=96:H1=0:V1=0:V3=0:H3=0
100 H1=RND(320):V1=RND(190):H2=RND(50):V2=RND(100):T=RND(50)+50:CIRCLE(H,V),3,1
110 IF H1+H2>329 OR V1+V2>189 THEN A=2 ELSE A=1
120 H=H1:V=V1
130 TIMER=0
140 Z1=RND(15):Z2=RND(15):PSET(H,V,1):X=JOYSTK(0)*4:Y=JOYSTK(1)*3
150 ON A GOTO 250,270
160 IF H<0 OR H>255 OR V<0 OR V>190 THEN GOTO90
170 CIRCLE(H,V),3,1
180 IFPEEK(65280)=254ORPEEK(65280)=126THENS=S+1:H3=160:V3=192:HS=X-160:VS=192-Y:S1=HS/20:S2=VS/20:FORT=1TO10:PSET(H3,V3,1):PSET(H3,V3,2):PSET(H3,V3,3):PSET(H3,V3,0):H3=H3+S1:V3=V3-S2:NEXTT:PLAY"V31L255T255O5AC":CIRCLE(X,Y),R,1:PCLS:GOTO220
190 DRAW"BM=X;,=Y;C1"+A$:CIRCLE(H,V),3,0:DRAW"BM=X;,=Y;C0"+A$
200 PSET(X,Y,1):PSET(X,Y,0)
210 IF TIMER>TM THENPLAY "O3T25BAGFEDCCO2BAGFEDCCC":GOTO340 ELSE GOTO 140
220 D1=H-X:D2=V-Y:IF (D1*D1)+(D2*D2)<(R*R) THEN PLAY"T255O3DCDCDCDCDCDCDECED":HT=HT+1:GOTO 240 ELSE A=RND(2):H=H+RND(5)*RND(2)-1:V=V+RND(5)*RND(2)-1
230 GOTO 140
240 FOR CD=1 TO 20 STEP 5:POKE&HFF22,RND(32)+223:CIRCLE(H,V),CD,1,.3:PLAY "T25L8O3C":CIRCLE(H,V),CD,0,.3:POKE&HFF22,255:NEXT CD:GOTO290
250 PSET(H,V,0):H=H+Z1:V=V+Z2:IF H>H1+H2 OR V>V1+V2 THEN A=2:GOTO160
260 GOTO 160
270 PSET(H,V,0):H=H-Z1:V=V-Z2:IF H<H1-H2 OR V<V1-V2 THEN A=1:GOTO 160
280 GOTO 160
290 NEXT RD
300 SCREEN 0,0:CLS0
310 PLAY"V31T25L2O3CV21L5CV10L16CCV5L64CCCV2L255CCCCC":PRINT "YOUR AVERAGE SHOTS PER HIT WAS  "+STR$(S/10)+" FOR ROUND";RN
320 PRINT @256,"PRESS fire BUTTON FOR NEXT ROUND";
330 IF PEEK(65280)<>254 AND PEEK(65280)<>126 THEN 320
340 CLS3:PRINT@32,"PRESS FIRE BUTTON TO RESTART"
350 IF PEEK(65280)<>254 AND PEEK(65280)<>126 THEN 350
360 RN=RN+1:R=R-(LV/4):IF R<1 THEN R=1
370 GOTO 50
