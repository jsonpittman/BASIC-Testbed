10 '****************************
20 ' NOTE: THIS PROGRAM IS USES   THE GEMINI-10X PRINTER CODE     FOR DISREGARDING THE SIGNAL     FROM THE "PAPER-OUT" DETECTOR.
30 ' WHEN USING A DIFFERENT      PRINTER YOU MAY NEED TO REMOVE  LINE #330 OR REPLACE IT WITH    YOUR PRINTER'S APPROPRIATE      CODE.
40 '****************************
50 CLS:PRINT@133,"personal check printer":PRINT@205,"BY":PRINT@263,"ROBERT A. GREEN":PRINT@326,"SEPTEMBER 30, 1986":FOR Z=1TO2000:NEXTZ
60 CLS:PRINT@162,"PLACE PERSONAL CHECK IN YOUR  PRINTER AND TURN THE PRINTER ON.":PRINT@290,"HIT <ENTER> TO CONTINUE";:INPUTZ$
70 D$="MONTH AND DAY":Y$="YEAR":A$="AMOUNT-digits":AW$="AMOUNT-printed":N$="NAME":P$="PAY TO":M$="MEMO":LY$=" ONLY":NL=0
80 CLS:PRINT"ENTER THE CURRENT MONTH AND DAY";:INPUTDA$
90 PRINT:PRINTY$;:LINEINPUT"? ";YR$
100 PRINT:PRINTP$;:LINEINPUT"? ";PA$
110 IF LEN(PA$)>35 THEN GOTO440
120 PRINT:PRINTA$;:INPUTAM
130 IFAM>99999.99 THEN GOTO460
140 PRINT:PRINTAW$;"?":LINEINPUT"";AM$
150 IF LEN(AM$)>45 THEN GOTO480
160 PRINT:PRINTM$;:LINEINPUT"? ";MM$
170 IF LEN(MM$)>25 THEN GOTO500
180 PRINT:PRINT"IS THIS INFORMATION CORRECT?":PRINT"<Y> YES - <N> NO";
190 C$=INKEY$
200 IF C$=""THEN GOTO190
210 CLS:IF C$="N" OR C$="n" THEN GOTO220 ELSE GOTO320
220 PRINT"1 ";D$,DA$:PRINT"2 ";Y$,YR$:PRINT"3 ";P$,PA$:PRINT"4 ";A$,AM:PRINT"5 ";AW$:PRINTAM$:PRINT"6 ";M$,MM$
230 PRINT:PRINT"WHICH LINE IS INCORRECT":INPUTNL:IF NL>6 THEN GOTO230
240 PRINT:PRINT"ENTER CORRECT"
250 IF NL=1 THEN PRINTD$;:INPUTDA$
260 IF NL=2 THEN PRINTY$;:INPUTYR$
270 IF NL=3 THEN PRINTN$;:INPUTPA$:IF LEN(PA$)>35 THEN GOTO440
280 IF NL=4 THEN PRINTA$;:INPUTAM:IF AM>99999.99 THEN GOTO460
290 IF NL=5 THEN PRINTAW$;:INPUTAM$:IFLEN(AM$)>45 THEN GOTO480
300 IF NL=6 THEN PRINTM$;:INPUTMM$:IF LEN(MM$)>25 THEN GOTO500
310 CLS:PRINTD$,DA$:PRINTY$,YR$:PRINTP$,PA$:PRINTA$,AM:PRINTAW$:PRINTAM$:PRINTM$,MM$:GOTO180
320 CLS:PRINT@170,"PRINTING":YR$=RIGHT$(YR$,2):LL=LEN(DA$):AD=43-LL
330 PRINT#-2,CHR$(27) CHR$(56)
340 PRINT#-2:PRINT#-2,TAB(AD)DA$;:PRINT#-2,",";:PRINT#-2,TAB(46)YR$
350 PRINT#-2:PRINT#-2,TAB(8)PA$;:PRINT#-2,TAB(45);:PRINT#-2,USING"**,###.##";AM
360 IF LEN(AM$)<39 THEN AM$=AM$+LY$
370 ZZ=44-LEN(AM$)
380 PRINT#-2:PRINT#-2,TAB(1)AM$;:PRINT#-2,(STRING$(ZZ,CHR$(42)))
390 FOR L=1 TO 4:PRINT#-2:NEXTL:PRINT#-2,TAB(5)MM$
400 PRINT@160,"DO YOU WANT TO PRINT ANOTHER    COPY OF THE SAME CHECK?":PRINT:PRINT"HIT <Y> IF YES, <S> FOR ANOTHER CHECK, SAME DATE, <N> FOR A NEW DATE, OR <E> TO END PROGRAM."
410 X$=INKEY$
420 IF X$=""THEN GOTO410
430 IF X$="Y" OR X$="y" THEN GOTO 320 ELSE IF X$="S" OR X$="s" THEN CLS:GOTO100 ELSE IF X$="N" OR X$="n" THEN GOTO70 ELSE IF X$="E"OR X$="e" THEN CLS:END ELSE GOTO410
440 CLS:PRINT"LINE SPACE IS LIMITED TO 35     CHARACTERS -- TRY AGAIN."
450 IF NL=3 THEN GOTO270 ELSE GOTO100
460 CLS:PRINT"AMOUNT OF CHECK IS LIMITED TO   $99,999.99 IN THIS PROGRAM  --  -- TRY AGAIN."
470 IF NL=4 THEN GOTO280 ELSE GOTO120
480 CLS:PRINT"SPACE FOR THE WRITTEN AMOUNT IS LIMITED TO 45 CHARACTERS   --   -- TRY AGAIN."
490 IF NL=5 THEN GOTO290 ELSE GOTO140
500 CLS:PRINT"SPACE FOR THE MEMO IS LIMITED TO25 CHARACTERS -- TRY AGAIN.":PRINT
510 IF NL=6 THEN GOTO300 ELSE GOTO160
