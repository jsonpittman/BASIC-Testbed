10 '**************************
20 '**   LLIST FORMATTER    **
30 '**************************
40 '**   BASIC LOADER FOR   **
50 '**      FLIST/BIN       **
60 '** ML CODE BY C.ROSLUND **
70 '** DECEMBER 1982 ISSUE  **
80 '**    OF THE RAINBOW    **
90 '**************************
100 BD=1:POKE150,BD:'** BAUD RATE **
110 A$=STRING$(32,188):B$=STRING$(32,179)
120 DV=PEEK(188):ST=DV*256
130 FORX=ST TO ST+103:POKEX,&HFF:NEXTX
140 IF DV=6 THEN C$="CASSETTE" ELSE IF DV=14 THEN C$="  DISK":POKE&H95A,0
150 L$="* * * LOADING * * *":L1$="READY PRINTER-LLIST AT OK PROMPT"
160 '**************************
500 DATA 6F,8D,00,19,BE,01,68,AF,8C,11,30,8C,11,BF,01,68,86,7E
510 DATA B7,01,67,86,39,A7,8C,EA,39,00,00,00,34,17,D6,6F,C1,FE
520 DATA 27,06,35,17,6E,9D,FF,EF,81,0D,26,06,6C,8D,FF,E9,20,F0
530 DATA 96,9C,81,50,25,13,86,0D,BD,A2,BF,86,20,C6,04,BD,A2,BF
540 DATA 5A,26,FA,6C,8D,FF,CE,A6,8D,FF,CA,81,3C,25,CF,C6,06,86
550 DATA 0D,BD,A2,BF,5A,26,F8,6F,8D,FF,B8,20,BF,XX
560 '**************************
1000 CLS:PRINT:PRINT:PRINTA$;
1010 PRINT"      **** MAIN MENU ****"
1020 PRINTA$;
1030 PRINT:PRINT"      <1> SET PRINTER BAUD"
1040 PRINT:PRINT"      <2> CHANGE DEFAULTS"
1050 PRINT:PRINT"      <3> LLIST PROGRAM"
1060 PRINT:PRINTB$;
1070 GOSUB 3500
1080 A=VAL(Q$):IFA<1 OR A>3 THEN 1000
1090 ON A GOSUB 1500,2500,3000
1100 '*************************
1500 CLS:PRINT:PRINT:PRINTA$;
1510 PRINT"      **** BAUD MENU ****"
1520 PRINTA$;
1530 PRINT:PRINT"          <1> -  600"
1540 PRINT"          <2> - 1200"
1550 PRINT"          <3> - 2400"
1560 PRINT"          <4> - 4800"
1570 PRINT"          <5> - 9600"
1580 PRINT:PRINTB$;
1590 GOSUB 3500
1600 Q=VAL(Q$):IFQ<1 OR Q>5 THEN 1500
1610 E$="08741180701"
1620 BD=VAL(MID$(E$,(Q*2),2))
1630 POKE 150,BD
1640 GOTO 1000
1650 '*************************
2000 CLS:PRINT@128,A$;
2010 PRINT@198,"***** READING *****"
2020 PRINT@262,"***** D A T A *****"
2030 PRINT@320,B$;
2040 PRINTTAB(11)C$:PRINTB$;
2050 EN=ST
2060 READ D$
2070 IF D$="XX" THEN 2120
2080 C=VAL("&H"+D$)
2090 POKE EN,C
2100 EN=EN+1
2110 GOTO 2060
2120 RETURN
2130 '*************************
2500 IF PEEK(ST)<>&H6F THEN GOSUB 2000
2510 CLS:PRINT:PRINT:PRINTA$;
2520 PRINT"    **** DEFAULT MENU ****":PRINTA$;
2530 PRINT:PRINT"  <1> - COLUMN WIDTH ---->"PEEK(ST+57)
2540 PRINT"  <2> - INDENTATION ----->"PEEK(ST+68)
2550 PRINT"  <3> - PRINTOUT LENGTH ->"PEEK(ST+84)
2560 PRINT"  <4> - SKIP LINES ------>"PEEK(ST+88)
2570 PRINT"  <5> - MAIN MENU"
2580 PRINT:PRINTB$;
2590 GOSUB 3500
2600 Q=VAL(Q$):IFQ<1 OR Q>5 THEN 2510
2610 ON Q GOTO 2620,2630,2640,2650,1000
2620 PRINT@217,;:INPUTCW:IFCW<1 OR CW>255 THEN2620 ELSE POKEST+57,CW:GOTO 2510
2630 PRINT@249,;:INPUTID:IFID<1THEN2630 ELSE POKEST+68,ID:GOTO 2510
2640 PRINT@281,;:INPUTPL:IFPL<1 OR PL>65 THEN2640 ELSE POKEST+84,PL:POKEST+88,(66-PL):GOTO 2510
2650 PRINT@313,;:INPUTSL:IFSL<1 OR SL>65 THEN2650 ELSE POKEST+88,SL:POKEST+84,(66-SL):GOTO 2510
2660 RETURN
2670 '*************************
3000 IF PEEK(ST)<>&H6F THEN GOSUB 2000
3010 CLS:PRINT:PRINT:PRINTA$;
3020 PRINT"     **** LLIST MENU ****":PRINTA$;
3030 IF PEEK(ST)<>&H6F THEN GOSUB 2000:GOTO 3010
3040 IFDV=14 THEN F1$=" FILENAME/EXT" ELSE F1$=" FILENAME"
3050 PRINT@384,B$;
3060 PRINT@192,F1$;:INPUTF$
3070 IFDV=14 THEN 3080 ELSE 3130
3080 IF F$="" THEN PRINT@210,"directory":D=1 ELSE D=0
3090 PRINT@224,;:INPUT" DRIVE NUMBER - ";DN
3100 IF D=1 THEN DRIVEDN:DIR:INPUT"         PRESS <enter>";Q:GOTO3010
3110 PRINT@294,L$:PRINTL1$;:PRINT@416,;:
3120 EXECST:DRIVEDN:LOAD F$
3130 PRINT@224,;:INPUT" READY CASSETTE - <enter>";Q
3140 PRINT@294,L$:PRINTL1$;:PRINT@416,;:
3150 EXECST:CLOAD F$
3160 '*************************
3500 Q$=INKEY$:IFQ$=""THEN3500
3510 RETURN
3520 '*************************
3530 '**  BASIC LOADER BY:   **
3540 '**      CLAY HOWE      **
3550 '**  STURGIS, MICHIGAN  **
3560 '**   (616)-651-4248    **
3570 '*************************
3580 '**    NOVEMBER 1985    **
3590 '*************************
