20 CLS:PRINT"FINANCIAL PLANNER - M. ZANGER"
22 PRINT"        FEB. 12, 1986"
25 PRINT
30 PRINT"************  MENU  ************";
35 PRINT
40 PRINT"1. compound interest"
50 PRINT"2. mortgage/loan payments"
60 PRINT"3. years to reach desired amt."
70 PRINT"4. yield calculator"
75 PRINT"5. ira/savings contributions"
77 PRINT"6. what mortgage can i afford"
80 PRINT" ENTER YOUR CHOICE (1-6)"
90 A$=INKEY$:IFA$=""THEN90 ELSE100
100 A=VAL(A$)
110 ON A GO SUB 1000,2000,3000,4000,5000,6000
1000 CLS:PRINT"compound interest calculation"
1010 PRINTSTRING$(32,"=");
1012 INPUT"PRINCIPAL.....";M
1014 INPUT"RATE..........";R
1016 INPUT"YEARS.........";Y
1020 PRINT"+++++++  how compounded ++++++++";
1030 PRINT"1. Annually"
1040 PRINT"2. Semi-Annually"
1050 PRINT"3. Daily"
1055 PRINT"4. Monthly"
1060 PRINT"enter your choice (1-4)"
1070 A$=INKEY$:IFA$=""THEN1070 ELSE 1080
1080 A=VAL(A$)
1085 IF A=1THENC$="annually"ELSEIFA=2THENC$= "semi-annually" ELSEIFA=3THENC$="daily"ELSEIFA=4THENC$="monthly"
1090 ON A GOTO 1100,1110,1120,1130
1100 T=1:P=Y:RR=R/100:GOTO1150
1110 T=2:P=Y*2:RR=R/200:GOTO1150
1120 T=360:P=Y*360:RR=R/36000:GOTO1150
1130 T=12:P=Y*12:RR=R/1200:GOTO1150
1150 PRINTSTRING$(32,"%");
1200 TV = M*(1+RR)^P
1210 PRINT"AFTER ";Y;" YEARS"
1220 PRINT"YOUR $";M;" DOLLARS"
1230 PRINT"INVESTED AT ";R;" PERCENT"
1235 PRINT"(Compounded ";C$;")"
1240 PRINT"WILL BE WORTH ";
1250 PRINTUSING"$$###,###.##";TV
1260 PRINTSTRING$(32,"$");
1300 PRINT"DO YOU WANT A HARD COPY (Y/N)"
1310 A$=INKEY$:IFA$=""THEN 1310 ELSE 1320
1320 IF A$="N" THEN 1900 ELSE 1330
1330 IF A$<"Y" ORA$>"Y" THEN 1310 ELSE GOTO1500
1500 PRINT#-2,STRING$(55,"-")
1510 PRINT#-2,"AFTER ";Y;" YEARS"
1520 PRINT#-2,"YOUR $";M;" DOLLARS"
1530 PRINT#-2,"INVESTED AT ";R;" PERCENT (COMPOUNDED ";C$;")"
1540 PRINT#-2,"WILL BE WORTH ";
1550 PRINT#-2,USING"$###,###.##";TV
1560 PRINT#-2,STRING$(55,"-")
1570 PRINT#-2,CHR$(10)
1900 PRINT"another calculation?"
1910 A$=INKEY$:IFA$=""THEN1910ELSE1920
1920 IFA$="N"THEN END ELSE 1930
1930 IFA$="Y" THEN 20 ELSE IF A$<"Y"ORA$>"Y" THEN 1900
2000 CLS:PRINT"  mortgage payment calculation":PRINT
2010 INPUT"principal.....";P
2020 INPUT"rate..(%).....";R
2030 INPUT"years.........";Y:YY=-(12*Y)
2040 R =R/1200
2050 PRINTSTRING$(32,"%");
2052 Z = 1-(1+R)^YY:X=Z/R
2060 MR = P/X
2070 PRINT"your monthly payment is ";
2080 PRINTUSING"$$###.##";MR
2083 PRINT"total repayment";
2085 PRINTUSING"$$###,###.##";MR*12*Y
2090 PRINTSTRING$(32,"%");
2200 PRINT"DO YOU WANT A HARD COPY (Y/N)"
2210 A$=INKEY$:IFA$=""THEN2210ELSE2220
2220 IF A$="N" THEN 1900 ELSE 2230
2230 IFA$<"Y"ORA$>"Y" THEN 2200 ELSE 2500
2500 PRINT#-2,STRING$(45,"=")
2510 PRINT#-2,"AMT. OF LOAN $";P
2520 PRINT#-2,"BORROWED AT %";R*1200
2530 PRINT#-2,"FOR A PERIOD OF";Y;" YEARS"
2540 PRINT#-2,"YOUR MONTHLY PAYMENT WILL BE";
2550 PRINT#-2,USING"$###.##";MR
2552 PRINT#-2,"YOUR TOTAL PAYMENTS WILL BE";
2555 PRINT#-2,USING"$$###,###.##";MR*12*Y
2560 PRINT#-2,STRING$(45,"=")
2570 PRINT#-2,CHR$(10):GOTO1900
3000 CLS:PRINT"years to reach maturity calcn."
3010 PRINTSTRING$(32,"#");
3020 INPUT"initial investment....";IV
3030 INPUT"final value...........";FV
3040 INPUT"interest rate.........";R:R=R/100
3050 PRINTSTRING$(32,"%");
3060 Y=(2.303*LOG(FV/IV))/(2.303*LOG(1+R))
3070 PRINT"years to reach final value";
3080 PRINTUSING"###.##";Y
3300 PRINT"DO YOU WANT A HARD COPY (Y/N)"
3310 A$=INKEY$:IF A$=""THEN3310 ELSE 3320
3320 IF A$="N"THEN1900 ELSE 3330
3330 IFA$>"Y"ORA$<"Y"THEN 3300 ELSE 3500
3500 PRINT#-2,STRING$(32,"="):PRINT#-2,"IF YOU INVEST $";IV
3510 PRINT#-2,"AT A RATE OF ";R*100;" PERCENT"
3520 PRINT#-2,"AND WANT TO RECEIVE ";
3530 PRINT#-2,USING"$###,##.##";FV
3540 PRINT#-2,"IT WILL TAKE ";
3545 PRINT#-2,USING"###.##";Y;:PRINT#-2," YEARS"
3550 PRINT#-2,STRING$(32,"=")
3560 PRINT#-2,CHR$(10):GOTO1900
4000 CLS:PRINT"        yield calculation"
4005 PRINTSTRING$(32,"*");
4010 INPUT"initial investment..$";IV
4020 INPUT"value at maturity...$";FV
4030 INPUT"years to maturity....";Y
4040 PRINTSTRING$(32,"%");
4050 R=(FV/IV)^(1/Y)-1:R=R*100
4070 PRINT"your rate of interest is";
4080 PRINTUSING"##.##";R;:PRINT"%"
4300 PRINT"DO YOU WANT A HARD COPY (Y/N)"
4310 A$=INKEY$:IFA$=""THEN4310ELSE4320
4320 IFA$="N"THEN1900ELSE4330
4330 IFA$<"Y"ORA$>"Y"THEN4300ELSE4500
4500 PRINT#-2,STRING$(41,"=")
4510 PRINT#-2,"IF YOU INVEST $";IV
4520 PRINT#-2,"FOR A PERIOD OF ";Y;" YEARS"
4530 PRINT#-2,"AND AT MATURITY WILL GET $";FV
4540 PRINT#-2,"YOUR RATE OF RETURN WILL BE ";
4545 PRINT#-2,USING"##.##";R;:PRINT#-2," PERCENT"
4550 PRINT#-2,STRING$(41,"="):PRINT#-2,CHR$(10):GOTO1900
4600 PRINT#-2,CHR$(10)
5000 CLEAR1000:CLS:PRINT"ira/savings contributions"
5002 PRINT:PRINT:PRINTSTRING$(32,"*")
5004 PRINT"IRA or MONTHLY SAVINGS (I/M)"
5006 Q$=INKEY$:IFQ$=""THEN5006
5008 IFQ$="M"THEN5022
5010 INPUT"annual contribution";P
5020 INPUT"annual interest rate";I:GOTO5030
5022 INPUT"monthly contribution";P
5024 INPUT"interest rate";I:I=I/12
5030 R = 1+I/100
5040 INPUT"number of years";Y
5045 T=Y:IF Q$="M"THEN T=Y*12
5060 SUM=0
5070 FOR N = 1 TO T
5080 SI= P*R^N
5090 SUM = SUM+SI
5100 NEXT:CLS:PRINT:PRINT:PRINT
5105 PRINTSTRING$(32,"*");
5107 IFQ$="M"THEN5115
5110 PRINT"WITH AN ANNUAL contribution OF  $";P;"@ AN  interest rate OF  ";"  %";I;" -----AFTER----- ";Y;"years";:GOTO5120
5115 PRINT"WITH A MONTHLY contribution OF   $";P;"@ AN    interest rate OF    ";"  %";I*12;" --- AFTER";Y;"years"
5120 PRINT"   YOUR total value WILL BE":
5130 PRINT"      ";
5140 PRINTUSING"$$###,###.##";SUM
5150 PRINTSTRING$(32,"*");
5160 PRINT"DO YOU WANT A HARD COPY (Y/N)"
5170 A$=INKEY$:IFA$=""THEN5170ELSE5180
5180 IFA$="N"THEN1900ELSE5190
5190 IFA$<"Y"ORA$>"Y"THEN5160ELSE5200
5200 PRINT#-2,STRING$(50,"$")
5202 IF Q$="M"THEN 5232
5210 PRINT#-2,"With an annual investment of $";P
5220 PRINT#-2,"At a rate of ";I;" PERCENT"
5230 PRINT#-2,"For a total of ";Y;" Years":GOTO5240
5232 PRINT#-2,"With a monthly investment of $";P
5234 PRINT#-2,"At a rate of ";I*12;" PERCENT"
5236 PRINT#-2,"For a total of ";Y;" Years"
5240 PRINT#-2,"You will have a grand total of  ";
5250 PRINT#-2,USING"$$###,###.##";SUM
5260 PRINT#-2,STRING$(50,"$")
5265 PRINT#-2,CHR$(10)
5270 GOTO1900
6000 CLS: PRINT"WHAT MORTGAGE CAN YOU AFFORD?":PRINT
6005 PRINTSTRING$(32,"$");
6010 INPUT"monthly payment";MR
6020 INPUT"rate...%.......";R
6030 INPUT"years..........";Y:YY=12*Y:R=R/1200
6050 Z=1-(1+R)^-YY:X=Z/R
6060 P=MR*X
6070 PRINT"maximum mortgage is";
6080 PRINTUSING"$$###,###.##";P
6090 PRINTSTRING$(32,"$");
6100 PRINT"DO YOU WANT A HARD COPY (Y/N)"
6110 A$=INKEY$:IFA$=""THEN6110
6120 IFA$="N"THEN1900ELSE6130
6130 PRINT#-2,STRING$(50,"=")
6135 PRINT#-2,"IF MAXIMUM MONTHLY PAYMENT CAN BE ";
6150 PRINT#-2,USING"$$##,###.##";MR
6160 PRINT#-2,"@ A RATE OF.......................... %";R*1200
6170 PRINT#-2,"FOR......... ";Y;" .........YEARS"
6180 PRINT#-2,"YOUR MORTGAGE CAN BE .............. ";
6190 PRINT#-2,USING"$$###,###.##";P
6200 PRINT#-2,STRING$(50,"=");
6210 PRINT#-2,CHR$(10):GOTO1900
