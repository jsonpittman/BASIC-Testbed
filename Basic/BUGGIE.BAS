1 'COCO BUGGIE (C) 1988 FROM       BILL BERNICO SOFTWARE           
2 CLEAR500:CLS0:PRINTTAB(10)"COCO-BUGGIE":FORX=1024TO1055:POKEX,PEEK(X)-64:NEXTX:PRINT:PRINT"PLAYER'S NAME MUST BE LESS THAN EIGHT CHARACTERS EACH":PRINT:INPUT"PLAYER 1'S NAME";N1$:IFLEN(N1$)>8THEN2
3 PRINT@192,STRING$(32,143):PRINT@224,STRING$(32,143):PRINT@224,"";:INPUT"PLAYER 2'S NAME";N2$:IFLEN(N2$)>8THEN3
4 POKE65497,0:RGB:HSCREEN2:HCLS4:HCOLOR8,4:ONBRKGOTO131:DT$="RDLU2R2D3L3U3F":D$="BL4BUR22D22L22U22E4R22NG4D22NG4U22L22G4BF3":LR$="G10D20L4DR5U21E10RG10D20L5DNR6DR6U22E10RG10
5 HCLS4:HLINE(0,0)-(160,191),PSET,B:HLINE(5,5)-(155,186),PSET,B:HPAINT(2,2),2,8:HLINE(160,0)-(319,191),PSET,B:HLINE(165,5)-(314,186),PSET,B:HPAINT(167,2),3,8
6 IFP2=13THEN100ELSEGOSUB113:D=RND(6):HCOLOR2:PLAY"O2T20B":HPRINT(1,1),N1$+",HIT A KEY":EXEC44539:HDRAW"BM30,27"+D$:Z=D:GOSUB106:PLAY"O4T60CBDAEGFC
7 ON Z GOTO 8,12,18,26,34,40
8 HPRINT(10,4),"1=BODY
9 IFB1=1THEN11
10 IFB1=0THEN X=87:Y=93:H=60:GOSUB114:B1=1:P1=P1+1:GOTO52
11 GOSUB127:GOTO52
12 HPRINT(10,4),"2=HEAD
13 IFH1=1THEN16
14 IFB1=0THEN17
15 IFH1=0THEN H=100:GOSUB115:H1=1:P1=P1+1:GOTO52
16 GOSUB127:GOTO52
17 HPRINT(3,21),"YOU NEED A BODY":GOTO52
18 HPRINT(10,4),"3=EYES
19 IFH1=0THEN24
20 IFE1=2THEN25
21 IFE1=0THEN H=113:GOSUB116
22 IFE1=1THEN H=98:GOSUB117:P1=P1+2
23 E1=E1+1:GOTO52
24 HPRINT(3,21),"YOU NEED A HEAD":GOTO52
25 GOSUB127:GOTO52
26 HPRINT(10,4),"4=FEELERS
27 IFH1=0THEN33
28 IFF1=2THEN32
29 IFF1=0THEN H=95:GOSUB118
30 IFF1=1THEN H=102:GOSUB119:P1=P1+2
31 F1=F1+1:GOTO52
32 GOSUB127:GOTO52
33 HPRINT(3,21),"YOU NEED A HEAD":GOTO52
34 HPRINT(10,4),"5=TONGUE
35 IFH1=0THEN38
36 IFT1=1THEN39
37 IFT1=0THENX=100:H=106:GOSUB120:T1=1:P1=P1+1:GOTO52
38 HPRINT(3,21),"YOU NEED A HEAD":GOTO52
39 GOSUB127:GOTO52
40 HPRINT(10,4),"6=LEGS
41 IFB1=0THEN51
42 IFL1=6THEN50
43 IFL1=0THEN H=85:GOSUB121
44 IFL1=1THEN H=62:GOSUB122
45 IFL1=2THEN H=42:GOSUB123
46 IFL1=3THEN H=38:GOSUB124
47 IFL1=4THEN H=55:GOSUB125
48 IFL1=5THEN H=77:GOSUB126:P1=P1+6
49 L1=L1+1:GOTO52
50 GOSUB128:GOTO52
51 HPRINT(3,21),"YOU NEED A BODY
52 FORX=1TO1500:NEXTX:IFP1=13THEN99ELSEGOSUB113:F=RND(6):HCOLOR3:PLAY"O3T20B":HPRINT(21,1),N2$+",HIT A KEY":EXEC44539:HDRAW"BM190,27"+D$:Z=F:GOSUB106:PLAY"O4T60CBDAEGFC
53 ON Z GOTO 54,58,64,72,80,86
54 HPRINT(30,4),"1=BODY
55 IFB2=1THEN57
56 IFB2=0THEN X=247:Y=93:H=220:GOSUB114:B2=1:P2=P2+1:GOTO98
57 GOSUB129:GOTO98
58 HPRINT(30,4),"2=HEAD
59 IFH2=1THEN62
60 IFB2=0THEN63
61 IFH2=0THEN H=260:GOSUB115:H2=1:P2=P2+1:GOTO98
62 GOSUB129:GOTO98
63 HPRINT(23,21),"YOU NEED A BODY":GOTO98
64 HPRINT(30,4),"3=EYES
65 IFH2=0THEN70
66 IFE2=2THEN71
67 IFE2=0THEN H=273:GOSUB116
68 IFE2=1THEN H=258:GOSUB117:P2=P2+2
69 E2=E2+1:GOTO98
70 HPRINT(23,21),"YOU NEED A HEAD":GOTO98
71 GOSUB129:GOTO98
72 HPRINT(30,4),"4=FEELERS
73 IFH2=0THEN79
74 IFF2=2THEN78
75 IFF2=0THEN H=255:GOSUB118
76 IFF2=1THEN H=262:GOSUB119:P2=P2+2
77 F2=F2+1:GOTO98
78 GOSUB129:GOTO98
79 HPRINT(23,21),"YOU NEED A HEAD":GOTO98
80 HPRINT(30,4),"5=TONGUE
81 IFH2=0THEN84
82 IFT2=1THEN85
83 X=260:H=266:GOSUB120:T2=1:P2=P2+1:GOTO98
84 HPRINT(23,21),"YOU NEED A HEAD":GOTO98
85 GOSUB129:GOTO98
86 HPRINT(30,4),"6=LEGS
87 IFB2=0THEN97
88 IFL2=6THEN96
89 IFL2=0THEN H=245:GOSUB121
90 IFL2=1THEN H=222:GOSUB122
91 IFL2=2THEN H=202:GOSUB123
92 IFL2=3THEN H=198:GOSUB124
93 IFL2=4THEN H=215:GOSUB125
94 IFL2=5THEN H=237:GOSUB126:P2=P2+6
95 L2=L2+1:GOTO98
96 GOSUB130:GOTO98
97 HPRINT(23,21),"YOU NEED A BODY
98 GOTO6
99 GOSUB113:HPRINT(3,21),"THE WINNER!":PLAY"O1T60CDEFGABO2CDEFGABO3CDEFGABO4CDEFGABO5CDEFGAB":FORG=1TO2000:NEXTG:GOTO101
100 GOSUB113:HPRINT(23,21),"THE WINNER!":PLAY"O1T60CDEFGABO2CDEFGABO3CDEFGABO4CDEFGABO5CDEFGAB":FORG=1TO2000:NEXTG:GOTO101
101 HCOLOR6:HLINE(110,75)-(210,120),PRESET,BF:HLINE(110,75)-(210,120),PSET,B:HLINE(115,80)-(205,115),PSET,B:HPAINT(112,77),6,6:HPRINT(15,11),"PLAY AGAIN":HPRINT(17,13),"(Y/N)?
102 I$=INKEY$:IFI$=""THEN102
103 IFI$="Y"THENRUN
104 IFI$="N"THEN131
105 GOTO102
106 IF Z=1THENHDRAW"BR7BD8"+DT$
107 IF Z=2THENHDRAW"BR2BD2"+DT$+"BR10BD12"+DT$
108 IF Z=3THENHDRAW"BR2BD2"+DT$+"BR5BD6"+DT$+"BR5BD6"+DT$
109 IF Z=4THENHDRAW"BR2BD2"+DT$+"BR10BD12"+DT$+"BU12"+DT$+"BD12BL10"+DT$
110 IF Z=5THENHDRAW"BR2BD2"+DT$+"BR10BD12"+DT$+"BU12"+DT$+"BD12BL10"+DT$+"BU6BR5"+DT$
111 IFZ=6THENHDRAW"BR2BD2"+DT$+"BD6"+DT$+"BD6"+DT$+"BR10"+DT$+"BU6"+DT$+"BU6"+DT$
112 RETURN
113 HLINE(6,6)-(150,48),PRESET,BF:HLINE(166,6)-(310,48),PRESET,BF:HLINE(9,165)-(150,175),PRESET,BF:HLINE(169,165)-(310,175),PRESET,BF:RETURN
114 HCOLOR1:HCIRCLE(H,100),35,,.4,.11,.9:HDRAW"BM"+STR$(X)+","+STR$(Y)+"D15":HPAINT(H,100),1,1:RETURN
115 HCOLOR0:HCIRCLE(H,100),13,,1.7:HPAINT(H,100),0,0:RETURN
116 HCOLOR6:HCIRCLE(H,90),5:HPAINT(H,90),6,6:RETURN
117 HCOLOR6:HCIRCLE(H,91),5:HPAINT(H,91),6,6:RETURN
118 HCOLOR2:HDRAW"BM"+STR$(H)+",80H10RF10RH10UHLGDFRULUR":RETURN
119 HCOLOR2:HDRAW"BM"+STR$(H)+",80E10RG10RE10HUERFDGLURUL":RETURN
120 HCOLOR5:HDRAW"BM"+STR$(X)+",121D8RU8":HCIRCLE(H,130),7:HPAINT(H,130),5,5:RETURN
121 HCOLOR3:HDRAW"BM"+STR$(H)+",110NFLF2D15LU15H2LF2D16R6DL6DR6":RETURN
122 HCOLOR3:HDRAW"BM"+STR$(H)+",114ND13LD13LU13D14R6DL6DR6":RETURN
123 HCOLOR3:HDRAW"BM"+STR$(H)+",112ND13LD13LU13D14R6DL6DR6":RETURN
124 HCOLOR3:HDRAW"BM"+STR$(H)+",103"+LR$:RETURN
125 HCOLOR3:HDRAW"BM"+STR$(H)+",105"+LR$:RETURN
126 HCOLOR3:HDRAW"BM"+STR$(H)+",105"+LR$:RETURN
127 HPRINT(3,21),"ALREADY HAVE IT":RETURN
128 HPRINT(3,21),"YOU HAVE ENOUGH":RETURN
129 HPRINT(23,21),"ALREADY HAVE IT":RETURN
130 HPRINT(23,21),"YOU HAVE ENOUGH":RETURN
131 POKE65496,0:WIDTH32:CLS:END
