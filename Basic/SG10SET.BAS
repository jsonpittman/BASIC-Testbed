10 CLS
20 PRINTTAB(8);"SG-10 PRINTER SET-UP"
30 PRINT
40 PRINTTAB(5);"1. SET COMPUTER BAUD RATE"
50 PRINTTAB(5);"2. SET TYPE SIZE"
60 PRINTTAB(5);"3. SET TYPE FONT"
70 PRINTTAB(5);"4. SET MARGINS"
80 PRINTTAB(5);"5. SET MISC FUNCTIONS"
90 PRINTTAB(5);"6. SET LINE SPACING"
100 PRINTTAB(5);"7. RE-INITIALIZE PRINTER"
110 PRINTTAB(5);"8. END PROGRAM"
120 PRINT
130 PRINTTAB(5);"SELECTION-->";
140 I$=INKEY$:IF I$="" THEN 140
150 IF VAL(I$)<1 OR VAL(I$)>8 THEN 140
160 PRINT I$
170 ON VAL(I$) GOSUB 200,290,480,960,1260,1370,1660,1670
180 PRINT#-2,CHR$(7);
190 GOTO 10
200 CLS
210 INPUT"BAUD RATE";BR
220 IF BR<>600 AND BR<>1200 AND BR<>2400 AND BR<>4800 AND BR<>9600 THEN PRINT"INVALID BAUD RATE. TRY AGAIN.":PRINT:GOTO210
230 IF BR=600 THEN POKE 150,88:RETURN
240 IF BR=1200 THEN POKE 150,41:RETURN
250 IF BR=2400 THEN POKE 150,18:RETURN
260 IF BR=4800 THEN POKE 150,6:RETURN
270 POKE 150,1
280 RETURN
290 CLS:PRINTTAB(9)"SET TYPE SIZE"
300 PRINT
310 PRINTTAB(5);"1. PICA"
320 PRINTTAB(5);"2. ELITE"
330 PRINTTAB(5);"3. CONDENSED"
340 PRINTTAB(5);"4. EXPANDED ON"
350 PRINTTAB(5);"5. EXPANDED OFF"
360 PRINTTAB(5);"6. NEVER MIND"
370 PRINT
380 PRINTTAB(5);"SELECTION-->";
390 I$=INKEY$:IFI$=""THEN390
400 IF VAL(I$)<1 OR VAL(I$)>6 THEN 390 ELSE PRINT I$
410 IF VAL(I$)=6 THEN RETURN
420 ON VAL(I$) GOTO 430,440,450,460,470
430 PRINT#-2,CHR$(27);CHR$(66);CHR$(1);:RETURN
440 PRINT#-2,CHR$(27);CHR$(66);CHR$(2);:RETURN
450 PRINT#-2,CHR$(27);CHR$(66);CHR$(3);:RETURN
460 PRINT#-2,CHR$(27);CHR$(87);CHR$(1);:RETURN
470 PRINT#-2,CHR$(27);CHR$(87);CHR$(0);:RETURN
480 CLS:PRINTTAB(9);"SET TYPE FONT"
490 PRINT
500 PRINTTAB(5);"1. NORMAL"
510 PRINTTAB(5);"2. ITALIC"
520 PRINTTAB(5);"3. NEAR LETTER QUALITY"
530 PRINTTAB(5);"4. PROPORTIONAL"
540 PRINTTAB(5);"5. DOUBLE-STRIKE"
550 PRINTTAB(5);"6. EMPHASIZED"
560 PRINTTAB(5);"7. UNDERLINING"
570 PRINTTAB(5);"8. NEVER MIND"
580 PRINT
590 PRINTTAB(5);"SELECTION-->";
600 I$=INKEY$:IF I$=""THEN 600
610 IF VAL(I$)<1 OR VAL(I$)>8 THEN 600 ELSE PRINT I$
620 IF VAL(I$)=8 THEN RETURN
630 ON VAL(I$) GOTO 640,650,660,720,780,840,900
640 PRINT#-2,CHR$(27);CHR$(53);:RETURN
650 PRINT#-2,CHR$(27);CHR$(52);:RETURN
660 CLS:PRINT"NEAR LETTER QUALITY..."
670 PRINT"   ...ON  (1)"
680 PRINT"   ...OFF (2)"
690 I$=INKEY$:IFI$=""THEN690 ELSE IF VAL(I$)<1 OR VAL(I$)>2 THEN 690 ELSE PRINT I$
700 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(66);CHR$(4);:RETURN
710 PRINT#-2,CHR$(27);CHR$(66);CHR$(5);:RETURN
720 CLS:PRINT"PROPORTIONAL..."
730 PRINT"   ...ON  (1)"
740 PRINT"   ...OFF (2)"
750 I$=INKEY$:IFI$=""THEN750 ELSE IF VAL(I$)<1 OR VAL(I$)>2 THEN 750 ELSE PRINT I$
760 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(112);CHR$(1);:RETURN
770 PRINT#-2,CHR$(27);CHR$(112);CHR$(0);:RETURN
780 CLS:PRINT"DOUBLE-STRIKE..."
790 PRINT"   ...ON  (1)"
800 PRINT"   ...OFF (2)"
810 I$=INKEY$:IFI$="" THEN 810 ELSE IF VAL(I$)<1 OR VAL(I$)>2 THEN 810 ELSE PRINT I$
820 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(71);:RETURN
830 PRINT#-2,CHR$(27);CHR$(70);:RETURN
840 CLS:PRINT"EMPHASIZED..."
850 PRINT"   ...ON  (1)"
860 PRINT"   ...OFF (2)"
870 I$=INKEY$:IFI$="" THEN 870 ELSE IF VAL(I$)<1 OR VAL(I$)>2 THEN 870 ELSE PRINT I$
880 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(69);:RETURN
890 PRINT#-2,CHR$(27);CHR$(70);:RETURN
900 CLS:PRINT"UNDERLINING..."
910 PRINT"   ...ON  (1)"
920 PRINT"   ...OFF (2)"
930 I$=INKEY$:IFI$=""THEN 930 ELSE IF VAL(I$)<1 OR VAL(I$)>2 THEN 930 ELSE PRINT I$
940 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(45);CHR$(1);:RETURN
950 PRINT#-2,CHR$(27);CHR$(45);CHR$(0);:RETURN
960 CLS:PRINTTAB(12);"MARGINS"
970 PRINT
980 PRINTTAB(5);"1. TOP"
990 PRINTTAB(5);"2. BOTTOM"
1000 PRINTTAB(5);"3. CANCEL TOP & BOTTOM"
1010 PRINTTAB(5);"4. LEFT"
1020 PRINTTAB(5);"5. RIGHT"
1030 PRINTTAB(5);"6. NEVER MIND"
1040 PRINT
1050 PRINTTAB(5);"SELECTION-->";
1060 I$=INKEY$:IFI$=""THEN1060 ELSE IF VAL(I$)<1 OR VAL(I$)>6 THEN 1060 ELSE PRINT I$
1070 IF VAL(I$)=6 THEN RETURN
1080 ON VAL(I$) GOTO 1090,1130,1170,1180,1220
1090 CLS
1100 INPUT"LINES FOR TOP MARGIN";TM
1110 IF TM<0 OR TM>255 THEN PRINT"INVALID MARGIN SELECTION.":PRINT:GOTO1100
1120 PRINT#-2,CHR$(27);CHR$(82);CHR$(TM);:RETURN
1130 CLS
1140 INPUT"LINES FOR BOTTOM MARGIN";BM
1150 IF BM<0 OR BM>255 THEN PRINT"INVALID MARGIN SELECTION.":PRINT:GOTO1140
1160 PRINT#-2,CHR$(27);CHR$(78);CHR$(BM);:RETURN
1170 PRINT#-2,CHR$(27);CHR$(79);:RETURN
1180 CLS
1190 INPUT"SPACES FOR LEFT MARGIN";LM
1200 IF LM<0 OR LM>50 THEN PRINT"INVALID MARGIN SELECTION.":PRINT:GOTO1190
1210 PRINT#-2,CHR$(27);CHR$(77);CHR$(LM);:RETURN
1220 CLS
1230 INPUT"SPACES FOR RIGHT MARGIN";RM
1240 IF RM<0 OR RM>132 THEN PRINT"INVALID MARGIN SELECTION.":PRINT:GOTO1230
1250 PRINT#-2,CHR$(27);CHR$(81);CHR$(RM);:RETURN
1260 CLS:PRINTTAB(4);"MISCELLANEOUS FUNCTIONS"
1270 PRINT
1280 PRINTTAB(5);"1. ZERO WITH SLASH"
1290 PRINTTAB(5);"2. ZERO WITHOUT SLASH"
1300 PRINTTAB(5);"3. NEVER MIND"
1310 PRINT
1320 PRINTTAB(5);"SELECTION-->";
1330 I$=INKEY$:IF I$="" THEN 1330 ELSE IF VAL(I$)<1 OR VAL(I$)>3 THEN 1330 ELSE PRINT I$
1340 IF VAL(I$)=3 THEN RETURN
1350 IF VAL(I$)=1 THEN PRINT#-2,CHR$(27);CHR$(92);CHR$(1);:RETURN
1360 PRINT#-2,CHR$(27);CHR$(92);CHR$(0);:RETURN
1370 CLS:PRINTTAB(10);"LINE SPACING"
1380 PRINT
1390 PRINTTAB(5);"1. SET TO 1/8   INCH"
1400 PRINTTAB(5);"2. SET TO 7/72  INCH"
1410 PRINTTAB(5);"3. SET TO 1/6   INCH"
1420 PRINTTAB(5);"4. SET TO n/72  INCH"
1430 PRINTTAB(5);"5. SET TO n/144 INCH"
1440 PRINTTAB(5);"6. SET TO n/216 INCH"
1450 PRINTTAB(5);"7. NEVER MIND"
1460 PRINT
1470 PRINTTAB(5);"SELECTION-->";
1480 I$=INKEY$:IFI$=""THEN1480 ELSE IF VAL(I$)<1 OR VAL(I$)>8 THEN 1480 ELSE PRINT I$
1490 IF VAL(I$)=7 THEN RETURN
1500 ON VAL(I$) GOTO 1510,1520,1530,1540,1580,1620
1510 PRINT#-2,CHR$(27);CHR$(48);:RETURN
1520 PRINT#-2,CHR$(27);CHR$(49);:RETURN
1530 PRINT#-2,CHR$(27);CHR$(50);:RETURN
1540 CLS
1550 INPUT"HOW MANY 72ths OF AN INCH";SP
1560 IF SP<0 OR SP>255 THEN PRINT"INPUT ERROR":PRINT:GOTO1550
1570 PRINT#-2,CHR$(27);CHR$(65);CHR$(SP);:RETURN
1580 CLS
1590 INPUT"HOW MANY 144ths OF AN INCH";SP
1600 IF SP<0 OR SP>255 THEN PRINT"INPUT ERROR":PRINT:GOTO 1590
1610 PRINT#-2,CHR$(27);CHR$(51);CHR$(SP);:RETURN
1620 CLS
1630 INPUT"HOW MANY 216ths OF AN INCH";SP
1640 IF SP<0 OR SP>255 THEN PRINT"INPUT ERROR":PRINT:GOTO1630
1650 PRINT#-2,CHR$(27);CHR$(51);CHR$(SP);:RETURN
1660 PRINT#-2,CHR$(27);CHR$(64);:RETURN
1670 CLS:END
