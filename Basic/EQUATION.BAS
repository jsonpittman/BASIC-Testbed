1 '=============================================================
2 ' EQUATIONS 
3 ' BY LOUIS R. TOSCANO
4 ' MATHEMATICS DEPT.
5 ' MARTHA'S VINEYARD REGIONAL HIGH SCHOOL
6 ' P.O. BOX 2400
7 ' VINEYARD HAVEN, MA 02568
8 ' 617-693-1033 OR 617-693-13289 ' ============================================================
10 CLEAR50,32384:CLEAR1000
12 X=RND(-TIMER)
15 DIM N(5),P$(4),P(20)
20 TTL=0:SE=0:FT=0:ST=0
30 '----------------------------
40 ' ML DATA FOR ROCK SCREEN S/R
50 '----------------------------
60 DATA 8E,4,0,C6,80,A6,84,81,60,26,2,E7,84,A6,80,8C,6,0,26,F1,8E,4,0,A6,84,8B,10,A7,80,8C,6,0,26,F5,39
70 FOR X=32384 TO32418:READPP$:B=VAL("&H"+PP$):POKE X,B:NEXTX:DEFUSR0=32384
81 '-------------------
82 ' SECOND TRY REWARDS
83 '-------------------
84 DATA CORRECT,THAT'S IT,GOOD ANSWER,EXCELLENT,WAY TO GO,OUTSTANDING,YOU GOT IT
85 FORK=1TO7:READ REWARD$(K):NEXTK
100 GOTO 10000
400 '===========================================================
410 '                  GRAPHIC REWARDS
420 '===========================================================
430 '--------------
440 ' WOW! GRAPHICS
450 '--------------
460 CLS0
470 KOLOR=RND(8):KK=143+16*(KOLOR-1)
480 WOW$=STRING$(7,128)+CHR$(KK)+STRING$(3,128)+CHR$(KK)+STRING$(2,128)+CHR$(KK)+STRING$(2,140+16*(KOLOR-1))+CHR$(KK)+STRING$(2,128)+CHR$(KK)+STRING$(3,128)+CHR$(KK)+STRING$(2,128)+CHR$(138+16*(KOLOR-1))+STRING$(11,128)
490 WOW$=WOW$+CHR$(KK)+CHR$(128)+CHR$(KK)+CHR$(128)+CHR$(KK)+STRING$(2,128)+CHR$(KK)+STRING$(2,128)+CHR$(KK)+STRING$(2,128)+CHR$(KK)+CHR$(128)+CHR$(KK)+CHR$(128)+CHR$(KK)+STRING$(2,128)+CHR$(136+16*(KOLOR-1))+STRING$(11,128)
500 WOW$=WOW$+STRING$(5,140+16*(KOLOR-1))+STRING$(2,128)+STRING$(4,140+16*(KOLOR-1))+STRING$(2,128)+STRING$(5,140+16*(KOLOR-1))+STRING$(2,128)+CHR$(136+16*(KOLOR-1))
510 ZL=7:ZP=32*(ZL-1):PRINT@ZP,WOW$;
520 ZT=2:GOSUB4000:GOSUB6000:RETURN
530 '----------------
540 ' SUPER! GRAPHICS
550 '----------------
560 CLS0:FORZP=0TO384STEP96:KOLOR=RND(8):K1=143+16*(KOLOR-1):K2=140+16*(KOLOR-1)
570 SUPER$=STRING$(4,128)+CHR$(K1)+STRING$(2,K2)+STRING$(2,128)+CHR$(K1)+CHR$(128)+CHR$(K1)+STRING$(2,128)+CHR$(K1)+CHR$(K2)+CHR$(K1)+STRING$(2,128)+CHR$(K1)+STRING$(2,K2)+STRING$(2,128)+CHR$(K1)+CHR$(K2)+CHR$(K1)+CHR$(128)+CHR$(138+16*(KOLOR-1))
580 SUPER$=SUPER$+STRING$(7,128)+STRING$(2,K2)+CHR$(K1)+STRING$(2,128)+CHR$(K1)+CHR$(128)+CHR$(K1)+STRING$(2,128)+CHR$(K1)+STRING$(2,K2)+STRING$(2,128)+CHR$(K1)+CHR$(K2)+STRING$(3,128)+CHR$(K1)+CHR$(K2)+CHR$(142+16*(KOLOR-1))+CHR$(128)
590 SUPER$=SUPER$+CHR$(136+16*(KOLOR-1))
600 SUPER$=SUPER$+STRING$(7,128)+STRING$(3,K2)+STRING$(2,128)+STRING$(3,K2)+STRING$(2,128)+CHR$(K2)+STRING$(4,128)+STRING$(3,K2)+STRING$(2,128)+CHR$(K2)+CHR$(128)+CHR$(132+16*(KOLOR-1))+CHR$(128)+CHR$(136+16*(KOLOR-1))
610 PRINT@ZP,SUPER$;:NEXTZP
620 ZT=2:GOSUB4000:GOSUB6000:RETURN
630 '-----------------
640 'DYNAMITE GRAPHICS
650 '-----------------
660 CLS0:POKE359,57:SCREEN0,1:PRINT@204,CHR$(191);STRING$(17,188);CHR$(191);:PRINT@236,CHR$(191);STRING$(17,128);CHR$(191);:PRINT@268,CHR$(191);STRING$(17,179);CHR$(191);:PRINT@227,STRING$(9,"-");:PRINT@238,"D Y N A M I T E";
670 FOR ZJ=1TO8:PRINT@227,"#";:FOR ZT=1TO 40:NEXTZT:PRINT@227," ";:FOR ZT=1TO20:NEXTZT:NEXTZJ
680 FOR ZP=227 TO 235:FORZJ=1TO2:PRINT@ZP,"#";:FORZT=1TO40:NEXTZT:PRINT@ZP," ";:FORZT=1TO20:NEXTZT:NEXTZJ:PRINT@ZP,CHR$(128);:NEXTZP
690 GOSUB6000:RETURN
700 '------------------
710 ' CORRECT! GRAPHICS
720 '------------------
730 CLS0:FORZP=0TO384STEP96:KOLOR=RND(7)+1:KOLOR=16*(KOLOR-1):C0$=CHR$(128):C1$=CHR$(143+KO):C2$=CHR$(140+KO):C3$=CHR$(142+KO):C4$=CHR$(132+KO):C5$=CHR$(133+KO)
740 CRRECT$=C0$+C0$+C1$+C2$+C2$+C0$+C1$+C2$+C1$+C0$+C1$+C2$+C1$+C0$+C1$+C2$+C1$+C0$+C1$+C2$+C2$+C0$+C1$+C2$+C2$+C0$+C2$
745 CRRECT$=CRRECT$+C1$+C2$+C0$+C5$+C0$
750 CRRECT$=CRRECT$+C0$+C0$+C1$+C0$+C0$+C0$+C1$+C0$+C1$+C0$+C1$+C2$+C3$+C0$+C1$+C2$+C3$+C0$+C1$+C2$+C0$+C0$+C1$+C0$+C0$+C0$+C0$+C1$+C0$+C0$+C4$+C0$
760 CRRECT$=CRRECT$+C0$+C0$+C2$+C2$+C2$+C0$+C2$+C2$+C2$+C0$+C2$+C0$+C4$+C0$+C2$+C0$+C4$+C0$+C2$+C2$+C2$+C0$+C2$+C2$+C2$+C0$+C0$+C2$+C0$+C0$+C4$
770 PRINT@ZP,CRRECT$;:NEXTZP:ZT=2:GOSUB4000:GOSUB6000:RETURN
997 '===========================================================
998 '               GENERAL PURPOSE SUBROUTINES
999 '===========================================================
1000 GOTO1070:'INPUT
1070 ZZ=1:ZA$=INKEY$:ZA$="":ZZ$=""
1080 ZZ$=INKEY$:IF ZZ$<>""GOTO1090
1082 ZZ=ZZ+1:IFZZ>8THENZZ=2
1084 PRINT@ZP+LEN(ZA$),CHR$(143+16*(ZZ-1));:FORZT=1TO10:NEXTZT:GOTO1080
1090 IF ZZ$>CHR$(13) GOTO1130
1100 IF ZZ$=CHR$(12) THEN RETURN
1110 IF ZZ$=CHR$(13) AND LEN(ZA$)<>0 GOTO1220
1120 IF ZZ$=CHR$(8) GOTO1180
1130 IF LEN(ZA$)=XM GOTO1080
1140 IF XT=2 GOTO1160
1150 IF ZZ$>CHR$(64) AND ZZ$<CHR$(91) OR ZZ$=" " OR ZZ$="'" OR ZZ$="-" THEN1170 ELSE 1080
1160 IF(ZZ$<CHR$(48) OR ZZ$>CHR$(57))AND ZZ$<>"-" GOTO1080
1170 ZA$=ZA$+ZZ$:PRINT@ZP,ZA$;:GOTO1080
1180 IF LEN(ZA$)<1 GOTO1080
1190 PRINT@ZP+LEN(ZA$)-1,"  ";
1200 ZA$=LEFT$(ZA$,LEN(ZA$)-1)
1210 PRINT@ZP,ZA$;:GOTO1080
1220 PRINT@ZP+LEN(ZA$)," ";:IF XT=2 THEN ZA=VAL(ZA$)
1230 RETURN
1500 GOTO1590:'BOX AROUND A MESSAGE
1590 CH=140+16*(KOLOR-1)
1600 PRINT@ZP-34,STRING$(ZM+4,CH);
1610 CH=CH-9:PRINT@ZP-2+ZN*32,STRING$(ZM+4,CH);:CH=CH+12
1620 FOR ZK=ZP-34TO ZP-34+(ZN+1)*32 STEP 32
1630 PRINT@ZK,CHR$(CH);:PRINT@ZK+ZM+3,CHR$(CH);:NEXT ZK
1635 FORZK=ZP-1 TO ZP-1+(ZN-1)*32STEP32:PRINT@ZK,CHR$(133);:PRINT@ZK+ZM+1,CHR$(138);:NEXTZK
1640 XP=ZP:FOR ZK=1 TO ZN:PRINT@XP,STRING$(ZM,32);:XP=XP+32:NEXT ZK
1650 RETURN
2000 GOTO2110:'PROMPT
2110 GOSUB5000:IF XM<>0 GOTO2140
2120 FOR ZK=1 TO 900:NEXT ZK
2130 PRINT@ZP,STRING$(LEN(ZM$),32);:GOTO2170
2140 ZP=ZP+LEN(ZM$)+1
2150 GOSUB1000
2160 IF XT=3 AND LEFT$(ZA$,1)<>"Y"AND LEFT$(ZA$,1)<>"N"GOTO2110
2170 RETURN
2500 GOTO2570:'CENTER MESSAGE
2570 ZP=32*(ZL-1)+16-LEN(ZM$)/2:RETURN
2600 ZP=32*(ZL-1)+16-LEN(ZM$)/2:GOSUB5000:RETURN
3000 GOTO3050:'CLEAR TO END OF LINE
3050 ZK=INT((ZP+32)/32)*32-ZP
3060 PRINT@ZP,STRING$(ZK,32);:RETURN
3200 GOTO3250:'CLEAR TO END OF SCREEN FAST
3250 ZI=512-ZP:IFZI<255 GOTO 3270
3260 PRINT@ZP,STRING$(256-ZP,32);:PRINT@256,STRING$(255,32);:GOTO3280
3270 PRINT@ZP,STRING$(ZI-1,32);
3280 RETURN
3500 GOTO3600:'FLASH S/R
3600 ZT=.4:FORI0=1TO5:PRINT@Z1,STRING$(LEN(Z1$),32);:IFZ2$<>""THENPRINT@Z2,STRING$(LEN(Z2$),32);:IFZ3$<>""THENPRINT@Z3,STRING$(LEN(Z3$),32);
3610 GOSUB4000:PRINT@Z1,Z1$;:IFZ2$<>""THENPRINT@Z2,Z2$;:IFZ3$<>""THENPRINT@Z3,Z3$;
3620 GOSUB4000:NEXTI0
3630 Z2$="":Z3$="":RETURN
4000 GOTO4100:'TIME DELAY
4100 TIMER=0
4110 IF TIMER<ZT*60THEN4110
4120 RETURN
4500 GOTO4600:'ENTER KEY S/R
4600 Z1$="":Z2$="PRESS THE enter key TO CONTINUE":PRINT@480,Z2$;:K$=INKEY$
4610 ZZ$=INKEY$:IFZZ$=""THEN4610ELSEIFZZ$<>CHR$(13)THEN4610
4620 FORK9=1TOLEN(Z2$):Z1$=Z1$+" ":Z3=ASC(RIGHT$(Z2$,1)):Z2$=LEFT$(Z2$,LEN(Z2$)-1):PRINT@480,Z1$;Z2$;:NEXTK9:Z2$="":RETURN
5000 GOTO5100:'PRINT SLOWLY
5100 FOR ZI=1 TO LEN(ZM$):PRINT@ZP,LEFT$(ZM$,ZI);:ZT=.05:GOSUB4000:NEXT ZI:RETURN
6000 GOTO 6020:'ROCK SCREEN
6020 POKE 359,57:SCREEN 0,1:FOR ZI=1TO31:A=USR(0):PLAY"L200AGB":NEXTZI:POKE65314,5:RETURN
9970 '==========================================================
9980 '                    MAIN PROGRAM
9990 '==========================================================
9999 '-------------
10000 'OPENING PAGE
10010 '------------
10020 CLS:POKE359,57:SCREEN0,1:FOR ZH=0 TO 63:SET(ZH,0,5):SET(ZH,31,5):NEXT ZH
10030 FOR ZV=0 TO 31:SET(0,ZV,5):SET(63,ZV,5):NEXT ZV
10040 ZM$="EQUATIONS":ZL=5:GOSUB2600
10050 ZM$="BY  LOUIS R. TOSCANO":ZL=8:GOSUB2600
10060 ZM$="P.O. BOX 2400":ZL=9:GOSUB2600
10070 ZM$="VINEYARD HAVEN, MA 02568":ZL=10:GOSUB2600
10080 ZM$="TEL: (617)-693-1328":ZL=11:GOSUB2600
10090 ZM$="COPYRIGHT 1984":ZL=12:GOSUB2600
10100 ZT=3:GOSUB4000
10110 ZM$="PRESS enter TO BEGIN.":ZL=15:GOSUB2600
10120 ZZ$=INKEY$:IF ZZ$<>CHR$(13) THEN ZX=RND(100):GOTO10120
10130 CLS:POKE65314,5:ZM$="HELLO!":ZP=64:GOSUB5000
10140 ZM$="WHAT IS YOUR NAME?":ZP=128:GOSUB 5000:ZP=147:XM=10:XT=1:GOSUB1000:N$=ZA$
10150 ZM$="HELLO, "+N$+"!":ZP=192:GOSUB5000
10160 ZP=256:ZM$="WE ARE GOING TO PRACTICE SOLVINGEQUATIONS.":GOSUB5000
10170 ZP=352:ZM$="DO YOU WANT DIRECTIONS? ":GOSUB5000:ZP=ZP+LEN(ZM$):XM=3:GOSUB1000
10180 B$=LEFT$(ZA$,1)
10190 IF B$<>"Y"ANDB$<>"N"THENZP=352:GOSUB3000:GOTO10170
10200 IFB$="N"GOTO10350
10210 '------------
10220 'INSTRUCTIONS
10230 '------------
10240 CLS:ZM$= "IN THE FOLLOWING LESSON, A SET  OF TEN EQUATIONS WILL APPEAR,   ONE AT A TIME, ON THE SCREEN.":ZP=0:GOSUB5000:ZM$="COPY THE EQUATION ONTO A PIECE  OF PAPER AND SOLVE.":ZP=128:GOSUB5000
10250 ZM$="TAKE YOUR TIME AND BE SURE TO   CHECK YOUR ANSWER.":ZP=224:GOSUB5000:GOSUB4500:CLS
10260 ZM$="YOU WILL HAVE TWO CHANCES FOR   EACH PROBLEM.":ZP=0:GOSUB5000:ZM$="YOU WILL RECEIVE 10 POINTS FOR  EACH EQUATION YOU SOLVE CORRECT-LY ON THE FIRST TRY, BUT ONLY   FIVE POINTS ON THE SECOND TRY.":ZP=96:GOSUB5000
10270 ZM$="IF AFTER TWO ATTEMPTS YOU ARE   STILL NOT SUCCESSFUL, I WILL    SHOW YOU HOW I SOLVED IT.":ZP=256:GOSUB5000:GOSUB4500:CLS
10280 ZM$="YOU MAY TRY AS MANY SETS OF TEN AS YOU LIKE. I WILL KEEP SCORE  FOR YOU.":ZP=0:GOSUB5000:GOSUB4500
10290 CLS:ZM$="THERE ARE THREE LEVELS OF DIFFI-CULTY YOU MAY CHOOSE.":ZP=0:GOSUB5000:ZM$="LEVEL 1 INVOLVES EQUATIONS THAT CAN BE SOLVED IN ONE STEP, SUCH AS:":ZP=96:GOSUB5000
10300 ZM$="-15X = 75":ZL=7:GOSUB2600:ZM$="OR":ZL=9:GOSUB2600:ZM$="X - 8 = -13":ZL=11:GOSUB2600:GOSUB4500
10310 CLS:ZM$="LEVEL 2 INVOLVES EQUATIONS THAT REQUIRE TWO OR MORE STEPS FOR A SOLUTION, SUCH AS:":ZP=0:GOSUB5000:ZM$="-3X + 12 = -6":ZL=5:GOSUB2600:ZM$="OR EQUATIONS THAT REQUIRE THE   USE OF THE DISTRIBUTIVE PROPERTYSUCH AS:":ZP=192
10320 GOSUB5000:ZM$="3(X - 8) = 36":ZL=10:GOSUB2600:GOSUB4500
10330 CLS:ZM$="LEVEL 3 INVOLVES EQUATIONS THAT HAVE THE VARIABLE ON BOTH SIDES,SUCH AS:":ZP=0:GOSUB5000:ZM$="7X - 8 = -2X + 10":ZL=5:GOSUB2600:ZM$="YOU MAY ALSO CHOOSE LEVEL 4 IF  YOU WISH TO SOLVE A MIXTURE OF  THE THREE TYPES OF EQUATIONS."
10340 ZP=224:GOSUB5000:GOSUB4500
10350 CLS:ZM$="CHOOSE YOUR LEVEL:":ZL=1:GOSUB2600:ZM$="1.  ONE-STEP EQUATIONS":ZP=64:GOSUB5000:ZM$="2.  TWO OR MORE STEPS":ZP=96:GOSUB5000:ZM$="3.  VARIABLE ON BOTH SIDES":ZP=128:GOSUB5000
10360 ZM$="4.  A MIXTURE OF ALL THREE":ZP=160:GOSUB5000
10370 ZM$="1, 2, 3, OR 4? ":ZL=8:GOSUB2600:LINEINPUT LV$:IF VAL(LV$)<1 OR VAL(LV$)>4 THEN ZP=192:GOSUB3000:GOTO10370
10380 LV=VAL(LV$):ZM$="YOU CHOSE LEVEL "+LV$+".":ZL=10:GOSUB2600:ZM$="GOOD LUCK, "+N$+".":ZL=12:GOSUB2600:GOSUB4500
10390 '------------
10400 'START LESSON
10410 '------------
10420 SC=0:SE=SE+1:L1=0:L2=0:FOR PRB=1 TO 10
10430 IF LV=4 THEN LEVEL=RND(3) ELSE LEVEL=LV
10440 '------------------------
10450 'GENERATE RANDOM EQUATION
10460 '------------------------
10470 IF LEVEL=1 THEN I1=2 ELSE I1=4
10480 IF LEVEL=3 THEN TYPE=RND(3) ELSE TYPE=RND(4)
10490 FOR I=1 TO I1
10500 IF LEVEL=1 THEN N(I)=RND(19)+1 ELSE N(I)=RND(9)
10510 SIGN=RND(2):IF SIGN=2 THEN SIGN$(I)="+":GOTO10530
10520 N(I)=-N(I):SIGN$(I)="-"
10530 IF LEVEL=3 AND N(1)=1 THEN  10500
10540 IF LEVEL=2 AND (TYPE=2 OR TYPE=4) AND N(1)=1 THEN10500
10550 IF LEVEL=3 AND N(4)=N(1) THEN 10500
10560 NEXTI
10570 '-----------------
10580 ' DISPLAY EQUATION
10590 '-----------------
10600 CLS:ZM$="PROBLEM #"+STR$(PRB):ZL=1:GOSUB2600
10610 ON LEVEL GOTO 10650,10850,11080
10620 '-----------------
10630 'LEVEL 1 EQUATIONS
10640 '-----------------
10650 ON TYPE GOTO 10660,10660,10720,10760
10660 ANS = N(2) - N(1)
10670 IF N(1) < 0 THEN N(1) = ABS(N(1))
10680 PRINT@69,"X ";SIGN$(1);:IF N(1)<0 THEN PRINT" ";
10690 X9=POS(0):PRINTN(1)"=";: IF N(2)<0 THEN PRINT " ";
10700 X0=POS(0):PRINT N(2)
10710 GOTO 11410
10720 ANS = N(2)
10730 N(3) = N(1) * N(2)
10740 PRINT @ 69,N(1);"X ";TAB(10)"= ";N(3)
10750 GOTO 11410
10760 ANS = N(1)*N(2)
10770 PRINT@ 70,"X";
10780 PRINT@ 106,"=";STR$(N(2));
10790 PRINT @101,STRING$(LEN(STR$(N(1)))+1,"-");
10800 PRINT @ 133,N(1);
10810 GOTO 11410
10820 '------------------
10830 ' LEVEL 2 EQUATIONS
10840 '------------------
10850 ON TYPE GOTO 10860  ,10910  ,10970  ,11010  
10860 N=(N(1)*N(2))+N(3):ANS=N(2)
10870 IF N(3)<0 THEN N(3)=ABS(N(3))
10880 IF SIGN$(3)="-" THEN N(3)=ABS(N(3))
10890 PRINT@ 40,USING"###X ! ## =####";N(1);SIGN$(3);N(3);N
10900 GOTO 11410
10910 ANS=(N(3)-N(2))*N(1)
10920 IF N(2)<0 THEN N(2)=ABS(N(2))
10930 PRINT@40,"X";
10940 PRINT@71,USING"--- ! ### = ###";SIGN$(2),N(2),N(3)
10950 PRINT@103,N(1)
10960 GOTO 11410
10970 ANS=N(2):N=(N(1)*N(2))+N(3)
10980 IF N(3)<0 THEN N(3)=ABS(N(3))
10990 PRINT@40,USING"### = ###X ! ###";N;N(1);SIGN$(3);N(3)
11000 GOTO 11410
11010 ANS=N(2):TP=N(3):N=N(1)*(N(2)+N(3))
11020 IF N(3)<0 THEN TP=ABS(N(3))
11030 PRINT@40,USING"### (X ! ###) = ####";N(1);SIGN$(3);TP;N
11040 GOTO11410
11050 '-------------------
11060 ' LEVEL 3 EQUATIONS
11070 '------------------
11080 ANS=N(2)
11090 ON TYPE GOTO 11100,11200,11290
11100 P1=N(1)*N(2):P2=N(4)*N(2)
11110 P3=P1-P2:N(5)=P3+N(3)
11120 IF N(5)=0 THEN GOTO 10470
11130 T1=N(1)-N(4):T2=N(5)-N(3)
11140 P$="+": IF N(3)>0 THEN 11160
11150 N(3)=ABS(N(3)): P$="-"
11160 Q$="+": IF N(5)>0 THEN 11180
11170 N(5)=ABS(N(5)):Q$="-"
11180 PRINT@36,USING"###X ! ### = ###X ! ###";N(1);P$;N(3);N(4);Q$;N(5);
11190 GOTO 11410
11200 P1=N(1)*N(2):P2=N(4)*N(2)
11210 N(5)=P1-P2
11220 IF N(5)=0 THEN GOTO 10470
11230 T1=N(1)-N(4)
11240 IF N(4)>0 THEN P$="+"
11250 IF N(4)<0 THEN P$="-"
11260 N(4)=ABS(N(4))
11270 PRINT@36,USING"###X = #### ! ###X";N(1);N(5);P$;N(4);
11280 GOTO 11410
11290 P1=N(1)*N(2):P2=N(4)*N(2)
11300 P5=P1-P2:N(5)=P5+N(3)
11310 IF N(5)=0 THEN GOTO 10470
11320 T1=N(1)-N(4):T2=N(5)-N(3)
11330 P$="+":IFN(1)>0 THEN 11350
11340 N(1)=ABS(N(1)):P$="-"
11350 Q$="+":IFN(4)>0THEN11370
11360 N(4)=ABS(N(4)):Q$="-"
11370 PRINT@36,USING"### ! ###X = ### ! ###X";N(3);P$;N(1);N(5);Q$;N(4);
11380 '---------------------------------
11390 'INPUT ANSWER AND CHECK IF CORRECT
11400 '---------------------------------
11410 TRY=1
11420 IF TRY=1 THEN KOLOR=8 ELSEKOLOR=3
11430 ZM$="X =     ":ZL=9:GOSUB2500:ZM=LEN(ZM$)+2:ZN=3:ZP=ZP-32:GOSUB1500:ZP=ZP+32:GOSUB5000
11440 ZP=ZP+5:XM=4:XT=2:GOSUB1000
11450 IF ZA=ANS THEN 11510
11460 TRY=TRY+1:IF TRY=3 THEN 11640
11470 ZL=13:ZM$="SORRY, "+N$+". TRY AGAIN.":GOSUB2600:ZT=2:GOSUB4000:ZP=192:GOSUB3200:GOTO11420
11480 '-------------
11490 ' RIGHT ANSWER
11500 '-------------
11510 IF TRY=2 THEN 11600
11520 SC=SC+10:L1=L1+1
11530 IF L1=1 THEN REWARD=2
11540 IF L1=2 THEN REWARD=4
11550 IF L1=3 THEN REWARD=3
11560 IF L1=4 THEN REWARD=1
11570 IF L1>4 THEN REWARD=RND(4)
11580 ON REWARD GOSUB460,560,660,730
11590 GOTO 12960
11600 SC=SC+5:L2=L2+1:REWARD=RND(7):ZM$=REWARD$(REWARD)+", "+N$+"!":ZL=13:GOSUB2600:ZT=1.5:GOSUB4000:GOTO12960
11610 '-----------------
11620 ' DISPLAY SOLUTION
11630 '-----------------
11640 ZP=352:GOSUB3200
11650 ZP=384:ZM$="SORRY, "+N$+".":GOSUB5000
11660 ZM$="THE ANSWER IS:  X = "+STR$(ANS):ZP=416:GOSUB5000
11670 ZT=3:GOSUB4000:ZP=192:GOSUB3200:POKE65314,5:ZP=0:GOSUB3000
11680 ZL=1:ZM$="THIS IS HOW I DID IT.":GOSUB2600:PRINT@352,STRING$(32,131);:ZT=1:GOSUB4000:ZT=3
11690 ON LEVEL GOTO 11730,11950,12460
11700 '-----------------
11710 ' LEVEL 1 SOLUTION
11720 '-----------------
11730 ON TYPE GOTO 11740,11740,11810,11870
11740 S$ ="-":IF SIGN$(1) ="-" THEN S$="+"
11750 IF SIGN$(1) ="+" THEN N(1) = (-1)*N(1)
11760 Z1=384:Z1$="ADD "+STR$(N(1))+" TO BOTH SIDES.":GOSUB3500
11770 PRINT@ 96+X9,N(1);: PRINT@ 96+X0+(LEN(STR$(N(2)))-LEN(STR$(N(1)))),N(1);
11780 PRINT@130,STRING$(16,"-");
11790 PRINT@ 164,"X";: PRINT@ 160+X9+LEN(STR$(N1))," =";: IF ANS<0 THEN PRINT " ";ANS; ELSE PRINT ANS;
11800 ZT=2:GOSUB4000:GOSUB4500:GOTO12960
11810 Z1=384:Z1$="DIVIDE BOTH SIDES BY "+STR$(N(1))+".":GOSUB3500
11820 PRINT @ 101,STRING$(LEN(STR$(N(1)))+1,"-")"    "STRING$(LEN(STR$(N(3)))+1,"-");:PRINT@ 133,N(1)"    "N(1):GOSUB4000
11830 ZT=2:GOSUB4000:ZL=6:ZM$="THIS REDUCES TO ....":GOSUB2600
11840 PRINT @ 202,N(3):PRINT @ 230,"X = "STRING$(LEN(STR$(N(3)))+1,"-");:PRINT @ 266,N(1):GOSUB4000
11850 ZT=2:GOSUB4000:ZL=11:ZM$="AND FINALLY .... X =":GOSUB2600:IF ANS<0 THEN PRINT " "ANS; ELSE PRINT ANS;
11860 ZT=2:GOSUB4000:GOSUB4500:GOTO12960
11870 N1$=STR$(N(1)):IF N(1)>=0THEN N1$=RIGHT$(N1$,LEN(N1$)-1)
11880 Z1=384:Z1$="MULTIPLY BOTH SIDES BY "+STR$(N(1)):GOSUB3500
11890 N2$=STR$(N2):PRINT@99-LEN(N1$),"("N1$")";:PRINT@108+LEN(N2$),"("N1$")";
11900 ZT=2:GOSUB4000:ZP=192:ZM$="THIS REDUCES TO  X =":GOSUB5000:IFN(1)<0THENPRINT" ";
11910 PRINT N(1)"(";:IFN(2)<0THENPRINT" ";
11920 PRINT STR$(N(2))")";:GOSUB4000
11930 ZT=2:GOSUB4000:ZL=9:ZM$="AND FINALLY   X = "+STR$(ANS):GOSUB2600
11940 GOSUB 4500:GOTO12960
11950 '-----------------
11960 ' LEVEL 2 SOLUTION
11970 '-----------------
11980 ON TYPE GOTO 11990  ,12090  ,12210 ,12330 
11990 IF SIGN$(3)="+" THEN 12010  
12000 S$="+": TP=N+N(3): GOTO 12020  
12010 S$="-": TP=N-N(3)
12020 PRINT@78,USING"!##   !##";S$;N(3);S$;N(3);
12030 PRINT@384,"ADD "S$;N(3)"TO BOTH SIDES."
12040 GOSUB4000 :PRINT@105,STRING$(17,"-");:PRINT@136,USING"###X      =####";N(1);TP
12050 GOSUB4000 :PRINT@168,"----        ----";:PRINT@416,"DIVIDE BOTH SIDES BY "N(1)".";
12060 PRINT@200,USING"###         ###";N(1);N(1);
12070 GOSUB 4000 :PRINT@267,USING"X      = ###";ANS
12080 GOSUB4000:GOSUB4500:GOTO12960
12090 IF SIGN$(2)="+"THEN12110  
12100 S$="+":TP=N(3)+N(2):GOTO12120  
12110 S$="-":TP=N(3)-N(2)
12120 PRINT@141,USING"!##   !##";S$;N(2);S$;N(2);:PRINT@384,"ADD "S$;N(2)"TO BOTH SIDES.";
12130 PRINT@167,STRING$(17,"-");
12140 GOSUB4000 :PRINT@200,"X";
12150 PRINT@231,USING"---       = ###";TP
12160 PRINT@263,N(1);
12170 GOSUB4500:PRINT@234,"("STR$(N(1))")";:PRINT@246,"("STR$(N(1))")";
12180 PRINT@416,"MULTIPLY BOTH SIDES BY "N(1);
12190 GOSUB4000 :PRINT@328,"X        = ";ANS
12200 GOSUB4000:GOSUB4500:GOTO12960
12210 IF SIGN$(3)="+"THEN12230 
12220 S$="+":TP=N+N(3):GOTO12240 
12230 S$="-":TP=N-N(3)
12240 PRINT@72,USING"!##          !##";S$;N(3);S$;N(3);:PRINT@384,"ADD "S$N(3);"TO BOTH SIDES.";
12250 PRINT@104,STRING$(16,"-");
12260 GOSUB4000 :PRINT@136,USING"### = ###X";TP;N(1);
12270 GOSUB4500:PRINT@140," ";:PRINT@168,"--- = ----";
12280 PRINT@416,"DIVIDE BOTH SIDES BY ";N(1)
12290 PRINT@200,USING"###   ###";N(1),N(1)
12300 GOSUB4500 :PRINT@264,USING"### =    X";ANS;:GOSUB4000
12310 PRINT@291,"OR     X = "ANS;
12320 GOSUB4000:GOSUB4500:GOTO12960
12330 TP=N(1)*N(3):IF TP<0 THEN 12350 
12340 S$="+":S2$="-":GOTO12360 
12350 S$="-":S2$="+":TP=ABS(TP)
12360 PRINT@74,USING"###X ! ###  = ####";N(1);S$;TP;N;
12370 PRINT@384,"DISTRIBUTE "N(1);
12380 GOSUB4500 :PRINT@113,USING"!##     !##";S2$;TP;S2$;TP;
12390 PRINT@416,"ADD "S2$;TP;"TO BOTH SIDES."
12400 PRINT@136,STRING$(20,"-")
12410 GOSUB4000 :PRINT@170,USING"###X        = ####";N(1);ANS*N(1);
12420 GOSUB4500:PRINT@448,"DIVIDE BOTH SIDES BY ";N(1):PRINT@202,"----           ----"
12430 PRINT@234,USING"###            ###";N(1),N(1)
12440 GOSUB4000:PRINT@301,"X        =  ";ANS;
12450 GOSUB4000:GOSUB4500:GOTO12960
12460 '-----------------
12470 ' LEVEL 3 SOLUTION
12480 '-----------------
12490 ON TYPE GOTO 12500,12680,12790
12500 S$="-": IF N(4)>0 THEN 12520
12510 S$="+": N(4)=ABS(N(4))
12520 PRINT@68, USING "!##X         !##X";S$;N(4);S$;N(4);: PRINT@ 384, USING"ADD !##X";S$;N(4);:PRINT" TO BOTH SIDES."
12530 PRINT@ 100, STRING$(23,"-");
12540 IF Q$="+" THEN Q$=" "
12550 GOSUB 4000: PRINT@ 132,USING"###X ! ### =      ! ###";T1;P$;N(3);Q$;N(5);
12560 IF P$="+" THEN P1$="-": S$="-": GOTO 12580
12570 IF P$="-" THEN P1$="+": S$="+"
12580 GOSUB 4000:GOSUB4500
12590 PRINT@171, USING"!##          !##";P1$;N(3);P1$;N(3);: PRINT@ 416, "ADD "S$;N(3)"TO BOTH SIDES."
12600 PRINT@ 196,STRING$(23,"-")
12610 GOSUB 4000: PRINT@ 228, USING"###X       =        ###";T1;T2;
12620 IF T1=1 THEN 12650
12630 GOSUB 4000:GOSUB4500: PRINT@260,"----";: PRINT@280,"---";: PRINT@ 448,"DIVIDE BOTH SIDES BY ";T1
12640 PRINT@292,USING"###                 ###";T1,T1
12650 GOSUB 4000: PRINT@ 333,"X = ";N(2)
12660 GOSUB4000:GOSUB4500:GOTO 12960
12670 REM - SOLUTION FOR TYPE #2 -
12680 IF P$="+" THEN S$="-"
12690 IF P$="-" THEN S$="+"
12700 PRINT@ 68, USING "!##X          !##X";S$;N(4);S$;N(4);: PRINT@ 384, "ADD ";:PRINT USING "!##X";S$;N(4);:PRINT" TO BOTH SIDES."
12710 PRINT@100, STRING$(18,"-")
12720 GOSUB 4000: PRINT@132, USING"###X = ####";T1;N(5);:GOSUB4500
12730 IF T1=1 THEN 12760
12740 PRINT@164,"----   -----";: PRINT@ 416, "DIVIDE BOTH SIDES BY "T1
12750 PRINT @196,USING"###     ###";T1,T1
12760 GOSUB 4000: PRINT@263,"X = ";N(2)
12770 GOSUB4000:GOSUB4500:GOTO12960
12780 REM - SOLUTION FOR TYPE #3 -
12790 IF Q$="+" THEN Q1$="-"
12800 IF Q$="-" THEN Q1$="+"
12810 PRINT@74, USING "!##X         !##X";Q1$;N(4);Q1$;N(4);: PRINT@384, "ADD ";: PRINT USING "!##X";Q1$;N(4);:PRINT" TO BOTH SIDES."
12820 PRINT@ 100,STRING$(23,"-")
12830 P$="+": IF T1<0 THEN P$="-"
12840 GOSUB 4000: PRINT@132, USING "### ! ###X = ###";N(3);P$;ABS(T1);N(5);
12850 T3=ABS(N(3))
12860 S$="-": IF N(3)<0 THEN S$="+"
12870 GOSUB 4000:GOSUB4500
12880 PRINT@164, USING "!##          !##";S$;T3;S$;T3;: PRINT@416, "ADD "S$;T3;:PRINT" TO BOTH SIDES."
12890 PRINT@196,STRING$(18,"-")
12900 GOSUB 4000: PRINT@234, USING "###X = ###";T1;T2;
12910 IF T1=1 THEN 12940
12920 GOSUB 4000:GOSUB4500:PRINT@266, "----   ----";: PRINT@448, "DIVIDE BOTH SIDES BY";T1
12930 PRINT@298,USING"###    ###";T1,T1
12940 GOSUB4000: PRINT@333,"X = ";N(2);
12950 GOSUB4000: GOSUB4500:GOTO12960
12960 NEXT PRB
12970 CLS:POKE359,57:SCREEN0,1:ZM$="THAT'S THE END OF THIS SET OF   PROBLEMS, "+N$+".":ZP=160:GOSUB5000:ZM$="PRESS THE ENTER KEY TO SEE YOUR SCORE THUS FAR.":ZP=256:GOSUB5000:GOSUB4500:POKE65314,5
12980 '--------------
12990 ' DISPLAY SCORE
13000 '--------------
13010 CLS:KOLOR=4:ZP=34:ZM=28:ZN=11:GOSUB1500
13020 TTL=TTL+SC:FT=FT+L1:ST=ST+L2
13030 PRINT@65,STRING$(30,131+(KOLOR-1)*16);:PRINT@193,STRING$(30,140+(KOLOR-1)*16);:PRINT@257,STRING$(30,131+(KOLOR-1)*16);
13040 ZM$="***  CURRENT LESSON  ***":ZL=2:GOSUB2600:PRINT@98,"NO. OF";:PRINT@105,"CORRECT";:PRINT@113,"CORRECT";:PRINT@121,"GRADE";
13050 PRINT@130,"PROB.";:PRINT@137,"1ST TRY";:PRINT@145,"2ND TRY";
13060 FOR ZP=104TO168STEP32:CH$=CHR$(143+(KOLOR-1)*16):PRINT@ZP,CH$;:PRINT@ZP+8,CH$;:PRINT@ZP+16,CH$;:NEXT ZP
13070 PRINT@162,USING"  ##";10;:PRINT@169,USING"  ##";L1;:PRINT@177,USING"  ##";L2;:PRINT@185,USING" ###";SC;
13080 ZT=4:GOSUB4000
13090 ZP=416:IF SC<75 THEN ZM$="I THINK YOU NEED MORE PRACTICE, "+N$+".":GOSUB5000:GOTO 13140
13100 IF SC=75 OR SC=80 THEN ZM$="THAT'S A PRETTY GOOD SCORE FOR  THIS LESSON, "+N$+".":GOSUB5000:GOTO13140
13110 IF SC=85 THEN ZM$="THAT'S VERY GOOD, "+N$+".":GOSUB5000:GOTO13140
13120 IF SC=90 OR SC=95 THEN ZM$="THAT'S AN EXCELLENT SCORE FOR   THIS LESSON, "+N$+"!":GOSUB5000:GOTO13140
13130 IF SC=100 THEN ZM$="THAT'S A PERFECT SCORE FOR THIS LESSON, "+N$+"!!":GOSUB5000:ZT=.5:GOSUB4000:Z1$=ZM$:Z1=ZP:GOSUB3500
13140 ZT=2:GOSUB4000:ZP=416:GOSUB3200:ZM$="***  CUMULATIVE SCORE  ***":ZL=8:GOSUB2600
13150 FORZP=296TO360STEP32:PRINT@ZP,CH$;:PRINT@ZP+8,CH$;:PRINT@ZP+16,CH$;:NEXTZP
13160 PRINT@290,"NO. OF";:PRINT@297,"CORRECT";:PRINT@305,"CORRECT";:PRINT@313,"AVER.";
13170 PRINT@322,"PROB.";:PRINT@329,"1ST TRY";:PRINT@337,"2ND TRY";:PRINT@345,"GRADE";
13180 PRINT@354,USING" ###";10*SE;:PRINT@361,USING" ###";FT;:PRINT@369,USING" ###";ST;:PRINT@377,USING" ###";TTL/SE;:ZT=2:GOSUB4000:GOSUB4500:GOSUB6000
13190 CLS:ZM$="WOULD YOU LIKE TO TRY ANOTHER   SET OF PROBLEMS, "+N$+"?":ZP=224:XM=3:XT=3:GOSUB2000:IF LEFT$(ZA$,1)="Y"GOTO10350
13200 CLS:ZM$="BYE, "+N$+" ...":ZL=8:GOSUB2600:ZT=3:GOSUB4000:CLS
13210 '--
13220 END
13230 '--
1 'EQUATION SOLVER
2 '
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 REM EQUATION SOLVING PROGRAM
20 REM BY PERIOD 1 ALGEBRA CLASS AND MR. BOB TEAGUE
30 CLS
40 PRINT"EQUASOLV ....... EQUATION SOLVER"
50 PRINT"          M E N U"
60 PRINT
70 PRINT"     CHOOSE FORM:":PRINT
80 PRINT"     (1)  X+B=C"
90 PRINT"     (2)  AX=C"
100 PRINT"     (3)  AX+B=C"
110 PRINT"     (4)  X/A=C"
120 PRINT"     (5)  AX/B=C"
130 PRINT"     (6)  EXIT PROGRAM"
140 PRINT
150 PRINT"     ENTER CHOICE (1-6)"
160 INPUT C
170 IF C<1 OR C>6 THEN 150
180 ON C GOSUB 200,300,400,510,610,720
190 RUN
200 REM X+B=C TYPE SOLUTION
210 CLS:PRINT:PRINT"X+B=C SOLUTION"
220 PRINT
230 INPUT"ENTER B";B
240 INPUT"ENTER C";C
250 LET X=C-B
260 PRINT:PRINT"X +";B;"=";C:PRINT
270 PRINT"VALUE OF X IS ";X
280 PRINT:PRINT"ENTER TO CONTINUE"
290 INPUT C$:IF C$="" THEN RETURN
300 REM AX=C TYPE SOLUTION
310 CLS:PRINT:PRINT"AX=C SOLUTION"
320 PRINT
330 INPUT"ENTER A";A
340 INPUT"ENTER C";C
350 LET X=C/A
360 PRINT:PRINT A;"X =";C:PRINT
370 PRINT"VALUE OF X IS ";X
380 PRINT:PRINT"ENTER TO CONTINUE"
390 INPUT C$:IF C$="" THEN RETURN
400 REM AX+B=C TYPE SOLUTION
410 CLS:PRINT:PRINT"AX+B=C SOLUTION"
420 PRINT
430 INPUT"ENTER A";A
440 INPUT"ENTER B";B
450 INPUT"ENTER C";C
460 LET X=(C-B)/A
470 PRINT:PRINT A;"X +";B;"=";C:PRINT
480 PRINT"VALUE OF X IS ";X
490 PRINT:PRINT"ENTER TO CONTINUE"
500 INPUT C$:IF C$="" THEN RETURN
510 REM X/A=C TYPE SOLUTION
520 CLS:PRINT:PRINT"X/A=C SOLUTION"
530 PRINT
540 INPUT"ENTER A";A
550 INPUT"ENTER C";C
560 LET X=A*C
570 PRINT:PRINT"X /";A;"=";C:PRINT
580 PRINT"VALUE OF X IS ";X
590 PRINT:PRINT"ENTER TO CONTINUE"
600 INPUT C$:IF C$="" THEN RETURN
610 REM AX/B=C TYPE SOLUTION
620 CLS:PRINT:PRINT"AX/B=C SOLUTION"
630 PRINT
640 INPUT"ENTER A";A
650 INPUT"ENTER B";B
660 INPUT"ENTER C";C
670 LET X=(B*C)/A
680 PRINT:PRINT A;"X /";B;"=";C:PRINT
690 PRINT"VALUE OF X IS ";X
700 PRINT:PRINT"ENTER TO CONTINUE"
710 INPUT C$:IF C$="" THEN RETURN
720 CLS:END
