0 'PYRAMID2                     ** WRITTEN BY GEORGE QUELLHORST
1 'COPYRIGHT (C) SEPTEMBER 1991 ** BY FALSOFT, INC.             ** RAINBOW MAGAZINE
2 DATA 112,1,94,25,130,25,76,50,112,50,148,50,58,74,94,74,130,74,166,74,40,98,76,98,112,98,148,98,184,98,22,122,58,122,94,122,130,122,166,122,202,122,4,146,40,146,76,146,112,146,148,146,184,146,220,146
3 CLEAR1000:SH=1:POKE140,190:EXEC43350:DIM A(0,34),B(0,34),C(0,34),D(0,34),E(0,34),N$(13),X(29),F(29),Y(29),CA(52):R=RND(-TIMER)
4 POKE65495,0:CLS:IFSH=1 THENPRINT@194,"PLEASE WAIT. SHUFFLING CARDS":GOSUB53
5 GOSUB48:GOSUB43:T=25:DE=29
6 ' CURSOR CONTROL
7 P$=INKEY$
8 DRAW"C0BM"+STR$(X(T))+","+STR$(Y(T))+A$
9 IFDE=52 ANDMO=1 THENGOSUB63
10 IFP$=CHR$(8)THENT=T-1 ELSEIFP$=CHR$(9)THENT=T+1 ELSE13
11 IFT=>29THENT=1ELSEIFT=0THENT=28
12 IFF(T)=0 OR F(T)=2 THEN10ELSEEXEC
13 IFP$=CHR$(13)THENF(T)=2:GOTO19 ELSEIFP$=CHR$(94)THENEXEC:GOTO37
14 IFP$=CHR$(10) THENSH=0:CA(0)=AC:GOTO4ELSEIFP$=" "THENSH=1:GOTO4
15 DRAW"C1BM"+STR$(X(T))+","+STR$(Y(T))+A$
16 IFP$="Q"THENCLS:STOP
17 GOTO 7
18 ' THE ENTER KEY
19 GOSUB57:Q=0:EC=W:TT=T:T=0:GOSUB57:Q=0:TC=W
20 IFEC-TC=1 OR EC-TC=12 OR TC-EC=1 OR TC-EC=12 THENT=TT:GOTO22
21 GOSUB61:T=TT:F(T)=1:GOTO7
22 EXEC:H=T:GOSUB75:GOSUB71:H=0:GOSUB72
23 H=T-1:GOSUB76:H=T+1:GOSUB77:CN=T-(Y(T)/25):IFF(1)=2THEN67
24 L=CN-1:R=CN:IFY(L)=Y(R)THEN25ELSEIFX(T)>112THEN28 ELSEIFY(L)<>Y(R)THEN27
25 H=L:GOSUB70:H=R:GOSUB70
26 H=T-1:GOSUB73:H=T+1:GOSUB74:GOTO29
27 H=R:GOSUB70:H=T+1:GOSUB74:GOTO31
28 H=L:GOSUB70:H=T-1:GOSUB73:RS=1:GOTO29
29 IFF(T-1)=2 THENTT=T:T=L:F(L)=1:GOSUB47:GOSUB57:GOSUB55:Q=0:T=TT
30 IFRS=1THENRS=0:GOTO32
31 IFF(T+1)=2 THENTT=T:T=R:F(R)=1:GOSUB47:GOSUB57:GOSUB55:Q=0:T=TT
32 CA(0)=CA(T)
33 T=T+1:IFT=>29THENT=1
34 IFF(T)=0ORF(T)=2 THEN33
35 MO=1:GOTO7
36 ' THE UP-ARROW KEY
37 IFDE=52 THENGOSUB61:GOTO7
38 TT=T:DE=DE+1:T=DE:X=10:Y=10:PUT(X,Y)-(X+30,Y+42),B,PSET
39 GOSUB57:DRAW"S4BM10,10"+A$:T=0:GOSUB55
40 PUT(10,55)-(40,97),B,PSET:GOSUB78:DRAW"S4C0BM13,60"+R$(D1)+"BR14"+R$(D2)
41 T=TT:Q=0:CA(0)=W:GOTO7
42 ' DRAW SCREEN
43 A$="R30D42L30U42":MO=1:F(0)=2:PMODE4,1:PCLS5:SCREEN1,1:DRAW"S4C0BM10,10"+A$:POKE178,54:PAINT(20,20),,0:GET(10,10)-(40,52),A,G:GET(140,10)-(170,52),B,G
44 T=0:X(0)=10:Y(0)=10:GOSUB47:RESTORE:FORT=1TO21:F(T)=0:READX(T),Y(T):GOSUB46:NEXT:FORT=22TO28:F(T)=1:READX(T),Y(T):GOSUB47:NEXT
45 Q=0:W=0:T=0:GOSUB57:GOSUB55:Q=0:FORT=22TO28:GOSUB57:GOSUB55:Q=0:NEXT:DRAW"S4C0BM13,60"+R$(2)+"BR14"+R$(3):RETURN
46 PUT(X(T),Y(T))-(X(T)+30,Y(T)+42),A,PSET:DRAW"C0S4BM"+STR$(X(T))+","+STR$(Y(T))+A$:RETURN
47 PUT(X(T),Y(T))-(X(T)+30,Y(T)+42),B,PSET:DRAW"C0S4BM"+STR$(X(T))+","+STR$(Y(T))+A$:RETURN
48 N$(1)="BR3R3F3D4LU4H3LG3D4LU4E3BL2BD5R7":N$(2)="BRR7FD2GL6GD2R8ULDL8U2ER6EU2HBL6DLU":N$(3)="BRR7FDGFD2GL7HUBRDFBR5EU2HL3BR3EUHBL5GDBLUE":N$(4)="BR4R4D4R2L2D3LU7BL2G4R6BL7E4"
49 N$(5)="BR9L9D3RU3BD3R7FD2GL7HURDFBR5EU2H":N$(6)="BR9L8GD5FR7EU2HL6BLU2EBGBD2BRGD2FR5EU2H":N$(7)="R9DG6LE6UBL7DLU":N$(8)="BRR7FDGFD2GL7HU2EHUEBRGDFR5BL5GD2FBR5EU2HEUH":N$(9)="BRR7FD5GL8BR7EU2BLEU2HBL5GD2FR5BL5BLHU2E"
50 N$(10)="BR2RD7BR2L5BR2U6GLE2BR6R7FD5GL7HU5EBRGD5FBR5EU5H":N$(11)="BR4R5BL2D6GL5HURDFBR3EU6L2":N$(12)="R9D7L9U7BRD7BR7U7D6LF2R2UL2":N$(13)="R2D7LULDRU7LDRBRBD3E4R2GLG4BEF3R2HLH3":R$(0)="R7FD5GL7HU5EBRGD5FBR5EU5HBL8"
51 C$(1)="BR13R3F3D5G10H10U5E3R3F4E4":C$(2)="BR8R2F8DG8L2H8UE8":C$(3)="BR10F9D2G2L2H3LGD3F2L6E2U3HLG3L2H2U2E10":C$(4)="BR8R2F3D2G3F2E3RF3D2G3LH3L2D3F2L6E2U3L2G3LH3U2E3RF3E2H3U2E3":R$(1)="BR2RD7BR2L5BR2U6GLE2BL5":FORP=2TO9:R$(P)=N$(P):NEXTP:RETURN
52 ' SHUFFLE THE DECK
53 FORT=1TO52:CA(T)=T:NEXT:FORT=1TO52:R=RND(52):S=CA(R):CA(R)=CA(T):CA(T)=S:NEXT:SH=0:AC=CA(29):CA(0)=CA(29):RETURN
54 ' DRAW CARDS ON SCREEN
55 DRAW"S4C0BM"+STR$(X(T)+6)+","+STR$(Y(T)+20)+C$(Q):DRAW"S4BM"+STR$(X(T)+4)+","+STR$(Y(T)+4)+N$(W):POKE178,HC:PAINT(X(T)+15,Y(T)+25),,0:RETURN
56 ' COMPUTE SUIT AND VALUE OF CARD
57 W=CA(T)
58 IFW<=13THENQ=Q+1:GOTO59:ELSEW=W-13:Q=Q+1:GOTO58
59 IFQ<3THENHC=1ELSEHC=0
60 RETURN
61 POKE140,20:FORD=1TO10:EXEC:NEXT:POKE140,190:RETURN
62 ' CHECK STATUS OF GAME
63 MO=0:TT=T:FORK=1TO28:IFF(K)=1 THENT=K:GOSUB57:Q=0:TC=W:T=0:GOSUB57:Q=0:EC=W:T=TT ELSE65
64 IFEC-TC=1 OR EC-TC=12 OR TC-EC=1 OR TC-EC=12 THENRETURN
65 NEXTK:GOSUB61:CLS:PRINT@101,"GAME IS OVER YOU LOST":PRINT@133,STRING$(21,"*"):PRINT@205,"PRESS:":PRINT@259,"SPACEBAR FOR ANOTHER GAME":PRINT@323,"DOWN ARROW FOR SAME GAME":PRINT@390,"PRESS <Q> TO QUIT"
66 P$=INKEY$:IFP$=" "THENSH=1:GOTO4ELSEIFP$=CHR$(10)THENSH=0:CA(0)=AC:GOTO4ELSEIFP$="Q"THENCLS:STOPELSE66
67 CLS:PRINT@36,"WELL, YOU FINALLY DID IT.":PRINT@72,"YOU WON THE GAME":PRINT@104,"CONGRATULATIONS!":PRINT@129,STRING$(30,"*"):PRINT@205,"PRESS:":PRINT@259,"SPACEBAR FOR ANOTHER GAME":PRINT@323,"DOWN ARROW FOR SAME GAME":PRINT@390,"PRESS <Q> TO QUIT
68 PLAY"T2L5;O2BAGDO1DO2ABG":GOTO6
69 'PUT AND GET CARDS ON SCREEN.
70 PUT(X(H),Y(H))-(X(H)+30,Y(H)+42),A,PSET:RETURN
71 PUT(X(H),Y(H))-(X(H)+30,Y(H)+42),B,PSET:RETURN
72 PUT(X(H),Y(H))-(X(H)+30,Y(H)+42),C,PSET:RETURN
73 PUT(X(H),Y(H))-(X(H)+30,Y(H)+42),D,PSET:RETURN
74 PUT(X(H),Y(H))-(X(H)+30,Y(H)+42),E,PSET:RETURN
75 GET(X(H),Y(H))-(X(H)+30,Y(H)+42),C,G:RETURN
76 GET(X(H),Y(H))-(X(H)+30,Y(H)+42),D,G:RETURN
77 GET(X(H),Y(H))-(X(H)+30,Y(H)+42),E,G:RETURN
78 ED=52-DE:DE$=STR$(ED):IFED<10 THENMID$(DE$,1,1)="0" ELSEDE$=RIGHT$(DE$,2)
79 D1=VAL(LEFT$(DE$,1)):D2=VAL(RIGHT$(DE$,1)):RETURN
