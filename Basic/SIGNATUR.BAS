10 '**************************** SIGNATURE BY JIM BENNETT, 1988 ********************************
20 CLEAR500,22000:PCLEAR7:PMODE4,1:PCLS0:SCREEN1,1:COLOR1:DIML$(25),C(2,2):CC=22000:GOSUB560
30 LINE(4,17)-(251,49),PSET,B:LINE(6,19)-(249,47),PSET,BF:FORP=2TO4:PCOPYP TOP+3:NEXTP:GOSUB470
40 H$="40":V$="80":T$="SELECT YOUR OPTION":GOSUB640:LINE(38,82)-(211,82),PSET:H$="96":V$="100":T$="1 WRITE":GOSUB640:H$="92":V$="120":T$="2 LOAD":GOSUB640:V$="140":T$="3 SAVE":GOSUB640
50 V$="160":T$="4 PRINT":GOSUB640:V$="180":T$="5 ERASE":GOSUB640
60 I$=INKEY$:IFI$=""ORI$<"1"ORI$>"5"THEN60ELSEIFI$="2"THEN710ELSEIFI$="3"THEN800ELSEIFI$="4"THEN280ELSEIFI$="5"THENGOSUB890:GOTO60
70 '**********WRITE*************
80 GOSUB880:H$="20":V$="100":T$="UNDO":GOSUB640:V$="120":T$="MENU":GOSUB640:GOSUB500
90 H=8:V=34
100 PUT(H,V)-(H+1,V+1),C,NOT:PUT(H,V)-(H+1,V+1),C,NOT
110 I$=INKEY$:IFI$=""THEN100
120 L=INSTR("OP@L;,./",I$):IFL=0ANDI$<>CHR$(13)ANDI$<>"M"ANDI$<>"U"AND(I$<"2"ORI$>"5")THEN100
130 IFI$="2"THEN710ELSEIFI$="3"THEN800ELSEIFI$="4"THEN280ELSEIFI$="5"THENGOSUB890:GOTO110:ELSEIFI$="M"THENGOSUB880:GOTO40
140 ONL GOSUB200,210,220,230,240,250,260,270
150 IFPEEK(338)<200THENCOLOR0:LINE(H,V)-(H+1,V+1),PSET,BF:POKECC,H:POKECC+1,V:CC=CC+2:IFCC=32000THENCC=22000
160 IFPEEK(343)=251ANDCC>22000THENCOLOR1:H=PEEK(CC-2):V=PEEK(CC-1):LINE(H,V)-(H+1,V+1),PSET,BF:CC=CC-2:SOUND60,2:SOUND100,2:POKE343,255
170 IFH<6THENH=6ELSEIFH>247THENH=247
180 IFV<19THENV=19ELSEIFV>46THENV=46
190 GOTO100
200 H=H-1:V=V-1:RETURN
210 V=V-1:RETURN
220 H=H+1:V=V-1:RETURN
230 H=H-1:RETURN
240 H=H+1:RETURN
250 H=H-1:V=V+1:RETURN
260 V=V+1:RETURN
270 H=H+1:V=V+1:RETURN
280 '******PRINT SIGNATURE******
285 GOSUB880:H$="54":V$="100":T$="TURN ON PRINTER":GOSUB640:H$="64":V$="120":T$="PRESS ANY KEY":GOSUB640:EXEC44539
290 PRINT#-2,CHR$(27);CHR$(20);CHR$(18);CHR$(27);CHR$(16);CHR$(3);CHR$(31);:'PRINTER CODES FOR  CONDENSED CHARACTER SET, GRAPH- ICS MODE, READY PRINT HEAD, AND SPECIFYING LAST DOT COLUMN
300 GOSUB880:H$="16":V$="80":T$="SELECT POSITION ON PAPER":GOSUB640:H$="78":V$="100":T$="1 FAR LEFT":GOSUB640:H$="74":V$="120":T$="2 1/3 OVER":GOSUB640:V$="140":T$="3 CENTER":GOSUB640
310 V$="160":T$="4 2/3 OVER":GOSUB640:V$="180":T$="5 FAR RIGHT":GOSUB640
320 I$=INKEY$:IFI$=""ORI$<"1"ORI$>"5"THEN320ELSES=133*(VAL(I$)-1):SCREEN1,1:GOSUB880:H$="26":V$="110":T$="NOW PRINTING SIGNATURE":GOSUB640
330 PRINT#-2
340 FORV=19TO40STEP7
350 FORX=0TOS:PRINT#-2,CHR$(128);:NEXTX:'SPACING TO RIGHT
360 FORH=6TO248
370 D=128:'ADD UP DOTS IN COLUMN
380 IFPPOINT(H,V)=0THEND=D+1
390 IFPPOINT(H,V+1)=0THEND=D+2
400 IFPPOINT(H,V+2)=0THEND=D+4
410 IFPPOINT(H,V+3)=0THEND=D+8
420 IFPPOINT(H,V+4)=0THEND=D+16
430 IFPPOINT(H,V+5)=0THEND=D+32
440 IFPPOINT(H,V+6)=0THEND=D+64
450 PRINT#-2,CHR$(D);
460 NEXTH:PRINT#-2:NEXTV:SCREEN1,1:GOSUB880:GOTO40
470 '*******WRITE TITLE*********
480 DRAW "BM51,14;S4C1BR2E3RERERERERERUEUEUHLGDGDFDFDFD2G2L3H2R9ER2EREUEUBU2UBD5DGDGDFR3ERERERUEUERER6BL2BG3BHBL4BD3BRD2RE2REREUED2GDGDGDG2L5HUBR2BE6BR6RE2R3DGDGDGBE3E3RER2FDGDGD2R4ERERE2UE2R6BL4BG3BLBGD3RERERE2UEDGDGD2R3EREREREREREUEUEUEBLBG3
490 DRAW "BL3BGR12BL6BGBLBGBR2D2GDGFR4EREREREUEUBGBD3GD2R4EREREUEU2BGBD4DFR3EREREUEUEU2BD2FR4DGDGF2R6EREREREUEULHLGDG2D2F2R7ERER2F2DG":RETURN
500 '********DRAW KEYS**********
510 DRAW"BM86,75C1S4D23R27U23L27BH2H9D7BU7R7BUBF12BDBF2BDL3G2D4F2R2E2U4HBR29BUD19R28U23L28D4BFBR5D8BEBU2UR3"
520 DRAW "E2UH2L4BRBE3BR4BU4U9G6BR7BU5F4BR9BF6BR6BDBUBDD23R28U23L27BF4BD7U5E2R4FD6GL3U4R2BRBE7BR8BE4E9L7BF4BE3D6BLBG33BUBL38BDBG4BLBG2BLL28D24R28U23BLBG2BL19D8R5BRBL13BDBL3L10E5BEBG6F5BR49D7R29U24L29D17BE7BLBU5BRBUUD7BH3LR8BG3BDBGBD2DBD3D2G2
530 DRAW "BR7BE10BR11R10G5BE5H5BR9D17R52U23L52D6BF2BR5D6R4BU3L3BU3R3BR4D6BRBU6F2DF3U6BR3R7BDBL3D5BR10L4U3R4BL4U3R4BR3D6BEBU5R2FD2GL2BRF2RBLBG17BL21BDL30D24R30U23BGBL22BG2BDDUE2R2F2D2G2LD2BD2DBD5BG2E7BDBF9BRBF2BR5BFBR2F8BUU7BD8L8BL40BH7BU4U24
540 DRAW "L30D24R29BRBLBH21BL3F4G4BD7BRDBDBF5BRBFBR2BUBD3D8E6BL6BD6H5BL12BH7BL4U24L31D23UD2R30BH20BUBLG5F4BD6DG2BG3BL3BG3G8U7BD7R6BR"
550 RETURN
560 '******TEXT CHARACTERS******
570 DATA BUNFU2ERFD2GNLBR4,NU4BR3,BU3ERFGLGDR3BR2,BU4R2FGNLFGNL2BR3,BU2NE2R2NU2NRD2BR3,BU4NR2D2R2FGNL2BR3,BUNFUNE2R2FGLBR4,BU4R3G3DBR5,BRHEREHLGFRFGNLBR3,BU3NFERFDNLNG2BD2BR2
580 DATA U3ERFDNL3D2BR2,U4R2FGNL2FGNL2BR3,BU4BR3L2GD2FR2BR2,U4R2FD2GNL2BR3,BU4NR2D2NR2D2R2BR2,BU4NR2D2NR2D2BR4,BU4BR3L2GD2FR2NU2BR2,U2NU2R3NU2D2BR2
590 DATA BRNU4BR2,BR2BU4D3GLBR4,U2NU2RNE2F2BR2,NU4R2BR2,U4F2NDE2D4BR2,U4F3NU3DBR2,BUNFU2ERFD2GNLBR3,U4R2FGNL2BD2BR3,BUNFU2ERFD2GNLR2BR2,U4R2FGLNLF2BR2
600 DATA BU4BR3L2GFRFGNL2BR3,BR2U4NL2R2BD4BR2,BUNFU3BR3D3GNLBR3,BU2NU2F2E2NU2BD2BR2,NU4E2F2NU4BR2,UE3BL3F3DBR2,BU4F2NE2D2BR4,BU4R3G3DR3BR2
610 FORX=0TO9:READN$(X):NEXT:FORX=0TO25:READL$(X):NEXT
620 RETURN
630 '********WRITE TEXT*********
640 DRAW"BM"+H$+","+V$+"C1S8":FORX=1TOLEN(T$)
650 L$=MID$(T$,X,1):IFL$=" "THENDRAW"BR4":GOTO690
660 IFL$="/"THENDRAW"NE4BR6":GOTO690
670 IFL$<"A"THENDRAWN$(ASC(L$)-48):GOTO690
680 DRAWL$(ASC(L$)-65)
690 NEXTX:RETURN
700 '******LOAD ROUTINE*********
710 GOSUB880:H$="58":V$="75":T$="LOAD SIGNATURE":GOSUB640:LINE(56,78)-(192,78),PSET:GOSUB900:H$="18":V$="120":T$="TYPE AND ENTER THE NAME":GOSUB640:H$="72":N$="":COLOR1:LINE(78,128)-(164,144),PSET,B
720 I$=INKEY$:IFI$=""ORINSTR(CHR$(13)+CHR$(8)+"1234567890QWERTYUIOPASDFGHJKLZXCVBNM/ ",I$)=0THEN720ELSEH$=STR$(VAL(H$)+10):V$="140":T$=I$
730 IFI$=CHR$(8)ANDN$<>""THENFORU=1TO10:DRAW"BM"+H$+",140C0LNU4":H$=STR$(VAL(H$)-1):NEXTU:H$=STR$(VAL(H$)-10):N$=LEFT$(N$,LEN(N$)-1):GOTO720:ELSEIFI$=CHR$(8)THENH$=STR$(VAL(H$)-10):GOTO720:ELSEIFI$=CHR$(13)THEN750
740 IFLEN(N$)<8THENGOSUB640:N$=N$+I$:GOTO720
750 'LOADING
760 LOADMN$:'OMIT THIS LINE IF  USING TAPE
770 'CLOADMN$:'DELETE THE FIRST APOSTROPHE IF USING TAPE
780 GOSUB880:GOTO40
790 '******SAVE ROUTINE*********
800 GOSUB880:H$="58":V$="75":T$="SAVE SIGNATURE":GOSUB640:LINE(56,78)-(192,78),PSET:GOSUB900:H$="18":V$="120":T$="TYPE AND ENTER THE NAME":GOSUB640:H$="72":N$="":COLOR1:LINE(78,128)-(164,144),PSET,B
810 I$=INKEY$:IFI$=""ORINSTR(CHR$(13)+CHR$(8)+"1234567890QWERTYUIOPASDFGHJKLZXCVBNM/ ",I$)=0THEN810ELSEH$=STR$(VAL(H$)+10):V$="140":T$=I$
820 IFI$=CHR$(8)ANDN$<>""THENFORU=1TO10:DRAW"BM"+H$+",140C0LNU4":H$=STR$(VAL(H$)-1):NEXTU:H$=STR$(VAL(H$)-10):N$=LEFT$(N$,LEN(N$)-1):GOTO810:ELSEIFI$=CHR$(8)THENH$=STR$(VAL(H$)-10):GOTO810:ELSEIFI$=CHR$(13)THEN840
830 IFLEN(N$)<8THENGOSUB640:N$=N$+I$:GOTO810
840 'SAVING
850 SAVEMN$,3584,5119,3584:'OMITTHIS LINE IF USING TAPE
860 'CSAVEMN$,1536,3071,1536:'  DELETE FIRST APOSTROPHE IF USINGTAPE
870 GOSUB880:GOTO40
880 SOUND120,1:FORP=5TO7:PCOPYP TOP-3:NEXTP:RETURN:'ERASE
890 CC=22000:COLOR1:LINE(6,19)-(249,47),PSET,BF:RETURN:'CLEAR
900 H$="78":V$="100":IFMEM<2000THENT$="READY DISK"ELSET$="READY TAPE"
910 GOSUB640:RETURN
