1 'BINGO CARDS
2 'BY STEVE OSTROM
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
50 '
60 CLEAR1000
70 DIMS$(8),N(4,4),L$(4,4),R$(4,4),M$(4)
80 CLS:PRINT@8,"BINGO CARD MAKER":PRINT:PRINT"MAKE SURE YOUR PRINTER PRINT    HEAD IS PROPERLY SET AT THE TOP OF THE PAGE.  THIS PROGRAM USES ALL 66 STANDARD LINES ON AN     8 1/2 BY 11 SHEET OF PAPER.  SO PROPER PAPER, AND PRINT HEAD IS CRITICAL."
90 PRINT@480,"PRESS <ENTER> TO PRINT...";:INPUT A$
100 CLS:PRINT@235,"WORKING..."
110 X=RND(-TIMER)
120 S$(1)="BBBB    IIIII   NN    GGG     OOO  "
130 S$(2)="B   B     I     NNN   G       O   O "
140 S$(3)=" BBBB     I     N N  N   G GGG   O   O "
150 S$(4)=" B   B    I     N  N N   G   G   O   O "
160 S$(5)="(1-15) (16-30) (31-45) (46-60) (61-75)"
170 S$(6)="...... ....... ....... ....... ......."
180 S$(7)=".    . .     . .      . .     . .    ."
190 GOSUB 240
200 PRINT#-2,STRING$(79,"*")
210 PRINT#-2,STRING$(39," ");"*"
220 GOSUB 240
230 GOTO 80
240 GOSUB 630
250 L$(2,2)="FREE":R$(2,2)="FREE"
260 PRINT#-2,S$(1);"*";S$(1)
270 PRINT#-2,S$(2);"*";S$(2)
280 PRINT#-2,S$(3);"*";S$(3)
290 PRINT#-2,S$(4);"*";S$(4)
300 PRINT#-2,S$(1);"*";S$(1)
310 PRINT#-2,S$(5);"*";S$(5)
320 FORY=0TO4
330 PRINT#-2,S$(6);"*";S$(6)
340 PRINT#-2,S$(7);"*";S$(7)
350 PRINT#-2,S$(7);"*";S$(7)
360 FORX=0TO4
370 M$(X)=L$(X,Y)
380 NEXT X
390 GOSUB500
400 PRINT#-2,S$(0);"*";
410 FORX=0TO4
420 M$(X)=R$(X,Y)
430 NEXT X
440 GOSUB500
450 PRINT#-2,S$(0)
460 PRINT#-2,S$(7);"*";S$(7)
470 NEXT Y
480 PRINT#-2,S$(6);"*";S$(6)
490 RETURN
500 S$(0)=". "+M$(0)+". . "+M$(1)+". . "+M$(2)+". . "+M$(3)+". . "+M$(4)+".":RETURN
510 FOR Y=0TO4
520 FOR X=0TO4
530 N(X,Y)=0:NEXTX,Y
540 FORY=0TO4
550 FORX=0TO4
560 N(X,Y)=15*X+RND(15):FL=0
570 IFY=0 THEN 610
580 FORZ=0TOY-1
590 IFN(X,Y)=N(X,Z) THEN FL=1
600 NEXTZ:IFFL=1THEN560
610 NEXT X,Y
620 RETURN
630 GOSUB510
640 FORY=0TO4
650 FORX=0TO4
660 IFN(X,Y)<10THENL$(X,Y)=STR$(N(X,Y))+"  "ELSEL$(X,Y)=MID$(STR$(N(X,Y)),2,1)+" "+MID$(STR$(N(X,Y)),3,1)+" "
670 NEXT X,Y
680 GOSUB510
690 FORY=0TO4
700 FORX=0TO4
710 IFN(X,Y)<10THENR$(X,Y)=STR$(N(X,Y))+"  "ELSER$(X,Y)=MID$(STR$(N(X,Y)),2,1)+" "+MID$(STR$(N(X,Y)),3,1)+" "
720 NEXT X,Y
730 RETURN
