0 'COPYRIGHT 1990 FALSOFT, INC.
10 REM * INITIALIZATION
20 PCLEAR1:CLEAR700:SM=4000:RM=4105:SUM=4151:RUM=4164:SS=4017:RS=4061:RGB:LOADM"SPECTRA3":EXECSUM:EXECSS:HSCREEN2:HSCREEN0
30 WIDTH40:PRINT"INITALIZING...":F=2:B=4:POKE230,2:POKE282,0:POKE59078,18:POKE59079,18:POKE65497,0:HCLSB:HCOLOR8
40 PALETTE9,17:PALETTE10,7:PALETTE11,49:PALETTE12,34:PALETTE13,50:PALETTE14,32:PALETTE15,15
50 HBUFF1,15:HBUFF2,15:HBUFF3,3920:HBUFF4,3920:HDRAW"BM0,0R3G3U3F4":HGET(0,0)-(4,4),1:HCLSB:HCOLORF:EXECSUM
60 DIMDP(63),B$(90),O$(90):FORJ=0TO63:N=J-31:DP(J)=INT(.5+(ABS(N)*N/50)):NEXT:M=1:CP=1:P$=CHR$(94):GOSUB6010
100 REM * THE MAIN MENU
110 HDRAW"BM0,0R319D16L319NU16DR319BD12L319DR319BD10L80NU10L159NU10L80":FORJ=0TO304STEP16:HLINE(J,0)-(J,16),PSET:NEXT
120 HDRAW"BM12,2G9D2GE2DRE8L2UR3HLBM21,5D9R6U3L3U3R3D2U5L6E3DRFBM35,3R10D10L10U10BM70,4R3F3D3G3L3H3U3E3BM87,2D5EF3U2RFD5HU4G5H5E4BM100,5BM99,5D6R2U6L2BR4D6R2U6L2BR4ND6R2D3L2BM115,3F10BM131,13U6NR6E4R6NG4D6G4NL6U6
130 HDRAW"BM153,8M156,3BM153,8F5H5M151,14BM153,8M146,11BM165,5L2D6R2U2BU4BR4L2D3R2L2D3R2BR3U6LR2BM179,8R2U3L2D6BR4BU6D6R2U6BR2R2LD6BM198,2G3D2F3R5F3DH3LF3BL2BU5E2U2H3NL3DL4G2D2F2R4E2U2H2BM212,10NR3D2NR2D2BR8L2HU2ER2BM209,6R15
140 HDRAW"BM238,12L10D2R10UL12U4R7U3LU4R2D4":HCIRCLE(248,8),6,,.6:HDRAW"BM259,11U6R2D3LDFDBR2U6R2D3LRD3BR3U3LU3BR2D3BM276,5U2R7D2HL5R2D8GR3HU7BM292,3R6ND9FD3GNL3FD3GL4NU9L2EU8BM309,3ND9R5FNL5D8GNU8L5HNR6U8
150 HLINE(51,3)-(61,13),PSET,BF:HLINE(208,1)-(224,8),PSET,BF:HPRINT(0,4),"Files/Exit  Background":HPRINT(30,4),"Clear/Mode"
160 FORJ=0TO15:HCOLORJ:HLINE(J*16,18)-(J*16+16,28),PSET,BF:NEXT:FORP=2TO4:GOSUB5690:HLINE(P*16+224,18)-(P*16+240,28),PSET,BF:NEXT:P=22:GOSUB5690:HLINE(304,18)-(319,28),PSET,BF:GOSUB5700:HCOLORF,B:EXECSM:HSCREEN2:POKE59078,141:POKE59079,16
170 POKE41598,255:POKE41599,255:POKE113,0:ONERR GOTO5680
199 REM * MAIN LOOP
200 FORJ=1TO200:NEXT:IFS=6THEN210ELSEGOSUB5620
210 IFX=0ANDY=0ANDM=1THENEXECRUM:M=0:GOTO200
220 IFX=319ANDY=0ANDM=0THENEXECSUM:EXECRM:GOSUB330:M=1:IFS=6THEN300ELSE200
230 IFM=0THEN300ELSEIFY>30ANDY<41ANDX<80THENGOSUB5000
240 IFY>30ANDY<41ANDX>80ANDX<239THENGOSUB5700:HCOLOR3:HLINE(80,30)-(239,40),PSET,B:IFF<15THENGOSUB5300ELSEGOSUB5690:GOSUB5300
250 IFY>30ANDY<41ANDX>239THENGOSUB5400
260 IFY>40THEN300
270 IFY<16THENS=INT(X/16)+1:EXECRM:PLAY"O3T8C"
280 IFF<16THENGOSUB320ELSEGOSUB330
290 IFS=7ORS=14THEN300ELSEIFS=6THENEXECSS:ELSE200
300 XX=X:YY=Y:ONS GOSUB1000,1200,1400,1600,1800,2000,2200,2400,2600,2800,3000,3200,3400,3600,3800,4000,4200,4400,4600,4800
310 IFM=0THENEXECSUM:GOTO200ELSEIFS=10ORS=14THEN210ELSE200
320 IFS=0THENRETURNELSEHCOLOR3:HLINE((S-1)*16,0)-(S*16,16),PSET,B:IFF<16THENHCOLORF:RETURNELSERETURN
330 IFF>15THENGOSUB5700:GOSUB320:GOSUB5690:RETURNELSEGOSUB320:RETURN
999 REM * SUBROUTINES
1000 GOSUB5780:R=0:P=F:REM * PEN
1010 A$=INKEY$:IFA$=CHR$(13)THENRETURNELSEIFBUTTON(0)THEN1010
1020 IFPEEK(343)=254THENY=Y-1:IFY<41ANDM=1THENY=41ELSEIFY=-1THENY=0ELSEX=X+1:IFX=320THENX=319:Y=Y+1
1030 IFPEEK(342)=254THENX=X+1:IFX=320THENX=319
1040 IFPEEK(341)=254THENX=X+1:IFX=320THENX=319ELSEY=Y+1:IFY=192THENY=191:X=X-1
1050 IFPEEK(338)=247THENY=Y+1:IFY=192THENY=191
1060 IFPEEK(340)=247THENY=Y+1:IFY=192THENY=191ELSEX=X-1:IFX=-1THENX=0:Y=Y-1
1070 IFPEEK(339)=254THENX=X-1:IFX=-1THENX=0
1080 IFPEEK(339)=251THENY=Y-1:IFY<41ANDM=1THENY=41ELSEIFY=-1THENY=0ELSEX=X-1:IFX=-1THENX=0:Y=Y+1
1090 IFPEEK(345)=251THENY=Y-1:IFY<41ANDM=1THENY=41ELSEIFY=-1THENY=0
1100 IFPEEK(340)=239THENR=R+1:IFR>10THENR=10
1110 IFPEEK(339)=239THENR=R-1:IFR=-1THENR=0
1120 IFPEEK(341)=239THENIFP=F THENP=B ELSEP=F
1130 IFR<>0THENHCIRCLE(X,Y),R,P:GOTO1150
1140 HSET(X,Y,P)
1150 IFP=B THENHSET(X,Y,F):FORJ=1TO100:NEXT:HSET(X,Y,B):GOTO1010ELSEGOTO1010
1200 GOSUB5780:REM * SPRAY
1210 HLINE(XX,YY)-(X,Y),PSET,B:GOSUB5790:IFFB=1THEN1210
1220 J=XX+RND(X-XX):K=YY+RND(Y-YY):HSET(J,K):IFBUTTON(0)=0THEN1220ELSERETURN
1400 GOSUB5780:REM * BOX
1410 GOSUB5790:HLINE(XX,YY)-(X,Y),PSET,B:IFFB=1THEN1410ELSERETURN
1600 GOSUB5780:REM * BOX FILL
1610 GOSUB5790:HLINE(XX,YY)-(X,Y),PSET,BF:IFFB=1THEN1610ELSERETURN
1800 GOSUB5780:REM * CIRCLE
1810 GOSUB5790:HCIRCLE(XX,YY),(X+Y)/4:IFFB=1THEN1810ELSERETURN
2000 REM * PAINT
2010 GOSUB5620:IF(Y<41ANDM=1)OR(X=319ANDY=0ANDM=0)THENRETURNELSEEXECSS:HPAINT(X,Y),,B:IFM=0THENEXECSUM
2020 GOTO2010
2200 EXECRS:EXECRM:S=0:RETURN:REM * OOP
2400 GOSUB5780:REM * LINE
2410 GOSUB5790:HLINE(XX,YY)-(X,Y),PSET:IFFB=1THEN2410ELSERETURN
2600 GOSUB5780:REM * CUBE
2610 GOSUB5790:IFX<XX THEN2620ELSEC$=STR$(ABS(XX-X)):E$=STR$(INT(ABS(XX-X)/((INT(Y/10)+2)/5))):HDRAW"BM"+STR$(XX)+","+STR$(YY)+"R"+C$+"D"+C$+"L"+C$+"U"+C$+"E"+E$+"R"+C$+"NG"+E$+"D"+C$+"G"+E$:IFFB=1THEN2610ELSERETURN
2620 GOSUB5790:IFX>XX THEN2610ELSEC$=STR$(ABS(XX-X)):E$=STR$(INT(ABS(XX-X)/((INT(Y/10)+2)/5))):HDRAW"BM"+STR$(XX)+","+STR$(YY)+"L"+C$+"D"+C$+"R"+C$+"U"+C$+"H"+E$+"L"+C$+"NF"+E$+"D"+C$+"F"+E$:IFFB=1THEN2620ELSERETURN
2800 GOSUB5780:REM * POINT RAY
2810 GOSUB5790:HLINE(XX,YY)-(X,Y),PSET:IFFB=1THEN2810ELSEP9=XX:R9=YY:GOSUB5620:IF(Y<40ANDM=1)OR(X=319ANDY=0ANDM=0)THENRETURNELSEXX=P9:YY=R9:EXECSS:GOTO2810
3000 GOSUB5780:XX=X:YY=Y:REM * GET
3010 X=JOYSTK(0)-1:Y=JOYSTK(1)-1:IFXX+X>318THENX=318-XX ELSEIFYY+Y>191THENY=191-YY ELSEIFINT(X/2)<>X/2THENX=X+1
3020 HLINE(XX,YY)-(XX+X,YY+Y),PRESET,B:HLINE-(XX,YY),PSET,B:EXECRS:IFBUTTON(0)THEN3010ELSEHGET(XX,YY)-(XX+X,YY+Y),3:FORJ=1TO8:HPUT(XX,YY)-(XX+X,YY+Y),3,NOT:NEXT:PX=X:PY=Y:X=XX:Y=YY:RETURN
3200 GOSUB5780:REM * PUT
3210 GOSUB5650:IFY<1THENRETURNELSEIFX+PX>318THENX=318-PX
3220 IFY+PY>191THENY=191-PY ELSEIFINT(X/2)<>X/2THENX=X+1
3230 HGET(X,Y)-(X+PX,Y+PY),4:ONCP GOSUB3250,3260,3270,3280,3290:IFFB<>1THENHPUT(X,Y)-(X+PX,Y+PY),4,PSET
3240 GOTO3210
3250 HPUT(X,Y)-(X+PX,Y+PY),3,PSET:RETURN
3260 HPUT(X,Y)-(X+PX,Y+PY),3,PRESET:RETURN
3270 HPUT(X,Y)-(X+PX,Y+PY),3,AND:RETURN
3280 HPUT(X,Y)-(X+PX,Y+PY),3,OR:RETURN
3290 HPUT(X,Y)-(X+PX,Y+PY),3,NOT:RETURN
3400 IFF>15THENRETURNELSEGOSUB5780:IFB=3THENHCOLOR4,8ELSEHCOLOR3,8:REM * ENLARGE
3410 GOSUB5650:IFX>298THENX=298
3420 IFX/2<>INT(X/2)THENX=X+1:IFY>170THENY=170ELSEIFY>170THENY=170
3430 HLINE(X,Y)-(X+21,Y+21),PRESET,B:HLINE-(X,Y),PSET,B:EXECRS:IFBUTTON(0)=0THENHGET(X,Y)-(X+21,Y+21),3:ELSE3410
3440 XM=X:YM=Y:HSCREEN2:HLINE(5,5)-(187,187),PSET,B:HLINE(4,4)-(188,188),PSET,B:HLINE(204,171)-(258,188),PSET,B:HLINE(260,171)-(314,188),PSET,B:HLINE(205,25)-(228,48),PSET,B:HLINE(205,81)-(228,104),PSET,B
3450 HCOLOR5:HPRINT(27,1),"ENLARGE":HPRINT(30,4),"Cut":HPRINT(30,11),"Paste":HPRINT(30,18),"Color":HPRINT(27,22),"Done   Undo"
3460 FORJ=0TO15:HCOLORJ:HLINE(299,J*10)-(319,J*10+10),PSET,BF:NEXT
3470 HPUT(206,26)-(227,47),3,PSET:HPUT(206,82)-(227,103),3,PSET:HCOLORF:HLINE(205,137)-(229,158),PSET,BF:FORJ=26TO47:YY=(J-26)*8+9:FORK=206TO227:XX=(K-206)*8+9:MC=HPOINT(K,J):HCOLORMC:HLINE(XX,YY)-(XX+6,YY+6),PSET,BF:NEXTK,J:HCOLORF:MC=F
3480 GOSUB5620:IFX>298ANDY<160THENMC=INT(Y/10):HCOLORMC:HLINE(205,137)-(229,158),PSET,BF:GOTO3480
3490 IFY<185ANDX<185ANDY>8ANDX>8THENX=INT((X-9)/8):Y=INT((Y-9)/8):XX=X*8+9:YY=Y*8+9:IFHPOINT(XX,YY)=B THEN3510ELSEHCOLORB:GOTO3510
3500 IFX>259ANDX<315ANDY<189ANDY>170THENHGET(206,26)-(227,47),3:HPUT(206,82)-(227,103),3,PSET:GOTO3470ELSEIFX>203ANDX<259ANDY<189ANDY>170THENHGET(206,82)-(227,103),3:EXECRS:HPUT(XM,YM)-(XM+21,YM+21),3,PSET:HCOLORF,B:RETURNELSE3480
3510 HLINE(XX,YY)-(XX+6,YY+6),PSET,BF:HSET(X+206,Y+82):X=XX+1:Y=YY+1:HCOLORMC:GOTO3480
3600 REM * FOREGROUND COLOR
3610 GOSUB5620:GOSUB5700:IFY>32ORY<16THENEXECRM:RETURNELSEF=INT(X/16):PLAY"O3T8C":IFF<16THENEXECRM:HCOLORF:HLINE(209,1)-(223,8),PSET,BF:EXECSM:RETURN
3620 IFF=16THENP=2:GOSUB5690ELSEIFF=17THENP=3:GOSUB5690
3630 IFF=18THENP=4:GOSUB5690ELSEIFF=19THENP=22:GOSUB5690
3640 EXECRM:HLINE(209,1)-(223,8),PSET,BF:EXECSM:RETURN
3800 GOSUB5780:REM * ROLLER BRUSH
3810 GOSUB5650:HLINE(X,Y)-(X+5,Y+5),PRESET,BF:HLINE(X,Y)-(X+5,Y+5),PSET,BF:IFFB=1THEN3810ELSERETURN
4000 GOSUB5780:REM * ELLIPSE
4010 GOSUB5790:HCIRCLE(XX,YY),(X*4+Y)/5,,13.9-SQR(Y):IFFB=1THEN4010ELSERETURN
4200 GOSUB5780:REM * RAY
4210 GOSUB5650:HLINE(XX,YY)-(X,Y),PSET:IFFB=1THEN4210ELSERETURN
4400 GOSUB5780:REM * HPRINT TEXT
4410 I=INT(XX/8):K=INT(YY/8):L=40:GOSUB5710:RETURN
4600 GOSUB5780:REM * BOLD TEXT
4610 HDRAW"BM"+STR$(X)+","+STR$(Y)
4620 A$=INKEY$:IFA$=""THEN4620ELSEIFA$=CHR$(13)THENRETURNELSEIFASC(A$)<31THEN4620
4630 HDRAWB$(ASC(A$)-32):GOTO4620
4800 GOSUB5780:REM * OLD TEXT
4810 HDRAW"BM"+STR$(X)+","+STR$(Y)
4820 A$=INKEY$:IFA$=""THEN4820ELSEIFA$=CHR$(13)THENRETURNELSEIFASC(A$)<31THEN4820
4830 HDRAWO$(ASC(A$)-32):GOTO4820
5000 REM * FILE/EXIT
5010 GOSUB5810:EXECSS:D=0:L=8:I=2:K=39:HDRAW"BM89,41D49L89R2DR88U48RD49L89":HLINE(0,41)-(88,89),PRESET,BF:HPRINT(0,6),"Save File":HPRINT(0,7),"Load File":HPRINT(0,8),"Kill File":HPRINT(0,9),"Screen Dump":HPRINT(0,10),"Exit/BASIC"
5020 HGET(1,K+I*8)-(87,K+8+I*8),4:HPUT(1,K+I*8)-(87,K+8+I*8),4,NOT
5030 XX=JOYSTK(0):YY=JOYSTK(1):FORJ=1TO250:NEXT:IFBUTTON(0)=0THENONI GOTO5080,5090,5200,5230,5260ELSEIF(YY>22ANDYY<42)THEN5030
5040 HPUT(1,K+I*8)-(87,K+8+I*8),4,PSET
5050 IFYY<23THENI=I-1:IFI<1THENI=1
5060 IFYY>41THENI=I+1:IFI>5THENI=5
5070 GOTO5020
5080 GOSUB5770:IFFREE(0)<14THENHPRINT(7,9),"DISK FULL. USE NEW DISK":FORJ=1TO4000:NEXT:EXECRS:GOTO5220ELSEHPRINT(7,9),"SAVE FILENAME:":GOSUB5710:POKE65496,0:OPEN"O",1,Q$+"/PIC":EXEC4177:CLOSE:GOTO5220
5090 D=0:I=1:K=63:HLINE(51,64)-(172,116),PSET,B:HLINE(52,65)-(171,115),PRESET,BF:POKE65496,0:DSKI$0,17,3,C$,E$:FORJ=1TO128STEP32:GOSUB5100:NEXT:C$=E$:FORJ=1TO128STEP32:GOSUB5100:NEXT:GOTO5130
5100 Q$=MID$(C$,J,14):IFLEFT$(Q$,1)=CHR$(0)THENRETURN
5110 IFASC(Q$)>127THENRETURN
5120 IFMID$(Q$,9,3)<>"PIC"THENRETURNELSED=D+1:D$(D)=LEFT$(Q$,8)+"/"+"PIC":RETURN
5130 IFD=0THENHPRINT(7,9),"NO FILES SAVED":FORJ=1TO3000:NEXT:GOTO5220ELSEFORX=1TOD:HPRINT(7,8+X),D$(X):NEXT:HPRINT(7,8+X),"Exit to Menu"
5140 HGET(53,K+I*8)-(170,K+8+I*8),4:HPUT(53,K+I*8)-(170,K+8+I*8),4,NOT
5150 FB=BUTTON(0):XX=JOYSTK(0):YY=JOYSTK(1):IFFB=1ANDI=D+1THENGOTO5220ELSEIFFB=1THENHCLSB:OPEN"I",1,D$(I):EXEC4222:CLOSE:GOTO5220ELSEIFYY>22ANDYY<42THEN5150
5160 HPUT(53,K+I*8)-(170,K+8+I*8),4,PSET
5170 IFYY<23THENI=I-1:IFI<1THENI=1
5180 IFYY>41THENI=I+1:IFI>D+1THENI=D+1
5190 GOTO5140
5200 GOSUB5770:HPRINT(7,9),"KILL FILENAME:":GOSUB5710:GOSUB5800
5210 A$=INKEY$:IFA$=""THEN5210ELSEIFA$="Y"ORA$="y"THENKILLQ$+"/PIC"ELSEIFA$="N"ORA$="n"THEN5220ELSE5210
5220 EXECRS:POKE65497,0:F=F9:RETURN
5230 EXECRS:EXECRUM:REM * SCREEN DUMP
5240 REM * ADD YOUR SCREEN DUMP HERE
5250 GOTO5220
5260 GOSUB5800
5270 A$=INKEY$:IFA$=""THEN5270ELSEIFA$="Y"ORA$="y"THENPOKE65496,0:POKE282,255:RGB:STOPELSEIFA$="N"ORA$="n"THEN5220ELSE5270
5300 REM * BACKGROUND
5310 GOSUB5620:GOSUB5700:BG=B:IFY>32ORY<16THENEXECRM:RETURNELSEB=INT(X/16):IFB<16THENEXECRM:HCOLOR,B:HLINE(185,32)-(225,38),PRESET,BF:EXECSM:RETURN
5320 IFB>15THENEXECRM:B=BG:RETURN
5400 REM * CLEAR/MODE
5410 GOSUB5810:EXECSS:I=3:K=39:HDRAW"BM222,41D74R97L2DL96U73LD74R97":HLINE(223,41)-(319,114),PRESET,BF:HPRINT(28,6),"Palette":HPRINT(28,7),"Clear Screen":HPRINT(28,8),"PUT Mode":HPRINT(31,9),"Set":HPRINT(31,10),"Reset"
5420 HPRINT(31,11),"And":HPRINT(31,12),"Or":HPRINT(31,13),"Not":HPRINT(29,CP+8),">"
5430 HGET(224,K+I*8)-(319,K+8+I*8),4:HPUT(224,K+I*8)-(319,K+8+I*8),4,NOT
5440 XX=JOYSTK(0):YY=JOYSTK(1):FORJ=1TO250:NEXT:IFBUTTON(0)=0THENONI GOTO5500,5590,5600,5610,5610,5610,5610,5610ELSEIFYY>22ANDYY<42THEN5440
5450 HPUT(224,K+I*8)-(319,K+8+I*8),4,PSET
5460 IFYY<23THENI=I-1:IFI<1THENI=1
5470 IFYY>41THENI=I+1:IFI>8THENI=8
5480 GOTO5430
5500 R=B:C=63:REM * PALETTE
5510 HCOLORF,B:HSCREEN2:HPRINT(10,1),"PALETTE COLOR CHANGER":HPRINT(2,3),"Left & Right Keys -> Select Palette":HPRINT(5,5),"F1 & F2 Keys   -> Select Color"
5520 FORJ=0TO15:HCOLORJ:HLINE(J*16+28,60)-(J*16+44,100),PSET,BF:NEXT:HPRINT(R*2+4,13),P$:HPRINT(7,18),"Press Fire Button when done"
5530 A$=INKEY$:IFA$=CHR$(8)THENR=R-1:IFR=-1THENR=0ELSEHCOLORB:HPRINT(R*2+6,13),P$:HCOLORF:HPRINT(R*2+4,13),P$
5540 IFA$=CHR$(9)THENR=R+1:IFR=16THENR=15ELSEHCOLORB:HPRINT(R*2+2,13),P$:HCOLORF:HPRINT(R*2+4,13),P$
5550 IFA$=CHR$(103)THENC=C-1:IFC=-1THENC=63:PALETTER,C ELSEPALETTER,C
5560 IFA$=CHR$(4)THENC=C+1:IFC=64THENC=0:PALETTER,C ELSEPALETTER,C
5570 IFBUTTON(0)THENEXECRS:F=F9:RETURN
5580 GOTO5530
5590 HCLSB:EXECSUM:EXECRM:F=F9:RETURN
5600 EXECRS:F=F9:RETURN
5610 CP=I-3:EXECRS:F=F9:RETURN
5620 GOSUB5650:GOTO5640:REM * JOYSTICK
5630 HGET(X,Y)-(X+4,Y+4),2:HPUT(X,Y)-(X+4,Y+4),1:GOSUB5650:HPUT(XX,YY)-(XX+4,YY+4),2:IFFB=1THENRETURN
5640 XX=X:YY=Y:GOTO5630
5650 X=X+DP(JOYSTK(0)):Y=Y+DP(JOYSTK(1)):FB=BUTTON(0):IFX<0THENX=0ELSEIFX>319THENX=319
5660 IFY<0THENY=0ELSEIFY>191THENY=191ELSERETURN
5670 RETURN
5680 EXECRS:F=F9:GOTO280:REM * ERROR ROUTINE
5690 POKE59291,196:POKE59292,P:RETURN:REM * PATTERN ENABLE
5700 POKE59291,212:POKE59292,181:RETURN:REM * RESTORE CODE
5710 Q$=STRING$(L," "):FORJ=0TOL-1:REM * HPRINT INPUT
5720 A$=INKEY$:IFA$=""THEN5720ELSEIFA$=CHR$(13)THEN5760
5730 IFA$=CHR$(8)THENJ=J-1:IFJ>-1THENA$=" ":MID$(Q$,J+1,1)=" ":HLINE((I+J)*8,K*8)-((I+J)*8+8,K*8+7),PRESET,BF:J=J-1:GOTO5750
5740 IFJ<0THEN5750ELSEIFASC(A$)<32THENJ=J-1ELSEMID$(Q$,J+1,1)=A$:HPRINT(I+J,K),A$
5750 NEXT
5760 Q$=LEFT$(Q$,J):RETURN
5770 I=22:K=9:L=8:HLINE(52,64)-(245,87),PSET,B:HLINE(53,65)-(244,86),PRESET,BF:RETURN
5780 GOSUB5620:EXECSS:RETURN
5790 EXECRS:GOSUB5650:RETURN
5800 HLINE(67,82)-(260,101),PSET,B:HLINE(68,83)-(259,100),PRESET,BF:HPRINT(10,11),"ARE YOU SURE ? <Y/N>":RETURN
5810 F9=F:IFF>15THENGOSUB5700:F=15
5820 IFB=8THENHCOLOR0:RETURNELSEHCOLOR8:RETURN
5999 REM * FONT DATA
6000 REM * BOLD TEXT
6010 B$(0)="BR7"'SPACE
6020 B$(1)="BRD4RU4BD6LBR4BU6"'!
6030 B$(2)="DRUBR2DRUBR2"'"
6040 B$(3)="BRD6RU6BR2D6RU6BRBD2L6BD2R6BU4BR3"'#
6050 B$(4)="BR2RFRL4GRDR3DRG2LEL3BR8BU5"'$
6060 B$(5)="BDDRUBR5G5LE5BD4DRUBU5BR3"'%
6070 B$(6)="BR4FGU2L2DLF5LH4DGRGRDR2BU6BR5"'&
6080 B$(7)="BRDGREUBR2"''
6090 B$(8)="BR3G2D2F2LH2U2E2BR3"'(
6100 B$(9)="F2D2G2RE2U2H2BR5"')
6110 B$(10)="BRBDF4RH4D2L2R7LHG3LE4RBU2BR3"'*
6120 B$(11)="BR2BDD4RU2NL3NR2U2BUBR4"'+
6130 B$(12)="BD5BRD2NLEUBR2BU5"',
6140 B$(13)="BD3R5BU3BR2"'-
6150 B$(14)="BD5DRUBR2BU5"'.
6160 B$(15)="BR6G6RE6BR2"'/
6170 B$(16)="BRD6R3U6L3GD4NE3RE4D4BU5BR2"'0
6180 B$(17)="BR2NGD6L2R3NR2U6BR4"'1
6190 B$(18)="BDRUR3DRDLDL2G2DNE2R5HRBU5BR2"'2
6200 B$(19)="BDRUR3FDGNL2FDGNU5L3ULBU5BR7"'3
6210 B$(20)="BD3E3RG4R4NU3DGR3HELU4BR3"'4
6220 B$(21)="BD5RDR3NU3EU2HL4ELUR5BR2"'5
6230 B$(22)="BD2D3FNU2R3U2FUHL3U2RUR2BR3"'6
6240 B$(23)="DRUR3D2G2D2RU2E2U2BR2"'7
6250 B$(24)="BRGDFGDFNU5R3NU5EUHNL2EUHL2BR5"'8
6260 B$(25)="BRGDFNU2R4G3LR2E2U3GU2L2BR5"'9
6270 B$(26)="BD5DRUBU3LURBR2BU"':
6280 B$(27)="BRBD5DGREUBU3LURBUBR2"';
6290 B$(28)="BR3G3F3RH3E3BR2"'<
6300 B$(29)="BD2R5BD3L5BU5BR7"'=
6310 B$(30)="F3G3RE3H3BR5"'>
6320 B$(31)="BDR4G3BD2RBU2E2UHNL2BR3"'?
6330 B$(32)="BR5L4GD4FNU5R4BU2L2UR3UL3RERHBR3"'@
6340 B$(33)="BR3D2HD3NR2HD3HDBR7UGU3GU3GU2BR5"'A
6350 B$(34)="R5FDGNL2FDGNU5L5ENU4RU5BR6"'B
6360 B$(35)="BR3LDLGD2FNU3RDR3URBU4LHRBR3"'C
6370 B$(36)="RD5GR2U6RF2D2G2RE2U2H2BR4"'D
6380 B$(37)="RNR4D3NR3D2GR2NU6R4ULBU4RUBR2"'E
6390 B$(38)="RD5GR3HU2NR2U3R3DRUBR2"'F
6400 B$(39)="BR2DLND3GD2F2UFR2U2HR2ND2BU2LUL2BR5"'G
6410 B$(40)="D6RU3NR2U3BR3D6RU6BR2"'H
6420 B$(41)="R2D6L2R5LHU5R2BR2"'I
6430 B$(42)="BD4DED2R3NU5EU5L3BR5"'J
6440 B$(43)="RD5GR2NU6BR4UGU2LHRURU2FUBR3"'K
6450 B$(44)="RD5GR6ULBL3U5RBR5"'L
6460 B$(45)="D6RU5F2DHR2UED5RU6BR2"'M
6470 B$(46)="D6RU6NF3DF4NU5DRU6BR2"'N
6480 B$(47)="BR3LDLND3GD2FRDR2URNU3EU2HLUBR4"'O
6490 B$(48)="RD5GR3HU2NU3R3EUGU2L2BR5"'P
6500 B$(49)="BR3LDLND3GD2FRDR2UF2LU5RND2HLUBR4"'Q
6510 B$(50)="RD5GR2U3NU3RF3RH3RELELUL2BR6"'R
6520 B$(51)="BD5RDR3URUL2EL3EL2ELER3DRHBR3"'S
6530 B$(52)="DERD5GR3HU5RFUBR2"'T
6540 B$(53)="ND5RD6R3NU6EU5BR2"'U
6550 B$(54)="DED3ED3ED2RU2FU3FU3FUBR2"'V
6560 B$(55)="ND6RD6EURUFDFNU6RU6BR2"'W
6570 B$(56)="F6RH6BR5G6RE6BR2"'X
6580 B$(57)="DED2RDRD2GR3HU2RURU2FUBR2"'Y
6590 B$(58)="DRUR5G6R6ULGLHE4UBR2"'Z
6600 B$(65)="BD2BRR3FD2FLHGL2HER3UBU3BR4"'a
6610 B$(66)="RD5U2R2ERND3FD2GL3U6BR6"'b
6620 B$(67)="BD3BR5LUL3ND3GD2FR3URBU5BR2"'c
6630 B$(68)="BR3RD5GL2NU3HU2ERF4LU6BR3"'d
6640 B$(69)="BD2BRR3DRDL4ULD2RDR3BU6BR3"'e
6650 B$(70)="BRBDDGRD2GR3HUELU3R2DRHBR3"'f
6660 B$(71)="BD7RDR3URULUL3ULURUR2FDRU2RBR2BU2"'g
6670 B$(72)="RD5GR2U6D3RERND4FD3BU6BR2"'h
6680 B$(73)="BD2RD3GR3HU3BU2LBR4"'i
6690 B$(74)="BD6RDR3NU5EU4BU2LBR3"'j
6700 B$(75)="RD5GR2U3F3RH2E2LG3U5BR6"'k
6710 B$(76)="RD5GR3HU5BR3"'l
6720 B$(77)="BD2D4RU4F2EURD4RU3BR2BU3"'m
6730 B$(78)="BD2D4RU4R3D4RU3BU3BR2"'n
6740 B$(79)="BD3D2RU3R3ND3FD2GL3BE6"'o
6750 B$(80)="BD2RD4GR3HU3ER2DRDLDL2BE5"'p
6760 B$(81)="BR6BD2GD3FL2U5L3D2HDFR2BE5"'q
6770 B$(82)="BD2RD3GR3HU3FERFDLUBE3"'r
6780 B$(83)="BD6R4URHL3HRUR4BE2"'s
6790 B$(84)="BD2ED4FNU6REBU3LRBE2"'t
6800 B$(85)="BD2D3FNU4R2EU3RD4RBU6BR2"'u
6810 B$(86)="BD2D2FNU3RDEDENU3EU2BE2"'v
6820 B$(87)="BD2ND2RD4RUEUDFDRU4RND2BE2"'w
6830 B$(88)="BR5BD2G4LE2H2RF4RH2E2BE2"'x
6840 B$(89)="BD2ND2RD3R2G2LR2E2NU3EU2BE2"'y
6850 B$(90)="BD3UR4NG4RG4R4UBU5BR2"'z
7000 REM * OLD TEXT
7010 O$(0)="BR12"'SPACE
7020 O$(1)="BDFND5LD7BD3D2ED3BU16BR3"'!
7030 O$(2)="BD2D3ED4BR3U4GU3BUBR4"'"
7040 O$(3)="BD3BFR8GL8BD3BFR8GL8BR2BD3U12ED12BR3DU12ED12BU12BR6"'#
7050 O$(4)="BR4ND14DG3DF2U5FR2EG2BD4ND4F2DG2L3UHDLGBU13BR12"'$
7060 O$(5)="BRBDGFEBR6DRL2G6LFE7BD7GFEBU11BR5"'%
7070 O$(6)="BR2BDG2DF2G2DFNU2FRE3D3FNE2U3ENRGH2LNFHNH2E2UHND2HBUBR9"'&
7080 O$(7)="BDD2ED2GBU5BR4"''
7090 O$(8)="BR3NR2G3D9F3R2LH3U9E3BR3"'(
7100 O$(9)="R2F3D9G3NL2UE2U9H2BUBR6"')
7110 O$(10)="BRBD3NF3RF4NL5NR4NF4NG4DG3LHBR8BDH3U3NE2RE2FBU4BR3"'*
7120 O$(11)="BR5BD2D9GU5NU4NL4NR5BU7BR9"'+
7130 O$(12)="BD9D2ED2GBU13BR4"',
7140 O$(13)="BD7R9DL9BU8BR12"'-
7150 O$(14)="BD14NR2DR2BU15BR2"'.
7160 O$(15)="BR10G10DE10BUBR3"'/
7170 O$(16)="BR3G3D8NF3NERNF3RF3E3U8GU2GU2GU2LDBUBR7"'0
7180 O$(17)="BR3NG3D14NEHNHU11BU2BR5"'1
7190 O$(18)="BR3G2DE3NRDR2ND4FD3NG6LG6ND3FDRFR3URURBU12BR3"'2
7200 O$(19)="BR3NG3RNG2DRF2ND3LD3GNL2FND3RD3G3ULNHDH3BU11BR11"'3
7210 O$(20)="BR6ND12G6DR7NFG2D4NR2NFNHU11BU2BR6"'4
7220 O$(21)="BRR5GL4D7RE2R2ND5FD4G3LEL2ULHBU11BR10"'5
7230 O$(22)="BR4F2NEL2ULG3D7F3RE2NU5EU4H2L2GLD5F2RBU13BR6"'6
7240 O$(23)="BRNR7GR7D2G3ND7GD7BU14BR8"'7
7250 O$(24)="BR2G2D2NF6RF6D2G2L3HNU3HU2E3RE2NU3EU2H2L3BR9"'8
7260 O$(25)="BR3G3D3NFRF2E3ND5U2H2LURF3D7G4U2GULGBU13BR10"'9
7270 O$(26)="BRBD2GR2GBD5GR2GBU11BR4"':
7280 O$(27)="BD2DRDBD6HDFGBU12BR5"';
7290 O$(28)="BR6G6F6UH5E5BUBR3"'<
7300 O$(29)="BD4R8BD5L8BU9BR12"'=
7310 O$(30)="F6G6UE5H5BUBR9"'>
7320 O$(31)="BR2NG2R2DRND2FDNG4DG4BD3D2ED2BU14BR6"'?
7330 O$(32)="BR5BDR5F3ND2UHD4G3U4LD2ND2G3NU3HU2E2NRBL5E3NRG3ND5GD4F2UF2R5EREBU11BR4"'@
7340 O$(33)="BR4G4D3F2R2NG5E2NE3U2LGBD4NR5BD2NL2GR2GR3GLBR6ELNU14LHRU11LEL6NG3UR4BR7"'A
7350 O$(34)="BR4NG4R5GNL4G4ND8GD6NG3DFNR4FR3E4NU2LNG2U3LU2G5ENU4EU4E4D4EU3BUBR3"'B
7360 O$(35)="BR5G4ND7GD6F2U2FD2FNU10NR5ER5E2BU10GLDLUGU2GU2G2NG2D8BU11BR10"'C
7370 O$(36)="BR4NG4RNG2R7ND12GNL6GRD11G2L2UGUGU2GULUNG3NU7EU7E4BR7"'D
7380 O$(37)="BR5NG5RNG3R4GNL3G5ND5GD6NG2R6D2R2ENL6E2BL8NU5EUNU4ERNR4E6G3U3FL2U2ENG5BUBR6"'E
7390 O$(38)="BR6G3ERE2R4GNL3G4D8EU7BR2D8G4UH2LG2ER3FERE3U2E2NFG2U6E4F2DH2DFBU4BR4"'F
7400 O$(39)="BR4G3ND8GD7F4NR5UNU10R6ENU4EU4L7ND4NU5E7GL2DLU2GU2NG4BR8"'G
7410 O$(40)="BR4NG4RNG2R4GNL3G4D4GU5BD7LNG2R2DED2ENR2DR3BR3NU10EU10LG6D2U5RD4U5E5BR4"'H
7420 O$(41)="BR3NG3RNG2R4GNL3G3ND6GD6BD2LNG2RNF2R2DRNU9EU9E4BR"'I
7430 O$(42)="BR5NG3RNG2R4GNL3G3ND6GDGHGBR3D5BD3H3LNF3LNGF3R2E3NU7EU7E4BR"'J
7440 O$(43)="BR3NG3RNG2R4GNL3G3ND6GD6BD2LNG2NR3RFNE2NR3FR3BR3NE2UELEL2HNL3E4LENL3HL2G3D2U5LND6E5D2EDR2EBU"'K
7450 O$(44)="BR3NG3RNG2R4GNL3G3ND6GD6BD2NG2R3NU8NR3DR6GNL2E2BU2BL6U7E4D2ED2EDE2BU"'L
7460 O$(45)="BR3NG3RNG2RGR3EG4ND7GD7NG3DR2GNR4FR3E2LNU12LU5LGERNU7RE2BD8BRFNU14E2LU12HG3BU3BR7"'M
7470 O$(46)="BR2NG2DED12NG3DRGNR3FRLBU13D2RD2RD2RD2RD2RD2FU15GR2UFGNR2FRE2BU"'N
7480 O$(47)="BR4G3ND8GD7FED2ED2NR6FNU11R5E3NU7EU7NH4LNH4LH4DLG3E2ND9BU2BR11"'O
7490 O$(48)="BR4G2RD2L2GER2D3L2GER2D7GLHBR4BUNU12F2REBU7BL4NF2UF3E2NU6EU5HL2G3BU3BR9"'P
7500 O$(49)="BR4G3ND8GD7FED2ED2NR6FNU11R5E2H2LGER2F3RE2BL4NU7EU7NH4LNH4LH4DLG3E2ND9BU2BR11"'Q
7510 O$(50)="BR3NG3RNG2R4GNL3G3ND6GD6BD2LNG2R2DNR3FNR3U2EU7NE5RD6BD4BR4NU4ENU2EUH3DHLRE3NU5RNG2U5HBR4"'R
7520 O$(51)="BR13G3NL4GL2U2NL2ULG3F8UH7GF7DGH7UF6D2NL4DL2UHNL3HL2G2DFRE2BU14BR10"'S
7530 O$(52)="BR3NG3RNG2R5GL2NL2G3ND7GD6FED2ED2FR5E3G2L6U3NU6RU8NG2E2RGNR3FR2E2BUBR2"'T
7540 O$(53)="BR4ND11G2RD2GLGEFED7G2ER3GR3FNL3EFNU14E2LU12HNG2BR5"'U
7550 O$(54)="BR4ND11G2RD3L2GEFRD6NG3DR2GR2GRE4NU10EU9HNG2BR4"'V
7560 O$(55)="BR4ND11G2RD3L2GEFRD6G2ER3GRDE2RU13NG2FD13ED2E3NU11EU10HNG2BR4"'W
7570 O$(56)="BR2NF11DF11LNG2H11GBD5BR2R10BD7BL12E2R3GR3GLBU2BL3E11GL3EL3ERBR7"'X
7580 O$(57)="BR3ND7G2RD3L2GEFRD2G2ER3GR3GLR6D3GL5EL3GBR10BU2UHU10HND10NG2BR5"'Y
7590 O$(58)="BR3NG2R3GR3DNL2R4EG5NR3NL4G9ERE2R5FNL6GLR4GLR2E2BU13BR3"'Z
7600 O$(59)="R5G5NU4BD2E7F7G7H7BD2D5R5NH4BR4R5U5NG4BU4U5L5NF4BL2BD3G4F4E4H4BD3GFEBU7BR6"
7610 O$(60)="R15D15L15U14BR2BDR11D11L11U10BR2BDR7D7L7U6BR2BDR3D3L3U2BU7BR10"
7620 O$(61)="BR3G2NUDLD5NF7EDF6NE6RE6NHU5LU2LNL3HL2G2NRNH2DH3DLBUBR11"
7630 O$(65)="BRBD4NR3GDF3HGND5GD4FERENU8F2EGHU9NL2EBR4BU4"'a
7640 O$(66)="BR3GND13GHFD11LF2E2RNU6EU5HG2BU8BR6"'b
7650 O$(67)="BR2BD4ND10R2F2GUHL3GRD7F2URE2BU12BR3"'c
7660 O$(68)="F7RH7RF6EGD6GNU5LG2U2GH2RNU6FU7EBU5BR8"'d
7670 O$(69)="BR3BD4G3RD6LF2UNU8NE3U4RERURELUNL4HL2BU5BR7"'e
7680 O$(70)="BR7GLULGND13GD2NL2NR3D9LF2E2BU13BR5"'f
7690 O$(71)="BD4BR2ND9G2RD6GRFR2NE5NG5R3FD2G2L4HR5EU12HND9NG3BR6BU4"'g
7700 O$(72)="BR3GND12GHFD10LF2EBR2DFHUNU7EU6HG2BU7BR6"'h
7710 O$(73)="BR2DLDBD4EFLND8GLFD5LF2EBU14BR3"'i
7720 O$(74)="BR4DLDBD4NENR2NGD5RNU4DRDG2LNH2URBU14BR5"'j
7730 O$(75)="BR3GND13GHFD11LF2EBU7E2ND4FD3G2LRFND2RD3E2BU13BR3"'k
7740 O$(76)="BR3GND13GHFD11LF2EBU14BR3"'l
7750 O$(77)="BR2BD4G2RD7LF2ELU9FE2D10FELU9FE2D10FNE2U10EBR4BU4"'m
7760 O$(78)="BR2BD4G2RD7LF2ELU9FE2D10FNE2U10FBU6BR4"'n
7770 O$(79)="BR4BD4G2ND7GD6LF2RE2NU8RU6RH2BU5BR5"'o
7780 O$(80)="BR3BD5G2RD6NG2D6GE2FHU4NU8RFENU8EFHU7HNG2BU5BR5"'p
7790 O$(81)="BR2BD6ND8G2RD5LF2RE2NU6D5GE2FHU9EGHLULBU6BR7"'q
7800 O$(82)="BR4BD13G2NU10H2RU7LE2FDENRURF2GBR4BU7"'r
7810 O$(83)="BR3BD4GND3GD2FR6ND3FD2NG3LG2GUGU2GUL2GE9GL3ENL3HLBU4BR8"'s
7820 O$(84)="BR4G2D2L2R2D9LF2NE2U11R2L2U2UEBR4"'t
7830 O$(85)="BR2BD4ND10G2RD7LF2E3NU7D2R2GU11G2BU6BR6"'u
7840 O$(86)="BR2BD3G2DEDND9RD9E2RNU7EU6RHUG2BU6BR6"'v
7850 O$(87)="BR2BD3G2DED9LDR2UNU7E2ND3U7F2LD7RE2ND3U7F2LD7RBU14BR3"'w
7860 O$(88)="BD5F10UH9GF9BL9U3RD2E9D3LU3BU5BR4"'x
7870 O$(89)="BR2BD5ND9G2RD6LF2E3NU5D2FD3L5GER2DR2E2U2HU8RHG2BU7BR6"'y
7880 O$(90)="BR3BD4NG3RNG2RFNL2DR2EG4NL3NR3G5ER2DNR3FR2NE3UEBU13BR5"'z
7890 RETURN
7900 REM * END OF LISTING
