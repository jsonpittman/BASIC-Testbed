1 REM***************************
2 REM*   TIMECARD CALCULATOR   *
3 REM*   COPYRIGHT (C) 1988    *
4 REM*    BY FRED B. SCERBO    *
5 REM*    60 HARDING AVENUE    *
6 REM*  NORTH ADAMS, MA 01247  *
7 REM***************************
8 CLEAR1000
9 CLS0
10 PRINTSTRING$(32,188)STRING$(32,204);
15 FORI=1TO 160 :READ A:PRINTCHR$(A+128);:NEXT
20 PRINTSTRING$(32,195)STRING$(32,179);
25 PRINT@358," TIMECARD CALCULATOR ";
30 PRINT@390,"  BY FRED B. SCERBO  ";
35 PRINT@422," COPYRIGHT (C) 1988  ";
40 DATA94,92,94,92,90,93,88,93,92,93,92,93,88,93,92,92,90,21,28,28,26,30,28,29,20,30,28,29,20,30,28,29
45 DATA88,80,90,,88,85,,85,,85,,85,,85,,80,82,21,,,,26,,21,,26,,21,,26,,21
50 DATA,,90,,,85,,85,,85,,85,,85,92,92,90,21,,,,30,28,29,,30,29,28,,26,,21
55 DATA,,90,,,85,,85,,85,,85,,85,,,80,21,,,,26,,21,,26,21,18,,26,,21
60 DATA,81,91,,,87,82,87,82,,,87,82,87,83,83,90,21,19,19,26,27,16,23,17,27,,27,17,27,19,23
65 IFINKEY$<>CHR$(13)THEN65
70 CLS
75 PRINT@232,"ROUND-OFF (Y/N) ?";
80 X$=INKEY$:IFX$=""THEN80
85 IFX$="Y"THENR=1:GOTO100
90 IFX$="N"THENR=0:GOTO100
95 GOTO80
100 A$(4)="00:00"
105 A$(1)="00:00":A$(2)="00:00":A$(3)="00:00"
110 GOSUB115:GOTO185
115 PRINT@0,"";:PRINT:PRINT
120 PRINTTAB(5)"TIME CLOCK CALCULATOR"
125 PRINT
130 PRINTTAB(5)"STARTING TIME:   "A$(1)
135 PRINT
140 PRINTTAB(5)"ENDING TIME:     "A$(2)
145 PRINT
150 PRINTTAB(5)"TIME ELAPSED:    "A$(3)
155 PRINT
160 PRINTTAB(5)"TOTAL WORK TIME: "A$(4)
165 PRINT
170 PRINTTAB(5)"PRESS SPACEBAR FOR MORE"
175 PRINTTAB(5)"PRESS CLEAR FOR RESTART"
180 RETURN
185 REM START CALCULATIONS
190 FORQ=1TO2
195 FORI=1TO4
200 X$=INKEY$:IFX$=""THEN200
205 IFX$=CHR$(13) AND I=4THEN260
210 IFX$=" "THEN105
215 IFX$=CHR$(12)THEN70
220 X=ASC(X$)-48:IFX<0THEN200
225 IFX>9THEN200
230 Z$=Z$+X$
235 IFI=1THENA$(Q)="00:0"+Z$
240 IFI=2THENA$(Q)="00:"+Z$
245 IFI=3THENA$(Q)="0"+LEFT$(Z$,1)+":"+RIGHT$(Z$,2)
250 IFI=4THENA$(Q)=LEFT$(Z$,2)+":"+RIGHT$(Z$,2)
255 GOSUB115:NEXTI
260 Z$=""
265 CH=VAL(MID$(A$(Q),4,1)):IF CH=>6THEN Z$="":A$(Q)="00:00":GOSUB115:GOTO195
270 NEXTQ
275 A(2,2)=VAL(LEFT$(A$(2),2)):A(2,1)=VAL(RIGHT$(A$(2),2))
280 A(1,2)=VAL(LEFT$(A$(1),2)):A(1,1)=VAL(RIGHT$(A$(1),2))
285 IFA(2,1)<A(1,1)THEN A(2,1)=A(2,1)+60:A(2,2)=A(2,2)-1
290 A(3,1)=A(2,1)-A(1,1)
295 IF R=0THEN320
300 IF A(3,1)=>8 AND A(3,1)<15  THEN A(3,1)=15
305 IF A(3,1)=>23 AND A(3,1)<30 THEN A(3,1)=30
310 IF A(3,1)=>38 AND A(3,1)<45 THEN A(3,1)=45
315 IF A(3,1)=>53 AND A(3,1)<60 THEN A(3,1)=0:A(2,2)=A(2,2)+1
320 IFA(2,2)<A(1,2)THEN A(2,2)=A(2,2)+12
325 A(3,2)=A(2,2)-A(1,2)
330 C$=STR$(A(3,2)):IF LEN(C$)=2 THEN C$="0"+RIGHT$(C$,1) ELSE C$=RIGHT$(C$,2)
335 D$=STR$(A(3,1)):IF LEN(D$)=2 THEN D$="0"+RIGHT$(D$,1) ELSE D$=RIGHT$(D$,2)
340 A$(3)=C$+":"+D$
345 GOSUB115
350 C(1)=VAL(C$):D(1)=VAL(D$)
355 C(2)=VAL(LEFT$(A$(4),2)):D(2)=VAL(RIGHT$(A$(4),2))
360 D(3)=D(1)+D(2):IF D(3)=>60 THEN D(3)=D(3)-60:C(1)=C(1)+1
365 C(3)=C(1)+C(2)
370 C$=STR$(C(3)):IFLEN(C$)=2 THEN C$="0"+RIGHT$(C$,1) ELSE C$=RIGHT$(C$,2)
375 D$=STR$(D(3)):IFLEN(D$)=2 THEN D$="0"+RIGHT$(D$,1) ELSE D$=RIGHT$(D$,2)
380 A$(4)=C$+":"+D$
385 GOSUB115
390 X$=INKEY$:IFX$=""THEN390
395 IFX$=" "THEN105
400 IFX$=CHR$(12)THEN70
405 GOTO390
410 GOTO410
