0 ' COPYRIGHT 1990, FALSOFT INC
100 CLEAR 650
120 DIM LI$(8)
140 DIM RV(8)
160 CLS0:PRINT @0,"----- DATA BASE RETRIEVAL------a
180 LI$(1)="1. CHECK LISTING
200 LI$(2)="2. DEPOSIT LISTING
220 LI$(3)="3. BILL LISTING
240 LI$(4)="4. SPECIAL REPORTING
260 LI$(5)="5. RUN THE DATABASE UPDATE
280 LI$(6)="6. END THIS SESSION
300 SL=96:NL=6:RC=0:GOSUB 9020
320 ON A GOTO 460,444,442,3000,440,340
340 PRINT @416,"** SESSION OVER - TOUCH ANY KEY"
360 FOR I= 1 TO 150:AA$= INKEY$:IF AA$ <> "" GOTO 420
380 NEXT I:PRINT @416,STRING$(31," ")
400 FOR I= 1 TO 60:AA$= INKEY$:IF AA$ <> "" GOTO 420 ELSE NEXT I:GOTO 340
420 CLS:END
440 RUN "DATAB"
442 RT$="BILL":RV(8)=2:RX$="B":GOTO 470
444 RT$="DEPOSIT":RV(8)=3:RX$="D":GOTO 470
460 RT$="CHECK":RV(8)=1:RX$="C"
470 AD=1:LI$(1)="1. FIND A SPECIFIC "+RT$
480 LI$(2)="2. FIND A RANGE OF "+RT$+"S":LI$(3)="3. RETURN TO THE MAIN MENU
490 LI$(4)="4. RUN THE DATABASE UPDATE":LI$(5)="5. END THIS SESSION
500 CLS:PRINT @25,"------b":PRINT @0," --- FIND (SOME) ";RT$;"S ";
520 SL=96:NL=5:GOSUB 9020
540 ON A GOTO 560,1035,160,440,340
560 CLS:PRINT @25,"------c":PRINT @0,"-- SEARCH FOR A ";RT$;" ";
580 SL=64:VT$="N":LV=1000:HV=9999:P$="ENTER "+RT$+" NUMBER"
600 GOSUB 9220:SN$=VA$
620 SL=128:VT$="N":HV=12:LV=1
640 P$="ENTER MONTH OF "+RT$+": 1-12":GOSUB 9220:MM$=RIGHT$("0"+VA$,2)
660 SL=192:P$="NEXT ENTER THE DAY: 1-31"
680 VT$="N":LV=1:HV=31:GOSUB 9220
700 DD$=RIGHT$("0"+VA$,2):GOSUB 9800
720 MK$=MM$+DD$+RX$+SN$:GOSUB 9900:IF GE=2 GOTO 1022
740 CLS0
750 HF$="<<<<<<<<< CHECK FOUND >>>>>>>>>>"
755 IF RX$ = "B" THEN MID$(HF$,9,7) = "<< BILL"
757 IF RX$ = "D" THEN MID$(HF$,9,7) = "DEPOSIT"
758 PRINT HF$
760 J=INSTR(11,LI$,CHR$(127))
765 CA=VAL(MID$(LI$,11,J-11))/100:SL=64
770 CI$=MID$(LI$,10,1):IF CI$="Y" THEN CD$="' ' YES" ELSE CD$="'*'NO"
800 I=INSTR(17,LI$,CHR$(127)):CP$=MID$(LI$,17,I-17)
860 CF$=RIGHT$(LI$,(LEN(LI$)-I))
880 PRINT @224,"NUMBER  DATE CLEARED    AMOUNT"
900 PRINT @256," ";SN$;"   ";MM$;"/";DD$;" ";CD$;" ";
910 PRINT USING "##,###.##";CA
915 PRINT @288," ":FOR J=1 TO 4:PRINT " ":NEXT J
920 IF RT$="CHECK" THEN PRINT @288,"PAID TO: ";CP$:PRINT @352,"PAID FOR: ";CF$
930 IF RT$="DEPOSIT" THEN PRINT @288,"SOURCE: ";CP$
940 IF RT$="BILL" THEN PRINT @288,"FROM: ";CP$:PRINT @352,"FOR:";CF$
960 PRINT @192,"-----";RT$;" DETAILS -----"
980 LI$(1)="1. RETURN TO PREVIOUS SCREEN"
990 LI$(2)="2. RUN THE DATABASE UPDATE"
1000 LI$(3)="3. END THIS SESSION":NL=3:GOSUB 9020
1020 ON A GOTO 470,440,340
1022 CLS:PRINT "*** ";RT$;" ";SN$;" ";MM$;"/";DD$;" NOT FOUND"
1030 SL=128:GOTO 980
1035 FOR I=1 TO 7:RV(I)=0:NEXT I
1040 CLS:ST=0:NS$=RIGHT$("    "+RT$,7)
1045 PRINT @0,"--FIND ";NS$;"S WITHIN A RANGE--
1050 PRINT @31,"r":ST=0
1060 LI$(1)="1. BY "+RT$+" NUMBER":LI$(2)="2. BY DATE"
1080 LI$(3)="3. BY CLEARED/UNCLEARED":LI$(4)="4. BY AMOUNT"
1100 LI$(5)="5. BY PAID TO OR FROM":LI$(6)="6. BY PURPOSE"
1110 LI$(7)="7. PRINT TOTALS":LI$(8)="8. CONTINUE"
1112 IF RV(8)=3 THEN LI$(6)="6. # NOT USED #"
1115 FOR I=1 TO 7:IF RV(I) <> 0 THEN MID$(LI$(I),3,1)="*"
1118 NEXT I
1120 NL=8:SL=64:GOSUB 9020
1140 ON A GOTO 1160,1260,1440,1480,1580,1680,1155,1780
1155 IF RV(7)=0 THEN RV(7)=1 ELSE RV(7)=0
1157 GOTO 1040
1160 P$="ENTER LOWEST NUMBER 1000-9998":SL=320:VT$="N"
1180 LV=1000:HV=9998:GOSUB 9220:LN$=VA$
1200 P$="ENTER HIGHEST NUMBER "+STR$(INT(VV+1))+"-9999"
1220 SL=320:VT$="N":LV=VV+1:HV=9999:GOSUB 9220:HN$=VA$
1240 RV(1)=1:GOTO 1040
1260 P$="ENTER LOWEST MONTH 1-11":SL=320:VT$="N"
1280 HV=11:LV=1:GOSUB 9220:LM$=RIGHT$("0"+VA$,2)
1300 P$="ENTER LOWEST DAY 1-30":SL=320:VT$="N"
1320 LV=1:HV=30:GOSUB 9220:LD$=RIGHT$("0"+VA$,2)
1340 P$="ENTER HIGHEST MONTH 1-12":SL=320:VT$="N"
1360 HV=12:LV=1:GOSUB 9220:HM$=RIGHT$("0"+VA$,2)
1380 P$="ENTER HIGHEST DAY 1-31":SL=320:VT$="N"
1400 LV=1:HV=31:GOSUB 9220:HD$=RIGHT$("0"+VA$,2)
1420 RV(2)=1:LX$=LM$+LD$:HX$=HM$+HD$:GOTO 1040
1440 P$="ENTER 'Y' OR '*'":SL=320:VT$="A":HV=1
1460 GOSUB 9220:CC$=VA$:RV(3)=1:GOTO 1040
1480 P$="ENTER LOWEST AMOUNT":SL=320:VT$="D"
1500 LV=1:HV=99999.99:GOSUB 9220:LA=VV
1520 P$="ENTER HIGHEST AMOUNT":SL=320:VT$="D"
1540 LV=1:HV=99999.99:GOSUB 9220:HA=VV
1560 RV(4)=1:GOTO 1040
1580 P$="ENTER LOW VALUE OF TO/FROM":SL=320:VT$="A"
1600 HV=24:GOSUB 9220:LT$=VA$
1620 P$="ENTER HIGH VALUE OF TO/FROM":SL=320:VT$="A"
1640 HV=24:GOSUB 9220:HT$=VA$
1660 RV(5)=1:GOTO 1040
1680 IF RV(8)=3 THEN GOTO 1040
1690 P$="ENTER LOW VALUE PAID FOR":SL=320:VT$="A"
1700 HV=58:GOSUB 9220:LF$=VA$
1720 P$="ENTER HIGH VALUE PAID FOR ":SL=320:VT$="A"
1740 HV=58:GOSUB 9220:HF$=VA$
1760 RV(6)=1:GOTO 1040
1780 CLS:PRINT "---------- SEARCHING ----------":PL=64:CLOSE #1
1790 RC=0:ON RV(2) GOTO 1820
1800 SG$="M01D01/CHK":GOTO 1860
1820 IF LD$ > "15" THEN DD$ = "15" ELSE DD$ = "01"
1840 MM$=LM$:SG$ = "M"+MM$+"D"+DD$+"/CHK"
1860 OPEN "I",#1,SG$
1862 IF RV(8) = 4 THEN GOTO 3105
1863 IF RV(8) = 5 THEN GOTO 4105
1865 PL=1:GOSUB 1866:GOTO 1880
1866 PRINT @0,"-------   SEARCH FOUND ------"
1867 PRINT @2,RT$;
1868 PRINT @32,"DATE NUMBR CD   AMOUNT     ";
1870 IF RV(8) = 1 THEN PRINT "TO" ELSE PRINT "FROM":RETURN
1879 'RI=1 RECORD FOUND - PRINT, RI=2 END-OF FILE
1880 GOSUB 10280:ON RI GOTO 1900,2020
1900 RC=RC+1:PL=PL+1
1920 J=INSTR(10,LI$,CHR$(127)):K=INSTR(J+1,LI$,CHR$(127))
1940 PRINT @(32*PL),LEFT$(LI$,2);"/";MID$(LI$,3,2);" ";
1950 PRINT MID$(LI$,6,4);" ";MID$(LI$,10,1);" ";
1952 CA=VAL(MID$(LI$,11,J-11))
1955 PRINT USING "##,###.## ";CA/100;
1960 ST=ST+CA
1980 PRINT LEFT$(MID$(LI$,J+1,K-J-1),9);
1990 IF PL< 14 THEN GOTO 1880
1992 PRINT @480,"PRESS ANY KEY TO CONTINUE";
1994 A$=INKEY$:IF A$="" THEN GOTO 1994
1996 CLS:GOSUB 1866:PL=2:GOTO 1880
2020 IF RC=0 THEN PRINT "** NO RECORDS IN THIS RANGE"
2022 IF RV(7) = 0 THEN PRINT @480,"-END OF SELECT - PRESS ANY KEY-";
2024 IF RV(7) = 1 THEN PRINT @480,"PRESS ANY KEY";:PRINT USING "##,###.##";ST/100;:PRINT "  TOTALS";
2030 CLOSE #1
2040 A$=INKEY$:IF A$="" THEN GOTO 2040 ELSE GOTO 470
3000 CLS5:PRINT @0,"----- SPECIAL  REPORTING ------x
3002 LI$(1)="1. CHECKS & DEPOSITS (STATEMENT)
3004 LI$(2)="2. CHECKS & BILLS    (BALANCE)
3006 LI$(3)="3. RETURN TO THE MAIN MENU
3008 LI$(4)="4. RUN THE DATABASE UPDATE
3010 LI$(5)="5. END THIS SESSION
3011 CT=0:BT=0:DT=0:OB=0:RC=0
3012 RT$="RECORD":SL=96:NL=5:GOSUB 9020
3014 ON A GOTO 3020,4000,160,440,340
3020 FOR I=1 TO 7:RV(I)=0:NEXT I
3030 CLS0:PRINT "----- OPENING BALANCE? -------y
3040 LI$(1)="1. ENTER AN OPENING BALANCE     
3050 LI$(2)="2. ENTER RECORD SELECTION RANGES
3060 LI$(3)="3. RETURN TO REPORTING MENU-x
3070 SL=128:NL=3:GOSUB 9020
3072 ON A GOTO 3080,3100,3000
3080 P$="BALANCE ( . PLUS 2 DECIMALS REQUIRED)":SL=320:VT$="D"
3090 LV=1:HV=99999.99:GOSUB 9220:OB=VV:GOTO 3030
3100 RV(8)=4:GOTO 1040
3105 GOSUB 3900
3110 GOSUB 10280:ON RI GOTO 3120,3250
3120 RC=RC+1:PL=PL+1
3130 J=INSTR(10,LI$,CHR$(127)):K=INSTR(J+1,LI$,CHR$(127))
3140 PRINT @(32*PL),LEFT$(LI$,2);"/";MID$(LI$,3,2);" ";
3150 PRINT MID$(LI$,6,4);" ";MID$(LI$,10,1);"  ";
3160 CA=VAL(MID$(LI$,11,J-11))
3170 IF MID$(LI$,5,1)="C" THEN PRINT "         ";
3180 PRINT USING "#####.##";CA/100;
3190 IF MID$(LI$,5,1)="C" THEN CT=CT+CA ELSE DT=DT+CA
3210 IF PL< 14 THEN GOTO 3110
3220 PRINT @480,"PRESS ANY KEY TO CONTINUE";
3230 A$=INKEY$:IF A$="" THEN GOTO 3230
3240 CLS:GOSUB 3900:GOTO 3110
3250 IF RV(7)=0 THEN PRINT " ":GOTO 3270
3255 PRINT " ":PRINT "SUBTOTALS     ";
3257 PRINT USING "#####.##";DT/100;
3260 PRINT USING " #####.##";CT/100
3265 PRINT "OP BAL/TOTALS ";
3266 PRINT USING "#####.##";OB;
3267 FT=OB+((DT-CT)/100)
3268 PRINT USING " #####.##";FT
3270 IF RC=0 THEN PRINT "** NO RECORDS IN THIS RANGE
3275 PRINT "-END OF SELECT - PRESS ANY KEY-";:CLOSE#1
3280 A$=INKEY$:IF A$="" THEN GOTO 3280 ELSE GOTO 3000
3900 CLS:PRINT "--- CHECK/DEPOSIT REPORT -----"
3910 PRINT @32,"DATE  NMBR CD  DEPO-AMT CHCK-AMT"
3920 PL=1:RETURN
4000 RV(8)=5:FOR I=1 TO 7:RV(I)=0:NEXT I
4100 GOTO 1040
4105 GOSUB 4900
4110 GOSUB 10280:ON RI GOTO 4120,4250
4120 RC=RC+1:PL=PL+1
4130 J=INSTR(10,LI$,CHR$(127)):K=INSTR(J+1,LI$,CHR$(127))
4140 PRINT @(32*PL),LEFT$(LI$,2);"/";MID$(LI$,3,2);" ";
4150 PRINT MID$(LI$,6,4);" ";MID$(LI$,5,1);MID$(LI$,10,1);" ";
4160 CA=VAL(MID$(LI$,11,J-11)):TF$=MID$(LI$,J+1,K-J-1)
4170 PRINT LEFT$(TF$+"        ",9);
4175 IF MID$(LI$,5,1)="C" THEN PC=-1*CA/100 ELSE PC=CA/100
4180 PRINT USING "#####.##";PC;
4190 IF MID$(LI$,5,1)="C" THEN CT=CT+CA ELSE DT=DT+CA
4210 IF PL< 14 THEN GOTO 4110
4220 PRINT @480,"PRESS ANY KEY TO CONTINUE";
4230 A$=INKEY$:IF A$="" THEN GOTO 4230
4240 CLS:GOSUB 4900:GOTO 4110
4250 IF RV(7)=0 THEN GOTO 4270
4255 PRINT " BILL TOTAL           ";:PRINT USING " ######.##";DT/100
4260 PRINT "CHECK TOTAL          ";:PRINT USING " ######.##";-CT/100
4265 PRINT "OUTSTANDING AMOUNT    ";
4267 FT=((DT-CT)/100):PRINT USING " #####.##";FT
4270 IF RC=0 THEN PRINT "** NO RECORDS IN THIS RANGE
4275 PRINT "-END OF SELECT - PRESS ANY KEY-";:CLOSE#1
4280 A$=INKEY$:IF A$="" THEN GOTO 3280 ELSE GOTO 3000
4900 CLS:PRINT "------ CHECK/BILL REPORT -----"
4910 PRINT @32,"DATE  NMBR TC DESCRIP     AMOUNT"
4920 PL=1:RETURN
9020 FOR I= 1 TO NL:PRINT @SL,LI$(I)
9040 SL= SL+32:NEXT I
9060 PRINT @32,"*   SELECT FROM THE FOLLOWING"
9080 FOR I = 1 TO 200
9100 A$ = INKEY$:IF A$ <> "" GOTO 9180 ELSE NEXT I
9120 PRINT @32," ":FOR I = 1 TO 65
9140 A$ = INKEY$:IF A$ <> "" GOTO 9180 ELSE NEXT I
9160 GOTO 9060
9180 A = VAL(A$):IF A > 0 AND A < NL+1 THEN RETURN
9200 GOTO 9060
9220 PRINT @SL,P$ 'PROMPT FOR VARIABLE
9240 PRINT @SL+32," " 'CLEAR LINE
9260 PRINT @SL+32,""; 'REPOSITION CURSOR
9280 LINE INPUT "==> ";VA$ 'INPUT THE VARIABLE
9300 LA= LEN(VA$):IF VT$ = "D" GOTO 9460
9320 IF VT$ = "N" GOTO 9380
9340 IF LA > INT(HV) GOTO 9220
9360 RETURN 'VARIABLE IS IN RANGE
9380 VV= VAL(VA$)
9400 IF VV < LV GOTO 9220
9420 IF VV > HV GOTO 9220
9440 GOTO 9360
9460 IF LA > 9 OR LA < 3 GOTO 9220
9480 IF MID$(VA$,LA-2,1) <> "." GOTO 9220
9500 GOTO 9380
9800 IF DD$ > "15" THEN DF$ = "15" ELSE DF$ = "01"
9820 SG$ = "M" + MM$ + "D" + DF$ + "/CHK"
9840 CLOSE:OPEN "I",#1,SG$:RETURN
9860 'AD=1 LOCATE, 2 CHANGE, 3 DELETE
9880 'RA 1 ADDED,CHANGED,DELETED  0 NO ACTION
9900 IF EOF(1) = -1 GOTO 9990
9920 INPUT #1,LI$:IK$ = LEFT$(LI$,9)
9940 IF IK$<MK$ THEN GOTO 9900
9960 IF IK$=MK$ THEN GE=1 ELSE GE =2
9980 CLOSE:RETURN
9990 GE=2:CLOSE:RETURN
10000 IF IK$ < LK$ OR IK$ > HK$ GOTO 9900
10020 RC=RC+1:WRITE #2,LI$:GOTO 9900
10040 MV=VAL(MM$)
10060 IF MV = 12 AND DF$ ="15" THEN RETURN
10080 IF MV = 12 THEN DF$ = "15"
10100 IF DF$ = "01" THEN DF$ = "15" ELSE MV=MV+1
10120 MM$ = RIGHT$(STR$(MV+100),2):CLOSE #1
10140 SG$ = "M" + MM$ + "D" + DF$ + "/CHK"
10160 OPEN "I",#1,SG$:GOTO 9900
10180 IF RV(2) = 1 THEN GOTO 10200 ELSE MM=1:DD=1:GOTO 10220
10200 MM=VAL(LM$):IF LD$ < 16 THEN DD=1 ELSE DD=15
10220 SG$ = "M" + RIGHT$("0"+STR$(MM),2) + "D" + RIGHT$("0"+STR$(DD),2)+"/CHK"
10240 FOR I = 1 TO 6:IF MID$(SG$,I,1)= " " THEN MID$(SG$,I,1)="0"
10260 NEXT I:CLOSE #1:OPEN "I",#1,SG$
10280 A$=INKEY$:IF A$=" " THEN RI=2:RETURN 'FORCED RETURN
10282 IF EOF(1) = -1 GOTO 10680 'CHANGE FILES?
10300 INPUT #1,LI$:RI=0:RJ=0 '  EXAMINE RECORD
10310 ON RV(1) GOSUB 10380 'CHECK #
10312 ON RV(2) GOSUB 10430 'DATE
10313 IF RI=2 THEN RETURN
10314 ON RV(3) GOSUB 10440 'CLEARED
10316 ON RV(4) GOSUB 10480 'AMOUNT
10318 ON RV(5) GOSUB 10560 'PAID TO
10319 ON RV(6) GOSUB 10600 'PURPOSE
10320 IN$=MID$(LI$,5,1)
10322 ON RV(8) GOSUB 10520,10530,10540,10550,10556 'TYPE
10326 IF RJ <> 0 THEN GOTO 10280
10340 RI=1:RETURN
10380 IN$=MID$(LI$,6,4) 'NUMBER
10400 IF IN$ < LN$ OR IN$ > HN$ THEN RJ=1
10420 RETURN
10430 IN$=LEFT$(LI$,4)
10432 IF IN$ < LX$ THEN RJ=1:RETURN
10434 IF IN$ > HX$ THEN RI=2:RETURN 
10436 RETURN 'HIT
10440 IN$=MID$(LI$,10,1) 'CLEARED INDICATOR
10460 IF IN$ <> CC$ THEN RJ=1
10470 RETURN
10480 I=INSTR(11,LI$,CHR$(127))
10490 IN = VAL(MID$(LI$,11,I-11))/100
10500 IF IN < LA OR IN > HA THEN RJ=1
10510 RETURN
10520 IF IN$ <> "C" THEN RJ=1:RETURN ELSE RETURN
10530 IF IN$ <> "B" THEN RJ=1:RETURN ELSE RETURN
10540 IF IN$ <> "D" THEN RJ=1:RETURN ELSE RETURN
10550 IF IN$  = "B" THEN RJ=1:RETURN ELSE RETURN
10556 IF IN$  = "D" THEN RJ=1:RETURN ELSE RETURN
10560 J=1+INSTR(11,LI$,CHR$(127)):K=INSTR(J+1,LI$,CHR$(127))
10570 IN$=MID$(LI$,J,K-J)
10580 IF IN$ < LT$ OR IN$ > HT$ THEN RJ=1
10590 RETURN
10600 J=1+INSTR(11,LI$,CHR$(127)):K=INSTR(J+1,LI$,CHR$(127))
10620 J=LEN(LI$):IN$=RIGHT$(LI$,J-K)
10640 IF IN$ < LF$ OR IN$ > HF$ THEN RJ=1
10660 RETURN
10680 IF LEFT$(SG$,6)="M12D15" THEN RI=2:RETURN
10700 MM=VAL(MID$(SG$,2,2)):DD=VAL(MID$(SG$,5,2))
10720 IF DD=1 THEN DD=15:GOTO 10760
10740 IF DD=15 THEN DD=1:MM=MM+1
10760 MM=100+MM:DD=100 + DD
10770 RS$=RIGHT$(STR$(MM),2)
10780 MID$(SG$,2,2)=RS$
10790 RS$=RIGHT$(STR$(DD),2)
10800 MID$(SG$,5,2)=RS$
10820 CLOSE #1:OPEN "I",#1,SG$:GOTO 10280
