0 ' COPYRIGHT 1989  FALSOFT,INC
5 'EZELABLS' by Keiran  Kenny,     Sydney, 1988.
10 CLS:DIMN$(8)
20 TB=20 'TAB(20)
30 POKE150,18 '2400 baud.
40 PRINT@64,"DO YOU WANT TO:":PRINT
50 PRINTTAB(2)"1. PRINT ADDRESSES IN DATA?":PRINT
60 PRINTTAB(2)"2. TYPE AND PRINT ONE-OFF",TAB(5)"ADDRESSES?":PRINT
70 PRINTTAB(2)"3. END PROGRAM?":PRINT
80 PRINTTAB(8)"PRESS 1, 2 OR 3."
90 K$=INKEY$:IFK$=""THEN90
100 IFK$="1"THEN140
110 IFK$="2"THEN140
120 IFK$="3"THENCLS:END
130 GOTO90
140 PRINT:PRINT"PRINTER READY? - PRESS ANY KEY.":EXEC44539
150 PRINT#-2,CHR$(27);CHR$(18);CHR$(27);CHR$(31); 'NLQ Bold
160 IFK$="2"THENCLS:PRINT@32,"TYPE ADDRESS:":PRINT@64:GOTO270
170 N=N+1:READN$(N):IFN$(N)="ZZ"THENCLS:RESTORE:N=0:PRINT"DATA END.":GOTO40ELSEIFN$(N)="XX"THENN=N-1:CLS:GOTO180ELSE170
180 PRINT@128
190 FORT=1TON
200 PRINTTAB(3)N$(T)
210 NEXT
220 PRINT:PRINTTAB(3)"PRINT? Y/N"
230 C$=INKEY$:IFC$=""THEN230
240 IFC$="Y"THEN310
250 IFC$="N"THENN=0:GOTO170
260 GOTO230
270 FORN=1TO7
280 PRINT"LINE"N;CHR$(8)":";:LINEINPUTN$(N)
290 IFN=7THEN310ELSEIFN$(N)=CHR$(95)THENN=N-1:GOTO310
300 NEXT
310 TM=INT((9-N)/2)
320 FORT=1TON
330 IFLEN(N$(T))>Z THEN340ELSE350
340 Z=LEN(N$(T))
350 NEXT
360 FORD=1TOTM:PRINT#-2:NEXT
370 FORT=1TON
380 PRINT#-2,TAB(TB-(Z/2))N$(T)
390 NEXT
400 FORD=1TO9-N-TM:PRINT#-2:NEXT
410 N=0:IFK$="1"THEN170ELSE40
420 DATA "Keiran Kenny,","2/45 Cremorne Road,",Cremorne N.S.W. 2090.,XX
1000 DATA "The Submissions Editor,","RAINBOW,","P.O. Box 385,","Prospect KY,",U.S.A. 40059.,XX
1010 DATA "The Editor,","Australian CoCo Magazine,","P.O. Box 1742,",Southport Q'ld. 4215,XX
1020 DATA MR. William P. Nutt,XX
2000 DATA ZZ
