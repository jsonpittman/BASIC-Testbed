0 ' COPYRIGHT 1989  FALSOFT,INC
2 ' DRAW INTRO SCREEN
5 POKE 65495,0
10 CLS0:PMODE3,1
12 PRINT@224,"      B   L   A   C   K"
14 PRINT@288,"          B   O   X"
16 PRINT@448," [C] COPYRIGHT 1985 EVAN VEAL"
18 TIMER=0
19 IF TIMER>=300 THEN20 ELSE 19
20 CLEAR 2500
25 CLS0:PRINT@256,"     INSTRUCTIONS(Y OR N)?"
26 A$=INKEY$:IF A$<>"Y" AND A$<>"N" THEN 26 ELSE IF A$="Y" THEN GOSUB 2750
27 '
28 ' INITIALIZE VARIABLES
30 DATA 62,1,88,25,106,43,124,61,142,79,160,97,178,115,196,133,214,151,236,174
40 DATA U4NR5U4R5D8BR4,R7L5U8L2R7D4NL5D4BR4,BU8NR5D8R5BR4,NL1NR5U8NL1R5D8BR4,NR5U4NR2U4R5BD8BR4,U4NR2U4R5BD8BR4,U8R5ND1L5D8R5U3NL2D3BR4,U4NU4R5U4D8BR4,R4L2U8NL2R2BD8BR5,NU3R4U8NL2NR2BD8BR4,U8D4R2NE4F4BR4,NU8R5BR4,U8F4E4D8BR4,U8F8NU8BR4,NR5U8R5D8BR4
50 DIM NUM$(40),L$(15,2),CL(0,10),CC(0,10),CS(0,10),CD(0,10),GR(9,9),ZX(9),ZY(9),BL(5,2),B(10,2),MV$(40,2),M(20,2),AL$(26)
60 DATA U8R5D4L5D4BR9,U8R8D8NL8NH2NF2BR4,U8R5D4L5R1F4BR4,R5U4L5U4R5BD8BR4,BR3U8NL3R3BD8BR3,NU8R5NU8BR4,BU8D4F4E4U4BD8BR4,BU8D8E4F4U8BD8BR4,U2E4U2BL4D2F4D2BR4,BR3U4NE4NH4D4BR8,BU8R8G8R8BR4
70 FOR N=0TO9:READA,B:ZX(N)=A:ZY(N)=B:NEXT N
80 FOR N=1TO26:READA$:AL$(N)=A$:NEXT N
130 NUM$(1)="R4L2U8NG2BR7BD8":NUM$(2)="NR6U4R4U4NL5BR7BD8":NUM$(3)="R5U4NL2U4NL5BR5BD8":NUM$(4)="BR4U6D2NR2L4U4BR10BD8":NUM$(5)="R6U5L4U3R5BR5BD8"
140 NUM$(6)="NU8R5U3L5BR10BD3":NUM$(7)="U2E6NL7BR5BD8":NUM$(8)="NR5U4NR5U4R5D8BR5":NUM$(9)="BU5NR5U3R5D8BR5":NUM$(0)="NR8U8R8NG8D8BR5"
150 FOR N=10TO40
160 K$=STR$(N):K1=VAL(MID$(K$,2,1)):K2=VAL(RIGHT$(K$,1))
170 NUM$(N)=NUM$(K1)+NUM$(K2)
180 K$=""
190 NEXT N
210 PCLS:COLOR2,5:LINE(4,3)-(12,10),PSET,BF:GET(0,0)-(15,15),CC,G:COLOR3,5:LINE(2,2)-(12,12),PSET,BF:GET(0,0)-(15,15),CD,G
220 PCLS:GET(0,0)-(15,15),CD,G
230 L$(1,1)="3,3":L$(1,2)="R10D10U5NL10D5L10U10":L$(2,1)="1,10":L$(2,2)="R12H6G6"
240 L$(3,1)="7,2":L$(3,2)="R4D5R4D2L4D5L4U5L4U2R4U5":L$(4,1)="3,3":L$(4,2)="R10G10R10H10"
250 L$(5,1)="3,3":L$(5,2)="R10NG10D10NH10L10U10":L$(6,1)="BM1,5":L$(6,2)="R14G7H7C1D5C3R14H7G7"
260 L$(7,1)="7,2":L$(7,2)="D6L3F5E5L3U6L4":L$(8,1)="3,7":L$(8,2)="R2U2R2U2R4D2R2D2R2D3L2D2L2D2L4U2L2U2L2U3"
270 L$(9,1)="3,3":L$(9,2)="R10D10L10U10F10":L$(10,1)="3,3":L$(10,2)="R10D10L10U10"
280 L$(11,1)="6,7":L$(11,2)="U6R4D6R4D3NL12L2D4L8U4L2U3R4":L$(12,1)="7,9":L$(12,2)="S2U10R4D10F8L20E8S4"
290 L$(13,1)="2,13":L$(13,2)="U8NR4U4R4ND4R8D8NL4D4L4NU4L8":L$(14,1)="2,14":L$(14,2)="U12R6ND12R6D6NL12D6L12"
300 L$(15,1)="7,10":L$(15,2)="U5H2U1R5D1G2D6R4D4L9U4R4"
310 PMODE3,1:PCLS:SCREEN1,1:COLOR2,5
320 MV=0:P=1:BL=1:M=0:CO=0:MA=0:TD=0:SC=5
324 '
325 ' DRAW GAME SCREEN
330 FOR Y=24TO 168 STEP 18:LINE(86,Y)-(230,Y),PSET:NEXTY
340 FOR X=86TO 230 STEP 18:LINE(X,24)-(X,168),PSET:NEXTX
350 LINE(81,18)-(235,173),PSET,B:LINE(81,18)-(60,0),PSET,BF:LINE(235,173)-(254,191),PSET,BF:LINE(235,18)-(254,0),PSET,BF:LINE(81,173)-(60,191),PSET,BF:LINE(60,0)-(254,191),PSET,B
360 FOR Y=24 TO 168 STEP 18:LINE(60,Y)-(81,Y),PSET:LINE(235,Y)-(254,Y),PSET:NEXTY:FOR X=86 TO230 STEP 18:LINE(X,0)-(X,18),PSET:LINE(X,173)-(X,191),PSET:NEXTX:PAINT(203,172),2,2
370 DRAW"BM3,30C2;"+AL$(2)+AL$(12)+AL$(1)+AL$(3)+AL$(11)
380 DRAW"BM13,46C2;"+AL$(2)+AL$(15)+AL$(24)
390 LINE(0,18)-(50,54),PSET,B
400 DRAW"BM6,109C4;"+AL$(19)+AL$(15)+AL$(12)+AL$(22)+AL$(5)
405 DRAW"BM8,132C4;"+AL$(2)+AL$(1)+AL$(3)+AL$(11)
410 DRAW"BM10,154C4;"+AL$(11)+AL$(9)+AL$(12)+AL$(12)
414 '
415 'PLACE BALLS
420 FOR X=1TO8:FOR Y=1TO8:GR(X,Y)=0:NEXTY,X
430 FOR N=1TO5
440 X=RND(8):Y=RND(8)
450 BL(N,1)=X:BL(N,2)=Y
460 GR(X,Y)=-1
470 FOR M=1TO5
480 IF M=N THEN M=5:GOTO500
490 IF (BL(M,1)=BL(N,1)) AND (BL(M,2)=BL(N,2)) THEN  GOTO 440
500 NEXT M,N
640 DRAW"BM0,174C2;"+AL$(13)+AL$(15)+AL$(22)+AL$(5)+AL$(19)+"U2BU2U2"
644 '
645 'START POLLING ROUTINE
650 PUT(0,176)-(30,184),CL,PSET
660 DRAW"BM0,184C2"+NUM$(MV)
670 X2=X1:Y2=Y1
680 X=JOYSTK(0):Y=JOYSTK(1)
690 Z=INT(X/7):GOSUB1770:X1=ZX
700 OX=Z
710 Z=INT(Y/7):GOSUB1760:Y1=ZY
720 OY=Z
730 PUT(X2,Y2)-(X2+15,Y2+15),CS,PSET
740 GET(X1,Y1)-(X1+15,Y1+15),CS,G
750 PUT(X1,Y1)-(X1+15,Y1+15),CC,PSET
760 A=PEEK(65280):IF A=254 OR A=126 THEN COLOR1,5:LINE(X1,Y1)-(X1+15,Y1+15),PSET,BF:COLOR3,5:GOTO780
770 X2=X1:Y2=Y1:GOTO680
780 SOUND150,2
790 IF OX<>0 AND OX<>9 AND OY<>0 AND OY<>9 THEN GOTO2020
800 IF OX=0 AND OY=0 THEN 2190
810 IF OX=0 AND OY=9 THEN 2190
820 IF OX=9 AND OY=0 THEN 2190
830 IF OX=9 AND OY=9 THEN 2190
834 '
835 'SAVE MOVE -CHECK IF REPEAT
840 K$=STR$(OX)+","+STR$(OY)
850 FOR N=0 TO MV
860 IF MV$(N,1)=K$ THEN 900
870 NEXT N
880 MV$(MV,1)=K$
890 GOTO1000
894 '
895 'REPEAT MOVE PROCESSING
900 Z=OX:GOSUB1770:X1=ZX
910 Z=OY:GOSUB1760:Y1=ZY
920 X$=MV$(N,0):GOSUB1940:X=K1:Y=K2
930 L=P
940 P=VAL(MV$(N,2))
950 IF P=17 THEN P=L:COLOR4,5:MV=MV-1:GOTO 1720
960 IF P=18 THEN P=L:COLOR3,5:MV=MV-1:GOTO1530
970 ZP$="Y"
990 GOTO 1600
994 '
995 'CONVERT X,Y TO GRAPHICS LOCATIONS
1000 IF X1=62 OR X1=236 THEN GOTO 1020
1010 IF Y1=1 OR Y1=174 THEN 1070
1020 IF X1=236 THEN 1050
1030 Y=INT(Y1/18):X=0
1040 FLG$="X+1":GOTO 1120
1050 Y=INT(Y1/18):X=9
1060 FLG$="X-1":GOTO1120
1070 IF Y1=174 THEN GOTO1100
1080 X=INT((X1-66)/18):Y=0
1090 FLG$="Y+1":GOTO1120
1100 X=INT((X1-66)/18):Y=9
1110 FLG$="Y-1":GOTO1120
1114 '
1115 'PROCESSING OF BEAM PATH
1120 IF LEFT$(FLG$,1)="Y" THEN 1320
1130 IF RIGHT$(FLG$,2)="-1" THEN GOTO 1230
1140 IF X=9 THEN 1580
1150 IF GR(X,Y)=-1 THEN 1510
1160 IF Y+1<10 THEN IF X=0 AND GR(X+1,Y+1)=-1 THEN 1580
1170 IF Y-1>-1 THEN IF X=0 AND GR(X+1,Y-1)=-1 THEN 1580
1180 IF Y-1<>0 THEN IF GR(X+1,Y-1)=-1 THEN FLG$="Y+1":GOTO1120
1190 IF Y+1<>9 THEN IF GR(X+1,Y+1)=-1 THEN FLG$="Y-1":GOTO1120
1200 IF GR(X,Y)=-1 THEN 1510
1210 IF X+1<>10 THEN X=X+1:GOTO1120
1220 GOTO1120
1230 IF X=0 THEN 1580
1240 IF GR(X,Y)=-1 THEN 1510
1250 IF Y+1<10 THEN IF X=9 AND GR(X-1,Y+1)=-1 THEN 1580
1260 IF Y-1>-1 THEN IF X=9 AND GR(X-1,Y-1)=-1 THEN 1580
1270 IF Y-1<>0 THEN IF GR(X-1,Y-1)=-1 THEN FLG$="Y+1":GOTO1120
1280 IF Y+1<>9 THEN IF GR(X-1,Y+1)=-1 THEN FLG$="Y-1":GOTO1120
1290 IF GR(X,Y)=-1 THEN 1510
1300 IF X-1<>-1 THEN X=X-1:GOTO1120
1310 GOTO1120
1320 IF RIGHT$(FLG$,2)="-1" THENGOTO 1420
1330 IF Y=9 THEN 1580
1340 IF GR(X,Y)=-1 THEN 1510
1350 IF X+1<10 THEN IF Y=0 AND GR(X+1,Y+1)=-1 THEN 1580
1360 IF X-1>-1 THEN IF Y=0 AND GR(X-1,Y+1)=-1 THEN 1580
1370 IF X-1<>0 THEN IF GR(X-1,Y+1)=-1 THEN FLG$="X+1":GOTO1120
1380 IF X+1<>9 THEN IF GR(X+1,Y+1)=-1 THEN FLG$="X-1":GOTO1120
1390 IF GR(X,Y)=-1 THEN 1510
1400 IF Y+1<>10 THEN Y=Y+1:GOTO1120
1410 GOTO1120
1420 IF Y=0 THEN 1580
1430 IF GR(X,Y)=-1 THEN 1510
1440 IF X+1<10 THEN IF Y=9 AND GR(X+1,Y-1)=-1 THEN 1580
1450 IF X-1>-1 THEN IF Y=9 AND GR(X-1,Y-1)=-1 THEN 1580
1460 IF X-1<>0 THEN IF GR(X-1,Y-1)=-1 THEN FLG$="X+1":GOTO1120
1470 IF X+1<>9 THEN IF GR(X+1,Y-1)=-1 THEN FLG$="X-1":GOTO1120
1480 IF GR(X,Y)=-1 THEN 1510
1490 IF Y-1<>-1 THEN Y=Y-1:GOTO1120
1500 GOTO1120
1504 '
1505 ' ***HIT***
1510 COLOR3,5
1520 MV$(MV,0)=MV$(MV,1):MV$(MV,2)="18"
1530 LINE(X1,Y1)-(X1+15,Y1+15),PRESET,BF
1540 LINE(X1,Y1)-(X1+15,Y1+15),PSET,BF
1550 GET(X1,Y1)-(X1+15,Y1+15),CS,G
1560 COLOR2,5
1565 SOUND 180,4
1570 MV=MV+1:GOTO650
1574 '
1575 '***REFLECTION***
1580 IF X=OX AND Y=OY THEN GOTO1700
1590 MV$(MV,0)=STR$(X)+","+STR$(Y):MV$(MV,2)=STR$(P)
1600 Z=X:GOSUB1770:X3=ZX
1610 Z=Y:GOSUB1760:Y3=ZY
1620 X$=L$(P,1):GOSUB1940:C=K1:C1=K2
1630 T=C+X1:T1=C1+Y1:T2=C+X3:T3=C1+Y3
1640 COLOR1,5:LINE(X3,Y3)-(X3+15,Y3+15),PSET,BF:COLOR3,5
1650 DRAW"BM"+STR$(T)+","+STR$(T1)+"C3"+L$(P,2):DRAW"BM"+STR$(T2)+","+STR$(T3)+"C3"+L$(P,2)
1660 GET(X1,Y1)-(X1+15,Y1+15),CS,G
1670 IF ZP$="Y" THEN ZP$="N":P=L:GOTO650
1680 P=P+1:MV=MV+1
1690 GOTO650
1694 '
1695 '***DETOUR***
1700 COLOR4,5
1710 MV$(MV,0)=MV$(MV,1):MV$(MV,2)="17"
1720 LINE(X1,Y1)-(X1+15,Y1+15),PSET,BF
1730 GET(X1,Y1)-(X1+15,Y1+15),CS,G
1740 COLOR3,5
1750 MV=MV+1:GOTO650
1754 '
1755 'X,Y/PIXEL CONVERSION 
1760 ZY=ZY(Z):RETURN
1770 ZX=ZX(Z):RETURN
1940 T=INSTR(1,X$,","):K1=VAL(LEFT$(X$,T-1)):K2=VAL(RIGHT$(X$,LEN(X$)-T)):X$="":RETURN
2014 '
2015 'PLACE BALL ON GRID
2020 K$=STR$(OX)+","+STR$(OY)
2030 FOR N=1TO10
2040 IF (B(N,1)=OX) AND (B(N,2)=OY) THEN LINE(X1,Y1)-(X1+15,Y1+15),PRESET,BF:GET(X1,Y1)-(X1+15,Y1+15),CS,G:GOTO2140
2050 NEXTN
2060 IF BL>=10 THEN GOTO680
2070 FOR N=1TO6
2080 CIRCLE(X1+9,Y1+9),N,2
2090 NEXT N
2100 GET(X1,Y1)-(X1+15,Y1+15),CS,G
2110 B(BL,1)=OX:B(BL,2)=OY
2120 BL=BL+1
2130 GOTO680
2140 BL=BL-1
2150 FOR H=N TO 9
2160 B(H,1)=B(H+1,1):B(H,2)=B(H+1,2)
2170 NEXTH
2180 GOTO680
2184 '
2185 ' MENU SELECTION
2190 J1=JOYSTK(0):J2=JOYSTK(1)
2200 IF J2<21 THEN LINE(0,95)-(56,114),PSET,B
2210 IF J2>42 THEN LINE(0,140)-(56,159),PSET,B
2215 IF (J2>=21) AND (J2<=42) THEN LINE(0,117)-(56,136),PSET,B
2220 A=PEEK(65280):IF A=254 OR A=126 THEN 2240
2230 LINE(0,95)-(56,114),PRESET,B:LINE(0,117)-(56,136),PRESET,B:LINE(0,140)-(56,159),PRESET,B:GOTO2190
2240 IF J2>42 THEN 310
2243 IF (J2>=21) AND (J2<=42) THEN LINE(0,117)-(56,136),PRESET,B:GOTO 650
2244 '
2245 'SOLVE GAME
2250 FOR N=1TO5
2270 Z=BL(N,1):GOSUB1770:X=ZX:Z=BL(N,2):GOSUB1760:Y=ZY
2280 FOR T=1TO5
2290 IF (B(T,1)=BL(N,1)) AND (B(T,2)=BL(N,2)) THEN BL(N,1)=-10:GOTO 2320
2300 NEXT T
2305 SOUND 30,7
2310 COLOR4,5:LINE(X,Y)-(X+15,Y+15),PSET,BF:GOTO2410
2320 COLOR2,5:LINE(X,Y)-(X+15,Y+15),PSET,BF
2330 FOR T=1TO500:NEXTT
2340 SOUND190,3
2350 FOR T=1 TO10
2360 COLOR4,5:LINE(X,Y)-(X+15,Y+15),PSET,BF
2370 FOR Z=1TO100:NEXTZ
2380 COLOR2,5:LINE(X,Y)-(X+15,Y+15),PSET,BF
2390 NEXT T
2400 FOR W=1TO750:NEXTW
2410 NEXT N
2414 '
2415 'SCORING
2420 FOR N=1TO5
2430 IF BL(N,1)<>-10 THEN SC=SC-1
2440 NEXT N
2450 IF SC=0 THEN SO=0:GOTO2500
2460 A=SC*100:B=1-(MV/32):SO=INT(A+(B*125))
2494 '
2495 'DRAW END SCREEN
2500 LINE(0,95)-(57,160),PRESET,BF
2510 DRAW"BM0,120C2;"+AL$(19)+AL$(3)+AL$(15)+AL$(18)+AL$(5)
2520 SO$=STR$(SO)
2530 DRAW"BM3,136C3"
2540 FOR N=1TOLEN(SO$)
2550 K$=MID$(SO$,N,1)
2560 IF K$=" " THEN 2600
2570 IF K$=""THEN 2600
2580 DRAWNUM$(VAL(K$))
2590 K$=""
2600 K$="":NEXT N
2610 LINE(0,15)-(56,55),PRESET,BF
2620 DRAW"BM6,12C2;"+AL$(16)+AL$(12)+AL$(1)+AL$(25)
2630 DRAW"BM0,25C2;"+AL$(1)+AL$(7)+AL$(1)+AL$(9)+AL$(14)+"BR4U1BU2U3R5U4L7D2"
2640 DRAW"BM9,45C2;"+AL$(25)+"BR6"+AL$(14)
2650 J1=JOYSTK(0)
2660 IF J1<=32 THEN LINE(6,34)-(21,48),PSET,B:LINE(25,34)-(39,48),PRESET,B
2670 IF J1>32 THEN LINE(25,34)-(39,48),PSET,B:LINE(6,34)-(21,48),PRESET,B
2680 IF PEEK(65280)=254 OR PEEK(65280)=126 THEN GOTO2690 ELSE GOTO2650
2690 IF J1<=32 THEN 310 ELSE END
2700 TIMER=0
2705 IF TIMER<=65 THEN 2705
2710 PRINT@480," press fire button to continue";
2720 IF PEEK(65280)<>126 AND PEEK(65280)<>254 THEN 2720 ELSE CLS:PRINT@11,"BLACK BOX":RETURN
2730 TIMER=0
2731 IF TIMER<=30 THEN 2731
2735 PRINT" <-MORE->"
2740 IF PEEK(65280)<>126 AND PEEK(65280)<>254 THEN 2740 ELSE RETURN
2745 'INSTRUCTIONS
2750 CLS:PRINT@11,"BLACK BOX"
2760 PRINT"OBJECT:"
2770 PRINT"  THE OBJECT OF BLACK BOX IS    TO LOCATE FIVE BALLS WHICH ARE  HIDDEN WITHIN THE GRID OR BOX.  YOU LOCATE THEM BY FIRING RAYS  OF LIGHT INTO THE BOX AND       OBSERVING WHERE THEY EXIT."
2780 PRINT"  THE LIGHT RAYS ARE BENT IN    PARTICULAR WAYS WHEN THEY ENCOU -NTER THE BALLS WITHIN THE BOX  AND YOU MUST DEDUCE THE BALLS'  POSITIONS BY OBSERVING HOW EACH RAY OF LIGHT IS BENT WITHIN THE GRID."
2790 GOSUB2700
2810 PRINT"PLAYING THE GAME:"
2820 PRINT"   WHEN THE GAME BEGINS THE     CURSOR WILL BE SOMEWHERE ON THE GRID. TO SHOOT RAYS OF LIGHT IN -TO THE GRID YOU POSITION THE   CURSOR IN ONE OF THE BORDER     BOXES AND PRESS THE FIRE BUTTON"
2830 PRINT" WHEN YOU DO THIS THE BOX MAY   TURN PURPLE OR ORANGE OR AN     ICON WILL APPEAR IN IT. IF IT   TURNS PURPLE THIS SIGNIFIES A   HIT. IF THE BOX TURNS ORANGE    IT SIGNIFIES A REFLECTION."
2840 GOSUB2700
2850 PRINT"  IF AN ICON APPEARS IT MEANS   THE BEAM OF LIGHT HAS EXITED    THE GRID AT SOME OTHER POINT."
2860 PRINT"   WHEN YOU THINK YOU HAVE      LOCATED A BALL MOVE THE CURSOR  ONTO ITS SUSPECTED LOCATION ON  THE GRID AND PRESS THE FIRE     BUTTON. A BALL WILL APPEAR      AT THAT POINT. TO REMOVE A BALL MERELY REPOSITION THE CURSOR"
2870 PRINT"OVER IT AND PRESS THE FIRE BUT  -TON AND IT WILL BE REMOVED."
2875 GOSUB2700
2880 PRINT"    WHEN YOU THINK YOU HAVE     LOCATED ALL OF THE BALLS OR AS  MANY AS POSSIBLE, THEN MOVE THE CURSOR TO ONE OF THE FOUR CORN  -ERS OF THE GRID AND PRESS THE  FIRE BUTTON. THIS WILL PUT YOU  IN THE SIDE MENU."
2890 PRINT"    HERE, SELECTING THE SOLVE   OPTION WILL GIVE THE SOLUTION   TO THE MAZE AND YOUR SCORE."
2892 GOSUB2730
2895 PRINT"   YOU MAY ALSO AT ANYTIME      DURING THE GAME GO TO THE SIDE  MENU AND SELECT THE <KILL>      OPTION WHICH WILL KILL THE"
2900 PRINT"CURRENT GAME AND GIVE YOU A NEW ONE."
2905 GOSUB2700
2920 PRINT:PRINT"   YOU ARE NOW READY TO FACE         THE CHALLENGE OF THE                   BLACK BOX"
2930 PRINT:PRINT"  GOOD LUCK AND GOOD HUNTING!"
2940 GOSUB2700
2950 RETURN
