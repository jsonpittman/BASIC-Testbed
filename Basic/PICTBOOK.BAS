1 GOTO 200
3 I=22500
4 CLS0:INPUT"THE COMPUTER WILL SHOW YOU PICTURES. WRITE A LINE FOR EACH ONE, THEN PRESS <ENTER>. AT THE END THE COMPUTER WILL SHOW YOU YOUR STORY. TYPE HOW MANY PICTURES YOU WANT, UP TO 10, THEN PRESS ENTER.";EN:IF EN<1 OR EN<10 THEN 4
5 FOR N=1 TO EN
6 Z=RND(-TIMER)
10 R=RND(20):R(N)=R
15 D=I+(R-1)*512
20 DI=USR1(D)
25 PRINT@480,"";
30 T$=INKEY$:IF T$=""THEN GOTO 30
35 IF T$=CHR$(13) THEN GOTO 45
40 PRINT T$;:A$(N)=A$(N)+T$:GOTO 30 
45 NEXT N
50 INPUT"PRESS<ENTER>TO SEE YOUR PICTURE BOOK";AN$
55 FOR N=1 TO EN
60 R=R(N)
65 D=I+(R-1)*512
70 DI=USR1(D)
75 PRINT@480,A$(N);
80 FOR TI=1 TO 3000:NEXT TI
85 NEXT N
90 INPUT"PRESS R AND ENTER IF YOU WANT TO SEE IT AGAIN. IF NOT, JUST PRESS ENTER.";AN$
95 IF LEFT$(AN$,1)="R" THEN GOTO 55
100 CLS0:PRINT@0,"HERE IS YOUR STORY:";
105 FOR N=1 TO 5
110 PRINT A$(N);
115 NEXT N
120 IF EN>5 THEN LINE INPUT"...PRESS ENTER FOR PAGE 2";AN$
125 FOR N=6 TO EN
130 PRINT A$(N)
135 NEXT N
140 INPUT"DO YOU WANT TO WRITE ANOTHER STORY";AN$
145 IF LEFT$(AN$,1)="N" THEN END
150 FOR N=1 TO 10
155 A$(N)="":NEXT N
160 GOTO 4
200 REM PICTUREBOOK C. SOLLA CARROCK 1/87
205 CLEAR 10240,22500:CLEAR 20,22470:PCLEAR 1:CLEAR 1000:CLS0
210 DIM A$(10),R(10)
215 DATA &HBD,&HB3,&HED,&H1F,&H01,&H10,&H8E
220 DATA &H04,&H00,&HA6,&H80,&HA7,&HA0,&H10
225 DATA &H8C,&H06,&H00,&H26,&HF6,&H39
230 FOR J=22470 TO 22489
235 READ DA:POKE J,DA
240 NEXT J
245 DEFUSR1=22470
315 CLS0:PRINT"LOAD THE PICTURE TAPE. CHOOSE THE ONE YOU WANT TO USE, PUT IT IN THE TAPE RECORDER AND REWIND IT. PRESS DOWN THE PLAY BUTTON."
320 LINE INPUT"PRESS<ENTER>WHEN READY";AN$
325 CLS0:PRINT@202,"TAPE LOADING":PRINT@224,"THIS WILL TAKE ABOUT 10 MINUTES"
330 I=22500:K=I+20*512-1
335 OPEN"I",#-1,"PICTURES"
340 FOR LD=I TO K
345 IF EOF(-1) THEN LD=K:XT=1:CLOSE#-1:GOTO 360
350 INPUT#-1,A
355 POKE LD,A
360 NEXT LD
365 IF XT<>1 THEN CLOSE#-1
370 CLS0:PRINT@192,"PRESS ANY KEY TO BEGIN"
375 PLAY"CDE"
380 I$=INKEY$:IF I$="" THEN GOTO 375 ELSE GOTO 4
