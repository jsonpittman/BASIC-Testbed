10 '  MEMOCARDS  VERSION 1.0
20 '  WRITTEN BY ALLAN J. BELANGER
30 '  COPYRIGHT (C) 1985
40 '  FOR 16K  EXTENDED COLOR BASIC
50 '
60 CLEAR200:CLS:A=8:T1=61:Z=RND(-TIMER):POKE65495,0:DIMC$(14),S$(4),I(52),CP(40),B(52),H(2),D(2):GOSUB300:PMODE4,1:PCLS1:LINE(7,7)-(249,184),PRESET,B:POKE178,1:PAINT(0,0),,2
70 PRINT@71,"MEMOCARDS  VER.1.0";:PRINT@199,"building";CHR$(128);CHR$(128);"graphics";:PRINT@358,"BY:ALLAN J. BELANGER":PRINT@393,"COPYRIGHT (C)1985"
80 N=0:Z=0:U=0:SP=3:O=0:T=T1:X=10:Y=30:V=0:GOSUB360
90 Z=Z+1
100 Q=RND(52):IFB(Q)=1THEN100ELSEI(Z)=Q:I(Z+20)=Q:B(Q)=1:IFZ<20THEN90ELSEGOSUB360
110 Z=Z+1
120 Q=RND(40):IFB(Q)=1THEN120ELSECP(Z)=I(Q):B(Q)=1:N=N+1:X1=X+4:Y1=Y+21:IFN<10THENE=1
130 Q=Z:R=0:GOSUB220:X1=X+4:Y1=Y+21:GOSUB340:E=2:IFZ<40THEN110ELSEGOSUB360:DRAW"C2S6BM41,150BU2R35D8L36U8BD2BR3NR4D2R4D2L4BR7NR4U4R4BR3R4D4L4U4BR7ND4R4D2L4R2F2BR3NR4U2NR4U2R4"
140 DRAW"BM157,150BU2R28D8L28U8BD2BR2R4L2ND4BR4D4R4U4BR3ND4R4D2L4R2F2BR3U4F4U4"
150 FORZ=1TO2:DRAW"C2S12BM"+STR$(18+Z)+",12ND3F3E3D3BR4NR4UNR4UR4BR4BUND3F3E3D3BR4U2R4D2L4BR8NR4U2R4BR4ND2R4DNL4DBR4U2R4DL4R2FRBR4U2R3FGL3BR8R4UL4UR4":NEXT:GOSUB270:GOSUB280:SCREEN1,1
160 FORP=1TO2
170 D(2)=0:D$="":FORZ=1TO2
180 A$=INKEY$
190 IFA$=""THEN180ELSEIFA$<"0" ORA$>"9" OR(Z=1ANDA$>"4")THEN260ELSED$=D$+A$:PLAY"T50O4F":NEXTZ:D(P)=VAL(D$):IFD(P)>40ORD(P)<1ORD(1)=D(2)THEN260ELSEIFB(D(P))=1THEN260ELSEH(P)=CP(D(P)):V=1:GOSUB210:NEXTP:V=0:IFH(1)=H(2)THENGOSUB390:U=1:GOSUB270
200 FORP=1TO100*A:NEXT:FORP=1TO2:GOSUB210:NEXT:GOSUB280:U=0:GOTO160
210 C=H(P):Q=D(P):R=0:IFU=1THENB(D(P))=1
220 IFQ>10THENR=R+1:Q=Q-10:GOTO220ELSEX=10+(20*Q):Y=30+(28*R)
230 S=1
240 IFC>13THENS=S+1:C=C-13:GOTO240ELSECOLOR1:LINE(X,Y)-(X+16,Y+19),PSET,BF:IFV=0THEN250ELSEDRAW"S4C2BM"+STR$(X+3)+","+STR$(Y+2)+C$(C):DRAW"BM"+STR$(X+7)+","+STR$(Y+14)+S$(S):RETURN
250 COLORU:LINE(X,Y)-(X+16,Y+19),PSET,B:LINE(X+3,Y+2)-(X+13,Y+17),PSET,BF:RETURN
260 PLAY"T15O2D":GOTO170
270 O=2:SP=4:N=SC:COLOR2:LINE(93,147)-(135,159),PSET,BF:X1=96:Y1=150:GOSUB340:RETURN
280 O=2:SP=4:T=T-1:N=T:X1=202:Y1=150:COLOR2:LINE(200,147)-(216,159),PSET,BF:GOSUB340
290 IFT=0THEN370ELSEFORZ=1TO40:IFB(Z)=0THENRETURNELSENEXTZ:FORZ=1TOT:PLAY"T10G":SC=SC+10:GOSUB270:NEXTZ:T1=T1-5:SCREEN1,0:GOTO80
300 C$(1)="BRNGR3FD2GL3GD2R5BU7BR":C$(2)="BRNGR3FDGNLFD2GL3HBR6BU6":C$(3)="BD4NE4R5LD3U7BR2":C$(4)="NR5D3R4FD2GL3HBR6BU6":C$(5)="BRR3FHL3GD5FR3EU2HL3GBU4BR5"
310 C$(6)="NDR5DG3D3BR4BU7":C$(7)="BRNGR3FDGNL3FD2GL3HU2EHUBEBR5":C$(8)="BRNR3GD2FR3EU2HFD5GL3NHBR5BU7":C$(9)="BLNGD7BR3HU5ER3FD5GL3":C$(10)="BLBD6NUR4U6LR2":C$(11)="R4D6NHL4NU6R5ND":C$(12)="D6U3RNE3F3":C$(13)="BDER3FHL3GD3NR5D3BR5U6"
320 N$(0)="NR4D6R4U6":N$(1)="BR2ND6":N$(2)="R4D3L4D3R4BU6":N$(3)="R4D3NL4D3NL4U6":N$(4)="D3R4U3ND6":N$(5)="NR4D3R4D3NL4BU6":N$(6)="NR4D6R4U3NL4BU3":N$(7)="R4ND6":N$(8)="R4D6L4U3NR4U3R4":N$(9)="NR4D3R4U3D6NL4U6"
330 S$(1)="BLERFDGLHUBE3ERFDGLHUBF3ERFDGLHUBLD4LU4BR3D2E2L2BL5D2H2R2BE3D2H2R2":S$(2)="E4F4DGLH2ND4NU2G2LHUBRE3F3GH2G2HEND3R4D3E2BL6D2H2":S$(3)="E4F4G4H4":S$(4)="BUE2F2E2F2DG4H4U":RETURN
340 POKE178,O:Q$=STR$(N):FORQ=E TOLEN(Q$):M=VAL(MID$(Q$,Q,1)):DRAW"S"+STR$(SP)+"BM"+STR$(X1)+","+STR$(Y1)+N$(M):IFM=1ANDSP=3THENX1=X1+SP+2ELSEX1=X1+(SP*2)
350 NEXTQ:RETURN
360 FORZ=1TO52:B(Z)=0:NEXT:Z=0:RETURN
370 FORZZ=1TO40:IFB(ZZ)=0THENPLAY"T4AGAT1F":V=1:FORZ=1TO40:C=CP(Z):R=0:Q=Z:GOSUB220:NEXTZ:SC=0:T1=61ELSENEXTZZ:T1=T1-5:SCREEN1,0:GOTO80
380 IFINKEY$=""THEN380ELSE70
390 PLAY"T10O3AGDEFGDGEAAGDEGF":SC=SC+((C+1)*2):RETURN
