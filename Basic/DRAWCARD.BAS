100 ' SAMPLE SETUP
110 CLEAR 500
120 DIM V$(13)
130 HSCREEN2
140 HCLS
150 ' INCLUDE THESE STRING DEFINITIONS SOMEWHERE
160 V$(2)="R5L5UE2R2EU2H2L2G"
170 V$(3)="BUFR2E2UHLRFU3H2L2G"
180 V$(4)="BR5U4RL6E4RD4"
190 V$(5)="BUFR3EU2HL4U4R5"
200 V$(6)="BRR3EU2HL3GD2BU3U2E3RF"
210 V$(7)="E6U2L6"
220 V$(8)="BUFR3EU2HL3GD2BU4U2ER3FD2G"
230 V$(9)="BUFR3EU6HL3GD2FR4"
240 V$(10)="R4L2U8G2E2BM+4,+8;R3EU6HL2GD7"
250 V$(1)="U7ER3FD7U4L5"
260 V$(11)="BUF3REU7L2"
270 V$(12)="BUFR3H2F2EU6HL3GD6"
280 V$(13)="U8D4F4H4E4"
290 D$="E6F6G6H6"
300 H$="U2EURUR2DRDFEURUR2DRDFD2G6H6"
310 S$="E6F6D2G2L4D2U2L4H2U2"
320 C$="E2RU2E2R2F2D2RF2D2G2L4D2U2L4H2U2"
330 B$="R50D60L50U60D30BR19"
340 ' SAMPLE CALLING SEQUENCE
350 ZX=100:ZY=100:ZA$="QD":GOSUB 10020
360 GOTO 360
10000 ' SUBROUTINE TO DRAW CARD ON SCREEN
10010 ' ZA$="VS", ZX=SCREEN X COORD, XY=SCREEN Y COORD
10020 ZU$=RIGHT$(ZA$,1)
10030 IF ZU$="C" THEN ZU$=B$+C$ ELSE IF ZU$="H" THEN ZU$=B$+H$ ELSEIF ZU$="S" THEN ZU$=B$+S$ ELSE ZU$=B$+D$
10040 ZV$=LEFT$(ZA$,1)
10050 IF ZV$="T" THEN ZV=10 ELSE IF ZV$="J" THEN ZV=11 ELSE IF ZV$="Q" THEN ZV=12 ELSE IF ZV$="K" THEN ZV=13
10060 IF VAL(ZV$)<>0 THEN ZV=VAL(ZV$)
10070 GOSUB 10140
10080 HDRAW ZU$
10090 GOSUB 10140
10100 HDRAW "BM+4,+10"+V$(ZV)
10110 GOSUB 10140
10120 HDRAW "BM+47,+48"+"A2"+V$(ZV)
10130 RETURN
10140 HDRAW "BM"+STR$(ZX)+","+STR$(ZY)+";"
10150 RETURN
