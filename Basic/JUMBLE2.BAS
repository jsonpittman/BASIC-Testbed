10 '***************************
20 '**** COCO JUMBLE V 2.0 ****
30 '*** COPYRIGHT (C)  1989 ***
40 '**    BY FALSOFT, INC.   **
50 '*   ALL RIGHTS RESERVED   *
60 '**       - - - - -       **
70 '***     WRITTEN BY:     ***
80 '****   SHANE  MESSER   ****
90 '***************************
100 GOSUB 1450
110 FOR V= 1 TO 500:NEXT V
120 '
130 CLEAR 1000:DIM C$(9),D$(18),E$(16,16),V(26)
140 GOSUB 210
150 GOSUB 430
160 GOSUB 270
170 GOSUB 350
180 GOSUB 580
190 GOTO 170
200 '
210 'TITLE SUBROUTINE
220 P=1
230 CLS:PRINT:PRINT:PRINT:FOR I=1 TO 10:READ I$
240 A=LEN(I$):A=32-A:A=A/2:A=INT(A):PRINTSTRING$(A," ");:FOR A=1 TO LEN(I$):PRINTMID$(I$,A,1);:IF I=1 AND MID$(I$,A,1)<>" " THEN PLAY"V30T255O3L255EA"
250 NEXT A:PRINT:NEXT I
260 RETURN
270 'SCREEN SETUP
280 FOR B=1 TO 8:READ D$(B):D$(16-B)=D$(B):NEXT
290 FOR A=1 TO 26:READ V(A):NEXT A
300 FOR A=1 TO 15:FOR B=1 TO 15:L$=MID$(D$(A),B,1)
310 IF L$="R" THEN PRINT CHR$(191); ELSE IF L$="P" THEN PRINT CHR$(255); ELSE IF L$="B" THEN PRINT CHR$(175); ELSE IF L$="L" THEN PRINT CHR$(223); ELSE PRINT CHR$(128);
320 IF RR=1 THEN RETURN
330 NEXT B:PRINT:NEXT A
340 RETURN
350 ' CLEAR AND PRINT MENU
360 PRINT @16,"    C O C O";:PRINT@48,"  J U M B L E ";:PRINT@80,"  WRITTEN BY:";:PRINT@112," SHANE  MESSER";:PRINT@239,"- - - - - - - - -";
370 GOSUB 890
380 X=271:PRINT@X,"(V)IEW LETTERS";:X=X+32:PRINT@X,"(L)OAD - (S)AVE";:X=X+32:PRINT@X,"(E)NTER WORD";:X=X+32:PRINT@X,"(N)EXT PLAYER";:X=X+32:PRINT@X,"(C)HANGE LETTERS";:X=X+32:PRINT@X,"(Q)UIT GAME";:X=X+64:PRINT@X+3,"PLAYER ->";:IF P=1 THEN PRINT"1";
390 IF P=2 THEN PRINT"2";
400 X=X-32:PRINT@X,"- - - - - - - - -";
410 EXEC 44539:A$=INKEY$
420 IF INSTR("LSCNQVE",A$)>0 THEN RETURN ELSE 410
430 GOSUB 550
440 P=1:GOSUB 680:P=2:GOSUB 680:P=1:RETURN
450 DATA C O C O  J U M B L E ,,JUST FOR THE COCO 1 AND 2 !!!,,WRITTEN BY SHANE MESSER,"COPYRIGHT (C), 1989",BY FALSOFT,,,"PLEASE WAIT...."
460 DATA RWWLWWWRWWWLWWR
470 DATA WPWWWBWWWBWWWPW
480 DATA WWPWWWLWLWWWPWW
490 DATA LWWPWWWLWWWPWWL
500 DATA WWWWPWWWWWPWWWW
510 DATA WBWWWBWWWBWWWBW
520 DATA WWLWWWLWLWWWLWW
530 DATA RWWLWWWPWWWLWWR
540 DATA 1,3,3,2,1,4,2,5,1,6,6,2,2,1,1,4,10,4,2,3,2,6,4,10,6,10
550 LL$="AAAAAAAAABBBBCCCDDDDEEEEEEEEEEEEFFFGGGHHIIIIIIIIIJJKLLLLMMNNNNNNOOOOOOOOPPQRRRRRRSSSSSTTTTTUUUUVVVWWXYZ**":RETURN
560 '
570 NEXT
580 IF A$="Q" THEN GOSUB 660
590 IF A$="V" THEN GOSUB 810
600 IF A$="N" THEN GOSUB 860
610 IF A$="E" THEN GOSUB 900
620 IF A$="C" THEN GOSUB 1290
630 IF A$="S" THEN GOSUB 1390
640 IF A$="L" THEN GOSUB 1440
650 RETURN
660 X=271:FOR A=1 TO 6:PRINT@X,"                ";:X=X+32:NEXT:X=271:PRINT@X," ARE YOU SURE?";:PRINT@X+32,"      Y/N";
670 EXEC 44539:B$=INKEY$:IF B$="Y" THEN CLS:END:ELSE IF B$="N" THEN RETURN ELSE 670
680 'REPLACE LETTERS FOR PLAYER
690 N=0
700 IF P=1 THEN X$=P1$ ELSE IF P=2 THEN X$=P2$
710 IF X$="" THEN X$="       "
720 FOR A=1 TO 7:IF MID$(X$,A,1)=" " THEN N=N+1:NEXT A
730 FOR A=1 TO N
740 IF LL$=STRING$(105," ") THEN 1460
750 V=RND(105):IF MID$(LL$,V,1)=" " THEN 750
760 FOR I=1 TO 7:IF MID$(X$,I,1)=" " THEN MID$(X$,I,1)=MID$(LL$,V,1):GOTO 770 ELSE NEXT I
770 MID$(LL$,V,1)=" ":NEXT A
780 IF INSTR(X$," ")>0 THEN 710
790 IF P=1 THEN P1$=X$ ELSE P2$=X$
800 RETURN
810 GOSUB 880
820 X=272:PRINT@X,"READY  PLAYER";STR$(P);
830 EXEC 44539:A$="":X$=""
840 X=X+64:IF P=1 THEN X$=P1$ ELSE X$=P2$
850 FOR A=1 TO 7:A$=A$+MID$(X$,A,1)+" ":NEXT:PRINT@X+1,A$:EXEC 44539:GOSUB 880:RETURN
860 IF P=1 THEN P=2 ELSE P=1
870 RETURN
880 X=271:FOR A=1 TO 6:PRINT@X,"                ";:X=X+32:NEXT A:RETURN
890 PRINT@143,"- - - - - - - - -";:PRINT@175,"PLAYER 1 ->"+STR$(S1);:PRINT@207,"PLAYER 2 ->"+STR$(S2);:RETURN
900 C$=CHR$(207)
910 X=8:Y=8
920 A$=INKEY$:GOSUB 960:IF A$="" THEN 920
930 IF A$="^" THEN Y=Y-1:ELSE IF A$=CHR$(9) THEN X=X+1 ELSE IF A$=CHR$(8) THEN X=X-1 ELSE IF A$=CHR$(10) THEN Y=Y+1 ELSE IF A$=CHR$(13) THEN GOTO 970 ELSE 920
940 IF X>15 THEN X=1 ELSE IF X<1 THEN X=15 ELSE IF Y<1 THEN Y=15 ELSE IF Y>15 THEN Y=1
950 GOTO 920
960 YY=(Y-1)*32:XX=X-1:L=YY+XX:A=PEEK(L+1024):POKE L+1024,ASC(C$):POKE L+1024,A:RETURN
970 RO=RO+1:X1=X:Y1=Y:IF P=1 THEN X$=P1$ ELSE X$=P2$
980 IF (X1=15 OR X1=1 OR Y1=15 OR Y1=1) AND RO=1 THEN RO=RO-1: GOTO 900
990 WD$="":DD=0:SS=0:PRINT@480,"(S)IDE - (D)OWN";:EXEC 44539:A$=INKEY$:IF A$="D" THEN DD=1 ELSE IF A$="S" THEN SS=1 ELSE 990
1000 IF DD=1 AND SS=1 THEN 990 ELSE IF DD=0 AND SS=0 THEN 990
1010 EXEC 44539:A$=INKEY$
1020 IF A$=CHR$(8) AND WD$>""THEN GOSUB 1100:GOTO 1010
1030 N=0:IF A$="g" OR A$=CHR$(4) THEN FOR I=1 TO LEN(WD$) :IF MID$(WD$,I,1)< CHR$(255) THEN N=N+1:NEXT I
1040 IF A$="g" OR A$=CHR$(4) THEN FOR A=1 TO N:IF WD$>"" THEN GOSUB 1100:NEXT A
1050 IF A$=CHR$(4) THEN RETURN
1060 IF A$=CHR$(13) THEN GOSUB 1520:IF GG=1 THEN 1150
1070 IF E$(X,Y)<>"" THEN GOSUB 1610
1080 IF X<16 AND Y<16 AND INSTR(X$,A$)>0 THEN E$(X,Y)=A$:POKE 1024+X-1+((Y-1)*32),ASC(A$):X=X+SS:Y=Y+DD:WD$=WD$+A$:MID$(X$,INSTR(X$,A$),1)=" ":ELSE SOUND 1,1
1090 GOTO 1010
1100 K=LEN(WD$)
1110 IF MID$(WD$,K,1)=CHR$(255) THEN WD$=LEFT$(WD$,K-1):X=X-SS:Y=Y-DD:K=K-1:GOTO 1110 ELSE 1120
1120 PRINT@X-SS+((Y-DD-1)*32)-1,"";:L$=MID$(D$(Y-DD),X-SS,1):RR=1:GOSUB 310:RR=0:E$(X-SS,Y-DD)="":X=X-SS:Y=Y-DD
1130 MID$(X$,INSTR(X$," "),1)=MID$(WD$,LEN(WD$),1):WD$=LEFT$(WD$,LEN(WD$)-1)
1140 RETURN
1150 D=0:T=0:TS=0:CS=0:S=0:IF P=1 THEN P1$=X$ ELSE P2$=X$
1160 L$=E$(X1,Y1):FW$=FW$+L$:IF X1=X AND Y1=Y THEN 1230
1170 M$=MID$(D$(Y1),X1,1):IF M$="R" THEN T=T+1 ELSE IF M$="P" THEN D=D+1
1180 IF L$="*" THEN L$="A"
1190 L=ASC(L$)-64:L=V(L):IF M$="L" THEN L=L*2 ELSE IF M$="B" THEN L=L*3
1200 CS=CS+L
1210 X1=X1+SS:Y1=Y1+DD
1220 GOTO 1160
1230 IF D>0 THEN FOR A=1 TO D:CS=CS*2:NEXT
1240 IF T>0 THEN FOR A=1 TO T:CS=CS*3:NEXT
1250 IF X$="       " THEN CS=CS+75
1260 IF P=1  THEN S1=S1+CS ELSE S2=S2+CS
1270 GOSUB 680
1280 GOSUB 890:PRINT@480,"               ";:IF P=1 THEN P=2:RETURN ELSE P=1:RETURN
1290 GOSUB 880:IF P=1 THEN X$=P1$ ELSE X$=P2$
1300 C=271:FOR X=1 TO 7:IF X=5 THEN Q=X:GOSUB 880:X=Q:C=271
1310 L$=MID$(X$,X,1)
1320 L$="CHANGE "+L$+"?":PRINT@C,L$:C=C+32
1330 EXEC 44539:A$=INKEY$:IF A$="N" THEN NEXT X:GOTO 1350: ELSE IF A$="Y" THEN GOTO 1340 ELSE 1330
1340 MID$(LL$,INSTR(LL$," "),1)=MID$(X$,X,1):MID$(X$,X,1)=" ":NEXT X
1350 IF P=1 THEN P1$=X$ ELSE P2$=X$
1360 GOSUB 680
1370 IF P=1 THEN P=2 ELSE P=1
1380 RETURN
1390 SAVEM"SAVEGAME",1024,1535,1024
1400 OPEN"O",#1,"SAVEGAME":FOR I=1 TO 15:FOR A=1 TO 15:WRITE #1,E$(I,A):NEXT A,I:WRITE #1,LL$,P1$,P2$,S1,S2,P
1410 CLOSE
1420 PLAY"T5L5A"
1430 RETURN
1440 LOADM "SAVEGAME":OPEN"I",#1,"SAVEGAME":FOR I=1 TO 15:FOR A=1 TO 15:INPUT #1,E$(I,A):NEXT A,I:INPUT #1,LL$,P1$,P2$,S1,S2,P:CLOSE:PLAY"T5L5A":RETURN
1450 CLS:PRINT:PRINT:PRINT:PRINT" PRESS ANY KEY TO STOP TIMER..":FOR I=1 TO 500:PRINT@164,"        >      <":PRINT@173,I;:A$=INKEY$:IF A$<>"" THEN RETURN ELSE C=RND(I):V=RND(I/4):NEXTI:GOTO 1450
1460 CLS:IF S1>S2 THEN P$="PLAYER 1" ELSE P$="PLAYER 2"
1470 IF S1>S2 THEN S=S1 ELSE S=S2
1480 V$="":V$=P$:V1$=" WINS WITH A SCORE OF"+STR$(S)
1490 PRINT"            "V$
1500 PRINT@484,V1$;
1510 GOTO 1510
1520 IF RO=1 THEN 1590
1530 IF INSTR(WD$,CHR$(255))>0 THEN 1590
1540 FOR A=X1-1 TO X1+1:FOR B=Y1-1 TO Y1+1
1550 IF A<1 THEN A=1 ELSE IF A>15 THEN A=15
1560 IF B<1 THEN B=1 ELSE IF B>15 THEN B=15
1570 IF SS=1 THEN IF E$(A,B) <>"" AND (B<>Y OR (A<X1 OR A>X) ) THEN 1590 ELSE NEXT B,A:GOTO 1600
1580 IF E$(A,B) <>"" AND (A<>X OR (B<Y1 OR B>Y) ) THEN 1590 ELSE NEXT B,A:GOTO 1600
1590 GG=1:RETURN
1600 GG=0:RETURN
1610 WD$=WD$+CHR$(255):X=X+SS:Y=Y+DD
1620 IF X>14 OR Y>14 THEN RETURN
1630 IF E$(X,Y)<>"" THEN 1610 ELSE RETURN
