0 CLS:PRINT"  IN THIS ADVENTURE, YOU MUST   FIND THE STOLEN CHRISTMAS TOYS, WHICH ARE HIDDEN IN A MAGIC     WORLD, AND RETURN THEM TO YOUR  HOUSE."
10 PRINT:PRINT:PRINT"  THIS ADVENTURE USES TWO WORD  COMMANDS SUCH AS, get knife OR  say help. THE GAME HAS NO SAVE/ LOAD FUNCTION, BUT IF YOU SHOULDPRESS BREAK TYPE CONT TO CONTIN-UE.  I HOPE YOU ENJOY IT."
20 PRINT@448," PRESS <SPACEBAR> TO CONTINUE";INKEY$
30 IFINKEY$=" "THEN40ELSE30
40 CLS:PRINT"  THE VERBS ARE:"
50 PRINT"                 ASK                             GET                             DROP                            PUT                             USE                             EAT                             SAY"
60 PRINT"                 LOOK                            PULL":PRINT@448," PRESS <SPACEBAR> TO CONTINUE";INKEY$
70 IFINKEY$=" "THEN80ELSE70
80 CLS
90 DIM A$(8),B$(8),C$(8):L=1
100 DATA IN THE ENTRANCE TO THIS ENCHANTING WORLD,MAGIC KNIFE,WEST
110 DATA IN THE UPSIDE DOWN      FOREST,UPSIDE DOWN TREES,NORTH EAST
120 DATA IN A COURTYARD,A BEAUTIFUL FOUNTAIN,NORTH NORTHEAST      SOUTH WEST
130 DATA AT A POND,PINK WATER,SOUTHWEST
140 DATA IN FRONT OF A LARGE     STONE,STONE,SOUTH
150 DATA IN A FRUIT ORCHARD,FRUIT,NORTH EAST
160 DATA IN FRONT OF AN ELF,ELF,SOUTH
170 DATA IN A SMALL SHACK,GIFTS,NOWHERE!  use THE    force LUKE!
180 FORI=1TO8
190 READ A$(I),B$(I),C$(I)
200 NEXT
210 PRINT"YOU ARE:";A$(L)
220 PRINT"YOU SEE:";B$(L)
230 PRINT"YOU CAN GO:";C$(L)
240 INPUT"WHAT DO YOU WISH FOR";M$
250 ON L GOSUB 270,330,400,470,510,550,610,650
260 FORX=1TO1500:NEXTX:CLS:GOTO210
270 IFM$="GET KNIFE"THENPRINT"OKAY, YOU HAVE IT.":KNIFE=1:B$(1)="A RAINBOW...THE RAINBOW"
280 IFM$="LOOK KNIFE"THENPRINT"IT IS SHAPED WEIRD."
290 IFM$="GET RAINBOW"THENPRINT"YOU HAVE IT.":B$(1)="LOTS OF INTERESTING     THINGS."
300 IFM$="LOOK RAINBOW"THENPRINT"IT IS MADE UP OF RAINBOW MAGA-  ZINES."
310 IFM$="W"THENL=2
320 RETURN
330 IFM$="LOOK TREES"THENPRINT"THE limbS ARE SHAPED FUNNY.":B$(2)="UPSIDE DOWN TREES  FUNNY limbS"
340 IFM$="PULL LIMB"THENPRINT"YOU HERE A NOISE IN THE DISTANCE":LIMB=1
350 IFM$="LOOK LIMB"THENPRINT"IT'S FUNNY, OKAY?"
360 IFM$="N"THENL=3
370 IFM$="E"THENL=1
380 IFKNIFE=2ANDLIMB=1ANDRAIN=1ANDM$="SAY GIFT"THENL=8
390 RETURN
400 IFM$="LOOK FOUNTAIN"THENPRINT"THE WATER IS A RAINBOW OF COLORS"
410 IFM$="GET FOUNTAIN"THENPRINT"DON'T TAKE ALL THE BEAUTY AWAY."
420 IFM$="N"THENL=5
430 IFM$="NE"THENL=4
440 IFM$="W"THENL=6
450 IFM$="S"THENL=2
460 RETURN
470 IFM$="LOOK POND"THENPRINT"A RAINBOW WOULD LOOK BEATIFUL   HERE!"
480 IFM$="DROP RAINBOW"THENPRINT"YOU HEAR ANOTHER SOUND IN THE   DISTANCE":RAIN=1:B$(4)="RAINBOW OVER POND"
490 IFM$="SW"THENL=3
500 RETURN
510 IFM$="LOOK STONE"THENPRINT"THERE IS A SLOT IN IT."
520 IFM$="PUT KNIFE"ANDKNIFE=1THENINPUT"WHERE";DD$:IFDD$="STONE"THENPRINT"YOU HEAR A THIRD SOUND IN THE   DISTANCE!":KNIFE=2:B$(5)="KNIFE IN STONE"
530 IFM$="S"THENL=3
540 RETURN
550 IFM$="LOOK ORCHARD"THENPRINT"IT IS FILLED WITH FRUIT TREES."
560 IFM$="GET FRUIT"THENPRINT"OKAY, YOU HAVE IT.":FRU=1
570 IFFRU=1ANDM$="EAT FRUIT"THENPRINT"THEN FRUIT POISONED YOU, BECAUSEYOU WERE NOT USED TO IT."
580 IFM$="N"THENL=7
590 IFM$="E"THENL=3
600 RETURN
610 IFM$="ASK ELF"THENPRINT"IF YE EQUALS THREE              AND SOUND IS AROUND             THEN SAY GIFT                   IN THE UPSIDE DOWN FOREST.":FORX=1TO5000:NEXTX
620 IFM$="LOOK ELF"THENPRINT"HE IS A CRISTMAS ELF."
630 IFM$="S"THENL=6
640 RETURN
650 IFM$="LOOK GIFTS"THENPRINT"THEY ARE THE STOLEN ONES."
660 IFM$="GET GIFTS"THENPRINT"YOU HAVE THEM.":GF=1:B$(8)="LOTS OF INTERESTING  THINGS"
670 IFGF=1ANDM$="USE FORCE"THEN690
680 RETURN
690 CLS:PRINT"  YOU DID IT!  YOU RETURNED THE GIFTS NOT ONLY TO YOUR HOUSE BUTTO EVERY HOUSE.  YOU MADE       CHRISTMAS A REAL CHRISTMAS!":END
