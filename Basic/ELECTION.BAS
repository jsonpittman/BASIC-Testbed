10 REM"ELECTION PREVIEW"
20 REM"<C>STEVE BLYN,COMPUTER ISLAND,STATEN ISLEAND,NY,1988"
30 DIM A$(7),B$(7),A(7),B(7)
40 FOR T=1 TO 7:READ A$(T),A(T),B$(T),B(T):NEXT T
50 N=0:CLS:Q$=STRING$(32,239)
60 PRINT@5,"OUR  ELECTION  PREVIEW";:PRINT@32,Q$
70 PRINT@64,"  DEMOCRATS       REPUBLICANS":PRINT@96,Q$
80 SOUND 200,3:N=0:FOR T=1 TO 7:PRINT@128+N,A$(T);
90 PRINT@138+N,A(T);
100 N=N+32:NEXT T
110 N=0:FOR T=1 TO 7:PRINT@143+N,CHR$(233)+" "+B$(T);
120 PRINT@352,Q$
130 PRINT@156+N,B(T);
140 N=N+32:NEXT T
150 PRINT@416," ":PRINT@416,"YOUR CHOICE IS ";
160 INPUT C$
170 IF C$="E" THEN 250
180 IF C$="L" THEN GOTO 410
190 IF C$="S" THEN GOTO 340
200 FOR Y=1 TO 7
210 IF C$=A$(Y) THEN A(Y)=A(Y)+1:GOTO 50
220 IF C$=B$(Y) THEN B(Y)=B(Y)+1:GOTO 50
230 NEXT Y
240 GOTO 150
250 PRINT@416," VOTING COMPLETED.....THANKYOU";:END
260 DATA HART,0,BUSH,0
270 DATA JACKSON,0,DOLE,0
280 DATA SIMON,0,KEMP,0
290 DATA DUKAKIS,0,ROBERTSON,0
300 DATA GORE,0,HAIG,0
310 DATA GEPHARDT,0,DUPONT,0
320 DATA BABBITT,0,KIRKPATRICK,0
330 REM OPTION TO SAVE THE FILE
340 PRINT@448,"saving data";:OPEN"O",#1,"DATA"
350 FOR T= 1 TO 7
360 WRITE #1,A$(T),A(T),B$(T),B(T)
370 NEXT T
380 CLOSE #1
390 GOTO 50
400 REM OPTION TO LOAD THE FILE
410 PRINT@448,"loading data";:OPEN"I",#1,"DATA"
420 FOR T= 1 TO 7
430 INPUT #1,A$(T),A(T),B$(T),B(T)
440 NEXT T
450 CLOSE #1
460 GOTO 50
10 '***************************
20 '*      ELECTION 88        *
30 '*    (C) 7/88 L HYRE      *
40 '*     CAMBRIDGE  MD       *
50 '* *************************
60 '
100 DIM M(600):REM MAP ARRAY
110 DIM LS$(51),S$(51),S(51),EV(51),DP$(17),DV$(17),RP$(17),RV$(17),OV(51),DW(51),SV(51),WN(17),PP(51)
120 '
130 '***** TITLE SCREEN *****
140 '
150 CLS5:PRINT STRING$(64,175);
160 FOR X=2TO14 STEP 2:PRINT@X*32, STRING$(32,159);:NEXT
170 FOR X=170 TO 298 STEP 32:PRINT@X,STRING$(12," ");CHR$(128);:NEXT:PRINT@182,CHR$(207);
180 PRINT@331,STRING$(12,128);
190 PRINT@203,"*ELECTION*";:PRINT@237,"* 88 *";:PRINT@268,"COVERAGE";:PRINT@299,"BY RAINBOW";
200 '
210 ' ***** PROGRAM DATA *****
220 '
230 DATA ALABAMA,AL,1,9,ALASKA,AK,2,3,ARIZONA,AZ,3,7
240 DATA ARKANSAS,AR,4,6,CALIFORNIA,CA,5,47,COLORADO,CO,6,8,CONNECTICUT,CN,7,8
250 DATA DELAWARE,DE,8,3,DIST OF COLUMBIA,DC,9,3,FLORIDA,FL,10,21,GEORGIA,GA,11,12
260 DATA HAWAII,HI,12,4,IDAHO,ID,13,4,ILLINOIS,IL,14,24
270 DATA INDIANA,IN,15,12,IOWA,IO,16,8,KANSAS,KS,17,7,KENTUCKY,KY,18,9,LOUISIANA,LA,19,10
280 DATA MAINE,ME,20,4,MARYLAND,MD,21,10,MASSACHUSETTS,MA,22,13,MICHIGAN,MI,23,20
290 DATA MINNESOTA,MN,24,10,MISSISSIPPI,MS,25,7,MISSOURI,MO,26,11,MONTANA,MT,27,4
300 DATA NEBRASKA,NE,28,5,NEVADA,NV,29,4,NEW HAMPSHIRE,NH,30,4,NEW JERSEY,NJ,31,16
310 DATA NEW MEXICO,NM,32,5,NEW YORK,NY,33,36,NORTH CAROLINA,NC,34,13,NORTH DAKOTA, ND,35,3
320 DATA OHIO,OH,36,23,OKLAHOMA,OK,37,8,OREGON,OR,38,7
330 DATA PENNSYLVANIA,PA,39,25,RHODE ISLAND,RI,40,4
340 DATA SOUTH CAROLINA,SC,41,8,SOUTH DAKOTA,SD,42,3,TENNESSEE,TN,43,11,TEXAS,TX,44,29
350 DATA UTAH,UT,45,5,VERMONT,VT,46,3,VIRGINIA,VA,47,12
360 DATA WASHINGTON,WA,48,10,WEST VIRGINIA,WV,49,6,WISCONSIN,WI,50,11,WYOMING,WY,51,3
370 DATA JAMES M. COX,FRANKLIN D. ROOSEVELT,JOHN W. DAVIS,CHARLES W. BRYAN,ALFRED E. SMITH,JOSEPH T. ROBINSON,FRANKLIN D. ROOSEVELT,JOHN N. GARNER,FRANKLIN D. ROOSEVELT, JOHN N. GARNER,FRANKLIN D. ROOSEVELT, HENRY A. WALLACE
380 DATA FRANKLIN D. ROOSEVELT, HARRY S. TRUMAN, HARRY S. TRUMAN, ALBEN W. BARKLEY, ADLAI E. STEVENSON, JOHN J. SPARKMAN, ADLAI E. STEVENSON, ESTES KEFAUVER, JOHN F. KENNEDY, LYNDON B. JOHNSON, LYNDON B. JOHNSON, HUBERT H. HUMPHREY
390 DATA HUBERT H. HUMPHREY, EDMUND S. MUSKIE, GEORGE S. MCGOVERN, R. SARGENT SHRIVER JR., JIMMY CARTER, WALTER F. MONDALE, JIMMY CARTER, WALTER F. MONDALE
400 DATA WALTER F. MONDALE, GERALDINE FERRARO
410 DATA WARREN G. HARDING, CALVIN COOLIDGE, CALVIN COOLIDGE, CHARLES D. DAWES, HERBERT HOOVER, CHARLES CURTIS, HERBERT HOOVER, CHARLES CURTIS, ALFRED M. LANDON, FRANK KNOX
420 DATA WENDELL L.WILKIE,CHARLES MCNARY,THOMAS E.DEWEY,JOHN W.BRICKER,THOMAS E.DEWEY,EARL WARREN,DWIGHT D.EISENHOWER,RICHARD M.NIXON,DWIGHT D.EISENHOWER,RICHARD M.NIXON,RICHARD M.NIXON,HENRY CABOT LODGE
430 DATA BARRY M.GOLDWATER,WILLIAM E.MILLER,RICHARD M.NIXON,SPIRO T.AGNEW,RICHARD M.NIXON,SPIRO T.AGNEW,GERALD R.FORD,ROBERT J.DOLE,RONALD REAGAN,GEORGE BUSH,RONALD REAGAN,GEORGE BUSH
440 DATA 152,100,212,78,76,92,132,88
450 DATA 44,80,92,72,200,52,192,70
460 DATA 212,98,172,116,164,100,212,88
470 DATA 64,44,144,60,156,60,128,56
480 DATA 108,76,160,76,136,108,212,32
490 DATA 184,70,204,48,160,48,128,36
500 DATA 144,100,132,72,80,32,108,60
510 DATA 60,72,204,44,196,64,92,92
520 DATA 188,48,176,84,108,28,164,60
530 DATA 120,88,48,44,184,64,209,53
540 DATA 176,98,108,44,160,84,116,108
550 DATA 76,72,200,40,180,76,48,28
560 DATA 172,72,140,40,88,52
570 DATA 2,2,2,1,1,1,1,1,2,2,1,1,2,2,1,2,2
580 '
590 '****** READ IN DATA ******
600 '
610 FOR X=1 TO 51:READ LS$(X),S$(X),S(X),EV(X):NEXT
620 FOR X=1TO17:READDP$(X),DV$(X):NEXT:FOR X=1TO17:READRP$(X),RV$(X):NEXT
630 FOR X=1 TO 51:READ OV(X):READ DW(X):NEXT X
640 FOR X=1 TO 17:READ WN(X):NEXT X
650 '
660 '**EXIT TITLE AFTER READ**
670 '
680 PRINT@464,"<PRESS ANY KEY>";
690 AK$=INKEY$:IF AK$=""THEN 690
700 '
710 '***** MAIN MENU HERE *****
720 '
730 CLS:PRINT STRING$(32,175);:PRINT@33,"ELECTION COVERAGE BY RAINBOW";:PRINT@64, STRING$(32,159);
740 PRINT:PRINT"       MENU SELECTIONS....":PRINT:PRINT"   1>ENTER VOTING RESULTS":PRINT"   2>VIEW  CURRENT VOTE STATUS":PRINT"   3>PRINT CURRENT VOTE STATUS"
750 PRINT"   4>U.S.MAP WITH VOTE STATUS":PRINT"   5>HISTORY SUBMENU"
755 PRINT"   6>SAVE OR LOAD DATA FILE":PRINT"   7>PLAY";CHR$(34);"POLITICAL PREDICTION";CHR$(34)
760 PRINT:PRINT"    PRESS # OF SELECTION---";
770 AK$=INKEY$:IF AK$=""THEN 770
780 IF VAL(AK$)>7 THEN 770
790 IF AK$="1" THEN GOSUB 880
800 IF AK$="2" THEN GOSUB 1120
810 IF AK$="3" THEN GOSUB 1330
820 IF AK$="4" THEN GOSUB 1520
830 IF AK$="5" THEN GOSUB 1620
832 IF AK$="6" THEN GOSUB 4000
834 IF AK$="7" THEN GOSUB 4500
840 GOTO 730
850 '
860 '**** STATE RESULTS ****
870 '
880 CLS:PRINT STRING$(32,159):PRINT@35,"VOTING RESULT ENTRY SCREEN":PRINT STRING$(32,175);
890 PRINT@448,STRING$(32,128);:PRINT@484,"<ENTER XX FOR MAIN MENU>";
900 PRINT@128,"ENTER STATE ABBREVIATION";:INPUT ST$
910 IF ST$="XX"THEN RETURN
920 FOR TEST=1 TO 51:IF ST$=S$(TEST) THEN 950
930 NEXT TEST
940 PRINT@128,"NO SUCH ABBREVIATION! TRY AGAIN!":SOUND 1,1:FOR X=1 TO 800:NEXT:GOSUB 3070:GOTO 880
950 PRINT:PRINT"THE STATE OF ";LS$(TEST):PRINT"WITH (";EV(TEST);") VOTES HAS VOTED:"
960 PRINT"<D>EMOCRAT   <R>EPUBLICAN"
970 PRINT"<U>NDECIDED":PRINT:PRINT"ENTER D, R, OR U "
980 AK$=INKEY$:IF AK$=""THEN 980
990 SV(TEST)=0:IF AK$="U" THEN 1070
1000 IF AK$="D" THEN SV(TEST)=1
1010 IF AK$="R" THEN SV(TEST)=2
1020 DV=0:RV=0
1030 FOR X=1 TO 51
1040 IF SV(X)=1 THEN DV=DV+EV(X)
1050 IF SV(X)=2 THEN RV=RV+EV(X)
1060 NEXT X
1070 GOSUB 3070:GOTO 880
1080 RETURN
1090 '
1100 '** SCREEN STATUS REPORT **
1110 '
1120 CLS:PRINT"UPDATE OF CURRENT STATUS":PRINT STRING$(32,175);"NEEDED TO WIN: 270 VOTES"
1130 PRINT@96,"DEMOCRATIC CURRENT TOTAL =";:PRINT USING"###";DVDV
1140 PRINT@128,"REPUBLICAN CURRENT TOTAL =";:PRINT USING"###";RV
1150 PRINT STRING$(32,159);:PRINT"STATE/DC     VOTES   RESULTS"
1160 ZZ=1:XC=0:XV=0
1170 FOR X=224 TO 384 STEP 32:PRINT@X,LS$(ZZ)
1180 PRINT @X+16,;:PRINT USING"##";EV(ZZ)
1182 XT$=" "
1183 IF SV(ZZ)=1 AND PP(ZZ)=1 THEN XT$="*":XC=XC+1
1184 IF SV(ZZ)=2 AND PP(ZZ)=2 THEN XT$="*":XC=XC+1
1190 IF SV(ZZ)=1 THEN PRINT@X+19,"DEMOCRATIC";XT$:ELSE IF SV(ZZ)=2 THEN PRINT@X+19,"REPUBLICAN";XT$:ELSE PRINT@X+19,"-----------";XT$
1191 IF SV(ZZ)<>0 THEN XV=XV+1
1200 ZZ=ZZ+1
1210 IF ZZ/6=INT(ZZ/6) THEN 1220 ELSE GOTO 1250
1220 PRINT:PRINT"<PRESS ANY KEY>"
1230 AK$=INKEY$:IF AK$=""THEN 1230
1240 FOR Y=224 TO 416 STEP 32:PRINT@Y,STRING$(32," "):NEXT Y:GOTO 1170
1250 IF ZZ=51 THEN 1265
1260 NEXT X
1265 IF XV>0 THEN PRINT:PRINT"YOUR PREDICTIONS:":PRINT"YOU HAVE ";XC;"OUT OF";XV;"CORRECT":PRINT"FOR A";INT((XC/XV)*100);" % ACCURACY RATE."
1270 PRINT@448,STRING$(32,128);:PRINT@488,"< PRESS ANY KEY >";
1280 AK$=INKEY$:IF AK$=""THEN 1280
1290 RETURN
1300 '
1310 '** PRINTOUT STATUS **
1320 '
1330 CLS:PRINT STRING$(32,159):PRINT@38,"PRINTOUT CURRENT STATUS":PRINT STRING$(32,175);
1340 PRINT:PRINT"MAKE SURE PRINTER IS READY!"
1350 PRINT:PRINT"PRESS <P> TO PRINT":PRINT
1360 PRINT"ANY OTHER KEY TO RETURN TO MENU"
1370 AK$=INKEY$:IF AK$=""THEN 1370
1380 IF AK$="P"THEN 1390 ELSE GOSUB 3070:RETURN
1390 PRINT#-2,STRING$(80,"-"):PRINT#-2,"ELECTION NIGHT 88 - COVERAGE BY RAINBOW           *=CORRECT PREDICTION"
1400 PRINT#-2,STRING$(80,"-")
1405 XC=0:XV=0
1410 FOR X=1 TO 51:PRINT#-2,LS$(X);:PRINT #-2,TAB(24);S$(X);:PRINT#-2,TAB(40);"Votes =";
1411 XT$=" "
1420 PRINT #-2,USING "##";EV(X);
1421 IF SV(X)=1 AND PP(X)=1 THEN XT$="*":XC=XC+1
1422 IF SV(X)=2 AND PP(X)=2 THEN XT$="*":XC=XC+1
1423 IF SV(X)<>0 THEN XV=XV+1
1430 IF SV(X)=1 THEN PRINT#-2,TAB(58);"DEMOCRATIC";XT$
1440 IF SV(X)=2 THEN PRINT#-2,TAB(58);"REPUBLICAN";XT$
1450 IF SV(X)=0 THEN PRINT#-2,TAB(58);"----------"
1460 NEXT X
1470 PRINT#-2,STRING$(80,"-"):PRINT#-2,"DEMOCRATS HAVE ";DV;" VOTES.";:PRINT#-2,TAB(40);"REPUBLICANS HAVE ";RV;" VOTES."
1475 IF XV>0 THEN PRINT#-2,"YOUR PREDICTIONS:";XC;" OUT OF ";XV;" FOR A ";INT((XC/XV)*100);"% CORRECT SELECTON RATE"
1480 RETURN
1490 '
1500 '**** MAP FUNCTIONS ****
1510 '
1520 GOSUB 2330
1530 FOR X=1 TO 51
1540 IF SV(X)=1 THEN PAINT(OV(X),DW(X)),0,3
1550 IF SV(X)=2 THEN PAINT(OV(X),DW(X)),2,3
1560 NEXT X
1570 AK$=INKEY$:IF AK$=""THEN 1570
1580 RETURN
1590 '
1600 '**** HISTORY SUBMENU ****
1610 '
1620 CLS:PRINT STRING$(32,159);"* HISTORICAL INFORMATION  MENU *";STRING$(32,207)
1630 PRINT@129,"1> DEMOCRATIC TICKETS (1920-)"
1640 PRINT@161,"2> REPUBLICAN TICKETS (1920-)"
1650 PRINT@193,"3> U.S.PRESIDENTS (1920-1984)":PRINT@225,"4> ELECTORAL COLLEGE FACTS"
1660 PRINT@289,"ENTER 1,2,3 OR 4 TO SELECT-"
1670 PRINT@321,"ANY OTHER KEY FOR MAIN MENU"
1680 FY=1920
1690 AK$=INKEY$:IF AK$=""THEN 1690
1700 IF VAL(AK$)>4 THEN SOUND 1,1:GOTO 1690
1710 IF AK$="1" THEN 1790
1720 IF AK$="2" THEN 1950
1730 IF AK$="3" THEN 2110
1740 IF AK$="4" THEN 2250
1750 RETURN
1760 '
1770 '** DEMOCRATIC NOMINEES **
1780 '
1790 CLS:PRINT STRING$(32,159);"DEMOCRATIC PARTY NOMINEES-":PRINT STRING$(32,207):PRINT" YEAR....CANDIDATES":SS=160
1800 FOR X=1 TO 17:PRINT@SS,FY;"   ";DP$(X):PRINT@SS+41,DV$(X)
1810 SS=SS+96:FY=FY+4
1820 IF X/3<>INT(X/3) THEN 1840 ELSE PRINT@480,"<PRESS ANY KEY>";:AK$=INKEY$:IF AK$=""THEN 1820
1830 FOR WP=160 TO 384 STEP 32:PRINT@WP,STRING$(32," "):NEXT WP:SS=160
1840 NEXT X
1850 PRINT@480,"<P=PRINT-ANY OTHER=MAIN MENU>";
1860 AK$=INKEY$:IF AK$=""THEN 1860 ELSE IF AK$="P"THEN 1870 ELSE RETURN
1870 PRINT#-2,STRING$(80,"-"):PRINT#-2,"DEMOCRATIC CANDIDATES FOR PRESIDENT/VICE PRESIDENT (1920-1984)":PRINT#-2,STRING$(80,"-")
1880 FY=1920:FORX=1TO17
1890 IF WN(X)=1 THEN WN$="*" ELSE WN$=""
1900 PRINT#-2,FY;TAB(10);"PRESIDENT:";DP$(X);WN$;TAB(50);"VP:";DV$(X):PRINT#-2,"":FY=FY+4:NEXT X
1910 PRINT#-2,STRING$(80,"-"):PRINT#-2,"* = Winner In Election":RETURN
1920 '
1930 '** REPUBLICAN NOMINEES **
1940 '
1950 CLS:PRINT STRING$(32,159);"REPUBLICAN PARTY NOMINEES-":PRINT STRING$(32,207):PRINT" YEAR....CANDIDATES":SS=160
1960 FOR X=1 TO 17:PRINT@SS,FY;"   ";RP$(X):PRINT@SS+41,RV$(X)
1970 SS=SS+96:FY=FY+4
1980 IF X/3<>INT(X/3) THEN 2000 ELSE PRINT@480,"<PRESS ANY KEY>";:AK$=INKEY$:IF AK$=""THEN 1980
1990 FOR WP=160 TO 384 STEP 32:PRINT@WP,STRING$(32," "):NEXT WP:SS=160
2000 NEXT X
2010 PRINT@480,"<P=PRINT-ANY OTHER=MAIN MENU>";
2020 AK$=INKEY$:IF AK$=""THEN 2020 ELSE IF AK$="P"THEN 2030 ELSE RETURN
2030 PRINT#-2,STRING$(80,"-"):PRINT#-2,"REPUBLICAN CANDIDATES FOR PRESIDENT/VICE PRESIDENT (1920-1984)":PRINT#-2,STRING$(80,"-")
2040 FY=1920:FORX=1TO17
2050 IF WN(X)=2 THEN WN$="*" ELSE WN$=""
2060 PRINT#-2,FY;TAB(10);"PRESIDENT:";RP$(X);WN$;TAB(50);"VP:";RV$(X):PRINT#-2,"":FY=FY+4:NEXT X
2070 PRINT#-2,STRING$(80,"-"):PRINT#-2,"* = Winner In Election":RETURN
2080 '
2090 '**** PRESIDENTS ****
2100 '
2110 CLS:PRINT STRING$(32,207);"PRESIDENTS OF THE UNITED STATES ";STRING$(32,159)
2120 FOR X=1 TO 17
2130 IF X=9 THEN 2140 ELSE 2160
2140 PRINT:PRINT"<PRESS ANY KEY>"
2150 AK$=INKEY$:IF AK$=""THEN 2150 ELSE:FOR WP=128 TO 448 STEP 32:PRINT@WP,STRING$(32," ");:NEXT WP:PRINT@128,"";
2160 IF WN(X)=1 THEN PRINT FY;"   ";DP$(X)
2170 IF WN(X)=2 THEN PRINT FY;"   ";RP$(X)
2180 FY=FY+4
2190 NEXT X
2200 PRINT@448,"<PRESS ANY KEY>";
2210 AK$=INKEY$:IF AK$=""THEN 2210 ELSE RETURN
2220 '
2230 '** ELECTORAL COLLEGE **
2240 '
2250 CLS:PRINTSTRING$(32,159):PRINT@38,"ELECTORAL COLLEGE":PRINTSTRING$(32,207);
2260 PRINT"   THE PRESIDENT AND VP ARE":PRINT"ELECTED  BY THE  ELECTORAL":PRINT"COLLEGE. EACH STATE AND DC HAVE AS MANY ELECTORS AS SENATORS":PRINT"AND REPRESENTATIVES.WE ACTUALLY":PRINT"VOTE FOR ELECTORS."
2270 PRINT"   THERE ARE 538 VOTES. TO WIN, A CANDIDATE NEEDS 270 VOTES. IF":PRINT"NO CANDIDATE GETS THE NEEDED":PRINT"MAJORITY, THE HOUSE AND SENATE":PRINT"ELECT THE PRESIDENT."
2280 PRINT@484,"<PRESS ANY KEY>";
2290 AK$=INKEY$:IF AK$=""THEN 2290 ELSE RETURN
2300 '
2310 '****** U.S.MAP ******
2320 '
2330 PMODE3,1:PCLS5:SCREEN1,1:COLOR3,1
2340 LINE(0,0)-(255,191),PSET,B
2350 LINE(10,10)-(245,181),PSET,B
2360 DRAW"BM110,160;D15F2R8E2U15"
2370 LINE(106,156)-(150,179),PSET,B
2380 DRAW"BM134,160;BR10BD2H2L8G2D4F2R8F2D4G2L8H2"
2390 LINE(36,20)-(36,22),PSET
2400 IF MP=2 THEN PUT(30,20)-(220,135),M:GOTO 2890
2410 DATA6,34,22,36,34,36,41,33,47,32,64,35,75
2420 GOSUB 2960:DRAW"R2D2L2"
2430 DATA7,41,89,45,90,54,101,54,103,64,103,77,107,87,107
2440 GOSUB2960:DRAW"U1R5"
2450 DATA42,97,112,97,115,103,117,104,115,109,116,116,125,124,128,124,122,126,118,129,122,130,115,139,115,141,117,148,117
2460 DATA 145,113,156,112,160,114,163,112,167,118,167,121,171,127,171,129,173,131,178,129,178,121,177,116,172,107,175,102,178,101,179,98,191,85,189,75,193,77,194,75,194,72,197,72,200,65,198,64,199,61,197,59,207,56,212,53
2470 GOSUB 2960:DRAW"U2R2D1":LINE-(215,51),PSET:DRAW"U2L2U1L2U6"
2480 DATA12,220,40,221,35,218,34,218,26,215,27,210,25,207,35,204,36,192,36,181,43,178,43,174,47
2490 GOSUB 2960:DRAW"R2D2L2"
2500 DATA2,163,54,168,43,2,169,34,159,32,4,147,22,142,25,126,20,36,20,36,34,39,35
2510 GOSUB 2960:DRAW"U3R2D2R2U2":GOSUB2960:DRAW"U7L4U2":GOSUB2960:GOSUB3030:DRAW"U2"
2520 DATA2,48,36,57,36,57,20,57,44,3,55,46,58,47,58,53
2530 GOSUB2960:GOSUB3030:GOSUB2960
2540 DATA2,32,54,75,54,49,54,49,72,4,65,87,67,90,64,93,64,103
2550 GOSUB3000:GOSUB2960
2560 DATA68,54,68,84,2,67,37,69,44,6,75,42,97,42,76,60,105,60,84,60,84,107,68,81,127,81,105,60,105,81,102,81,102,104
2570 GOSUB3030:DRAW"L3D3":DRAW"BM62,20;D8R2D8":GOSUB2960:DRAW"R7U2D18R21U40":GOSUB3000
2580 DATA2,92,104,92,106,102,84,112,84,4,112,93,119,96,131,96,133,115,119,20,119,26
2590 GOSUB2960:GOSUB3030:GOSUB2960
2600 DRAW"BM127,81;D2R2D14":GOSUB3030
2610 DATA3,121,29,118,37,97,37,118,37,121,40,5,121,54,118,53,117,54,115,53,97,53,121,53,123,56,4,122,62,125,66,127,69,127,81
2620 GOSUB2960:GOSUB3030:GOSUB2960:GOSUB3030:GOSUB2960
2630 DATA2,105,66,125,66,143,25,136,32,3,134,40,137,51,148,51,2,121,48,135,48,138,51,141,57,2,137,65,124,65,149,51,150,53
2640 GOSUB3000:DRAW"D2L2":GOSUB2960:DRAW"D1R2":GOSUB3000:GOSUB2960:GOSUB3030
2650 DATA5,151,56,151,70,149,73,145,75,143,78,137,65,145,81,142,84,137,97,137,97,139,100,4,138,103,140,108,144,108,145,113,2,139,90,168,90,149,90,148,112,158,90
2660 GOSUB2960:GOSUB3030:DRAW"D3L3U2L14":GOSUB3030:DRAW"D1L6":GOSUB3030:GOSUB2960:GOSUB3000:DRAW"R4U4R8":GOSUB2990:DRAW"DM159,109;R9U1R4"
2670 PAINT(128,56),2,1
2680 DATA173,104,168,93,169,89,180,90,183,93,158,90,160,87,2,163,87,168,82,149,73,150,74,4,153,72,154,72,160,69,160,54,160,67,161,69,10,162,68,165,70,170,78,173,78,174,74,177,73,178,72,181,71,187,72,189,77
2690 GOSUB3030:DRAW"D1L2":GOSUB2990:DRAW"R5D1R6":GOSUB3030
2700 DRAW"BM144,83;R4U1R41"
2710 GOSUB3030:GOSUB2960:GOSUB3030
2720 GOSUB2960:GOSUB3030:GOSUB2960
2730 DATA2,163,81,168,78,136,31,140,30,6,144,34,146,34,148,40,151,38,149,44,148,51,142,31,150,27,7,149,30,152,31,156,29,159,33,155,34,150,35,149,39,159,33,155,37,3,153,43,153,47,150,53,157,35,161,37
2740 GOSUB3000:GOSUB2960:GOSUB3030
2750 GOSUB2960:GOSUB3030:GOSUB2960:GOSUB3030
2760 DRAW"D7R2U3"
2770 DATA2,165,42,166,47,150,54,169,54,4,176,50,176,47,183,44,184,42,172,52,172,68,3,191,68,194,69,194,72,172,64,169,67,164,70,193,58,192,58,191,63,2,193,64,191,68,197,51,209,51,210,54
2780 GOSUB2960:GOSUB3030:GOSUB2960:GOSUB3030:GOSUB2960
2790 DRAW"BM189,68;D3R6"
2800 GOSUB3030:GOSUB2990
2810 DRAW"BM175,68;D4R5"
2820 DRAW"BM175,51;D4R13"
2830 GOSUB2990:DRAW"R4U9R2U12"
2840 GOSUB3030:GOSUB2960:GOSUB3030:GOSUB2990:GOSUB3000:GOSUB2990
2850 DATA2,205,51,205,57,199,46,207,47,210,44,204,36,205,39,2,203,43,203,47,209,32,208,35,2,209,43,210,43,202,60,209,58,3,210,60,206,61,202,60
2860 GOSUB 3030:GOSUB2960:GOSUB3030:GOSUB2960
2870 DRAW"BM189,69;C5D2R3":DRAW"BM190,69;C3D3R2"
2880 IF MP<>2 THEN GET(30,20)-(220,135),M:MP=2
2890 DRAW"BM210,80;U6R6D6L6BR12U6R4D6U3L4BR9BD3U6BR4G3F3"
2900 DRAW"BM210,90;U6R6D6L6BR12U6D3R5U3D6BR6U6"
2910 DRAW"BM210,100;U6R6D6L6BR12U6R3F1D4G1L3BR9U6R3F1BD4G1L3"
2920 RETURN
2930 '
2940 '** READ MAP ONCE ONLY **
2950 '
2960 READA
2970 FORX=1 TO A:READ B,C
2980 LINE-(B,C),PSET:NEXTX:RETURN
2990 READA,B:LINE-(A,B),PSET:RETURN
3000 READA
3010 FOR X=1 TO A:READB,C,D,E
3020 LINE(B,C)-(D,E),PSET:NEXTX:RETURN
3030 READA,B,C,D:LINE(A,B)-(C,D),PSET:RETURN
3040 '
3050 '** SCREEN CLEANUP **
3060 '
3070 FOR WIPE=128 TO 416 STEP 32:PRINT@WIPE,STRING$(32," ");:NEXT:RETURN
4000 CLS:PRINT STRING$(32,175):PRINT"LOAD OR SAVE DATA FILES...":PRINT STRING$(32,159)
4002 PRINT"CHOOSE 1>CASSETTE 2>DISK ":INPUT CD$
4004 PRINT"DATA FILES WILL INCLUDE VOTES & PREDICTIONS."
4006 PRINT:PRINT"CHOOSE 1> SAVE OR 2> LOAD":INPUT SL$
4008 IF SL$="1" THEN 4012
4009 IF SL$="2" THEN 4020
4010 SOUND 1,1:PRINT"CHOOSE 1 OR 2!":FOR DL=1 TO 460:NEXT:GOTO 4000
4012 IF CD$="2"THEN 4016
4013 OPEN "O",#-1,"ELECT.DAT"
4014 FOR X=1 TO 51:WRITE #-1,SV(X),PP(X):NEXT X:CLOSE #-1
4015 RETURN
4016 OPEN "O",1,"ELECT.DAT"
4017 FOR X=1 TO 51:WRITE #1,SV(X),PP(X):NEXT X:CLOSE 1
4018 RETURN
4020 IF CD$="2" THEN 4026
4021 OPEN "I",#-1,"ELECT.DAT"
4022 FOR X=1 TO 51:INPUT #-1,SV(X),PP(X):NEXT X:CLOSE #-1
4023 RETURN
4026 OPEN "I",1,"ELECT.DAT"
4027 FOR X=1 TO 51:INPUT #1,SV(X),PP(X):NEXT X:CLOSE 1
4028 RETURN
4500 CLS:PRINT STRING$(32,175):PRINT"POLITICAL PREDICTION.....":PRINT STRING$(32,159);
4502 PRINT"YOUR CHANCE TO OUT-EXPERT THE   EXPERTS. ENTER YOUR PREDICTION  AS TO HOW EACH STATE WILL VOTE.";
4504 PRINT" ELECTION 88 WILL COMPARE YOUR   PREDICTIONS WITH THE ACTUAL VOTEAS IT IS ENTERED.":PRINT
4506 PRINT"<PRESS ANY KEY TO CONTINUE>"
4508 AK$=INKEY$:IF AK$=""THEN 4508 ELSE GOSUB 3070
4509 FOR X=1 TO 51
4510 PRINT@128,"ENTER YOUR PREDICTION FOR":PRINT "THE STATE OF ";LS$(X):PRINT
4512 PRINT"<D>EMOCRAT OR <R>EPUBLICAN"
4514 PRINT"<X> FOR MENU   ";:INPUT CH$
4515 IF CH$="X"THEN 4530
4516 IF CH$="D" THEN PP(X)=1:GOTO 4520
4517 IF CH$="R" THEN PP(X)=2:GOTO 4520
4518 PP(X)=0
4520 GOSUB 3070:NEXT X
4530 RETURN
