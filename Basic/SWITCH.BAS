10 REM MICHAEL B. KROMEKE
20 DIM A,A$,B,B$,X,P(9):CLS0
30 FOR X=1057 TO 1086:POKE X,140:POKEX+64,131:POKEX+160,140:POKEX+288,131:NEXTX
40 PRINT@7," CORRECT SOLUTION ";:FOR X=0 TO 9 STEP 3:PRINT@68+X,CHR$(191);:PRINT@83+X,CHR$(175);:NEXT X
50 FOR X=1056 TO 1120 STEP 32:POKE X,143:POKE X+31,143:NEXT X:FOR X=1216 TO 1344 STEP 32:POKE X,143:POKE X+31,143:NEXT X
60 B$=CHR$(128):B$=B$+B$
70 PRINT@291,"";:FOR X =1 TO 9:PRINT X;:NEXT X
80 FOR X=1TO4:P(X)=1:P(X+5)=2:NEXT X
90 PRINT@390,"PRESS # 1-9 TO MOVE ";:PRINT@384,CHR$(175);:PRINT@386,"=> ";:PRINT@411," <=";:PRINT@415,CHR$(191);
100 PRINT@459,"PRESS 'X'";:PRINT@489,"TO TRY AGAIN ";:GOTO260
110 A$=INKEY$:IF A$=""THEN 110
120 IF A$="X" THEN RUN 20
130 A=VAL(A$):IF A<1 OR A>9 THENGOTO 250
140 IF P(A)=1THEN 210
150 IF P(A)=0 THEN 250
160 IF A-1=0 THEN 250
170 IF P(A-1)=0 THEN P(A-1)=P(A):P(A)=0:GOTO 260
180 IF A-2=0 THEN 250
190 IF P(A-2)=0 THEN P(A-2)=P(A):P(A)=0:GOTO 260
200 GOTO 250
210 IF A+1=10 THEN 250
220 IF P(A+1)=0 THEN P(A+1)=P(A):P(A)=0:GOTO 260
230 IF A+2=10 THEN 250
240 IF P(A+2)=0 THEN P(A+2)=P(A):P(A)=0:GOTO 260
250 SOUND1,3:GOTO 110
260 PRINT@228,"";:FOR X=1 TO 9
270 IF P(X)=1 THEN PRINT(CHR$(175));B$;
280 IF P(X)=0 THEN PRINT B$+CHR$(128);
290 IF P(X)=2 THEN PRINT CHR$(191)B$;
300 NEXT X:SOUND 99,2:SOUND 140,1:IF B=1 THEN PRINT@170,"CHANGE places";:B=0:ELSE IF B=0 THEN PRINT@170,"change PLACES";:B=1
310 FOR X=1TO4:IF P(X)<>2 THEN 110 ELSE NEXT
320 IF P(5)<>0 THEN 110
330 FOR X=1 TO 255 STEP 3:SOUNDX,2:POKE 65314,X:NEXT X
340 CLS3:FOR X=1 TO 20
350 PRINT@RND(500)," YOU DID IT ";:SOUND RND(255),4:NEXT X:END
