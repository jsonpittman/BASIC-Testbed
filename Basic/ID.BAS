1 'ID
2 'BY NICK JOHNSON
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 POKE &HD7C0,0:POKE&HD816,20
20 POKE 150,1
30 VERIFYON
40 WIDTH 32:PALETTE12,63:PALETTE13,0:PRINT"INSERT A DISK AND PRESS [ENTER]."
50 FOR A=1024 TO 1055:W=PEEK(A):FOR B=A+32 TO 1503 STEP 32:POKEB,W:POKEB-32,96:IF INKEY$=CHR$(13) THEN 70 ELSE NEXTB:NEXTA
60 FOR Q=1 TO 14:PRINT@480:FORI=1TO3:NEXTI:NEXTQ:GOTO 50
70 PALETTE12,0:PALETTE13,18:CLEAR 4096
80 ONBRKGOTO80:POKE&HF77E,33:POKE&HF798,33:POKE&HF850,33:POKE&HF89D,33:POKE&HF91C,33:POKE&HF812,33:POKE&HF7ED,33
90 POKE &HE047,0
100 POKE &HFF94,0:POKE&HFF95,1
110 POKE 65497,0
120 POKE &HE045,19:POKE&HE047,0
130 WIDTH 80
140 PALETTE0,0:PALETTE8,63:ATTR0,0:CLS
150 DIM F$(68):DIME$(68):DIMT(68):DIMA(68):DIMG(68):DIMN(68)
160 ON BRK GOTO 160
170 ON ERR GOTO 1590
180 GOSUB 720
190 CLS
200 ATTR0,0
210 PALETTE0,0:PALETTE8,63
220 CLS
230 LOCATE 34,0:ATTR4,4,B:PRINT"ID      .BAS";:ATTR0,0
240 LOCATE 32,1:ATTR0,0,B,U:PRINT"BY NICK JOHNSON.";:ATTR0,0
250 LOCATE12,23:ATTR 0,0,U:PRINT"USE ARROW KEYS TO POSITION AND ENTER TO MAKE SELECTION.";:ATTR0,0:LOCATE0,0
260 LOCATE 70,2:ATTR4,4:PRINT"[BREAK]:":LOCATE 70,3:PRINT" RE-READ":LOCATE70,4:PRINT"   DIR  ":LOCATE70,6:PRINT" [Q] QUIT ";:ATTR0,0
270 H=0:V=2
280 FOR I=1 TO Q-1
290 LOCATE H,V:PRINT F$(I)"."E$(I);
300 V=V+1:IF V=23 THEN V=2:H=H+14
310 NEXT I
320 HF=1:D=0:B=0:GOSUB1130
330 GOSUB 1220
340 D=1:GOSUB1130
350 IF UA=1 THEN HF=HF-1:IF HF<1 THEN HF=1
360 IF LA=1 THEN HF=HF-21:IF HF<1 THEN HF=HF+21
370 IF RA=1 THEN HF=HF+21:IF HF>Q-1 THEN HF=HF-21
380 IF DA=1 THEN HF=HF+1:IF HF>Q-1 THEN HF=Q-1
390 IF E=1 THEN GOSUB 1790:GOTO420
400 B=0:D=0:GOSUB 1130
410 GOSUB 1220:D=1:GOSUB1130:GOTO 350
420 B=1:D=0:GOSUB 1130
430 FOR X=64 TO 0 STEP-16:PALETTE8,X:FOR Y=1 TO 100:NEXTY:NEXTX
440 LOCATE 0,2:PRINT:LOCATE0,2:ATTR 4,4,B:PRINT"OPENING ";:ATTR4,4,U:PRINTF$(HF)"."E$(HF);:ATTR4,4
450 POKE65496,0:OPEN"D",#1,F$(HF)+"."+E$(HF)+"",1
460 B=LOF(1)
470 CLOSE
480 POKE65497,0
490 PRINT
500 INPUT"ECHO OUTPUT TO PRINTER";Q$:IF Q$="Y" THEN POKE 360,162:POKE 361,191 ELSE POKE 360,&HCC:POKE361,&H1C
510 LOCATE0,2:PRINT "The file ";:ATTR4,4,U:PRINTF$(HF)"."E$(HF);:ATTR4,4:PRINT" contains"B"bytes ";
520 PRINT"and occupies"N(HF)"granules of disk space.":PRINT"There are"(N(HF)*2304)-B"wasted bytes in these granules."
530 PRINT"This is a(n) ";
540 IF T(HF)=0 THEN PRINT"BASIC "; ELSE IF T(HF)=1 THEN PRINT"ASCII "; ELSE IF T(HF)=2 THEN PRINT"Machine Language "; ELSE IF T(HF)=3 THEN PRINT"Asm. Source Code "; ELSE PRINT"unknown ";
550 PRINT"file."
560 PRINT"It is in ";
570 IF A(HF)=255 THEN PRINT"ASCII "; ELSE PRINT"BINARY ";
580 PRINT"format."
590 IF T(HF)=2 THEN GOSUB 1330
600 POKE 360,&HCC:POKE361,&H1C
610 LINE INPUT"Would you like to see the file data?";Q$
620 IF Q$="Y" THEN 630 ELSE 700
630 INPUT"ECHO FILE DATA TO PRINTER";Q$:IF Q$="Y" THEN POKE 360,162:POKE361,191
640 POKE65496,0:OPEN"I",#1,F$(HF)+"."+E$(HF)
650 IF EOF(1) THEN 660 ELSE INPUT#1,A$:PRINTA$;:GOTO 650
660 CLOSE#1
670 POKE65497,0
680 IF INKEY$="" THEN 680
690 PRINT
700 POKE 360,&HCC:POKE361,&H1C:GOTO 190
710 GOTO710
720 ' READ DIRECTORY
730 ATTR0,0:PALETTE8,63:CLS:LOCATE40-9,12:ATTR4,4,B:PRINT"READING DIRECTORY";:ATTR0,0
740 LOCATE 40,13
750 C=1:Q=1
760 UNLOAD
770 FOR I=3 TO 11
780 PRINTCHR$(128+I-3);:LOCATE40,13
790 POKE65496,0
800 DSKI$0,17,I,A$,B$
810 POKE 65497,0
820 A$=A$+LEFT$(B$,127)
830 F$=MID$(A$,C,32)
840 IF ASC(F$)=0 THEN 940
850 IF ASC(F$)=255 THEN 970
860 F$(Q)=F$
870 E$(Q)=MID$(F$,9,3)
880 T(Q)=ASC(MID$(F$,12,1))
890 A(Q)=ASC(MID$(F$,13,1))
900 G(Q)=ASC(MID$(F$,14,1))
910 F$(Q)=LEFT$(F$,8)
920 Q=Q+1
930 IF Q>68 THEN 970
940 C=C+32
950 IF C>255 THEN C=1:IF I>=11 THEN 970 ELSE NEXT I
960 GOTO 830
970 'READ GRANULE TABLE
980 DSKI$0,17,2,C$,D$
990 POKE 65497,0
1000 C$=LEFT$(C$,68)
1010 'AND MATCH GRANULES WITH FILES
1020 FOR I=1 TO Q-1
1030 N(I)=0
1040 LOCATE 39,13:PRINT CHR$(I+32);
1050 N=G(I)
1060 IF N>68 THEN ATTR 4,4,B,U:LOCATE34,11:PRINT"GRANULE ERROR!";:ATTR0,0:LOCATE41,13:GOTO 1110
1070 N=ASC(MID$(C$,N+1,1))
1080 N(I)=N(I)+1
1090 PRINTCHR$((N(I)+32));:LOCATE39,13
1100 IF N<192 THEN 1070
1110 NEXT I
1120 RETURN
1130 ' HIGHLIGHT A FILE
1140 IF HF<22 THEN H=0:V=HF ELSE IF HF>21 AND HF<43 THEN H=14:V=HF-21 ELSE IF HF>42 AND HF<64 THEN H=28:V=HF-42 ELSE H=42:V=HF-63
1150 V=V+1
1160 LOCATE H,V
1170 IF B=1 THEN ATTR4,4,B:PRINT F$(HF)"."E$(HF);:ATTR0,0:GOTO 1190
1180 IF D=0 THEN ATTR4,4:PRINT F$(HF)"."E$(HF);:ATTR0,0 ELSE ATTR 0,0:PRINT F$(HF)"."E$(HF); 
1190 RETURN
1200 TIMER=0
1210 IF TIMER>=4 THEN RETURN ELSE 1210
1220 'READ KEYS
1230 UA=0:DA=0:LA=0:RA=0:E=0
1240 X$=INKEY$
1250 IF INKEY$=CHR$(13) THEN E=1:GOTO1320
1260 IF PEEK(339)=251 THEN WIDTH 32:POKE65496,0:CMP:STOP
1270 IF PEEK(341)=247 THEN UA=1
1280 IF PEEK(342)=247 THEN DA=1
1290 IF PEEK(343)=247 THEN LA=1
1300 IF PEEK(344)=247 THEN RA=1
1310 IF UA=0 AND DA=0 AND LA=0 AND RA=0 THEN 1250
1320 RETURN
1330 ON ERR GOTO 1580
1340 POKE65497,0:PRINT"START, END ":POKE65496,0
1350 OPEN"D",#1,F$(HF)+"."+E$(HF),1
1360 B=1
1370 GET#1,B+1:INPUT#1,L1$:GET#1,B+2:INPUT#1,L2$
1380 GET#1,B+3:INPUT#1,A1$:GET#1,B+4:INPUT#1,A2$
1390 IF L1$="" THEN L1$=CHR$(0) ELSE IF L2$="" THEN L2$=CHR$(0)
1400 IF A1$="" THEN A1$=CHR$(0) ELSE IF A2$="" THEN A2$=CHR$(0)
1410 S=ASC(A1$)*256+ASC(A2$)
1420 E=S+ASC(L1$)*256+ASC(L2$)-1
1430 POKE 65497,0
1440 PRINTUSING"%  %   %  %";HEX$(S),HEX$(E)
1450 POKE65496,0
1460 B=B+ASC(L1$)*256+ASC(L2$)+5
1470 GET#1,B:INPUT#1,B1$
1480 IF B1$="" THEN B1$=CHR$(0)
1490 IF ASC(B1$)=255 THEN 1500 ELSE 1370
1500 GET#1,B+3:INPUT#1,E1$:GET#1,B+4:INPUT#1,E2$
1510 IF E1$="" THEN E1$=CHR$(0) ELSE IF E2$="" THEN E2$=CHR$(0)
1520 POKE65497,0
1530 PRINT"EXEC="HEX$(ASC(E1$)*256+ASC(E2$))
1540 POKE 65496,0
1550 CLOSE
1560 POKE65497,0
1570 ON ERR GOTO 1590:RETURN
1580 POKE65497,0:POKE111,0:PRINT"FILE STRUCTURE ERROR!":POKE65496,0:CLOSE:GOTO 1610
1590 PRINT:POKE111,0:PRINT"AN ERROR HAS BEEN ENCOUNTERED WHILE PROCESSING FILE DATA."
1600 CLOSE
1610 IF Q<20 THEN PRINTERNO,ERLIN
1620 IF ERNO=20 THEN PRINT"I/O ERROR, TYPE ";:GOTO 1630 ELSE 1710
1630 Q=PEEK(&HF0)
1640 IF Q AND 128 THEN PRINT"DRIVE NOT READY."
1650 IF Q AND 64 THEN PRINT"DISK IS WRITE PROTECTED"
1660 IF Q AND 32 THEN PRINT"READ OR WRITE ERROR"
1670 IF Q AND 16 THEN PRINT"ACK! RECORD NOT FOUND!"
1680 IF Q AND 8 THEN PRINT"CYCLIC REDUNDANCY CHECK ERROR"
1690 IF Q AND 4 THEN PRINT"LOST DATA"
1700 GOTO 1760
1710 IF ERNO=23  THEN PRINT"FILE NOT CLOSED PROPERLY OR FILE STRUCTURE BAD."
1720 IF ERNO=26 THEN 1760
1730 IF ERNO=27 OR ERNO=32 OR ERNO=37 THEN PRINT"FILE STRUCTURE BAD!"
1740 IF ERNO=36 THEN PRINT"VERIFICATION ERROR, DISK MAY BE BAD."
1750 GOSUB 1790:GOSUB1790
1760 IF INKEY$="" THEN1760
1770 POKE 360,&HCC:POKE361,&H1C
1780 GOTO 180
1790 PLAY"L128O4V31CV29CV27CV25CV23CV21CV19CV17CV15CV13CV11CV9CV7CV5CV3CV1C"
1800 RETURN
1810 PRINTPEEK(339):GOTO 1810
