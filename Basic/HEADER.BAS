0 'COPYRIGHT 1990 FALSOFT, INC.
10 '***************************
20 'TV HEADER GENERATOR
30 'WRITTEN BY CHRIS MAYEUX
40 '
50 'BE SURE YOU ARE IN 32 COLUMN
60 'VDG MODE BEFORE OPERATING
70 'THIS PROGRAM
80 '
90 '***************************
100 ' WHERE EVER COMMANDS ARE
110 ' CRAMMED TOGETHER, IT IS
120 ' FOR TIMING PURPOSES, DO
130 ' NOT SPREAD THEM OUT !!!
140 '**************************
150 'GENERATE COLORBAR PATTERN
160 '**************************
170 CLS
180 PRINT@0,"30 SECONDS COLOR BAR LOADED...":PRINT:GOSUB 280:CLS0
190 CB$=STRING$(4,32)+STRING$(4,159)+STRING$(4,175)+STRING$(4,191)+STRING$(4,207)+STRING$(4,223)+STRING$(4,239)+STRING$(4,255)
200 PRINT@32,"";:FOR X=1 TO 13:PRINT CB$;:NEXT X
210 TIMER=0
220 IF TIMER<1800 THEN 220
230 GOSUB 1760
240 GOTO 360
250 '**************************
260 'GIVE DIRECTIONS TO USER
270 '**************************
280 PRINT"STEP 1. SET RECORDER TO RECORD"
290 PRINT"        AND PUT IT ON pause"
300 PRINT
310 PRINT"STEP 2. WHEN READY TO RECORD,"
320 PRINT"        TAKE RECORDER OFF OF "
330 PRINT"        PAUSE AND PRESS A KEY."
340 K$=INKEY$:IF K$="" THEN 340
350 RETURN
360 '**************************
370 'ENTER SLATE INFORMATION
380 '**************************
390 CLS
400 PRINT"WE WILL NOW MAKE A SLATE SCREEN"
410 PRINT"ENTER THE FOLLOWING INFORMATION"
420 PRINT
430 '**************************
440 ' THESE ARE THE DEFAULTS
450 '**************************
460 NM$="SHOW NAME"
470 SN$="0001"
480 PD$="PRODUCER'S NAME"
490 DT$="00/00/00"
500 LN$="30:00"
510 CU$="00:30"
520 '**************************
530 PRINT"25 CHARACTERS MAX"
540 LINEINPUT"SHOW NAME:";K$
550 IF LEN(K$)>25 THEN K$=LEFT$(K$,25)
560 IF K$=""THEN NM$=NM$ ELSE NM$=K$
570 LINEINPUT"SHOW NUMBER (####):";K$
580 IF LEN(K$)>4 THEN K$=LEFT$(K$,4)
590 IFK$=""THEN SN$=SN$ ELSE SN$=K$
600 LINEINPUT"PRODUCER NAME:";K$
610 IFK$="" THEN PD$=PD$ ELSE PD$=K$
620 LINEINPUT"DATE (##/##/##):";K$
630 IF MID$(K$,3,1)<>"/" THEN L$=LEFT$(K$,2)+"/"+RIGHT$(K$,5):K$=L$
640 IF MID$(K$,6,1)<>"/" THEN L$=LEFT$(K$,5)+"/"+RIGHT$(K$,2):K$=L$
650 IF (MID$(K$,3,1)="/" AND MID$(K$,6,1)="/") THEN DT$=K$:GOTO 670
660 IF K$="" THEN DT$=DT$
670 LINEINPUT"SHOW LENGTH (##:##):";K$
680 IFK$="" THEN LN$=LN$ ELSE LN$=K$
690 LINEINPUT "CUE TIME (##:##):";K$
700 IF K$="" THEN CU$=CU$ ELSE CU$=K$
710 LINEINPUT"FACILITY NAME:";FC$
720 IF FC$="" THEN FC$="HERE"
730 CLS:PRINT"20 SECONDS OF SLATE IS LOADED.":PRINT:GOSUB 280
740 '**************************
750 'DRAW SLATE SCREEN
760 '**************************
770 CLS(0)
780 PRINT@96,"TITLE:"+CHR$(128)+NM$;
790 PRINT@135,"SHOW #";SN$;
800 PRINT@192,"PRODUCER:"+CHR$(128)+PD$;
810 PRINT@260,"DATE:"+CHR$(128)+DT$;
820 PRINT@322,"LENGTH:"+CHR$(128)+LN$+CHR$(128)+"MINUTE(S)";
830 PRINT@389,"CUE:"+CHR$(128)+CU$+CHR$(128)+"MINUTE(S)";
840 PRINT@448,"FACILITY:"+CHR$(128)+FC$;
850 TIMER=0
860 IF TIMER<1200 THEN 860
870 GOSUB 1760
880 '**************************
890 'TEN SECOND COUNT-DOWN
900 '**************************
910 CLS:PCLS:PRINT@238,"wait"
920 N0$="C0D9R1C5R9C0U9L10D16R10U16"
930 N1$="C5R10L9D16U16L1D9R10L10D7R10C0U16"
940 N2$="C5D8R1U8L1C0R10D9L10D7R10U1C5U6R1D6"
950 N3$="C0R11D9L10R10D7L10U1C5U14L1C5D14"
960 N4$="C5R10L10C0D9R10U9D16L1C5L9U6R1D6"
970 N5$="C0R10D1C5D8R1U9C0L11D9R10D7L10U1C5U5R1D5"
980 N6$="C0R10D1C5D8R1U9C0L11D9R10D7L10U7"
990 N7$="C0R10D16L1C5L9U7R1D7L1U7R8L9U8"
1000 N8$="C0R10D16L10U7R10L10U9"
1010 N9$="C0R10D9U9L10D9R10D7L10U1C5U6R1D6"
1020 P1$="BM100,82":P2$="BM116,82":P3$="BM136,82":P4$="BM152,82"
1030 PMODE 3,1:SCREEN 1,0:PCLS0:SCREEN 0,0
1040 CIRCLE(130,90),50,5
1050 PAINT(130,90),3,5
1060 LINE(95,80)-(167,80),PSET
1070 LINE -(167,100),PSET
1080 LINE -(95,100),PSET
1090 LINE -(95,80),PSET
1100 PAINT(96,81),5,4
1110 DRAW"BM170,60C5E5F3G5"
1120 DRAW"BM93,60H5G3F5"
1130 DRAW"BM129,40U5R4D5"
1140 CIRCLE(131,27),15,5
1150 CIRCLE(131,27),12,5
1160 DRAW P1$+N0$:DRAW P3$+N1$
1170 DRAW P2$+N0$:DRAW P4$+N0$
1180 CLS:PRINT@0,"10 SECONDS COUNTDOWN LOADED...":PRINT:GOSUB 280
1190 SCREEN 1,0
1200 DRAW P2$+N0$
1210 T$="10":GOTO 1270
1220 '**************************
1230 TIMER=0
1240 IF TIMER<50 THEN 1240
1250 '**************************
1260 GOSUB 1310:GOSUB 1460
1270 DRAW P3$+NT$:DRAW P4$+NO$
1280 GOSUB 1720
1290 IFT$=" 2"THEN1610
1300 GOTO 1230
1310 '*************************
1320 ' THE TENS PORTION
1330 '*************************
1340 TT$=LEFT$(T$,1)
1350 IFTT$=" "THENNT$=N0$:RETURN
1360 IFTT$="1"THENNT$=N1$:RETURN
1370 IFTT$="2"THENNT$=N2$:RETURN
1380 IFTT$="3"THENNT$=N3$:RETURN
1390 IFTT$="4"THENNT$=N4$:RETURN
1400 IFTT$="5"THENNT$=N5$:RETURN
1410 IFTT$="6"THENNT$=N6$:RETURN
1420 IFTT$="7"THENNT$=N7$:RETURN
1430 IFTT$="8"THENNT$=N8$:RETURN
1440 IFTT$="9"THENNT$=N9$:RETURN
1450 RETURN
1460 '*************************
1470 ' THE ONES PORTION
1480 '*************************
1490 TT$=RIGHT$(T$,1)
1500 IFTT$="0"THENNO$=N0$:RETURN
1510 IFTT$="1"THENNO$=N1$:RETURN
1520 IFTT$="2"THENNO$=N2$:RETURN
1530 IFTT$="3"THENNO$=N3$:RETURN
1540 IFTT$="4"THENNO$=N4$:RETURN
1550 IFTT$="5"THENNO$=N5$:RETURN
1560 IFTT$="6"THENNO$=N6$:RETURN
1570 IFTT$="7"THENNO$=N7$:RETURN
1580 IFTT$="8"THENNO$=N8$:RETURN
1590 IFTT$="9"THENNO$=N9$:RETURN
1600 RETURN
1610 '*************************
1620 ' DONE COUNTING NUMBERS
1630 '*************************
1640 SCREEN 0,0:CLS 0
1650 TIMER=0
1660 IF TIMER<240 THEN 1660
1670 GOSUB 1760:GOTO 1800
1680 GOTO 1680
1690 '*************************
1700 ' DECREMENT DIGIT VALUES
1710 '*************************
1720 R=VAL(T$)
1730 R=R-1
1740 R$=STR$(R)
1750 T$=R$:RETURN
1760 PRINT@482,"press"+CHR$(128)+"pause"+CHR$(128)+"on"+CHR$(128)+"your"+CHR$(128)+"recorder";
1770 TIMER=0
1780 IF TIMER<600 THEN 1780
1790 RETURN
1800 '*************************
1810 'CHOOSE TO RE-DO SOMETHING
1820 '*************************
1830 CLS
1840 PRINT " (1) REDO COLORBARS"
1850 PRINT
1860 PRINT " (2) REDO SLATE"
1870 PRINT
1880 PRINT " (3) REDO COUNTDOWN"
1890 PRINT
1900 PRINT " (4) DON'T REDO"
1910 PRINT
1920 PRINT " CHOICE =-->";
1930 K$=INKEY$:IF K$="" THEN 1930
1940 IF INSTR("1234",K$)=0 THEN SOUND 200,1:GOTO 1930
1950 PRINT K$
1960 IF K$="4" THEN END
1970 ON VAL(K$) GOTO 170,390,910
