1 REM**************************
2 REM*  DRIVING INSTRUCTOR 2  *
3 REM*   KNOWING ROAD SIGNS   *
4 REM*    BY FRED B.SCERBO    *
5 REM*     60 HARDING AVE     *
6 REM*  NORTH ADAMS,MA 01247  *
7 REM*   COPYRIGHT (C) 1987   *
8 REM**************************
10 CLS0:CLEAR1000:FORI=1TO32:PRINTCHR$(188);:NEXT:FORI=1TO192:READA:IFA=0THENA=16
15 PRINTCHR$(A+112);:NEXT
20 DATA126,124,122,126,124,122,126,124,122,125,124,125,,,30,28,26,29,,,30,20,30,20,30,16,20,30,,21,28,29
25 DATA122,,122,122,,122,122,96,122,117,,117,,,26,,24,21,16,22,16,,26,,26,,16,26,,21,,20
30 DATA123,115,122,122,,122,123,115,122,117,96,117,,,27,19,18,21,22,16,,,26,,26,,16,26,,21,19,19
35 DATA122,117,,122,,122,122,112,122,117,112,117,,,,,26,21,20,18,,,26,,26,,16,26,,,,21
40 DATA122,117,96,122,,122,122,,122,117,,117,,16,26,,26,21,,20,18,,26,,26,21,16,26,21,21,,21
45 DATA122,117,114,123,115,122,122,112,122,119,115,119,,,27,19,26,23,18,,27,17,27,17,27,23,17,27,23,21,19,23
50 FORI=1TO32:PRINTCHR$(179);:NEXT
55 PRINT@293," DRIVING INSTRUCTOR 2 ";
60 PRINT@325,"  KNOWING ROAD SIGNS  ";:PRINT@389,"   BY FRED B.SCERBO   ";
65 PRINT@421,"  COPYRIGHT (C) 1987  ";
70 PRINT@485," (T)ALKING OR (N)OT ? ";
75 X$=INKEY$:IFX$="T"THEN95
80 IFX$="N"THEN90
85 GOTO75
90 NT=1
95 CLS0
100 XX=&HFF00:YY=&HFF7E
105 POKEXX+1,52:POKEXX+3,63
110 POKEXX+35,60
115 PMODE4,1:PCLS1
120 DIMR(23),L$(26),Y(40):C$(1)="C1":C$(2)="C2":C$(3)="C3":C$(4)="C4"
125 FORI=1TO26:READL$(I):NEXT
130 GOTO260
135 AA$=JK$
140 A$=STR$(A):B$=STR$(B)
145 DRAW"S4BM"+A$+","+B$+C$(CL)
150 IF LEN(JK$)<=21THEN170
155 FOR T=21TO0STEP-1:IF MID$(JK$,T,1)=" "THEN165
160 NEXT T:GOTO170
165 L$=LEFT$(JK$,T):W$=L$:GOSUB175:JK$=" "+RIGHT$(JK$,(LEN(JK$))-T):GOTO140
170 W$=JK$:B=B+14:GOSUB175:RETURN
175 SL=LEN(W$):FORI=1TOSL:BB$=MID$(W$,I,1):C=ASC(BB$)-64:IF C=-32THEN DRAW"BR12":GOTO195
180 IF C=-18THENDRAW"BR2RBR9":GOTO195
185 IFC=-20THENDRAW"BR2R2D2G2E4BR7":GOTO195
190 DRAWL$(C)
195 NEXTI:B=B+14:RETURN
200 IFNT=1THEN240
205 FORII=1TOLEN(AA$)
210 IF PEEK(YY)AND 128=0 THEN210
215 POKEYY,ASC(MID$(AA$,II,1))
220 NEXTII
225 IFPEEK(YY)AND128=0THEN225
230 POKEYY,13
235 FORHH=1TO1600:NEXTHH:RETURN
240 FORHH=1TO3000
245 X$=INKEY$:IFX$=CHR$(13)THEN255
250 NEXTHH
255 RETURN
260 PMODE2,1:PCLS1:SCREEN1,1:PMODE1:SCREEN1,1:PCLS0:POKE65314,248
265 PCLS3:A=0:B=56:CL=4:JK$=" PRESS RESET AND RUN IF SCREEN IS BLUE.":GOSUB135:B=B+20:JK$=" PRESS ENTER WHEN THE SCREEN IS RED.":GOSUB135
270 X$=INKEY$:IFX$<>CHR$(13)THEN270
275 PCLS0:SCREEN0,0:R=3:BL=2:FORI=0TO256STEP4:PSET(I,1,3):PSET(I+2,3,3):NEXT:DIMA(20):GET(0,0)-(256,4),A,G:PCLS0
280 DATA U6E2R2F2D2NL4D4BR6,U8R4F2G2NL4F2G2NL4BR8,U8R4BD8NL4BR6,U8R4F2D4G2NL4BR8,U8NR4D4NR4D4R4BR6,U8NR4D4NR4D4BR10,U8R6BD4NL2D4NL4BR6,U4NU4R6U4D8BR6
285 DATA R2U8L2R4L2D8R2BR6,NU4R4U8L4R6BD8BR6,U8D4R2NE4F4BR6,NU8R4BR6,U8F4E4D8BR6,U8F6NU6D2BR6,U8R6D8NL6BR6,U8R6D4L6D4BR12,U8R6D8NL6NH4NF2BR6
290 DATA U8R6D4L4F4BR6,R6U4L6U4R6BD8BR6,BR4U8L4R8BD8BR6,NU8R6NU8BR6,BU8D4F4E4U4BD8BR6,NU8R4NU6R4NU8BR6,E8G4H4F8BR6,BU8D2F4ND2E4U2BD8BR6,NR8E8NL8BD8BR6
295 PMODE2,1:PCLS1:SCREEN1,1:PMODE1:SCREEN1,1:PCLS0:POKE65314,248:PMODE2,3:PMODE1,3:COLOR2,3:GOTO320
300 COLOR2,3:LINE(0,138)-(256,192),PSET,BF:B=152:A=0:CL=1:GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200:RETURN
305 COLOR3,2:LINE(0,138)-(256,192),PSET,BF:B=152:A=0:CL=1:GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200:RETURN
310 COLOR1,1:LINE(0,138)-(256,192),PSET,BF:B=152:A=0:CL=3:GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200:RETURN
315 COLOR2,4:LINE(0,138)-(256,192),PRESET,BF:LINE(0,138)-(256,192),PSET,B:B=152:A=0:CL=3:GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200:RETURN
320 PCLS4:COLOR2,3:LINE(0,0)-(256,92),PSET,BF:B=16:A=0:CL=1:JK$=" THIS PROGRAM WILL INTRODUCE YOU TO SOME OF THE MOST COMMON ROAD SIGNS USED IN THE UNITED STATES TODAY.":GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200
325 COLOR3,2:LINE(0,98)-(256,192),PSET,BF:A=0:CL=1:JK$=" WHILE NOT ALL SIGNS USED HAVE BEEN INCLUDED, THE SIGNS PRESENTED ARE A GOOD CROSS SECTION OF THOSE IN USE.":GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200
330 PCLS4:DRAW"S16C3BM128,120R10E10U10H10L20G10D10F10R10":PAINT(128,20),3,3:DRAW"C4BM128,116R9E9U10H9L18G9D10F9R9"
335 JK$=" THIS IMPORTANT ROAD SIGN HAS EIGHT SIDES AND IS RED.":GOSUB300
340 JK$=" THIS ROAD SIGN IS THE ONLY ONE WHICH IS THIS SHAPE.":GOSUB305
345 JK$=" IT IS ALWAYS RED AND IS PRINTED WITH LARGE WHITE LETTERS.":GOSUB310
350 DRAW"S12BM62,72C4"+L$(19)+L$(20)+L$(15)+L$(16):JK$=" YOU MUST ALWAYS OBEY A STOP SIGN WHEN YOU SEE IT.":GOSUB315
355 JK$=" YOU MUST COME TO A COMPLETE STOP WHEN YOU SEE IT.":GOSUB300
360 JK$=" YOU MAY THEN PROCEED WHEN IT IS SAFE TO DO SO.":GOSUB310
365 PCLS4
370 DRAW"S24BM128,132C1R2M+16,-20H2L30G2M+16,+20R2"
375 DRAW"S25BM134,126C3M+14,-18HL26GM+14,+18"
380 DRAW"BM134,86M+7,-9HL12GM+7,+9":PAINT(128,18),3,3
385 JK$=" THIS TRIANGULAR SHAPED SIGN IS USED FOR JUST ONE SIGN.":GOSUB305
390 JK$="YIELD":B=42:A=108:CL=3:GOSUB135:JK$=" YIELD MEANS THAT YOU DO NOT HAVE THE RIGHT OF WAY.":GOSUB300
395 JK$=" YIELD MEANS YOU MUST LET THE OTHER CAR GO BEFORE YOU.":GOSUB315
400 JK$=" YOU MAY EVEN HAVE TO STOP BEFORE THE WAY TO GO IS CLEAR.":GOSUB300
405 GOSUB410:GOTO430
410 PCLS4:DRAW"S8C1BM128,6R2M+38,+30DM-38,+30L4M-38,-30UM+38,-30R2"
415 PAINT(128,10),1,1
420 FORI=0TO128STEP4:PUT(0,I)-(256,I+3),A,OR:NEXT
425 DRAW"S8C1BM130,10M+36,+28M-36,+28M-36,-28M+36,-28":RETURN
430 JK$=" WARNING SIGNS ARE DIAMOND SHAPED AND ARE YELLOW AND BLACK":GOSUB300
435 JK$=" SOME WARNING SIGNS HAVE WORDS WHILE SOME OTHERS DO NOT.":GOSUB305
440 JK$=" THEY ARE USED TO SHOW THAT SOME KIND OF DANGER IS AHEAD.":GOSUB315
445 DRAW"S12BM128,30C1R4D10R12D4L12D10L6U10L12U4R12U10R2":PAINT(128,34),1,1:JK$=" THIS SIGN IS SEEN BEFORE COMING TO AN INTERSECTION.":GOSUB310
450 GOSUB410:DRAW"S8BM86,74C1"+L$(19)+L$(12)+L$(15)+L$(23):JK$=" THIS SIGN MEANS THAT YOU SHOULD SLOW DOWN.":GOSUB300
455 JK$=" IT MEANS THAT ROAD CONDITIONS ARE NOT SAFE FOR HIGH SPEEDS":GOSUB315
460 GOSUB410:DRAW"S8BM110,94C1U14EUEUERERER6M-4,-8M+18,+6G12U6L4GLGLGDGD12L6":PAINT(114,90),1,1:JK$=" THIS SIGN MEANS THAT THE ROAD AHEAD CURVES TO THE RIGHT.":GOSUB300
465 JK$=" YOU SHOULD REDUCE YOUR SPEED BEFORE REACHING THE CURVE.":GOSUB305
470 GOSUB410:DRAW"S8BM146,94C1U14HUHUHLHLHL6M+4,-8M-18,+6F12U6R4FRFRFDFD12R6":PAINT(142,90),1,1:JK$=" THIS SIGN MEANS THAT THE ROAD AHEAD CURVES TO THE LEFT.":GOSUB300
475 JK$=" AGAIN YOU SHOULD REDUCE YOUR SPEED BEFORE THE CURVE.":GOSUB305
480 GOSUB410:DRAW"BM108,98C1NU32RNU32RNU32BR4U4BU4U4RD4BD4D4BR4NU12RU12NH4LH4BU4U4BU4U4LD4BD4D4BU12BR6D12F12D8LU8H12U12LD12F12D8BL4U4BU4U2LD2BD4D4"
485 JK$=" THIS SYMBOL MEANS THAT A RIGHT LANE DROP LIES AHEAD.":GOSUB315
490 JK$=" TRAFFIC TO THE RIGHT MUST MERGE WITH THE LEFT LANE.":GOSUB300
495 GOSUB410:DRAW"BM152,98C1NU32LNU32LNU32BL4U4BU4U4LD4BD4D4BL4NU12LU12NE4RE4BU4U4BU4U4RD4BD4D4BU12BL6D12G12D8RU8E12U12RD12G12D8BR4U4BU4U2RD2BD4D4"
500 JK$=" THIS SYMBOL MEANS THAT A LEFT LANE DROP LIES AHEAD.":GOSUB310
505 JK$=" TRAFFIC TO THE LEFT MUST MERGE WITH THE RIGHT LANE.":GOSUB315
510 PCLS4
515 CIRCLE(128,58),68,1,.9:PAINT(128,10),1,1
520 FORI=0TO128STEP4:PUT(0,I)-(256,I+3),A,OR:NEXT
525 CIRCLE(128,58),62,1,.9:CIRCLE(130,58),62,1,.9
530 DRAW"S4BM128,52C1NE36NH36BD16NF36NG36BU8BR8NE36NF36BL16NH36NG36":PAINT(128,58),1,1
535 DRAW"S12BM80,70U8R4D4L4F4RNH4BR23U8R4D4L4F4RNH4"
540 JK$=" THIS SIGN MEANS A RAILROAD CROSSING IS JUST AHEAD.":GOSUB305
545 JK$=" YOU MUST STOP AND LOOK BOTH WAYS BEFORE CROSSING.":GOSUB310
550 JK$=" YOU MUST NEVER CROSS THE TRACKS IF THE GATES ARE DOWN.":GOSUB300
555 GOSUB560:GOTO565
560 PCLS4:DRAW"S8C1BM128,0R30F2D64G2L60H2U64E2R30BD2R28F2D60G2L56H2U60E2R28":RETURN
565 JK$=" REGULATORY SIGNS ALWAYS HAVE FOUR SIDES AND ARE WHITE.":GOSUB300
570 DRAW"BM78,100S8C1"+L$(19)+L$(16)+L$(5)+L$(5)+L$(4):DRAW"BM78,126"+L$(12)+L$(9)+L$(13)+L$(9)+L$(20):FORI=78TO132STEP54:DRAW"S4BM"+STR$(I)+",70R40E4U20H4L36U20R36U4L40D28R36F4D12G4L36D4":NEXT
575 PAINT(80,68),1,1:PAINT(134,68),1,1:JK$=" SPEED LIMIT SIGNS ARE EXAMPLES OF REGULATORY SIGNS.":GOSUB315
580 JK$=" THEY SHOW THE FASTEST SPEED WHICH YOU MAY TRAVEL.":GOSUB300
585 GOSUB560:DRAW"BM106,40S8C1"+L$(14)+L$(15):DRAW"BM88,74"+L$(12)+L$(5)+L$(6)+L$(20):DRAW"BM78,110"+L$(20)+L$(21)+L$(18)+L$(14)
590 JK$=" OTHER TIMES THEY MAY HAVE WRITTEN DIRECTIONS.":GOSUB315
595 PCLS4:COLOR2,3:LINE(0,0)-(256,92),PSET,BF:B=16:A=0:CL=1:JK$=" BY KNOWING THE SHAPES OF TRAFFIC SIGNS, YOU CAN SAVE TIME IN KNOWING THE ROAD RULES IN JUST A SPLIT SECOND.":GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200
600 COLOR3,2:LINE(0,98)-(256,192),PSET,BF:A=0:CL=1:JK$=" INFORMATION SUCH AS THIS COULD SAVE YOUR LIFE, OR THE LIFE OF A LOVED ONE. KNOWLEDGE IS SAFETY WHEN DRIVING.":GOSUB135:PCOPY3TO1:PCOPY4TO2:GOSUB200
605 IFINKEY$<>CHR$(13)THEN605
610 RUN
