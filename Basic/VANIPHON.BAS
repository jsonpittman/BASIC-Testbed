1 REM  *************************
2 REM  *                       *
3 REM  *       VANIPHON        *
4 REM  *                       *
5 REM  *          BY           *
6 REM  *                       *
7 REM  *  ROBERT C. LAKE, CDP  *
8 REM  *                       *
9 REM  *                       *
10 REM *      (C) 1985         *
11 REM *                       *
12 REM *************************
13 CLS:PRINT@202,"PERSONALIZED"
14 PRINT@266,"PHONE NUMBER"
15 PRINT @334,"AID"
16 PRINT@460,"(C) 1985"
17 FOR J=1TO2000:NEXTJ
18 CLS
19 PRINT "THIS PROGRAM IS DESIGNED"
20 PRINT "TO SEARCH FOR A PERSONALIZED"
21 PRINT "MNEMONIC FOR YOUR PHONE"
22 PRINT "NUMBER.  IT WILL DISPLAY ALL"
23 PRINT "THE POSSIBLE COMBINATIONS"
24 PRINT "OF LETTERS FOR THE PHONE "
25 PRINT "NUMBER YOU PROVIDE."
26 PRINT:PRINT"TO HALT DISPLAY, PUSH"
27 PRINT"THE SPACE BAR.  TO RESTART"
28 PRINT "THE DISPLAY, PUSH IT AGAIN."
29 PRINT:PRINT"DO YOU WANT THE DISPLAY"
30 PRINT"SENT TO THE pRINTER OR"
31 PRINT"TO THE sCREEN";
32 A$=INKEY$:IF A$="S"ORA$="s" THEN DEV=0 ELSE IF A$="P" OR A$="p" THEN DEV=-2 ELSE 32
33 IF LEFT$(A$,1)="P" OR LEFT$(A$,1)="p" THEN DEV=-2 ELSE DEV=0
34 CLS:PRINT"GIVE THE 7-DIGIT PHONE"
35 PRINT "NUMBER YOU WANT TRANSLATED."
36 PRINT "DO NOT INCLUDE A DASH, ONLY"
37 PRINT "THE SEVEN DIGITS.
38 DIM D$(10,3)
39 M(0)=1
40 M(1)=1
41 FOR J=2 TO 9:M(J)=3:NEXTJ
42 FOR J=0TO9
43 FOR K=1TO3
44 READ D$(J,K)
45 NEXT K
46 NEXT J
47 DATA "Z","*","*","1","*","*"
48 DATA "A","B","C","D"
49 DATA "E","F","G","H","I","J"
50 DATA "K","L","M","N","O","P"
51 DATA "R","S","T","U","V","W"
52 DATA "X","Y"
53 INPUT N$
54 N1=0
55 FOR J=1TO7
56 N(J)=VAL(MID$(N$,J,1))
57 IF N(J)=0 OR N(J) =1 THEN N1=N1+1
58 NEXT J
59 SUM=INT(3^(7-N1))
60 PRINT:PRINT SUM;" COMBINATIONS "
61 PRINT "WILL BE DISPLAYED":PRINT
62 PRINT#DEV:PRINT #DEV,"MNEMONICS FOR: ";MID$(N$,1,3);"-";MID$(N$,4,4):PRINT #DEV
63 FOR J1=1TO3
64 FOR J2=1TO3
65 FOR J3=1TO3
66 FOR J4=1TO3
67 FOR J5=1TO3
68 FOR J6=1TO3
69 FOR J7=1TO3
70 IF D$(N(1),J1)="*" OR D$(N(2),J2)="*" OR D$(N(3),J3)="*" THEN 76
71 IF D$(N(4),J4)="*" OR D$(N(5),J5)="*" OR D$(N(6),J6)="*" THEN 76
72 IF D$(N(7),J7)="*" OR D$(N(8),J8)="*" OR D$(N(9),J9)="*" THEN 76
73 PRINT #DEV,D$(N(1),J1);D$(N(2),J2);D$(N(3),J3);D$(N(4),J4);D$(N(5),J5);D$(N(6),J6);D$(N(7),J7);" ";
74 A$=INKEY$:IFA$=" "THEN 75 ELSE 76
75 A$=INKEY$:IFA$=" "THEN 76 ELSE 75
76 NEXTJ7:NEXTJ6:NEXTJ5:NEXTJ4:NEXTJ3:NEXTJ2:NEXTJ1
77 PRINT #DEV
