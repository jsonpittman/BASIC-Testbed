0 'COPYRIGHT 1990 FALSOFT, INC.
10 CLEAR1000,&H7000-1:PCLEAR8:FILES 1,310:CLS
20 IF PEEK(&H7040)<>142 THEN LOADM"MANDEL"
30 DIM NM$(20),TI$(20),XL,YL,XD,YD,XS,YS,JA,JB,V,H,X1,Y1,X2,Y2,LX,LY,DX,DY,AJ,BJ,X,A,S,B,C,D,J,CT,NU,CO
40 DIM GE(11),CL(11),NU(11),A$(10),A$,B$,C$,D$,DA$,CO$,XL$,XD$,YL$,YD$,JA$,JB$
50 GOSUB 1700:J=0
60 PRINT" MANDELBROT/JULIA PROGRAM":GOSUB1500
70 PMODE3,1:PCLS:GET(0,0)-(55,7),CL
80 PRINT"MENU SELECTION -":PRINT
90 PRINT" 1)INPUT CURRENT DATA":PRINT
100 PRINT" 2)LOAD PICTURE FROM DISK":PRINT
110 A$=INKEY$:IF A$=""THEN 110 ELSE ON VAL(A$) GOTO 120,700
120 CLS:PRINT:INPUT"XLOCATION";XL
130 INPUT"XDISTANCE";XD:IF XD<=0 THEN 130
140 PRINT:INPUT"YLOCATION";YL
150 INPUT"YDISTANCE";YD:IF YD<=0 THEN 150
160 PRINT:PRINT"<M>ANDELBROT OR <J>ULIA SET"
170 A$=INKEY$:IF A$="M" THEN J=0:GOTO 210: ELSE IF A$="J" THEN 180 ELSE 170
180 IF A$="J" THEN J=1
190 PRINT:INPUT"JA LOCATION";JA
200 INPUT"JB LOCATION";JB
210 POKE &H703A,J:GOSUB1200:PMODE3,1:PCLS:SCREEN1,0
220 EXEC &H704A:SOUND 50,4:EXEC &HADFB
300 CLS:PRINT"OPTIONS -"
310 PRINT" 1)SAVE TO DISK"
320 PRINT" 2)LOAD FROM DISK
330 PRINT" 3)SAVE TO MEMORY AND SCALE
340 PRINT" 4)SCALE PICTURE
350 PRINT" 5)LOAD FROM MEMORY AND SCALE
360 PRINT" 6)RECOLOR",," 7)END PROGRAM"
370 A$=INKEY$:IF A$=""THEN 370
380 ON VAL(A$) GOTO 900,700,500,600,1100,1800,400
390 GOTO370
400 PCLEAR4:CLEAR200,&H7FFF:NEW
500 FOR N=1 TO 4:PCOPY N TO N+4:NEXT
510 LX=XL:DX=XD:LY=YL:DY=YD:AJ=JA:BJ=JB
600 PMODE3,1:SCREEN1,0
610 EXEC &H751A
620 H1=PEEK(&H7500)*4:V1=PEEK(&H7501):V1=192-V1
630 X1=XL+H1*XS:Y1=YL+V1*YS
640 H2=(PEEK(&H7502)+1)*4:V2=PEEK(&H7503):V2=191-V2
650 X2=XL+H2*XS:Y2=YL+V2*YS
660 XD=X2-X1:YD=Y1-Y2
670 XL=X1:YL=Y2
680 GOTO 210
700 CLS:PRINT"DISK DIRECTORY -"
710 IF S>0 THEN GOSUB 1750
720 IF S=0 THEN GOSUB 1700
730 PRINT
740 INPUT"ENTER DESIRED TITLE NUMBER";A:IF A>S THEN 740
750 PMODE3,1:PCLS:SCREEN1,0:POKE65494,0:LOADM TI$(A)
760 OPEN"D",#1,"MJDATA",307
770 FIELD#1,1 AS J$,12 AS A$,12 AS B$,5 AS XL$,5 AS XD$,5 AS YL$,5 AS YD$,5 AS JA$,5 AS JB$,252 AS C$
780 GET#1,A
790 J=VAL(J$):POKE&H703A,J
800 NM$=A$:TI$=B$
810 XL=CVN(XL$):XD=CVN(XD$):XS=XD/128
820 YL=CVN(YL$):YD=CVN(YD$):YS=YD/192
825 JA=CVN(JA$):JB=CVN(JB$)
830 CO$=C$
840 CLOSE#1:GOSUB1650:GOTO 610
900 PRINT:LINEINPUT"PICTURE NAME - ";NM$:IF LEN(NM$)>12 THEN PRINT"TITLE IS TOO LONG":GOTO900
910 S=S+1:TI$="MANDEL"+RIGHT$(STR$(S),2)+".PIX"
920 VERIFYON:SAVEM TI$,&HE00,&H25FF,&HE00
930 PRINT"GETTING COLORS -"
940 GOSUB 1600
950 OPEN"D",#1,"MJDATA",307
960 FIELD#1,1 AS J$,12 AS A$,12 AS B$,5 AS XL$,5 AS XD$,5 AS YL$,5 AS YD$,5 AS JA$,5 AS JB$,252 AS C$
970 LSET J$=RIGHT$(STR$(J),1)
980 LSET A$=NM$
990 LSET B$=TI$
1000 LSET XL$=MKN$(XL)
1010 LSET XD$=MKN$(XD)
1020 LSET YL$=MKN$(YL)
1030 LSET YD$=MKN$(YD)
1035 LSET JA$=MKN$(JA)
1036 LSET JB$=MKN$(JB)
1040 LSET C$=CO$
1050 PUT#1,S:CLOSE#1
1060 NM$(S)=NM$:TI$(S)=TI$
1070 GOTO 300
1100 PMODE3,1:SCREEN1,1
1110 FOR N=5 TO 8:PCOPY N TO N-4:NEXT
1120 XL=LX:XD=DX:YL=LY:YD=DY:JA=AJ:JB=BJ
1130 GOTO600
1200 XS=XD/128:V=VARPTR(XS)
1210 FOR N=0 TO 4:POKE&H7001+N,PEEK(V+N):NEXT
1220 YS=YD/192:V=VARPTR(YS)
1230 FOR N=0 TO 4:POKE&H701B+N,PEEK(V+N):NEXT
1240 V=VARPTR(XL)
1250 FOR N=0 TO 4:POKE&H7006+N,PEEK(V+N):NEXT
1260 V=VARPTR(YL)
1270 FOR N=0 TO 4:POKE&H7020+N,PEEK(V+N):NEXT
1280 IF J=0 THEN 1330
1290 V=VARPTR(JA)
1300 FOR N=0 TO 4:POKE&H703B+N,PEEK(V+N):NEXT
1310 V=VARPTR(JB)
1320 FOR N=0 TO 4:POKE&H7040+N,PEEK(V+N):NEXT
1330 CLS
1340 PRINT"CURRENT DATA -":PRINT
1350 PRINT" XLOCATION :";XL
1360 PRINT" XDISTANCE :";XD:PRINT
1370 PRINT" YLOCATION :";YL
1380 PRINT" YDISTANCE :";YD:PRINT:IF J=0 THEN 1410
1390 PRINT" JA LOCATION :";JA
1400 PRINT" JB LOCATION :";JB:PRINT
1410 PRINT"PRESS ANY KEY TO BEGIN -"
1420 EXEC &HADFB:RETURN
1500 CO=&H72C9
1510 FOR X=0 TO 10
1520 POKE CO+X,0:NEXT
1530 FOR X=11 TO 250
1540 POKE CO+X,(X AND 3)*&H55
1550 NEXT
1560 POKE CO+251,&HFF:RETURN
1600 CO$="":CO=&H72C9
1610 FOR X=0 TO 251
1620 C=PEEK(CO+X)/&H55+1
1630 CO$=CO$+RIGHT$(STR$(C),1)
1640 NEXT:RETURN
1650 CO=&H72C9:FOR X=1 TO 252
1660 C=VAL(MID$(CO$,X,1))
1670 POKE CO+X-1,C
1680 NEXT:RETURN
1700 OPEN"D",#1,"MJDATA",307
1705 IF LOF(1)=0 THEN CLOSE#1:S=0:RETURN
1710 FIELD#1,1 AS J$,12 AS A$,12 AS B$,5 AS XL$,5 AS XD$,5 AS YL$,5 AS YD$,5 AS JA$,5 AS JB$,252 AS C$
1720 FOR S=1 TO LOF(1):GET #1,S
1730 NM$(S)=A$:TI$(S)=B$
1740 NEXT:CLOSE#1:S=S-1:RETURN
1750 FOR X=1 TO S
1760 PRINTUSING"##>";X;:PRINT NM$(X),
1770 NEXT:RETURN
1800 'RECOLOR DISPLAY
1810 CLS:PRINT"RECOLOR"
1820 PRINT"1> PRINT TO SCREEN","2> TO PRINTER",,"3> SKIP TO RECOLOR"
1830 PRINT:PRINT"WHICH ONE -"
1840 A$=INKEY$:IF A$=""THEN 1840
1850 IF A$="1" THEN 1890
1860 IF A$="2" THEN 2800
1870 IF A$="3" THEN 2110
1880 GOTO 1840
1890 CT=&H719D:CO=&H72C9:DA$="-"
1910 CLS:C=0:FOR X=0 TO 63:IF PEEK(CT+X)=0 THEN 1950
1920 PRINTUSING"###";X;:PRINTDA$;
1930 PRINTUSING"# ";PEEK(CO+X)/&H55+1;
1940 C=C+1:IF C=5 THEN C=0:PRINT
1950 NEXT:PRINT:PRINT"PRESS ANY KEY TO CONTINUE";:EXEC&HADFB:C=0
1960 CLS:C=0:FOR X=64 TO 127:IF PEEK(CT+X)=0 THEN 2000
1970 PRINTUSING"###";X;:PRINTDA$;
1980 PRINTUSING"# ";PEEK(CO+X)/&H55+1;
1990 C=C+1:IF C=5 THEN C=0:PRINT
2000 NEXT:PRINT:PRINT"PRESS ANY KEY TO CONTINUE";:EXEC&HADFB:C=0
2010 CLS:C=0:FOR X=128 TO 191:IF PEEK(CT+X)=0 THEN 2050
2020 PRINTUSING"###";X;:PRINTDA$;
2030 PRINTUSING"# ";PEEK(CO+X)/&H55+1;
2040 C=C+1:IF C=5 THEN C=0:PRINT
2050 NEXT:PRINT:PRINT"PRESS ANY KEY TO CONTINUE";:EXEC&HADFB:C=0
2060 CLS:C=0:FOR X=192 TO 255:IF PEEK(CT+X)=0 THEN 2100
2070 PRINTUSING"###";X;:PRINT DA$;
2080 PRINTUSING"# ";PEEK(CO+X)/&H55+1;
2090 C=C+1:IF C=5 THEN C=0:PRINT
2100 NEXT:PRINT
2110 PRINT"ENTER 3-DIGIT COUNT (000 - 250)"
2120 PRINT"THEN ENTER 1-DIGIT COLOR (1 - 4)";
2130 PRINT"PRESS ANY KEY TO BEGIN";
2140 EXEC &HADFB
2200 PMODE3,1:SCREEN1,0:GOSUB2600:GET(0,0)-(55,7),GE
2210 POKE 135,0:A$="":B$="":C$="":D$=""
2220 PUT(0,0)-(55,7),CL,PSET
2230 A$=INKEY$:IF A$=""THEN 2230
2240 IF ASC(A$)<48 OR ASC(A$)>50 THEN 2230
2250 A=VAL(A$):DRAW"BM1,1;XA$(A);"
2260 B$=INKEY$:IF B$=""THEN 2260
2270 IF B$=CHR$(8) THEN DRAW"C1;BM1,1;XA$(A);C4":GOTO 2230
2280 IF ASC(B$)<48 OR ASC(B$)>58 THEN 2260
2290 IF A=2 AND ASC(B$)>53 THEN  2260
2300 B=VAL(B$):DRAW"BM10,1;XA$(B);"
2310 C$=INKEY$:IF C$=""THEN 2310
2320 IF C$=CHR$(8) THEN DRAW"C1;BM10,1;XA$(B);C4":GOTO2260
2330 IF ASC(C$)<48 OR ASC(C$)>57 THEN 2310
2340 C=VAL(C$)
2350 NU=100*A+10*B+C:IF NU>255 THEN 2310
2360 DRAW"BM20,1;XA$(C);"
2370 POKE &H7A57,NU
2380 DRAW"BM30,1;XDA$;"
2390 D$=INKEY$:IF D$=""THEN 2390
2400 IF D$=CHR$(8) THEN DRAW"C1;BM20,1;XA$(C);C4":GOTO 2310
2410 D=VAL(D$):IF D<1 OR D>4 THEN 2390
2420 DRAW"BM40,1;XA$(D);"
2430 GET(0,0)-(55,7),NU:PUT(0,0)-(55,7),GE,PSET
2440 POKE &HB5,(D-1)*&H55:POKE CO+NU,(D-1)*&H55
2450 EXEC &H7A00
2460 PUT(0,0)-(55,7),NU,PSET
2470 A$=INKEY$:IF A$=""THEN 2470
2480 IF A$=CHR$(94) THEN PUT(0,0)-(55,7),GE,PSET:GOTO 300
2490 IF A$=CHR$(13) THEN 2210
2500 GOTO 2470
2600 A$(0)="R6D6L6U5"
2610 A$(1)="BR3NG3D6NL3R3"
2620 A$(2)="R6D3L6D3R6"
2630 A$(3)="R6D3NL4D3L6"
2640 A$(4)="D3R6NU3D3"
2650 A$(5)="NR6D3R6D3L6"
2660 A$(6)="D6R6U3L6"
2670 A$(7)="R6D6"
2680 A$(8)="NR6D3NR6D3R6U6"
2690 A$(9)="ND3R6D3NL6D3"
2700 DA$="BD3BR1R3":RETURN
2800 CO=&H72C9:CT=&H719D:DA$="-"
2810 C=0:FOR X=0 TO 252:IF PEEK(CT+X)=0 THEN 2850
2820 PRINT#-2,USING"###";X;:PRINT#-2,DA$;
2830 PRINT#-2,USING"# ";PEEK(CO+X)/&H55+1;
2840 C=C+1:IF C=13 THEN C=0:PRINT#-2
2850 NEXT:GOTO 2110
