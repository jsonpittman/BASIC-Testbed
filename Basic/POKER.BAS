1 CLS:POKE&HFFD9,0:PALETTERGB:PALETTE0,0:PALETTE2,11:PALETTE14,3:HSCREEN2:CLS1'POKER GAME BY R. L. BRIMNER
2 ONBRKGOTO5999:ONERRGOTO5999
3 POKE278,PEEK(274):POKE281,PEEK(275)
5 DIMC(4,15),H$(5),H(5),N$(11),S$(10):C(1,15)=8:C(2,15)=8:C(3,15)=8
8 F$="SCORES"
10 HBUFF1,1155:HBUFF2,6560:HBUFF3,8:HGET(0,0)-(160,80),2:H$(1)="4E":H$(2)="4D":H$(3)="4C":H$(4)="4B":H$(5)="4A":GOSUB5914
13 GOSUB7000:POKE&HFFD8,0:OPEN"I",#1,F$:FORQ=1TO9:INPUT#1,N$(Q),S$(Q):NEXT:CLOSE#1:POKE&HFFD9,0:IFS$(9)=""THENX$="20"ELSEX$=S$(9)
14 HCOLOR3:HPRINT(10,0),"FIVE CARD DRAW POKER":HLINE(76,10)-(244,10),PSET,B:X$="Can you top $"+X$+"?":HCOLOR1:HPRINT(12,10),X$:SOUND50,1:SOUND1,3:GOSUB5700:FORQ=1TO4000:NEXT:GOSUB5602:Z=108:PALETTE0,1:CLS1
15 HB$="":TM=1:SW=1:AB=1:H9=-1:N$="":HH=20:D=20
20 IFGB=0THENGOSUB5601:HCOLOR3:HPRINT(10,10),"Press <R> for rules!
22 GOSUB5200:D$="":RM=2:GOSUB5400:GOSUB5260:GOSUB1000:GOSUB1200
23 'BET LOOP
24 B$=INKEY$:IFB$=""THENIFD>0THENPR$="Place your bet":GOTO5210ELSE24ELSESOUND100,1:GOSUB5270:IFB$="R"THENH9=H9*-1:GOSUB1100:GOTO24ELSEIFB$="S"THENH9=H9*-1:GOSUB2000:GOTO24ELSEIFB$="E"THENH9=1:GOSUB2000:GOTO24
25 IFB$=" "THENB$=HB$ELSEHB$=B$
26 IFB$="A"THENB=D ELSEIFB$="H"THENB=INT(D/2)ELSEIFB$="Q"THENB=INT(D/4)ELSEIFB$="T"THENB=INT(D*3/4)ELSEB=VAL(B$):IFB$<"0"ORB$>"9"THENELSEIFB=0THENB=10
27 IFB=0ORB>D THENB$="":GOTO24
28 AB=B:BT$=STR$(B):L=LEN(BT$)+1:BT$=RIGHT$(BT$,L-2):D=D-B:PR$="You bet $"+BT$:GOSUB5212:GOSUB5200:IFB=>INT(D/4)THENRM=1:GOSUB5400
29 IFGB=0THENGOSUB5918ELSEGOSUB5602
30 FORZ=1TO5:GOSUB5890:NEXT:GOSUB5914:QT=0:QS=1:H9=-1:GOSUB1000
31 GOTO100
97 'DRAW LOOP
98 QT=0:IFH(1)+H(2)+H(3)+H(4)+H(5)=-5THENQS=1ELSEQS=0
100 B$=INKEY$:IFQT=150ANDQS=0THEN5990ELSEIFB$=""THENQT=QT+1:GOTO5220
102 GOSUB5270:IFB$=CHR$(32)ORB$=CHR$(13)ORB$="N"THEN200ELSEIFB$="S"THENH9=H9*-1:GOSUB2000:GOTO100ELSEIFB$="R"THENH9=H9*-1:GOSUB1300:GOTO100ELSEIFB$="E"THENH9=1:GOSUB2000:GOTO100ELSEIFB$="A"THENQS=0:GOSUB5602:GOTO98ELSEZ=VAL(B$):IFZ<1ORZ>5THEN98
104 SOUND80,1:H(Z)=H(Z)*-1:Y=14:X=24+(Z-1)*56
106 QS=0:IFH(Z)=1THENGOSUB5160:GOTO98ELSEGOSUB5140:GOTO98
200 GOSUB5601:FORZ=1TO5:IFH(Z)=1THENY=14:X=24+(Z-1)*56:H(Z)=0:GOSUB5300:C(S,R)=8:C(0,R)=C(0,R)-1:C(S,1)=C(S,14):C(0,1)=C(0,14):IFR<15THENC(S,0)=C(S,0)-1ELSEELSE210
205 GOSUB5890:GOSUB5140
210 NEXT:IFGB=0THENGB=1:GOSUB5601
299 'DETERMINE THE RETURN
300 P=0:JK=C(0,15):AC=C(0,14):ST=0:HJ=JK:FL=0:K3=0:K2=0:W=0:FORS=1TO4:IFC(S,0)>4-JK THENFL=1:S=4
301 NEXT:IFFL=0THEN310ELSEFL=0
302 FORR=10TO1STEP-1:GOSUB5500
304 IFFL=1THENIFR>9THENP=15:GOTO480ELSEP=16:GOTO480
306 NEXT:P=19:GOTO480
310 FORR=14TO2STEP-1:IFC(0,R)>1THENST=1
313 IFC(0,R)=>4-HJ THENP=17:GOTO480
314 IFC(0,R)=3-HJ THENK3=1:HJ=0:IFK2>0THEN470ELSE324
317 IFC(0,R)=2THENIFK2=1THENK2=2:GOTO330ELSEK2=1:IFK3=1THEN470
320 IFST=0THENIFR<11THENGOSUB5500:IFFL=1THEN326
324 NEXT:IFST=0THENGOSUB5500
325 IFFL<>1THEN330
326 P=20:GOTO480
330 IFK3=1THENP=21:GOTO480
332 IFK2=2THENP=22:GOTO480
334 IFAC=2ANDK2=1THENELSEIFAC=1ANDJK=1THENELSE340
335 P=23:GOTO480
340 IFD>0THENHCOLOR4:HPRINT(0,11),"Try Again!":SOUND80,1:SOUND1,4:GOTO480ELSE480
470 P=18
480 IFP>0THENHCOLOR5:GOSUB1004:FORQ=1TO13:K=RND(50):SOUNDK,1:NEXT:SOUND1,5:D=D+W:IFHH<D THENHH=D ELSEELSE495
490 GOSUB5200
495 GOSUB5700:IFD>0THENRM=3:GOSUB5400:AB=1:GOTO24ELSEGM=1:Q=0:H9=1:SOUND80,1:SOUND1,4:B$="":GOTO2000
999 'SHOW THE RETURNS
1000 X=0:GOSUB5250:GOSUB1099:FORP=15TO23:GOSUB1004:NEXT:RETURN
1004 X$=" ":ONP-14GOSUB1010,1020,1030,1040,1050,1060,1070,1080,1090:RETURN
1010 X$="$":W=100*AB:L=LEN(STR$(W)):PR$="Royal Flush":GOTO1093
1020 W=40*AB:PR$="St. Flush":GOTO1093
1030 W=20*AB:PR$="4 of a Kind":GOTO1093
1040 W=10*AB:PR$="Full House":GOTO1093
1050 W=5*AB:PR$="Flush":GOTO1093
1060 W=4*AB:PR$="Straight":GOTO1093
1070 W=3*AB:PR$="3 of a Kind":GOTO1093
1080 W=2*AB:PR$="2 Pair":GOTO1093
1090 W=AB:PR$="2 Aces
1093 PN=L-LEN(STR$(W)):PR$=X$+RIGHT$(STR$(W),LEN(STR$(W))-1)+" "+PR$:HPRINT(PN,P),PR$:RETURN
1099 HCOLOR2:X$="Returns on $"+RIGHT$(STR$(AB),LEN(STR$(AB))-1)+":":HPRINT(0,14),X$:RETURN
1100 IFH9=1THENX=160:GOSUB5250:HCOLOR7:HPRINT(21,14),"<Keys> for betting:"ELSEGOSUB1200:GOTO24 'SHOW BET RULES
1105 HPRINT(21,15),"<1-9,0> = $1-$9,$10
1110 HPRINT(21,16),"<Q>=1/4 of $":HPRINT(32,16),D
1115 HPRINT(21,17),"<H>=2/4 of $":HPRINT(32,17),D
1120 HPRINT(21,18),"<T>=3/4 of $":HPRINT(32,18),D
1125 HPRINT(21,19),"<A>=4/4 of $":HPRINT(32,19),D
1130 HPRINT(21,20),"<Spacebar> will re-":HPRINT(22,21),"peat your last bet"
1135 HPRINT(21,22),"<S> to see scores":HPRINT(21,23),"<E> to end game
1199 RETURN
1200 X=160:GOSUB5250:HCOLOR2:HPRINT(21,14),"Definitions:":HPRINT(21,15),"(AKQJ10 of a suit) 'SHOW DEFINITIONS
1210 HPRINT(21,16),"(Run of 5 in suit)
1220 HPRINT(21,18),"(3 of kind & pair)
1230 HPRINT(21,19),"(All of same suit)
1240 HPRINT(21,20),"(Run of 5 any suit)
1250 RETURN
1300 IFH9=1THENX=160:GOSUB5250:HCOLOR6:HPRINT(21,14),"<Keys> for drawing:"ELSEGOSUB1200:GOTO100 'SHOW DRAW RULES
1305 HPRINT(21,16),"<1-5> will discard,
1310 HPRINT(22,17),"or will recall, an":HPRINT(22,18),"erroneous discard.
1315 HPRINT(21,19),"<A> Discards all 5!
1320 HPRINT(21,21),"<Spacebar> will re-":HPRINT(22,22),"place cards and/or":HPRINT(22,23),"continue the game."
1399 RETURN
1999 'SHOW SCORES
2000 IFH9=-1THENX=160:GOSUB5250:GOSUB1200:RETURNELSEGOSUB2002:IFB$="S"THENRETURNELSEIFB$="E"THEN2100ELSE2050
2002 X=160:GOSUB5250:HCOLOR9:IFN$>""THENFORQ=9TO1STEP-1:IFHH>VAL(S$(Q))THENS$(Q+1)=S$(Q):S$(Q)=STR$(HH):N$(Q+1)=N$(Q):N$(Q)=N$ELSEQ=1ELSE2005
2003 NEXT
2005 HPRINT(21,14),"Nine high scores:
2010 FORQ=1TO9:Q$=RIGHT$(STR$(Q),2)+LEFT$(" "+N$(Q)+"        ",9)+S$(Q):HPRINT(20,14+Q),Q$:NEXT
2011 RETURN
2050 IFHH>VAL(S$(9))ANDHH>20THENELSE2067
2052 SOUND80,1:L=0:HCOLOR1:GOSUB5204:HPRINT(21,12),"Your name?"
2055 HLINE(257,103)-(263,103),PSET,B:HGET(257,103)-(263,103),3
2060 M$=INKEY$:IFM$=""THENHPUT(257+L*8,103)-(263+L*8,103),3,PSET:GOTO2060ELSEHPUT(257+L*8,103)-(263+L*8,103),2,PSET:IFM$=CHR$(8)THEN2068ELSEIFM$>CHR$(31)ANDL<8THENHPRINT(32+L,12),M$:N$=N$+M$:L=LEN(N$):GOTO2070
2065 POKE282,255:IFM$<>CHR$(13)THENN$="":GOTO2050ELSEIFN$>""THENGOSUB2002:GOTO2120
2067 GOSUB5204:IFB$="E"THEN2142ELSE2140
2068 HPUT(249+L*8,96)-(263+L*8,103),2,PSET:L=L-1:IFL<1THENN$="":GOTO2050ELSEN$=LEFT$(N$,L):GOTO2060
2070 IFL=1THENPOKE282,0:GOTO2060ELSE2060
2100 SOUND80,1:HPRINT(5,11),"Do you want to END GAME (Y/N)?"
2110 A$=INKEY$:IFA$=""THEN2110ELSEIFA$="Y"THEN2050ELSEGOSUB5270:GOSUB5200:RETURN
2120 GOSUB5270:HPRINT(13,11),"Saving scores!"
2125 POKE&HFFD8,0:OPEN"O",#1,F$
2130 FORQ=1TO9:WRITE#1,N$(Q),S$(Q):NEXT:CLOSE#1:IFH9=0THENCLS:PRINT"SCORES file established/cleared!":STOPELSEPOKE&HFFD9,0:GOTO2140
2140 SOUND80,1:GOSUB5270:HPRINT(6,11),"Press <ENTER> to play again"
2141 A$=INKEY$:IFA$<>CHR$(13)THEN2141
2142 GOSUB5270:GOSUB5602:Z=108:GOTO15
5000 'SUB ROUTINES
5140 IFH9=0THENHCOLOR14:HLINE(X,Y)-(X+40,Y+54),PSET,B:HCOLOR4:HLINE(X+1,Y+1)-(X+39,Y+53),PSET,BF:HGET(X,Y)-(X+40,Y+54),1:GOTO5150
5145 HPUT(X,Y)-(X+40,Y+54),1,PSET
5150 GOSUB5300:IFR=15THENHDRAW"S4C14":HX=X+1:HY=Y+8:GOSUB6000:GOSUB6010:RETURN ELSEHDRAW"S8C14":HX=X+3:HY=Y+15:GOSUB5152:IFS>2THENHCOLOR3ELSEHCOLOR2
5151 HX=HX+1:GOSUB5152:HY=HY+1:GOSUB5152:GOTO5155
5152 GOSUB6020:ONR-1 GOSUB6025,6030,6035,6040,6045,6050,6055,6060,6065,6080,6100,6085,6070:RETURN
5155 HX=X+8:HY=Y+35:GOSUB6020:ONS GOSUB6120,6125,6110,6115:RETURN
5160 HPUT(X,Y)-(X+40,Y+54),2,PSET:H(Z)=1:RETURN
5200 HCOLOR4:GOSUB5204:IFD>0THEND$=STR$(D):PR$="You  have $"+RIGHT$(D$,LEN(D$)-1)ELSEPR$="You are broke!"
5202 HPRINT(21,12),PR$:H$=STR$(HH):X$="Your high $"+RIGHT$(H$,LEN(H$)-1):HPUT(160,104)-(320,111),2,PSET:IFHH>20THENHPRINT(21,13),X$
5203 RETURN
5204 HPUT(160,96)-(320,103),2,PSET:RETURN
5210 HPUT(0,96)-(112,103),2,PSET:TM=TM*-1:IFTM=1THENELSEHCOLOR4:HPRINT(0,12),PR$:FORQ=1TO360:NEXT
5211 GOTO24
5212 HPUT(0,96)-(112,103),2,PSET:HCOLOR4:HPRINT(0,12),PR$:RETURN
5220 IFQS=1THENHPUT(0,88)-(112,95),2,PSET:TM=TM*-1:IFTM=1THENELSEHCOLOR4:HPRINT(0,11),"Draw?":FORQ=1TO360:NEXT
5221 GOTO100
5250 HPUT(X,112)-(X+160,192),2,PSET:RETURN
5260 HPUT(0,Z+4)-(320,Z+44),2,PSET:HPUT(0,Z+45)-(320,Z+85),2,PSET:RETURN
5270 HPUT(0,88)-(320,95),2,PSET:RETURN
5300 S=VAL("&H"+LEFT$(H$(Z),1)):R=VAL("&H"+RIGHT$(H$(Z),1)):RETURN
5400 HCOLOR4:IFRND(RM)=1THENC(4,15)=0:X$="Joker is IN"ELSEC(4,15)=8:X$="Joker is OUT"
5401 HPUT(72,104)-(95,111),2,PSET:HPRINT(0,13),X$:RETURN
5500 IFC(0,R)+C(0,R+1)+C(0,R+2)+C(0,R+3)+C(0,R+4)+C(0,15)>4THENFL=1
5501 RETURN
5601 HPUT(32,80)-(288,87),2,PSET:RETURN
5602 Y=14:Z=0:FORX=24TO264STEP56:Z=Z+1:GOSUB5160:NEXT:RETURN
5700 C(0,15)=0:FORR=0TO14:FORS=0TO4:C(S,R)=0:NEXT:NEXT:RETURN
5890 S=RND(4):R=RND(14)+1:IFC(S,R)>0THEN5890
5895 C(S,R)=9:C(0,R)=C(0,R)+1:H$(Z)=HEX$(S)+HEX$(R):C(S,1)=C(S,14):C(0,1)=C(0,14):IFR<15THENC(S,0)=C(S,0)+1:RETURNELSERETURN
5914 Y=14:Z=1:Q=1:FORX=24TO264STEP56:GOSUB5140:SOUNDQ,1:Q=Q+20:H(Z)=-1:Z=Z+1:NEXT:RETURN
5918 HCOLOR2:HPRINT(4,9),"<1>    <2>    <3>    <4>    <5>":RETURN
5990 SOUND100,2:QS=1:GOSUB5601:HCOLOR3:HPRINT(4,10),"Press <SPACEBAR> for new cards!":QT=0:GOTO100
5999 POKE282,255:PALETTERGB:WIDTH40:POKE&HFFD8,0:CLS1:PRINT"ERNO";ERNO;"ERLIN"ERLIN:IFERLIN=13THENLOCATE0,1:PRINT"Did you enter, 'F$="CHR$(34)"SCORES"CHR$(34)":GOTO 2125', to establish SCORES?":STOPELSESTOP
6000 GOSUB6020:GOSUB6080:GOTO6008 'SUB ROUTINES FOR HDRAW
6005 GOSUB6020:GOSUB6095
6008 GOSUB6090:GOSUB6085:GOSUB6075:GOSUB6105:RETURN
6010 DRAW"S4":HX=X+14:HY=Y+23:GOSUB6020:GOSUB6115:HX=X+26:HY=Y+32:GOSUB6020:GOSUB6120:HX=X+14:HY=Y+41:GOSUB6020:GOSUB6110:HX=X+3:HY=Y+32:GOSUB6020:GOSUB6125:RETURN
6020 X$="BM"+STR$(HX)+","+STR$(HY):HDRAWX$:RETURN
6025 HDRAW"BU4UER3FDGLG3R5BR4":RETURN'2
6030 HDRAW"BU5ER3FDGNL2FDGL3HBDBR9":RETURN'3
6035 HDRAW"BR4U6G4R5BD2BR4":RETURN'4
6040 HDRAW"BUFR3EUHL4U3R5BR4BD6":RETURN'5
6045 HDRAW"BUU4ER3NFBD3NL3FDGL3NHBR8":RETURN'6
6050 HDRAW"E5UL5BR9BD6":RETURN'7
6055 HDRAW"BRHUEHUER3FDGNL3FDGL3BR8":RETURN'8
6060 HDRAW"BUFR3EU4HL3GDFR3BR5BD3":RETURN'9
6065 HDRAW"R3LU6LGBR5BD5BUU4ER2FD4GL2NHBR9":RETURN'10
6070 HDRAW"U4E2F2D2NL4D2BR4":RETURN
6075 HDRAW"U3NR3U3R5BD6L5BR9":RETURN'E
6080 HDRAW"BU2DFR3EU5BD6BR2":RETURN'J
6085 HDRAW"U6D3RNE3F3BR4":RETURN'K
6090 HDRAW"U6R5D6L5BR9":RETURN'0
6095 HDRAW"U6R4FDGL4BD3BR9":RETURN'P
6100 HDRAW"BR2LHU4ER2FD3G2BU2F2BR4":RETURN'Q
6105 HDRAW"U6R4FDGL3F3BR5":RETURN'R
6110 C=14:HDRAW"C14E6F6G6H6":HCOLOR3:GOTO6130
6115 C=14:HDRAW"C14U2EURUR2DRDFEURUR2DRDFD2G6H6":HCOLOR3:GOTO6130
6120 C=14:HDRAW"C14E6F6D2G2L4D2U2L4H2U2":HCOLOR2:GOTO6130
6125 C=14:HDRAW"C14E2RU2E2R2F2D2RF2D2G2L4D2U2L4H2U2":HCOLOR2:GOTO6130
6130 HPAINT(PEEK(199)*256+PEEK(200)+8,PEEK(201)*256+PEEK(202)),,C:RETURN
6999 'SUB ROUTINE FOR THE TITLE
7000 Z=108:U=35:V=172:FORY=1TO3:HCOLOR3:IFY=2THENHCOLOR4ELSEIFY=3THENHCOLOR2
7005 HDRAW"S8":HX=35:HY=Z+20:GOSUB6000:HY=HY+1:GOSUB6000:HX=36:GOSUB6000:HLINE(125,HY-2)-(285,HY),PSET,BF
7010 HDRAW"S24":FORX=1TO3:HX=U:U=U+1:HY=V:V=V+1:GOSUB6005:NEXT
7015 HDRAW"S4":HY=Z+83:HLINE(35,HY-2)-(116,HY),PSET,BF:HPRINT(16,23),"R. L. Brimner, 11/86":NEXT:RETURN
