0 'COPYRIGHT 1989, FALSOFT INC.
10 '****************************
20 '*  THE MANDELBROT BUG      *
30 '*        AND THE           *
40 '*     COCO3 MICROSCOPE     *
50 '*    BY MARTY & JEREMY     *
60 '*        SPILLER           *
70 '*          1989            *
80 '****************************
90 CLEAR 200,&H6000
100 Y=O:X=0:FY=O:FX=0:R1=0:R2=0:I1=0:I2=0:ITER=0:
110 ON BRK GOTO 1030
120 HSCREEN2:HBUFF 1,600:HCOLOR0:HSCREEN0
130 POKE &HE6C6,33: REM DISABLES AUTOMATIC HSCREEN CLEAR
140 CLS:INPUT"ARE YOU USING AN RGB MONITOR    (Y OR N)";RB$:IF RB$<>"Y" AND RB$<>"N" THEN 140
150 '*******MAIN MENU**********
160 CLS:PRINT"MANDELBROT'S BUG AND THE COCO   MICROSCOPE"
170 PRINT:PRINT:PRINT"1. LOAD A PICTURE (COMPLETE OR     INCOMPLETE)"
180 PRINT:PRINT"2.CREATE A PICTURE FROM SCRATCH"
190 PRINT:PRINT"3. GO TO SECONDARY MENU (DON'T     DO THIS UNLESS THERE IS A       PICTURE IN MEMORY"
200 PRINT:PRINT"4. RUN A DIRECTORY ON DRIVE (0)":REM TEAP USERS DELETE THIS LINE
210 INPUT A:IF A<1 OR A>4 THEN 210:REM TAPE USERS CHANGE >4 TO >3
220 IF A=1 THEN INPUT"ENTER FILENAME";F$:GOSUB 1740:GOTO 1340
230 IF A=2 THEN GOSUB 270:GOTO360
240 IF A=3 THEN GOTO 1030
250 DIR:PRINT:PRINT"THERE ARE";:PRINT FREE(0);:PRINT"FREE GRANULES ON    THIS DISK.  IT TAKES 16 TO SAVE A SCREEN":PRINT:REM TAPE USERS DELETE THIS LINE
260 INPUT"<ENTER> TO GO BACK TO MENU";A:GOTO 150:REM TAPE USERS DELETE THIS LINE
270 '*****ENTER PARAMETERS******
275 FY=0:FX=0
280 CLS:INPUT"INPUT REAL NUMBER LOW END RANGE";R1
290 INPUT"INPUT REAL NUMBER HIGH END RANGE";R2
300 INPUT"INPUT IMAGINARY NUMBER LOW END RANGE";I1
310 INPUT"INPUT IMAGINARY NUMBER HIGH END RANGE";I2
320 INPUT"INPUT THE NUMBER OF ITERATIONS";ITER
330 INPUT"NAME OF FILE (7 LETTERS OR LESS)";F$:IF LEN(F$)>7 THEN 330
340 RSTEP=(R2-R1)/320
350 ISTEP=(I2-I1)/192:RETURN
360 GOSUB 1740:HSCREEN 2:HCLS 1:POKE65497,0
370 IMAG=I1
380 '****MAIN LOOP MANDELBROT ALGORITHM****
390 REM LINES 470 TO 520 HAVE BEEN COMPRESSED TO SPEED EXECUTION
400 FOR Y=FY TO 191
410 REAL=R1
420 IMAG=I1+Y*ISTEP
430 FOR X=FX TO 319
440 REAL=R1+X*RSTEP
450 ICOMP=0
460 RCOMP=0
470 FORIN=1TOIT
480 RI=RC*RC-IC*IC+RE
490 IC=RC*IC*2+IM
500 RC=RI
510 IFRC>2ORRC<-2ORIC>2ORIC<-2THEN540 
520 NEXTIN
530 C=0:GOTO 550
540 C=INT(SQR(255/ITER*INDX))
550 HSET(X,191-Y,C)
560 NEXT X:FX=0:NEXT Y
570 '*******CURSOR ROUTINE*****
580 POKE 65496,0
590 XC=20:YC=180
600 HSCREEN2:GOSUB 1740
610 P1=HPOINT(XC,YC)
620 P2=HPOINT(XC+1,YC)
630 P3=HPOINT(XC,YC+1)
640 P4=HPOINT(XC+1,YC+1)
650 C=C+1:IF C>15 THEN C=0
660 HSET(XC,YC,C):HSET(XC+1,YC,C):HSET(XC,YC+1,C):HSET(XC+1,YC+1,C):A$=INKEY$
670 HSET(XC,YC,P1)
680 HSET(XC+1,YC,P2)
690 HSET(XC,YC+1,P3)
700 HSET(XC+1,YC+1,P4)
710 IF A$<>"" THEN 730
720 GOTO650
730 IF A$=CHR$(94) THEN YC=YC-1:IF YC<12 THEN YC=12
740 IF A$=CHR$(10)THEN YC=YC+1:IF YC>180 THEN YC=180
750 IF A$=CHR$(9)THEN XC=XC+1:IF XC>300 THEN XC=300
760 IF A$=CHR$(8) THEN XC=XC-1:IF XC<20 THEN XC=20
770 IF A$=CHR$(95)THEN YC=YC-10:IF YC<12 THEN YC=12
780 IF A$=CHR$(91)THEN YC=YC+10:IF YC>180 THEN YC=180
790 IF A$=CHR$(93)THEN XC=XC+10:IF XC>300 THEN XC=300
800 IF A$=CHR$(21)THEN XC=XC-10:IF XC<20 THEN XC=20
810 IF A$=CHR$(13) THEN 980
820 A=VAL(A$):IF A>0 AND A<7 THEN 840
830 GOTO 610
840 '*******BOX ROUTINE********
850 HGET(XC-20,YC-12)-(XC+20,YC+12),1
860 ON A GOSUB 890,900,910,920,870,880:GOTO 930
870 XL=XC-17:XH=XC+18:YH=YC-10:YL=YC+11:RETURN
880 XL=XC-20:XH=XC+20:YH=YC-12:YL=YC+12:RETURN
890 XL=XC-7:XH=XC+8:YH=YC-4:YL=YC+5:RETURN
900 XL=XC-10:XH=XC+10:YH=YC-6:YL=YC+6:RETURN
910 XL=XC-12:XH=XC+13:YH=YC-7:YL=YC+8:RETURN
920 XL=XC-15:XH=XC+15:YH=YC-9:YL=YC+9:RETURN
930 HLINE(XL,YH)-(XH,YL),PSET,B
940 A$=INKEY$:IF A$=""THEN 940
950 HPUT(XC-20,YC-12)-(XC+20,YC+12),1,PSET
960 IF A$=CHR$(13)THEN 980
970 A=VAL(A$):IF A>0 AND A<7 THEN 860 ELSE 610
980 '*******CALCULATE RANGES FOR NEXT ENLARGEMENT*******
990 XL=R1+XL*RSTEP
1000 XH=R1+XH*RSTEP
1010 YL=I1+(191-YL)*ISTEP
1020 YH=I1+(191-YH)*ISTEP
1030 '****SECONDARY MENU*******
1040 FY=Y:FX=X-1:IF FX<0THEN FX=0
1045 POKE 65496,0
1050 IF RB$="Y" THEN PALETTE RGB ELSE PALETTE CMP
1060 HSCREEN0:CLS:PRINT"YOU HAVE CHOSEN THE FOLLOWING   ENLARGEMENT PARAMETERS:"
1070 PRINT"R1=";XL;" ITER=";ITER
1080 PRINT"R2=";XH
1090 PRINT"I1=";YL
1100 PRINT"I2=";YH
1110 PRINT:PRINT"1. BEGIN CONSTRUCTION OF NEW       ENLARGEMENT"
1120 PRINT"2. RETURN TO CURRENT SCREEN"
1130 PRINT"3. SAVE SCREEN TO DISK (TAPE)"
1140 PRINT"4. MAIN MENU"
1150 PRINT"5. CONTINUE THE PARTIALLY COM-     PLETED SCREEN IN MEMORY"
1160 PRINT"6. RETURN TO BASIC"
1170 INPUT A: IF A<1 OR A>6 THEN 1170
1180 ON A GOTO 1500,600,1190,150,1810,1830
1190 '*****SAVE SCREEN TO DISK (TAPE USERS CHANGE "SAVEM" TO "CSAVEM")*******
1200 P=VARPTR(R1):VA=&H67F00:GOSUB 1640
1210 P=VARPTR(R2):GOSUB 1640
1220 P=VARPTR(I1):GOSUB 1640
1230 P=VARPTR(I2):GOSUB 1640
1240 P=VARPTR(ITER):GOSUB 1640
1250 P=VARPTR(Y):GOSUB 1640
1260 P=VARPTR(X):GOSUB 1640
1270 POKE 65496,0
1280 POKE &HFFA3,&H30:A$=F$+"1":SAVEM A$,&H6000,&H7FFF,&HB44A
1290 POKE &HFFA3,&H31:A$=F$+"2":SAVEM A$,&H6000,&H7FFF,&HB44A
1300 POKE &HFFA3,&H32:A$=F$+"3":SAVEM A$,&H6000,&H7FFF,&HB44A
1310 POKE &HFFA3,&H33:A$=F$+"4":SAVEM A$,&H6000,&H7FFF,&HB44A
1320 POKE &HFFA3,&H3B
1330 GOTO 1030
1340 '*****LOAD SCREEN FROM DISK (TAPE USERS CHANGE "LOADM" TO "CLOADM")*****
1350 POKE 65496,0
1360 HSCREEN2:HCLS 1
1370 POKE &HFFA3,&H30:A$=F$+"1":LOADM A$
1380 POKE &HFFA3,&H31:A$=F$+"2":LOADM A$
1390 POKE &HFFA3,&H32:A$=F$+"3":LOADM A$
1400 POKE &HFFA3,&H33:A$=F$+"4":LOADM A$
1410 POKE &HFFA3,&H3B
1420 P=VARPTR(R1):VA=&H67F00:GOSUB 1690
1430 P=VARPTR(R2):GOSUB 1690
1440 P=VARPTR(I1):GOSUB 1690
1450 P=VARPTR(I2):GOSUB 1690
1460 P=VARPTR(ITER):GOSUB 1690
1470 P=VARPTR(Y):GOSUB 1690
1480 P=VARPTR(X):GOSUB 1690
1490 GOSUB 340:GOTO 570
1500 '****CHANGE PARAMETERS FOR NEW ENLARGEMENT*******
1510 CLS:INPUT"INPUT FILENAME FOR NEW ENLARGE- MENT (7 LETTERS OR LESS)";TEMP$:IF LEN(TEMP$)>7 THEN 1510
1520 INPUT"INPUT NEW NUMBER OF ITERATIONS";TEMP
1530 PRINT"THE PARAMETERS FOR YOUR NEXT    ENLARGEMENT ARE:"
1540 PRINT"R1=";XL
1550 PRINT"R2=";XH
1560 PRINT"I1=";YL
1570 PRINT"I2=";YH
1580 PRINT"ITER=";TEMP
1590 PRINT"FILENAME=";TEMP$
1600 INPUT"PRESS <ENTER> TO BEGIN OR       <A> <ENTER> TO ABORT";A$
1610 IF A$<>"" THEN GOTO 1030
1620 R1=XL:R2=XH:I1=YL:I2=YH:F$=TEMP$:ITER=TEMP:FX=0:FY=0
1630 GOSUB 340:GOTO 360
1640 '****POKE SCREEN VARIABLES INTO SCREEN MEMORY******
1650 FOR TEMP=0 TO 4
1660 LPOKE VA+TEMP,PEEK(P+TEMP)
1670 NEXT TEMP
1680 VA=VA+5:RETURN
1690 '***POKE SCREEN VARIABLES INTO BASIC******
1700 FOR TEMP=0 TO 4
1710 POKE P+TEMP,LPEEK(VA+TEMP)
1720 NEXT TEMP
1730 VA=VA+5:RETURN
1740 '****SET PALETTES*********
1750 DATA 00,00,49,04,36,39,16,34,40,25,24,30,09,44,05,09,16,34,27,46
1760 DATA 54,36,39,54,15,43,34,21,39,54,36,39,0
1770 IF RB$="N" THEN READ C
1780 FOR P=0 TO 15
1790 READ C:PALETTE P,C:READ C
1800 NEXT P:RESTORE:RETURN
1810 '***COMPLETE PARTIALLY COMPLETED SCREEN*******************
1820 GOSUB 1740:GOSUB 340:POKE 65497,0:HSCREEN2:GOTO 380
1830 END
