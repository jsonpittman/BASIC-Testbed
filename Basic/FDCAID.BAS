10 ' DRIVE EXERCIZER
15 ' BY SCOTT HONAKER  11/87
20 ' THIS PROGRAM DIRECTLY PROGRAMS THE 1773/1793 FDC WITH TYPE I COMMANDS
40 '
45 ' $FF40 - CONTROL REGISTER
50 ' $FF48 - COMMAND/STATUS REGISTER
55 ' $FF49 - TRACK REGISTER
60 ' $FF4A - SECTOR REGISTER
65 ' $FF4B - DATA REGISTER
70 '
130 CLS:PRINT:PRINT"    DISK DRIVE DEMONSTRATOR"
135 PRINT:PRINT"       BY SCOTT HONAKER"
145 PRINT:PRINT:PRINT"     USED TO DEMONSTRATE":PRINT" western digital 1773/1793 FDC"
150 PRINT"  TYPE I COMMANDS AND ERRORS"
155 FOR T=1 TO 3000:IF INKEY$="" THEN NEXT
160 ' INITIALIZE VARIABLES
165 HE=0:MO=0:DR=0:R=30:RT=3:DD=9:A=0:BL$="                               "
200 ' PRINT MENU SCREEN
205 T=PEEK(&HFF49):S=PEEK(&HFF48)
210 CLS:PRINT"TRACK: ";T,"STATUS: ";S,"DRIVE: ";D,"RATE: ";R;"MS"
215 PRINT:PRINT"<i> STEP IN (HIGHER TRACK)"
220 PRINT"<o> STEP OUT (TOWARDS TRACK 0)"
225 PRINT"<m> STEP- MOVE IN LAST DIRECTION";
230 PRINT"<r> RESTORE- MOVE TO TRACK 0"
235 PRINT"<s> SEEK- MOVE TO SPECIFIC TRACK"
240 PRINT"<c> CLEAN DRIVE MODE"
245 PRINT"<d> DEFAULT DRIVE"
250 PRINT"<t> STEPPING RATE"
255 IF HE=0 THEN PRINT"<h> LOAD HEAD"ELSE PRINT"<h> UNLOAD HEAD"
260 IF A=0 THEN PRINT"<a> KEEP DRIVE ACTIVE" ELSE PRINT"<a> DEACTIVATE DRIVE"
265 PRINT:PRINT"command:";
270 A$=INKEY$:IF A$="" THEN 270
275 PRINT A$;
280 IF A$="I" THEN 1000
285 IF A$="O" THEN 2000
290 IF A$="M" THEN 3000
295 IF A$="R" THEN 4000
300 IF A$="S" THEN 5000
305 IF A$="C" THEN 6000
310 IF A$="D" THEN 7000
315 IF A$="T" THEN 7500
320 IF A$="H" THEN 8000
325 IF A$="A" THEN 8500
330 IF A$="Q" THEN CLS:POKE&HFF40,0:END
335 GOTO 205
500 IF(PEEK(&HFF48)/2)<>INT(PEEK(&HFF48)/2)THEN500:' DELAY UNTIL NOT BUSY
510 RETURN
600 A$=INKEY$:IF A$=""THEN610 ELSE X=30:RETURN
610 IF(PEEK(&HFF48)/2)<>INT(PEEK(&HFF48)/2)THEN600
620 RETURN
1000 C=80+HE+RT:' STEP IN "N" NUMBER OF TRACKS
1010 PRINT@480,"step in how many tracks";:INPUT N
1020 IF A=0 THEN POKE&HFF40,DD
1030 FOR X=1 TO N
1040 POKE&HFF48,C
1050 GOSUB 500
1060 NEXT X
1070 IF A=0 THEN POKE&HFF40,0
1080 GOTO 205
2000 C=112+HE+RT:' STEP OUT "N" TRACKS
2010 PRINT@480,"step out how many tracks";:INPUT N
2020 IF A=0 THEN POKE&HFF40,DD
2030 FOR X=1 TO N
2040 POKE&HFF48,C
2050 GOSUB 500
2060 NEXT X
2070 IF A=0 THEN POKE&HFF40,0
2080 GOTO 205
3000 C=48+HE+RT:' STEP IN DIRECTION OF LAST MOVEMENT "N" TIMES
3010 PRINT@480,"step how many tracks";:INPUT N
3020 IF A=0 THEN POKE&HFF40,DD
3030 FOR X=1 TO N
3040 POKE&HFF48,C
3050 GOSUB 500
3060 NEXT X
3070 IF A=0 THEN POKE&HFF40,0
3080 GOTO 205
4000 C=0+HE+RT:' RESTORE TO TRACK 0
4010 IF A=0 THEN POKE&HFF40,DD
4020 POKE&HFF48,C
4030 GOSUB 500
4040 IF A=0 THEN POKE&HFF40,0
4050 GOTO 205
5000 C=16+HE+RT:' SEEK A TRACK
5010 PRINT@480,"enter track to seek";:INPUT ST
5020 IF ST>80 THEN 5010
5030 IF A=0 THEN POKE&HFF40,DD
5040 POKE&HFF4B,ST:POKE&HFF48,C
5050 GOSUB 500
5060 IF A=0 THEN POKE&HFF40,0
5070 GOTO 205
6000 PRINT@480,"insert head cleaner <hit a key>";
6010 POKE&HFF40,0
6020 A$=INKEY$:IFA$=""THEN6010
6030 POKE&HFF40,DD
6040 FOR X=1 TO 30
6050 POKE&HFF48,8+HE+RT
6060 GOSUB 600
6070 POKE&HFF4B,34:POKE&HFF48,24+HE+RT
6080 GOSUB 600
6090 NEXT X
6100 IF A=0 THEN POKE&HFF40,0
6110 GOTO 205
7000 PRINT@480,"enter new default drive (0-3)";
7010 INPUT D
7020 IF D>3 OR D<0 THEN7010
7030 IF D=0 THEN DD=9
7040 IF D=1 THEN DD=10
7050 IF D=2 THEN DD=12
7060 IF D=3 THEN DD=72
7070 GOTO 205
7500 PRINT@480,"new step rate (6,12,20,30ms)";' CHANGE STEPPING RATE
7510 INPUT R
7520 RT=-1
7530 IF R=6 THEN RT=0
7540 IF R=12THEN RT=1
7550 IF R=20THEN RT=2
7560 IF R=30THEN RT=3
7570 IF RT<0 OR RT>3 THEN7510
7580 GOTO205
8000 IF HE=0 THEN HE=8 ELSE HE=0:' SET LOAD/UNLOAD FLAG
8010 GOTO205
8500 IF A=0 THEN A=DD ELSE A=0' SET DRIVE ACTIVITY FLAG AND (DE)ACTIVATE DRIVE
8510 POKE&HFF40,A
8520 GOTO205
