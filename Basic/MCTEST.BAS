1 'LET YOUR COCO DO THE GRADING
2 'BY WARREN CHEVES
3 'COPYRIGHT (C) SEPTEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
15 REM A$ = TEST NAME OR NUMBER
20 REM B$ = COURSE NAME
25 REM C$ = SEMESTER
30 REM N = NUMBER OF QUESTIONS
35 REM D$ = CORRECT ANSWERS
40 REM E$ = STUDENT ANSWERS
45 REM F$ = CORRECT ANSWER STRING
50 REM I = QUESTION NUMBER
55 REM C = COUNTER TO COUNT INCORRECT ANSWERS
60 REM G$ = STUDENT NAME
65 REM H$ = STUDENT ANSWER STRING
70 REM J = TOTAL TIMES A QUESTION IS MISSED
75 REM K = STUDENT GRADE
80 REM L$ = YES OR NO ANSWER FOR ANOTHER STUDENT
85 REM M$ = PRINTER READY INPUT
100 CLS
105 PRINT "ENTER TEST NAME"
110 INPUT A$
115 CLS
120 PRINT "ENTER COURSE NAME"
125 INPUT B$
130 CLS
135 PRINT "ENTER SEMESTER"
140 INPUT C$
145 CLS
150 PRINT "HOW MANY QUESTIONS?"
155 INPUT N
160 DIM D$(N)
165 DIM E$(N)
170 DIM J(N)
175 CLS
180 PRINT "ENTER CORRECT ANSWER STRING"
185 INPUT F$
190 IF LEN(F$)<>N THEN 175
195 FOR I=1 TO N
200 D$(I) = LEFT$(F$,1)
205 F$ = RIGHT$(F$,N-I)
210 NEXT I
215 CLS
220 C=0
225 PRINT "ENTER STUDENT NAME"
230 INPUT G$
235 CLS
240 PRINT "ENTER STUDENT ANSWER STRING"
245 INPUT H$
250 IF LEN(H$)<>N THEN 235
255 FOR I=1 TO N
260 E$(I) = LEFT$(H$,1)
265 H$ = RIGHT$(H$,N-I)
270 NEXT I
275 CLS
280 PRINT #-2,G$
285 PRINT #-2
290 PRINT #-2,B$
295 PRINT #-2
300 PRINT #-2,A$
305 PRINT #-2
310 PRINT #-2,C$
315 PRINT #-2
320 FOR I=1 TO N
325 IF E$(I)=D$(I) THEN 345
330 PRINT #-2,"QUESTION "I" IS "D$(I)" INSTEAD OF "E$(I)
335 C=C+1
340 J(I)=J(I)+1
345 NEXT I
350 PRINT #-2
355 PRINT #-2, "QUESTIONS MISSED: "C
360 PRINT #-2
365 K=((N-C)/N)*100
370 PRINT #-2, "GRADE: "K
375 PRINT G$
380 PRINT B$
385 PRINT A$
390 PRINT C$
395 PRINT
400 PRINT "QUESTIONS MISSED: "C
405 PRINT "GRADE: "K
410 PRINT
415 PRINT
420 PRINT "ANOTHER STUDENT(Y/N)?"
425 INPUT L$
430 IF L$="Y" THEN 215
440 CLS
450 PRINT "GET PRINTER READY"
460 PRINT "HIT ANY KEY WHEN READY"
470 M$=INKEY$
480 IF M$="" THEN 470
490 PRINT #-2,A$
500 PRINT #-2,B$
510 PRINT #-2,C$
520 PRINT #-2
530 FOR I=1 TO N
540 PRINT #-2, "QUESTION "I" WAS MISSED "J(I)" TIMES"
550 NEXT I
560 END
