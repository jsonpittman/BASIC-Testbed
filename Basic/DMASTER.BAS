1 'DISK MASTER
2 'BY DWIGHT L. STEGALL
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 IFPEEK(33021)<>50THENCLS:PRINT"THIS PROGRAM WILL NOT FUNCTION  WITHOUT THE COLOR COMPUTER3.":PRINT:CLEAR200:NEW ELSEONERRGOTO945:POKE41382,255:POKE41384,1
15 '*** MAKE SYSTEM CHANGES
20 POKE41598,255:POKE41599,255:VERIFYON:POKE65496,0:POKE65344,0:POKE65497,0:POKE41954,250
25 POKE58953,16:RGB 'FORCE CMP & RGB TO RESET ALL 16 PALETTES INSTEAD OF 15
30 WIDTH40:POKE65434,0:GOSUB880
35 A$="185409606300295353290063600954186208":A=58964:FORI=1TOLEN(A$)STEP2:B$=MID$(A$,I,2):B=VAL(B$):POKEA,B:A=A+1:NEXT 'PALETTE VALUES FOR CMP & RGB
40 POKE38543,33:POKE38563,33:PCLEAR0 'PATCH PCLEAR TO INCLUDE 0
45 POKE1022,PEEK(114):POKE1023,PEEK(115):POKE114,3:POKE115,249:POKE1017,18:POKE1018,127:POKE1019,255:POKE1020,223:POKE1021,126 'RESET PROTECTION
50 POKE63468,0:POKE63503,0:POKE63567,0:POKE63644,0:POKE63771,0 'TURNS OFF 40 & 80 COLUMN CURSOR DURING PROGRAM OPERATION EXCEPT AT AN INPUT AND A LINEINPUT LIKE IN WIDTH32. ADOS3'S "MENU" UTILITY WILL DESTROY THESE POKES.
55 CLEAR200,32255:POKE32517,13:FORI=32518TO32523:READB$:POKEI,ASC(B$):NEXT:POKEI,13:POKEI+1,0:POKE44151,127:POKE44152,5:DATA R,E,A,D,Y,!
60 IFPEEK(41602)=23THEN80 'IF ADOS3 THEN BYPASS NEXT 3 LINES
65 'SETS 6MS DISK STEP RATE IF YOU ARE NOT USING ADOS-3
70 IFPEEK(49474)<>48THENDB=1 'DISK BASIC 1.1 IF <>48
75 IFDB=1THEN POKE55318,20:POKE55232,0 ELSEPOKE55075,20:POKE54989,0
80 FORI=55671TO55675:READA$:POKEI,ASC(A$):NEXT:DATAG,R,A,N,S 'CHANGES 'FREE' TO 'GRANS'
85 POKE65496,0:LOADM"STRDMOD3":CLEAR10000:ONERRGOTO945:HI=65497:LO=65496:POKEHI,0:POKE235,0:DIMA$(69),N(69),T$(69),E$(69),P$(69),F(69)
90 POKE61203,196 'FIX THE HPUT "NOT" OPTION
95 POKE62861,189:POKE62862,244:POKE62863,204 'FIX HDRAW BUG
100 POKE63164,255 'EXTEND 40 & 80 COLUMN "CLS" CHOICES FROM 1-8 TO 1-255
105 GOSUB890:GOSUB900:HBUFF0:HBUFF2,320:HBUFF3,1250:HBUFF4,1250:HBUFF5,3840:HCOLOR1,0:HSCREEN3:HGET(0,0)-(319,95),5
110 '*** WHEN USING RUN, LOAD, NAME, COPY, SCAN, AND KILL USE THE SPACEBAR TO SELECT FILENAMES
115 '*** MAIN SCREEN
120 POKEHI,0:HCLS1:HGET(0,0)-(437,15),4:HCOLOR0:FORI=0TO23:HPRINT(0,I),STRING$(11,92):HPRINT(69,I),STRING$(11,92):NEXT:GOSUB910
125 HCOLOR0:HPRINT(14,19),"Directory Master V1.01 written by: Dwight L. Stegall":HPUT(104,160)-(535,175),4,NOT:HCOLOR1:HPRINT(15,20),"R=Exec     L=Load     N=Fnme     C=Copy     K=Kill":HPRINT(15,21),"S=Scan     D=Driv     X=Dnme     V=Drty     Q=Qui
130 GOSUB335:HPUT(200,16)-(215,151),4:HPUT(312,16)-(327,151),4:HPUT(424,16)-(439,151),4:HGET(104,16)-(535,83),5:RGB:GOSUB245:GOSUB215:GOSUB275
135 POKE282,255:HI=65497:LO=65496:POKELO,0:POKE135,0:DN=PEEK(235):DN$=":"+CHR$(48+DN)
140 X$=INKEY$:IFX$=""THEN140ELSEPOKEHI,0
145 IFX$="R"THENAR=1:K$="EXEC":GOSUB535:GOSUB365:GOTO585
150 IFX$="L"THENAR=2:K$="LOAD":GOSUB535:GOSUB365:GOTO615
155 IFX$="N"THENAR=3:K$="NAME":GOSUB535:GOSUB365:GOTO645
160 IFX$="C"THENAR=4:K$="COPY":GOSUB535:GOSUB365:GOTO685
165 IFX$="K"THENAR=5:K$="KILL":GOSUB535:GOSUB365:GOTO725
170 IFX$="S"THENAR=6:K$="SCAN":GOSUB535:GOSUB365:GOTO735
175 IFX$="D"THENAR=7:GOSUB535:GOSUB200:GOSUB535:GOTO135
180 IFX$="X"THENAR=8:GOSUB535:GOTO805
185 IFX$="V"THENAR=9:GOSUB535:GOTO825
190 IFX$="Q"THENHGET(468,168)-(522,175),2:HPUT(468,168)-(522,175),2,NOT:GOSUB355:GOSUB355:GOTO945 ELSEFORI=1TO10:PALETTE1,RND(63):NEXT:RGB:POKELO,0:POKE135,0:GOTO135
195 '*** SELECT WORKING DRIVE
200 HCOLOR0:HPRINT(15,22),"Which drive do you need? 0,1,2,3
205 EXEC44539:Z=PEEK(135):IFZ<48ORZ>51THEN205ELSEPOKE235,Z-48:GOSUB345:GOSUB335:GOSUB245:GOSUB215:GOSUB275:RETURN
210 '*** PRINTS HEADER
215 GOSUB320:HPUT(104,0)-(535,15),4:HCOLOR1:HLINE(520,0)-(520,15),PSET:HCOLOR0:HPRINT(14,0),"THE CURRENT DISK NAME":HLINE(285,2)-(304,4),PSET,BF:HPRINT(38,0),CHR$(123)+" "+I$
220 HPRINT(13,1),"*":HPRINT(15,1),"CURRENT DRIVE #"+RIGHT$(STR$(DN),1):HPRINT(32,1),"***
225 IFN<10THENUJ$="0"+RIGHT$(STR$(N),1)ELSEUJ$=RIGHT$(STR$(N),2)
230 IFGR<10THENUK$="0"+RIGHT$(STR$(GR),1)ELSEUK$=RIGHT$(STR$(GR),2)
235 HPRINT(36,1),"FILENAMES="+UJ$:HCOLOR0:HPRINT(49,1),"***":HPRINT(53,1),"FREE GRNS="+UK$:HPRINT(66,1),"*":RETURN
240 '*** READS FILENAMES FROM DISK
245 POKELO,0:FORI=1TO68:P$(I)="":NEXT:DN=PEEK(235):N=0:P=0:FORX=3TO11:DSKI$DN,17,X,A$(1),A$(2):FORQ=1TO2:FORI=1TO128STEP32:N=N+1:T$(N)=MID$(A$(Q),I,8):IFASC(T$(N))=255THENN=N-1:GOTO260ELSEIFASC(T$(N))=0THENN=N-1:GOTO255
250 E$(N)=MID$(A$(Q),I+8,3):P$(N)=T$(N)+"."+E$(N):F(N)=ASC(MID$(A$(Q),I+11,1))
255 NEXTI,Q,X:POKEHI,0
260 IFP$(1)=""THENHCOLOR0:HPRINT(15,22),"THERE ARE NO FILES IN DRIVE #"+CHR$(DN+48):GOSUB355:POKEHI,0:GOSUB345:GOSUB200:GOSUB535:GOTO135
265 RETURN
270 '*** PRINTS FILENAMES ON SCREEN
275 POKEHI,0:GP=1:HCOLOR1:A=2:B=1:CL=13
280 FORI=B TO N:HPRINT(CL,A),P$(B):A=A+1:B=B+1
285 IFB>N THENRETURN
290 IFA>18THENGP=GP+1:GOTO300
295 NEXT:RETURN
300 IFGP=2THENA=2:CL=27:GOTO280
305 IFGP=3THENA=2:CL=41:GOTO280
310 IFGP=4THENA=2:CL=55:GOTO280ELSERETURN
315 '*** READS DISK NAME ON TRACK #17, SECTOR #1.
320 H$="THIS DISK HAS NO NAME!":DN=PEEK(235):POKELO,0:GR=FREE(DN):DSKI$DN,17,1,I$,J$:POKEHI,0:POKE65344,0:IFLEFT$(I$,1)=CHR$(255)THENI$=H$ ELSEIFLEFT$(I$,1)=CHR$(32)THENI$=H$ ELSEI$=LEFT$(I$,26)
325 RETURN
330 '*** CLEARS FILENAME FIELD
335 HPUT(104,16)-(535,83),5:HPUT(104,84)-(535,151),5:RETURN
340 '*** CLEARS ROWS 22 & 23
345 HPUT(104,176)-(535,191),4:RETURN
350 '*** PAUSE AFTER PRINTING ON SCREEN
355 FORT=1TO750:NEXTT:RETURN
360 '*** SELECTION OF FILENAMES
365 GP=1:AL=2:B=1:RX=104:RY=199
370 HGET(RX,AL*8)-(RY,AL*8+7),2:HPUT(RX,AL*8)-(RY,AL*8+7),2,NOT:POKE338,255:POKE345,255
375 IFPEEK(338)=191THENRETURN ELSEIFPEEK(345)=247THENAL=AL+1:B=B+1 ELSE375
380 HPUT(RX,AL*8-8)-(RY,AL*8-1),2
385 IFB>N THEN365
390 IFAL>18THENGP=GP+1:GOTO400
395 GOTO370
400 IFGP=2THENAL=2:B=18:RX=219:RY=310:GOTO370
405 IFGP=3THENAL=2:B=35:RX=330:RY=421:GOTO370
410 IFGP=4THENAL=2:B=52:RX=441:RY=532:GOTO370ELSERETURN
415 '*** HSCREEN 'LINEINPUT' SIMULATION
420 POKE135,0:Y$="":Z$="":X=PEEK(1024):Y=PEEK(1025):Z=PEEK(1026):FG=PEEK(1027):SL=X 'X=ROW POSITION OF CURSOR, Y=ROW POSITION OF CURSOR, Z=AMOUNT OF CHARACTERS THAT MAY BE INPUT AT ONE TIME, FG=FOREGROUND COLOR
425 HCOLOR2:HPRINT(X,Y),CHR$(127):HPUT(X*8,Y*8)-((X*8)+7,Y*8+7),4:Y$=INKEY$:IFY$=""THEN425
430 IFY$=CHR$(255)THEN425
435 IFASC(Y$)=13ORASC(Y$)=8THEN445
440 IFASC(Y$)=21THENX=SL:HPUT(X*8,Y*8)-((X+Z)*8,Y*8+7),4:GOTO420 'SHIFT LEFT ARROW
445 IFY$=CHR$(13)THENPOKE282,255:RETURN
450 IFY$=CHR$(8)AND LEN(Z$)=0THENX=SL:GOTO425 'LEFT TAB STOP FOR CURSOR
455 IFY$=CHR$(8)AND LEN(Z$)>0THENZ$=LEFT$(Z$,LEN(Z$)-1):HPUT((X*8)-8,Y*8)-(X*8,Y*8+7),4:HCOLORFG:X=X-1:GOTO425 'LEFT ARROW
460 Z$=Z$+Y$
465 IFLEN(Z$)>Z THEN470ELSEHCOLORFG:HPRINT(X,Y),Y$:X=X+1
470 IFLEN(Z$)>Z THENZ$=LEFT$(Z$,Z):HPRINT(36,Y)," "
475 GOTO425
480 '*** ELIMINATE BRAIN MALFUNCTION!
485 HCOLOR0:HPRINT(16,23),"ARE YOU SURE YOU WANT TO "+K$+" "+P$(B)+"? [Y/N]":POKE135,0
490 X$=INKEY$:IFX$=""THEN490
495 IFX$="Y"THENGOSUB345:IFP$(2)="" AND AR=5THENRETURN ELSEIFAR=4THENRETURN ELSEGOSUB535:RETURN
500 IFX$="N"THENGOSUB345:GOSUB555:GOSUB535:GOTO135 ELSEPOKE135,0:GOTO490
505 '*** ELIMINATE IGNORANCE ATTACK!
510 HCOLOR0:HPRINT(16,23),"ARE YOU SURE YOU WANT TO NAME THIS DISK? [Y/N]":POKE135,0
515 X$=INKEY$:IFX$=""THEN515
520 IFX$="Y"THENGOSUB345:GOSUB535:RETURN
525 IFX$="N"THENGOSUB345:GOSUB535:GOTO135 ELSEPOKE135,0:GOTO515
530 '*** INVERT & REINVERT COMMAND LINE
535 IF AR=1THEN X=15:Y=21:Z=20 ELSEIFAR=2THEN X=26:Y=32:Z=20 ELSEIFAR=3THEN X=37:Y=43:Z=20 ELSEIFAR=4THEN X=48:Y=54:Z=20 ELSEIFAR=5THEN X=59:Y=65:Z=20
540 IFAR=6THEN X=15:Y=21:Z=21 ELSEIFAR=7THEN X=26:Y=32:Z=21 ELSEIFAR=8THEN X=37:Y=43:Z=21 ELSEIFAR=9THEN X=48:Y=54:Z=21
545 HGET(X*8-4,Z*8)-(Y*8+3,Z*8+7),2:HPUT(X*8-4,Z*8)-(Y*8+3,Z*8+7),2,NOT:RETURN
550 '*** RESET FILE NAME FIELD
555 HGET(RX,AL*8)-(RY,AL*8+7),2:HPUT(RX,AL*8)-(RY,AL*8+7),2,NOT:RETURN
560 '*** DUMP FILENAME TO TEMPORARY BUFFER SO MEMORY CAN BE RE-ESTABLISHED WITH CLEAR200 AND PCLEAR4
565 V=1536:FORI=1TO12:U=ASC(MID$(P$(B),I,1)):POKEV,U:V=V+1:NEXT:IFF(B)=0 OR F(B)=2THENGOSUB915:GOSUB925:RETURN ELSERETURN
570 '*** RETREIVE FILENAME FROM BUFFER
575 FORI=1536TO1547:P$(B)=P$(B)+CHR$(PEEK(I)):NEXT:HBUFF0:DN$=":"+CHR$(PEEK(235)+48):RETURN
580 '*** RUN A FILENAME
585 IFF(B)=0THENGOSUB485:WIDTH32:GOSUB870:RGB:GOSUB605:GOSUB565:CLEAR200:PCLEAR4:GOSUB575:POKE65496,0:LOAD P$(B)+DN$,R
590 IFF(B)=2THENGOSUB485:WIDTH32:GOSUB870:RGB:GOSUB605:GOSUB565:CLEAR200:PCLEAR4:GOSUB575:PRINT:PRINT"IF YOU GET A ?SYNTAX ERROR JUST TYPE: EXEC TO EXECUTE THE FILE.":PRINT:POKE65496,0:LOADM P$(B)+DN$:POKE65344,0:EXEC
595 IFF(B)=1THENHCOLOR0:HPRINT(15,22),"You cannot execute a data file!":SOUND2,20:FORI=1TO4:GOSUB355:NEXTI:GOSUB345:GOSUB555:GOSUB535:GOTO135
600 IFF(B)=3THENHCOLOR0:HPRINT(15,22),"You cannot execute a text editor source file!":SOUND2,20:FORI=1TO4:GOSUB355:NEXTI:GOSUB345:GOSUB555:GOSUB535:GOTO135
605 PRINT"RUNNING: "P$(B)+DN$:GOSUB915:POKE41598,3:POKE41599,3:RETURN
610 '*** LOAD A FILENAME
615 IFF(B)=0THENGOSUB485:WIDTH32:GOSUB870:RGB:GOSUB635:GOSUB565:CLEAR200:PCLEAR4:GOSUB575:POKE65496,0:LOAD P$(B)+DN$
620 IFF(B)=2THENGOSUB485:WIDTH32:GOSUB870:RGB:GOSUB635:GOSUB565:CLEAR200:PCLEAR4:GOSUB575:POKE65496,0:LOADM P$(B)+DN$:POKE65344,0:END
625 IFF(B)=1THENHCOLOR0:HPRINT(15,22),"You cannot load a data file!":SOUND2,20:FORI=1TO4:GOSUB355:NEXTI:GOSUB345:GOSUB555:GOSUB535:GOTO135
630 IFF(B)=3THENHCOLOR0:HPRINT(15,22),"You cannot load a text editor source file!":SOUND2,20:FORI=1TO4:GOSUB355:NEXTI:GOSUB345:GOSUB555:GOSUB535:GOTO135
635 PRINT"LOADING: "P$(B)+DN$:GOSUB915:POKE41598,3:POKE41599,3:RETURN
640 '*** RENAME A FILE
645 HCOLOR0:HPRINT(19,22),"NEW FILE NAME:         .":HLINE(272,184)-(335,184),PSET:HLINE(343,184)-(366,184),PSET:HPRINT(47,22),"or hit [ENTER]":POKE1024,34:POKE1025,22:POKE1026,8:POKE1027,0
650 GOSUB420:IFZ$=""THENGOSUB345:GOSUB555:GOSUB535:GOTO135ELSEW$=Z$
655 HCOLOR0:HPRINT(42,22),".":POKE1024,43:POKE1025,22:POKE1026,3:POKE1027,0
660 GOSUB420:IFZ$=""THENGOSUB345:GOSUB555:GOSUB535:GOTO135
665 HCOLOR1:HLINE(120,184)-(460,184),PSET:GOSUB485:Y$=W$+STRING$(8-LEN(W$),32)+"."+Z$:IFY$=P$(B)THENHCOLOR0:HPRINT(15,22),"THE NEW NAME MUST NOT BE THE SAME AS THE OLD NAME!":POKELO,0:FORT=1TO3000:NEXT:POKEHI,0:GOSUB555:GOSUB345:GOTO135
670 POKELO,0:RENAME P$(B)+DN$ TO Y$+DN$:POKE65344,0
675 POKEHI,0:GOSUB345:GOSUB335:GOSUB245:GOSUB215:GOSUB275:GOTO135
680 '*** COPY A FILE
685 GOSUB485:HCOLOR0:HPRINT(15,22),"What is the target drive? 0,1,2,3
690 EXEC44539:Z=PEEK(135):IFZ<48ORZ>51THEN690ELSE S$=":"+CHR$(Z):S=Z-48:DN=PEEK(235):DN$=":"+CHR$(DN+48):GOSUB535:GOSUB345
695 IFPEEK(41602)<>23THENPOKELO,0:COPY P$(B)+DN$ TO P$(B)+S$:POKEHI,0:POKE235,DN:POKE65344,0:GOSUB555:GOSUB715:GOTO135 'CHECK FOR ADOS3
700 GOSUB710:GOSUB880:WIDTH80:CLS6:CMP:PALETTE3,36:LOCATE0,9:ATTR5,3:PRINTSTRING$(31,32);:ATTR6,5:LOCATE0,11:ATTR5,2:PRINTSTRING$(31,32);:ATTR6,5:LOCATE0,10:ATTR5,3:PRINT" ";:ATTR2,1:PRINT" COPYING "P$(B)+DN$" TO"S"";:ATTR5,2:PRINT" ";
705 ATTR6,5:POKE65428,32:POKE65429,2:LOCATE0,12:ATTR3,5,B:POKELO,0:COPY P$(B)+DN$ TO P$(B)+S$:POKEHI,0:POKE65344,0:POKE235,DN:POKE59078,33:CLS:GOSUB880:HSCREEN3:RGB:POKE59078,141:GOSUB555:GOSUB715:POKE65428,126:POKE65429,126:GOTO135
710 POKE57414,9:POKE63105,32:POKE63112,38:POKE63113,0:POKE63601,64:POKE63605,37:POKE63606,192:RETURN 'CONVERT 80X24 HIRES TEXT SCREEN INTO 32X24 HIRES TEXT SCREEN (C) APRIL 1989 TIM RITTER
715 POKE57414,21:POKE63105,80:POKE63112,47:POKE63113,0:POKE63601,160:POKE63605,46:POKE63606,96:RETURN 'RESET 32X24 HIRES TEXT SCREEN TO 80x24 HI-RES TEXT SCREEN
720 '*** KILL A FILE
725 DN=PEEK(235):DN$=":"+CHR$(48+DN):GOSUB485:POKELO,0:KILL P$(B)+DN$:POKEHI,0:POKE65344,0:GOSUB335:GOSUB245:GOSUB215:GOSUB275:GOTO135
730 '*** SCAN A FILE
735 GOSUB485:GOSUB555:POKEHI,0:GOSUB880:GOSUB790:WIDTH80:CLS6:CMP:DN$=":"+CHR$(PEEK(235)+48)
740 LOCATE2,0:ATTR7,5:PRINTSTRING$(28,42):LOCATE2,1:PRINT"* ";:ATTR6,5:PRINT"SCANNING: "P$(B)DN$;:ATTR7,5:PRINT" *":LOCATE2,2:PRINTSTRING$(28,42)
745 LOCATE32,0:ATTR7,5:PRINTSTRING$(30,42);:LOCATE32,1:PRINT"* ";:ATTR6,5:PRINT"SPACE=PAUSE - ENTER=RETURN";:ATTR7,5:PRINT" *";:LOCATE32,2:PRINTSTRING$(30,42);
750 LOCATE16,4:ATTR2,1:PRINT"YELLOW=VALUE LESS THAN ASCII 32";:ATTR0,5:LOCATE15,5:ATTR3,2:PRINT"BLUE=VALUE GREATER THAN ASCII 127";:ATTR3,5:PRINT:PRINT
755 OPEN"I",#1,P$(B)+DN$:CLOSE:OPEN"D",#1,P$(B)+DN$,1:POKE65344,0:FIELD#1,1AS A$:FORX=1TOLOF(1):GET#1:IFASC(A$)=13THENATTR3,5:PRINT:ATTR3,5:GOTO770
760 IFASC(A$)<32THENATTR0,1:PRINT" ";:ATTR3,5:GOTO770
765 IFASC(A$)<128THENATTR3,5:PRINTA$; ELSEATTR3,2:PRINTA$;:ATTR3,5
770 X$=INKEY$:IFX$=CHR$(32)THENPOKELO,0:EXEC44539:POKEHI,0:POKE135,0
775 IFX$=CHR$(13)THENPRINT:CLOSE#1:GOTO785
780 NEXT:POKE65344,0:ATTR3,5:PRINT:PRINT:CLOSE#1:LOCATE14,23:ATTR4,5:PRINT"PRESS ANY KEY TO RETURN TO THE MENU";:EXEC44539
785 POKEHI,0:POKE65344,0:CLS:GOSUB880:POKE59078,33:HSCREEN3:GOSUB795:RGB:POKE59078,141:GOTO135
790 POKE57414,19:POKE63105,64:POKE63112,44:POKE63601,128:POKE63605,43:POKE63606,128:POKE63724,64:RETURN 'CONVERT 80 X 24 HIRES TEXT SCREEN INTO 64 X 24 HIRES TEXT SCREEN (C) APRIL 1989 TIM RITTER
795 POKE57414,21:POKE63105,80:POKE63112,47:POKE63601,160:POKE63605,46:POKE63606,96:POKE63724,80:RETURN 'RESET 64x24 HI-RES TEXT SCREEN TO 80x24 HI-RES TEXT SCREEN
800 '*** RENAME DISK (NOTE: YOU MAY PRESS ENTER TO AVOID GIVING THE DISK A NAME. THE FIRST CHARACTER MUST NOT BE A SPACE.)
805 HCOLOR0:HPRINT(14,22),"Disk name:":HLINE(200,184)-(407,184),PSET:HPRINT(52,22),"or hit [ENTER]":POKE1024,25:POKE1025,22:POKE1026,26:POKE1027,0
810 GOSUB420:IFZ$=""ORLEFT$(Z$,1)=CHR$(32)THENGOSUB535:GOSUB345:GOTO135
815 HCOLOR3:HLINE(208,184)-(415,184),PSET:GOSUB510:Y$=Z$+STRING$(26-LEN(Z$),32)+STRING$(102,255):DN=PEEK(235):POKELO,0:DSKO$DN,17,1,Y$,STRING$(128,255):POKEHI,0:POKE65344,0:GOSUB345:GOSUB215:GOTO135
820 '*** DIRECTORY OF FILENAMES
825 GOSUB355:GOSUB535:GOSUB880:GOSUB710:WIDTH80:CMP:CLS6:GOSUB320
830 ATTR0,5:PRINT"***"I$;:LOCATE29,0:PRINT"***":ATTR1,5:PRINT"  ** DIRECTORY FOR DRIVE #";:PRINTUSING"#";DN;:PRINT" **":PRINT:ATTR7,5:PRINT"  SPACE=PAUSE - OTHER=CONTINUE":ATTR6,5
835 POKE41590,19:POKELO,0:DIRDN:POKEHI,0:IFPEEK(41602)<>23THENPRINTUSING"GRANS = ##";GR
840 POKE65344,0:POKE41590,32:ATTR4,5:LOCATE3,23:PRINT"PRESS ANY KEY FOR THE MENU";:POKE135,0
845 A=443847:B=443897
850 LPOKEA,(LPEEK(A)+64)AND127:LPOKEB,(LPEEK(B)+64)AND127:IFINKEY$<>""THEN865ELSEA=A+2:B=B-2:IFA<443873THEN850
855 A=443847:B=443897
860 LPOKEA,(LPEEK(A)-64)AND127:LPOKEB,(LPEEK(B)-64)AND127:IFINKEY$<>""THEN865ELSEA=A+2:B=B-2:IFA<443873THEN860ELSE845
865 POKE59078,33:POKEHI,0:CLS:GOSUB880:GOSUB715:HSCREEN3:RGB:POKE59078,141:GOTO135
870 A$="1836110763310938001800630018003818540936632745380018006300180038":A=58964:FORI=1TOLEN(A$)STEP2:B$=MID$(A$,I,2):C=VAL(B$):POKEA,C:A=A+1:NEXT:RETURN 'RESET RGB AND CMP PALETTES TO STARTUP VALUES.
875 '*** SET ALL PALETTES TO 'BLACK'. THIS WILL PREVENT STRANGE COLOR FLASHES ON THE SCREEN WHEN SWITCHING FORM GRAPHICS SCREEN TO TEXT SCREEEN AND RGB TO CMP AND BACK AGAIN.
880 FORI=0TO15:PALETTEI,0:NEXT:POKE65434,0:RETURN
885 '*** USE WIDTH32 AS A TEMPORARY 512 BYTE BUFFER TO DUMP ORIGINAL VALUES OF CHR$(92) & CHR$(123) ON HSCREENS. NO MATTER WHAT FONT YOU ARE USING IN THIS PROGRAM THEY WILL ALWAYS BE RESET WITH THE CORRECT VALUES.
890 A=62077:D=33000:FORI=1TO8:C=PEEK(A):POKED,C:A=A+1:D=D+1:NEXT:A=62325:D=33009:FORI=1TO8:C=PEEK(A):POKED,C:A=A+1:D=D+1:NEXT:RETURN
895 '*** CHANGE CHR$(92) TO RANDOM GRAPHICS CHARACTER AND CHR$(123) TO BOLD RIGHT ARROW. THERE 18.5 TRILLION DIFERENT POSSIBILITIES FOR CHR$(92). NOTE: CHR$(92) PRINTS THE GRAPHICS ON BOTH SIDES OF THE SCREEN.
900 FORI=62077TO62084:A=RND(256):IFA=256THENPOKEI,0:NEXT:RETURN ELSEPOKEI,A:NEXT:POKE62325,8:POKE62326,12:POKE62327,126:POKE62328,127:POKE62329,126:POKE62330,12:POKE62331,8:POKE62332,0:RETURN
905 '*** RESET CHR$(92) & CHR$(123)
910 A=62077:D=33000:FORI=1TO8:C=PEEK(D):POKEA,C:A=A+1:D=D+1:NEXT:RETURN
915 A=62325:D=33009:FORI=1TO8:C=PEEK(D):POKEA,C:A=A+1:D=D+1:NEXT:RETURN
920 '*** RESET ADDRESSES 33000-33016
925 A=33000:POKEA,69:POKEA+1,88:POKEA+2,84:POKEA+3,69:POKEA+4,78:POKEA+5,68:POKEA+6,69:POKEA+7,68:POKEA+8,32:POKEA+9,67:POKEA+10,79:POKEA+11,76:POKEA+12,79:POKEA+13,82:POKEA+14,32:POKEA+15,66:POKEA+16,65:RETURN
930 '*** IF YOU WOULD LIKE TO GENERATE A TEST DISK WITH 68 FILENAMES ON IT, FORMAT A DISK WITH DSKINI0 AND DELETE ALL LINE NUMBERS IN THIS PROGRAM EXCEPT THE NEXT LINE AND RUN IT
935 FORI=1TO68:PRINT"FILE "+RIGHT$(STR$(I),2):SAVE"FILE "+RIGHT$(STR$(I),2):NEXT:END
940 '*** LEAVE PROGRAM
945 POKE57556,55:HBUFF0:POKE57556,52:HBUFF0:WIDTH40:GOSUB915:GOSUB925:GOSUB870:CLS11:ATTR3,2:POKE41590,32:POKE41598,3:POKE41599,3:POKELO,0:CLEAR200:RGB:NEW
