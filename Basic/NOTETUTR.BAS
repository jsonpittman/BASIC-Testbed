10 'NOTE TUTOR
20 'WRITTEN BY WILLIAM MILLER
30 'COPYRIGHT (C) OCTOBER 1991
40 'BY FALSOFT, INC.
50 'RAINBOW MAGAZINE"
60 ' INTRO MEMORY, SCREEN
70 '
80 RGB:PALETTE1,0:PALETTE 0,63:DIMN(22),MS$(11):HSCREEN4:HBUFF1,100:HBUFF2,100:HBUFF3,100:HBUFF4,100:HBUFF5,500:HBUFF6,1000:HBUFF7,1000:HBUFF8,500:HBUFF9,500:TC$="FGABCDEFGABCDEFGABCDEF":BC$="CDEFGABCDEFGABCDEF":CC=1:X=112
85 HPRINT(35,1),"NOTE TUTOR":HPRINT (34,2),"WKM Software":HPRINT(38,3),"7/91"
90 '
100 ' MESSAGES
110 '
120 MS$(1)="Choose one":MS$(2)="<T>reble Clef":MS$(3)="<B>ass Clef":MS$(4)="Find <N>ote":MS$(5)="Find <K>eyboard position":MS$(6)="Note:":MS$(7)="Press <Q> to quit":MS$(8)="Keyboard Position":MS$(9)="Sorry, the note is: "
130 MS$(10)="Correct":MS$(11)="Sorry, the key is:"
140 GOTO300
150 '
160 ' SUBS
170 '
180 '    INKEY
190 Q$=INKEY$:IFQ$=""THEN 190 ELSE RETURN
200 '
210 ' PRINT MESSAGE
220 '
230 HPRINT(PX,PY),MS$(MS):RETURN
240 '
250 ' ERASE MESSAGE
260 '
270 HCOLOR0:GOSUB230:HCOLOR1:RETURN
280 '
290 '
300 '   DRAW STAFF
310 '
320 FORZ =59 TO 31 STEP-7:HLINE(10,Z)-(210,Z),PSET:NEXT:HLINE(10,31)-(10,59),PSET:HLINE(11,31)-(11,59),PSET:HLINE(16,31)-(16,59),PSET:HGET(180,0)-(194,90),5
330 '
340 ' DRAW KEYBOARD
350 '
360 FORZ=0TO21:FORA=0TO1:HLINE(195+20*Z-A*140,70+A*40)-(215-A*140+20*Z,100+A*40),PSET,B:IFZ=3 OR Z=6 OR Z=10 OR Z=13  OR Z=17 OR Z>19 THEN 370 ELSE HLINE(211+20*Z-A*140,70+A*40)-(220+20*Z-A*140,85+A*40),PSET,BF
370 NEXT A,Z
380 FORZ= 0 TO 6:HLINE(2+20*Z,150)-(12+20*Z,180),PSET,B:IF Z=2 OR Z=6 THEN 390 ELSE HLINE(15+20*Z,150)-(19+20*Z,170),PSET,BF
390 NEXT
400 '
410 ' DRAW BASS CLEF
420 '
430 HGET(25,25)-(73,65),6:HCIRCLE(32,38),5:HPAINT(33,37),1,1:HPAINT(33,39),1,1:HCIRCLE(44,38),16,1,1,.47,.15:HDRAW"BM58,42;L2D2L2D2L2":HCIRCLE(29,40),27,1,1,.1,.25:HCIRCLE(68,35),3:HPAINT(68,35),1,1:HCIRCLE(68,41),3:HPAINT(68,41),1,1
440 HGET(25,25)-(73,65),7:HPUT(25,25)-(73,65),6
450 '
460 ' DRAW TREBLE CLEF
470 '
480 HCIRCLE (44,53),12,1,1,.45,.28:HCIRCLE(42,51),18,1,1,.28,.70:HDRAW"BM32,45;R2UR2UR2UR2UR2UR2UR2UR2;U2R2U2R2U2R2;U3LULULUL3DLDLDLD33L2DL2DL4U2RDLU":HGET(25,25)-(73,65),6
490 '
500 '   INTRO NOTES
510 HCIRCLE (100,56),5:HPAINT(100,56),1,1:HLINE(104,59)-(104,42),PSET:HLINE(105,59)-(105,42),PSET:PLAY"O3;F"
520 HCIRCLE (120,52),5:HPAINT(120,51),1,1:HPAINT(120,53),1,1:HLINE(124,54)-(124,35),PSET:HLINE(125,54)-(125,35),PSET:PLAY"O3;G"
530 HCIRCLE (145,42),5:HPAINT(145,42),1,1:HLINE(140,40)-(140,57),PSET:HLINE(139,40)-(139,57),PSET:PLAY"O4;C"
540 HCIRCLE (165,38),5:HPAINT(165,39),1,1:HPAINT(165,37),1,1:HLINE(160,36)-(160,53),PSET:HLINE(159,36)-(159,53),PSET:PLAY"O4;D"
550 HGET(92,62)-(106,41),1:HGET(112,58)-(126,37),2:HGET(150,39)-(136,60),3:HGET(170,34)-(156,55),4
560 '
570 ' CHOOSE TREBLE CLEF OR BASS CLEF AND CHOOSE KEYBOARD OR NOTE
580 '
590 HLINE(415,30)-(635,61),PSET,B:HLINE(415,30)-(635,61),PSET,B:HLINE(415,30)-(633,60),PSET,B:PX=53:PY=4:MS=1:GOSUB230:PX=55:PY=5:MS=2:GOSUB230:PY=6:MS=3:GOSUB230
600 GOSUB190
610 IF Q$="T"THEN CC=1 ELSE IF Q$="B" THEN CC=2 ELSE 600
620 GOSUB270:PY=5:MS=2:GOSUB270
630 PX=55:PY=5:MS=4:GOSUB230:PY=6:MS=5:GOSUB230
640 GOSUB190
650 IFQ$="N"THEN KN=1 ELSE IF Q$="K"THEN KN=2 ELSE 640
660 HPUT(112,0)-(126,80),5:HPUT(92,0)-(106,80),5:HPUT(136,0)-(150,80),5:HPUT(156,0)-(170,80),5
669 PL=1
670 GOSUB270:PY=5:MS=4:GOSUB270:PX=53:PY=4:MS=1:GOSUB270:PX=55:PY=6:MS=7:GOSUB230:IFKN=1THEN PX=55:PY=4:MS=6:GOSUB230 ELSE PX=55:PY=4:MS=8:GOSUB230
680 '
690 ' INITIALIZE  NOTE BUFFER
700 '
710 IF CC=1 THEN N$=TC$:HPUT(25,25)-(73,65),6 ELSE N$=BC$:HPUT(25,25)-(73,65),7
720 FORZ=1TO22:N(Z)=Z:NEXT:IFCC=1 THEN TN=22 ELSE TN=18
730 '
740 ' MAIN LOOP
750 '
760 '      GET RANDOM NOTE
770 PL=RND(TN):N=N(PL)
780 NO$=MID$(N$,N,1):IF CC=1 THEN 830
790 '
800 ' DEFINE OCTAVE
810 '
820 IF N>14 THEN OC=3 ELSE IF N>7 THEN OC =2 ELSE OC=1
821 GOTO840
830 IF N>18 THEN OC=5 ELSE IF N>11 THEN OC=4 ELSE IF N>4 THEN OC=3 ELSE OC =2
840 OC$="O"+STR$(OC)
850 OE=(N-2*(INT(N/2))):IFCC=2 THEN 910 
860 ' TREBLE CLEF DEFINE NOTE VARIABLES
870 IF OE=1 AND N<11 THEN BF=2 ELSE IF OE=0 AND N<11 THEN BF=1 ELSE IF OE=1 AND N=>11 THEN BF=4 ELSE BF=3
880 IF N>10 AND OE=0 THEN Y=98 ELSE IF N>10 AND OE=1 THEN Y=97 ELSE Y=86
890 GOTO 930
900 ' BASS CLEF DEFINE NOTE VARIABLES
910 IF  OE=1 AND N<9 THEN BF=2 ELSE IF OE=0 AND N<9 THEN BF=1 ELSE IF OE=1 AND N=>9 THEN BF=4 ELSE BF=3
920 IF N>8 AND OE=0 THEN Y=91 ELSE IF N>8 AND OE=1 THEN Y=90 ELSE Y=79
930 NN=N-1
940 '
950 ' PUT NOTE ON STAFF
960 '
970 HPUT(X,Y-INT(3.5*NN))-(X+14,Y-21-INT(3.5*NN)),BF:PLAY OC$+";"+NO$:IFKN=2 THEN1120
980 '
990 'CHECK ANSWER FOR FIND NOTE
1000 '
1010 GOSUB190
1020 IF Q$="Q" THEN END ELSE IF Q$<"A" OR Q$>"G" THEN 940
1030 HPRINT(61,4),Q$
1040 IF Q$<>NO$ THEN SOUND100,2:SOUND50,3:HCOLOR3:PX=55:PY=5:MS=9:GOSUB230:HPRINT(75,5),NO$:FORT=1TO1000:NEXT:GOSUB270:HCOLOR0:HPRINT(75,5),NO$:HPRINT(61,4),Q$:HCOLOR1:HPUT(X,0)-(X+14,90),5:GOTO760
1050 ' RIGHT ANSWER
1060 SOUND200,1:FORT=1TO50:NEXT:SOUND200,1:HCOLOR2:PX=55:PY=5:MS=10:GOSUB230:FORT=1TO500:NEXT:GOSUB270
1070 FORZ=PL TO 21:N(Z)=N(Z+1):NEXT:TN=TN-1:GOSUB270:IF TN=0 THEN HCOLOR0:HPRINT(61,4),Q$:HPRINT(75,5),NO$:HCOLOR1:HPUT(X,0)-(X+14,90),5:PX=55:PY=5:MS=10:GOSUB270:PY=4:MS=6:GOSUB270:PY=6:MS=7:GOSUB270:PX=55:PY=4:MS=8:GOSUB270:GOTO590
1080 FORT=1TO500:NEXT:PX=55:PY=5:MS=9:GOSUB270:HCOLOR0:HPRINT(75,5),NO$
1090 IFKN=1 THEN HPRINT(61,4),Q$
1100 HCOLOR1:HPUT(X,0)-(X+14,90),5
1110 GOTO770
1120 '
1130 ' CHECK ANSWER FOR KEYBOARD POSITION
1140 '
1150 Q$=INKEY$:IFQ$="Q" THEN END
1160 JP=JOYSTK(0):IF INT(JP/2)*2<>JP THEN JP=JP-1
1170 HGET(6+JP*10,70)-(6+JP*10+5,180),9:HLINE(6+JP*10,70)-(6+JP*10+1,180),PSET,BF:HPUT(6+JP*10,70)-(6+JP*10+5,180),9:IF BUTTON (0)=0 THEN 1150
1180 HCOLOR3:IF CC=1 THEN NT=(N+9)*2 ELSE NT=(N-1)*2
1190 IF NT=JP THEN 1060
1200 'WRONG ANSWER
1210 PX=55:PY=5:MS=11:GOSUB230
1220 SOUND100,2:SOUND50,3:FORT=1TO10: HGET(6+NT*10,70)-(6+NT*10+5,180),9:HLINE(6+NT*10,70)-(6+NT*10+1,180),PSET,BF:HPUT(6+NT*10,70)-(6+NT*10+5,180),9:NEXT:HCOLOR1
1230 PX=55:PY=5:MS=11:GOSUB270:HPUT(X,0)-(X+14,90),5:GOTO770
