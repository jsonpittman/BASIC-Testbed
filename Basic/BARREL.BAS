10 '*>BARREL PUZZLE<*               *>BY PAUL D. BUTTACAVOLI<*      *>NOV. 1986<*
11 PALETTE RGB:ON BRK GOTO 10000
12 WIDTH 40:CLS:LOCATE11,11:ATTR0,1,B:PRINT"*>BARREL  PUZZLE<*";
13 ATTR0,1
14 LOCATE 11,23:PRINT"WANT INSTRUCTIONS? ";
15 X$=INKEY$:IF X$="" THEN 15 ELSE IF X$="Y" OR X$="y" THEN GOSUB 20000
16 CLS:LOCATE8,0:PRINT"JOYSTICK OR KEYBOARD? ";
17 J$=INKEY$:IF J$="" THEN 17
18 IF J$<>"J" AND J$<>"K" AND J$<>"j" AND J$<>"k" THEN 17
20 POKE&HFFD9,0
50 FOR X=0 TO 7
60 READ C
70 PALETTE X,C
80 NEXT X
90 DATA 63,0,15,32,53,55,20,7
100 HSCREEN 2
103 HCLS
105 C=2
110 FOR X=65 TO 265 STEP 50
120 FOR Y=20 TO 170 STEP 30
130 IF X=115 OR X=215 THEN IF Y=20 OR Y=170 THEN GOTO 150
140 HCIRCLE(X,Y),12,1:HPAINT(X,Y),C,1
150 NEXT Y
160 C=C+1
170 NEXT X
180 FOR X=65 TO 265 STEP 100
190 HPAINT(X,20),7,1
200 HPAINT(X,170),0,1
210 NEXT X
212 HPAINT(0,0),1,1
215 U=1
300 X$=INKEY$:IF X$="" THEN 300
301 ON ERR GOTO 300
303 IF X$="L" OR X$="l" GOSUB 40000:GOTO 340
310 FOR A=0 TO 40+RND(20)
315 B=RND(6):IF B=2 AND C=1 THEN 315 ELSE IF B=1 AND C=2 THEN 315 ELSE IF B=3 AND C=5 THEN 315 ELSE IF B=5 AND C=3 THEN 315 ELSE IF B=4 AND C=6 THEN 315 ELSE IF B=6 AND C=4 THEN 315
320 C=B:ON B GOSUB 1000,2000,3000,4000,5000,6000
330 NEXT A
340 ON ERR GOTO 340:IF J$="J" OR J$="j" THEN 600
500 X$=INKEY$:IF X$="" THEN 500
510 IF X$="^" THEN GOSUB  2000
520 IF X$=CHR$(10) THEN GOSUB 1000
530 IF X$=CHR$(21) THEN GOSUB 3000
540 IF X$=CHR$(93) THEN GOSUB 5000
550 IF X$=CHR$(8) THEN GOSUB 4000
560 IF X$=CHR$(9) THEN GOSUB 6000
563 IF X$="S" OR X$="s" GOSUB 30000
565 IF X$="L" OR X$="l" GOSUB 40000
570 GOTO 500
600 B=JOYSTK(0):C=JOYSTK(1):D=BUTTON(0):X$=INKEY$
610 IF B<20 AND D=0 GOSUB 4000
620 IF B>40 AND D=0 GOSUB 6000
630 IF B<20 AND D=1 GOSUB 3000
640 IF B>40 AND D=1 GOSUB 5000
650 IF C<20 GOSUB 2000
660 IF C>40 GOSUB 1000
663 IF X$="S" OR X$="s" GOSUB 30000
665 IF X$="L" OR X$="l" GOSUB 40000
670 GOTO 600
1000 REM *>DOWN<*
1005 IF U=0 THEN RETURN
1007 U=0
1010 FOR X=65 TO 265 STEP 100
1020 FOR Y=170 TO 20 STEP-30
1030 C=HPOINT(X,Y)
1040 HPAINT(X,Y+30),C,1
1050 NEXT Y,X
1060 FOR X=65 TO 265 STEP 100
1070 HPAINT(X,20),0,1
1080 NEXT X
1090 RETURN
2000 REM *>UP<*
2005 IF U=1 THEN RETURN
2007 U=1
2020 FOR X=65 TO 265 STEP 100
2030 FOR Y=50 TO 170 STEP 30
2040 C=HPOINT(X,Y)
2050 HPAINT(X,Y-30),C,1
2060 NEXT Y,X
2070 FOR X=65 TO 265 STEP 100
2080 HPAINT(X,170),0,1
2085 NEXT X
2090 RETURN
3000 REM *>TOP LEFT<*
3010 C1=HPOINT(65,50):C2=HPOINT(65,70)
3020 FOR X=115 TO 265 STEP 50
3030 C3=HPOINT(X,50):C4=HPOINT(X,80)
3070 HPAINT(X-50,50),C3,1:HPAINT(X-50,80),C4,1
3075 NEXT X
3100 HPAINT(265,50),C1,1:HPAINT(265,80),C2,1
3120 RETURN
4000 REM *>BOTTOM LEFT<*
4010 C1=HPOINT(65,110):C2=HPOINT(65,140)
4020 FOR X=115 TO 265 STEP 50
4030 C3=HPOINT(X,110):C4=HPOINT(X,140)
4070 HPAINT(X-50,110),C3,1:HPAINT(X-50,140),C4,1
4075 NEXT X
4100 HPAINT(265,110),C1,1:HPAINT(265,140),C2,1
4120 RETURN
5000 REM *>TOP RIGHT<*
5010 C1=HPOINT(265,50):C2=HPOINT(265,80)
5020 FOR X=215 TO 65 STEP-50
5030 C3=HPOINT(X,50):C4=HPOINT(X,80)
5060 HPAINT(X+50,50),C3,1:HPAINT(X+50,80),C4,1
5070 NEXT X
5100 HPAINT(65,50),C1,1:HPAINT(65,80),C2,1
5110 RETURN
6000 REM *>BOTTOM RIGHT<*
6010 C1=HPOINT(265,110):C2=HPOINT(265,140)
6020 FOR X=215 TO 65 STEP-50
6030 C3=HPOINT(X,110):C4=HPOINT(X,140)
6060 HPAINT(X+50,110),C3,1:HPAINT(X+50,140),C4,1
6070 NEXT X
6100 HPAINT(65,110),C1,1:HPAINT(65,140),C2,1
6110 RETURN
10000 POKE&HFFD8,0
10010 PALETTE RGB
10020 CLS:PRINT" THANKS FOR PLAYING!":
10030 PRINT:PRINT:PRINT:PRINT:END
20000 REM *>INSTRUCTIONS<*
20010 CLS:PRINT"   THE PUZZLE CONSISTS OF COLUMNS OF    COLORED BALLS. THERE ARE 4 BALLS OF 5   COLORS, AND 3 GRAY BALLS. THE OBJECT IS TO ARRANGE THE COLORED BALLS IN COLUMNS.THE GRAY BALLS GO AT THE TOP OF THE     LEFT, RIGHT AND CENTER COLUMNS."
20020 PRINT:PRINT"   WHEN YOU FIRST SEE THE PUZZLE, IT    WILL BE IN ONE OF A NUMBER OF POSSIBLE  SOLVED POSITIONS. PRESSING ANY KEY      EXCEPT BREAK WILL THEN RANDOMLY SCRAMBLETHE BALLS."
20030 PRINT:PRINT"   WHEN MOVING BALLS, THEY MOVE UP AND  DOWN IN GROUPS OF 15, OR LEFT AND RIGHT IN GROUPS OF 10. WHEN MOVING UP OR DOWN,YOU MOVE THE LEFT, RIGHT AND CENTER     COLUMNS. WHEN MOVING LEFT OR RIGHT, YOU MOVE THE 2 TOP ROWS, OR THE 2 BOTTOM"
20040 PRINT"ROWS."
20050 PRINT:INPUT"   PRESS <ENTER> TO CONTINUE";X$
20060 CLS:PRINT"   JOYSTICK MOVEMENT:":PRINT
20070 PRINT"FORWORD: MOVE COLUMNS UP."
20080 PRINT"BACK: MOVE COLUMNS DOWN."
20090 PRINT"LEFT: MOVE BOTTOM LEFT."
20100 PRINT"RIGHT: MOVE BOTTOM RIGHT."
20110 PRINT"LEFT+BUTTON: MOVE TOP LEFT."
20120 PRINT"RIGHT+BUTTON: MOVE TOP RIGHT."
20130 PRINT:PRINT"   KEYBOARD MOVEMENT:":PRINT
20140 PRINT"UP ARROW: MOVE COLUMNS UP."
20150 PRINT"DOWN ARROW: MOVE COLUMNS DOWN."
20160 PRINT"LEFT ARROW: MOVE BOTTOM LEFT."
20170 PRINT"RIGHT ARROW: MOVE BOTTOM RIGHT."
20180 PRINT"SHIFT LEFT ARROW: MOVE TOP LEFT."
20190 PRINT"SHIFT RIGHT ARROW: MOVE TOP RIGHT."
20193 PRINT:PRINT"PRESS S TO SAVE YOUR POSITION"
20195 PRINT"PRESS L TO LOAD YOUR OLD POSITION"
20200 PRINT:PRINT:INPUT"   PRESS <ENTER> TO BEGIN";X$
20210 RETURN
30000 REM *>SAVE<*
30010 OPEN"O",#1,"PUZZLE.DAT"
30020 PRINT#1,U
30030 FOR X=65 TO 265 STEP 50
30040 FOR Y=20 TO 170 STEP 30
30050 IF X=115 OR X=215 THEN IF Y=20 OR Y=170 THEN 30070
30060 F=HPOINT(X,Y):PRINT#1,F
30070 NEXT Y,X
30080 CLOSE #1
30090 RETURN
40000 REM *>LOAD<*
40020 OPEN"I",#1,"PUZZLE.DAT"
40030 INPUT #1,U
40040 FOR X=65 TO 265 STEP 50
40050 FOR Y=20 TO 170 STEP 30
40060 IF X=115 OR X=215 THEN IF Y=20 OR Y=170 THEN 40080
40070 INPUT #1,C:HPAINT(X,Y),C,1
40080 NEXT Y,X
40090 CLOSE #1
40100 RETURN
