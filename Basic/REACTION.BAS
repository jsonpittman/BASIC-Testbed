0 'REAXION' by Keiran Kenny,       Sydney, 1989.
1 ' COPYRIGHT 1989  FALSOFT,INC
10 DL=2
20 FORT=1TO20:CLS0
30 IFT/2=INT(T/2)THEN50
40 PRINT"RIGHT HAND";:GOTO60
50 PRINT"LEFT HAND";
60 PRINT@16,CHR$(159);
70 GOSUB280
80 IFINKEY$=CHR$(32)THENPRINT@454,"YOU JUMPED THE GUN!";:GOSUB280:CLS0:GOTO30
90 PRINT@16,CHR$(128);:P=48:FORX=1TO10
100 PRINT@P,CHR$(159);
110 PRINT@P,CHR$(128);
120 IFINKEY$=CHR$(32)THEN160
130 P=P+32
140 FORD=1TODL:NEXT
150 NEXT:X=X-1
160 IFT/2=INT(T/2)THEN180
170 SR=10-X:TR=TR+SR:PRINT@390,"RIGHT SCORE"SR"/ 10";:GOTO190
180 SL=10-X:TL=TL+SL:PRINT@390,"LEFT SCORE"SL"/ 10";
190 GOSUB280
200 K$=INKEY$:NEXTT
210 CLS0:PRINT@228,"RIGHT SCORE"TR"/ 100";
220 PRINT@260," LEFT SCORE"TL"/ 100";
230 PRINT@326,"TRY AGAIN? Y/N";
240 K$=INKEY$:IFK$=""THEN240
250 IFK$="Y"THENTR=0:TL=0:GOTO20
260 IFK$="N"THENCLS:END
270 GOTO240
280 FORD=1TO300+RND(600):NEXT:RETURN
