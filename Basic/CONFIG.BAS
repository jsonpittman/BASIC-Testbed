0 ' COPYRIGHT 1990 FALSOFT, INC
10 ' CONFIG FILE CREATOR
20 WIDTH 32:POKE &H95C9,&H57:POKE &HFF22,&H50
30 GOSUB 80
40 PRINT"   Please insert disk to save   the configuration file.....":GOSUB 80:GOSUB 90:EXEC 44539
50 GOSUB 80:PRINT"Thank you! Saving configuration file now....":GOSUB 80:GOSUB 90
60 OPEN "O",#1,"SETUP":WRITE #1,3,2,4,0,63:CLOSE
70 GOSUB 80:PRINT"Done...."::GOSUB 80:END
80 FOR T=1 TO 8:PRINT:NEXT:RETURN
90 PLAY"T10L10O4A"
100 RETURN
0 'DISK MASTER 3                   WRITTEN BY MIKE JORGENSON       COPYRIGHT (C) JANUARY 1991      BY FALSOFT, INC.
10 ' Disk Master 3
15 ' Configure File
20 ' Coco 3 Utility
25 ' Mike Jorgenson
30 '
35 CLEAR2500:D1=PEEK(2394)
40 ONERR GOTO360:ONBRK GOTO370
45 DSKI$ D1,17,18,Y$,Z$:V=VAL(MID$(Y$,1,1)):IFV<1 OR V>2THENPALETTE0,11:PALETTE8,59:GOTO65
50 BG=VAL(MID$(Y$,8,2))
55 FG=VAL(MID$(Y$,10,2))
60 PALETTE0,BG:PALETTE8,FG
65 PALETTE9,52:MID$(Y$,1,1)="1":FORX=2TO18:MID$(Y$,X,1)="0"
70 NEXT:WIDTH40:CLS1:LOCATE7,0:ATTR1,0,B:PRINT"<<< Configure Master 3 >>>":ATTR0,0
75 LOCATE3,5:PRINT"Monochrome Monitor:      <Y/N> ? ";
80 GOSUB375:IFQ$="Y"THEN MID$(Y$,1,1)="2"ELSEIFQ$<>"N"THEN80
85 PRINTQ$:LOCATE3,8:PRINT"32 Column Color Set <0-63>":PRINT
90 LINEINPUT"   Background Palette       =     ";Q$:Q=VAL(Q$):IF(Q<1AND Q$<>"0")OR Q>63THEN90
95 IFQ<10THENQ$="0"+Q$
100 MID$(Y$,2,2)=Q$
105 LINEINPUT"   Foreground Palette       =     ";Q$:Q=VAL(Q$):IF(Q<1AND Q$<>"0")OR Q>63THEN105
110 IFQ<10THENQ$="0"+Q$
115 MID$(Y$,4,2)=Q$
120 LINEINPUT"   Foreground Display       =     ";Q$:Q=VAL(Q$):IF(Q<1AND Q$<>"0")OR Q>63THEN120
125 IFQ<10THENQ$="0"+Q$
130 MID$(Y$,6,2)=Q$
135 PRINT:PRINT"   40/80 Column Colors <0-63>":PRINT
140 LINEINPUT"   Background Palette       =     ";Q$:Q=VAL(Q$):IF(Q<1AND Q$<>"0")OR Q>63THEN140
145 IFQ<10THENQ$="0"+Q$
150 MID$(Y$,8,2)=Q$
155 LINEINPUT"   Foreground Palette       =     ";Q$:Q=VAL(Q$):IF(Q<1AND Q$<>"0")OR Q>63THEN155
160 IFQ<10THENQ$="0"+Q$
165 MID$(Y$,10,2)=Q$
170 PRINT:PRINT:PRINT"   Set Text Width To 80:    <Y/N> ? ";
175 GOSUB375:IFQ$="Y"THEN MID$(Y$,12,1)="1"ELSEIFQ$<>"N"THEN175
180 PRINTQ$:PRINT:PRINT"   Want Disk Verify On:     <Y/N> ? ";
185 GOSUB375:IFQ$="Y"THEN MID$(Y$,13,1)="1"ELSEIFQ$<>"N"THEN185
190 PRINTQ$:PRINT:PRINT"   Do You Have Lowercase":PRINT"   Configed For WIDTH32:    <Y/N> ? ";
195 GOSUB375:IFQ$="Y"THEN215
200 IFQ$<>"N"THEN195
205 PRINTQ$:PRINT:PRINT"   Want Lowercase Poke:     <Y/N> ? ";
210 GOSUB375:IFQ$="Y"THEN MID$(Y$,14,1)="1"ELSEIFQ$<>"N"THEN210
215 PRINTQ$:PRINT:PRINT"   Do You Have 40-TRACK":PRINT"   Disk Drive Operation:    <Y/N> ? ";
220 GOSUB375:IFQ$="Y"THEN240
225 IFQ$<>"N"THEN220
230 PRINTQ$:PRINT:PRINT"   Want a 40-TRACK Poke:    <Y/N> ? ";
235 GOSUB375:IFQ$="Y"THEN MID$(Y$,15,1)="1"ELSEIFQ$<>"N"THEN235
240 PRINTQ$:PRINT:PRINT"   Want Step Rate Poke:     <Y/N> ? ";
245 GOSUB375:IFQ$="N"THENPRINTQ$:GOTO285 ELSEIFQ$<>"Y"THEN245
250 PRINTQ$:PRINT
255 PRINT:PRINT"   <1>  =  30 Millisecond Step Rate.":PRINT:PRINT"   <2>  =  20 Millisecond Step Rate.":PRINT:PRINT"   <3>  =  12 Millisecond Step Rate.":PRINT
260 PRINT"   <4>  =  06 Millisecond Step Rate.":PRINT:PRINT"           Enter a choice ... ";
265 GOSUB375:Q=VAL(Q$)
270 IFQ<1 OR Q>4THEN265
275 MID$(Y$,16,1)=Q$
280 PRINTQ$:PRINT
285 PRINT:PRINT"   Double-Sided Drives:     <Y/N> ? ";
290 GOSUB375:IFQ$="N"THEN310
295 IFQ$<>"Y"THEN290
300 PRINTQ$:PRINT:PRINT"   Drive Configure Poke:    <Y/N> ? ";
305 GOSUB375:IFQ$="Y"THEN MID$(Y$,17,1)="1"ELSEIFQ$<>"N"THEN305
310 PRINTQ$:PRINT:PRINT"   Printer Output Poke:     <Y/N> ? ";
315 GOSUB375:IFQ$="N"THEN350
320 IFQ$<>"Y"THEN315
325 PRINTQ$:PRINT:PRINT:PRINT"   <1>  =  0600 Baud Rate Output.":PRINT:PRINT"   <2>  =  1200 Baud Rate Output.":PRINT:PRINT"   <3>  =  2400 Baud Rate Output."
330 PRINT:PRINT"   <4>  =  4800 Baud Rate Output.":PRINT:PRINT"   <5>  =  7200 Baud Rate Output.":PRINT:PRINT"   <6>  =  9600 Baud Rate Output.":PRINT:PRINT"           Enter a choice ... ";
335 GOSUB375:Q=VAL(Q$)
340 IFQ<1 OR Q>6THEN335
345 MID$(Y$,18,1)=Q$
350 PRINTQ$:DSKO$ D1,17,18,Y$,Z$:CLEAR200:WIDTH32:PRINT"OK, YOUR DISK IS NOW CONFIGURED."
355 PRINT:END
360 PRINT:PRINT"   System Function Error Detected !!!":SOUND5,7
365 FORX=1TO1650:NEXT
370 CLEAR200:WIDTH32:END
375 Q$=INKEY$:IFQ$=""THEN375
380 RETURN
385 ' END
100 'CONFIG.BAS
105 '
110 WIDTH 32
115 PRINT "CONFIG V1": PRINT
120 READ BC
125 IF MEM-BC>=4096 THEN 140
130 PRINT "INSUFFICIENT MEMORY"
135 END
140 AD=PEEK(&H27)*256
145 AD=AD+PEEK(&H28)+1
150 CLEAR 200,AD-BC
155 AD=PEEK(&H27)*256
160 AD=AD+PEEK(&H28)+1
165 READ BC: TM=AD+BC
170 ON ERR GOTO 350
175 READ F$
180 IF F$="*" THEN 390
185 PRINT F$: F$=F$+".MOD"
190 ON ERR GOTO 360
195 OPEN "I",#1,F$: CLOSE #1
200 ON ERR GOTO 370
205 OPEN "D",#1,F$,1
210 FIELD #1,1 AS B$
215 GET #1
220 IF ASC(B$)<>0 THEN 370
225 GET #1
230 BC=ASC(B$)*256
235 GET #1
240 BC=BC+ASC(B$)
245 CLOSE #1
250 TM=TM-BC
255 IF TM<AD THEN 380
260 LOADM F$,TM
265 DEFUSR0=TM: ST=USR0(0)
270 IF ST=-1 THEN 295
275 IF ST=-2 THEN 315
280 PRINT "  LOADED ";
285 PRINT "MS =";BC;"BI =";BC-ST
290 TM=TM+ST: GOTO 170
295 TM=TM+BC
300 PRINT "  NOT LOADED ";
305 PRINT "[IN MEMORY]"
310 GOTO 170
315 TM=TM+BC
320 PRINT "  NOT LOADED ";
325 PRINT "[NEEDS MODULE]"
330 GOTO 170
335 '
340 'ERROR HANDLER
345 '
350 PRINT "OUT OF DATA"
355 GOTO 390
360 PRINT F$+" NOT FOUND"
365 GOTO 390
370 PRINT F$+" CORRUPT"
375 UNLOAD: GOTO 390
380 PRINT F$+" TOO BIG"
385 TM=TM+NB
390 CLEAR 200,TM
395 END
400 '
405 'NUMBER OF BYTES TO RESERVE
410 '
415 DATA 2048
