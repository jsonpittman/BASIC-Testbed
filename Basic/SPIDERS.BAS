0 ' COPYRIGHT 1990  FALSOFT,INC
1 'SPIDERS
2 'BY JOSEPH PENDELL
10 Q=5:'SPEED OF SPIDERS
20 FORI=1TO5:HBUFF I,700:NEXTI
30 DATA 0,47,25,61
40 DATA"BM122,99;R2;BR2;R2;BR2;R2;BR2;R2","BM122,99;NU1;M+6,+1;R2;M+6,-1;U1","BM124,100;U1;R8;D1;L8","BM122,100;M+6,-1;R2;M+6,+1","BM124,99;R8"
50 FORI=0TO3:READC:PALETTE I,C:NEXTI
60 HSCREEN4
70 FORI=1TO5:D(I)=RND(3):HDRAW"C"+STR$(D(I))
80 HCIRCLE(128,96),15:'BODY
90 HDRAW"BM140,91;M+15,-5;M+15,+2;BM142,94;M+12,-3;M+15,+3;BM142,97;M+12,-2;M+15,+4;BM140,100;M+15,-1;M+15,+5":'RIGHT LEGS
100 HDRAW"BM116,91;M-15,-5;M-15,+2;BM114,94;M-12,-3;M-15,+3;BM114,97;M-12,-2;M-15,+4;BM116,100;M-15,-1;M-15,+5":'LEFT LEGS
110 HDRAW"BM124,94;NU1;NR1;NL1;ND1":'LEFT EYE
120 HDRAW"BM131,94;NU1;NR1;NL1;ND1":'RIGHT EYE
130 HDRAW"BM128,96;D1":'NOSE
140 HDRAW"BM128,89;U13":'WEB
150 READ M$:HDRAW M$:'MOUTH
160 HGET(85,76)-(172,105),I
170 HCLS
180 NEXTI
190 FORI=1TO5:A(I)=0:HPUT((I-1)*130,0)-((I-1)*130+88,29),I,PSET:NEXTI
200 I=RND(5):A(I)=A(I)+Q:IFA(I)>162 THEN 240
210 HPUT((I-1)*130,A(I)+1)-((I-1)*130+88,A(I)+30),I,PSET
220 HPUT((I-1)*130,A(I))-((I-1)*130+88,A(I)+29),I,PSET
230 GOTO200
240 HCLS:HDRAW"C"+STR$(D(I))+"BM320,90;U90":HPUT(276,85)-(364,114),I,PSET:FORK=1TO10:J=RND(63):FORL=1TO4:PALETTE L,J:PLAY"L255T255CDEFGAB":NEXTL,K:PALETTE CMP:END
