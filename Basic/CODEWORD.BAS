10 REM"SPELLING WORD CODE"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1989"
30 XY=RND(-TIMER):N=20:TIMER=0
40 DIM A$(N)
50 FOR T= 1 TO N:READ A$(T):NEXT T
60 CLS:PRINT@0,"code   code word   spelling word";
70 R$=STRING$(32,175)
80 PRINT@32,R$;
90 L=65:R=64:S=66
100 FOR T=1 TO 13:PRINT@R,CHR$(L);"-":R=R+32:L=L+1:NEXT T
110 FOR T=1 TO 13:PRINT@S,CHR$(L):S=S+32:L=L+1:NEXT T
120 FOR T=0 TO 12:POKE1091+(T*32),175:NEXT T
130 FOR T=1475 TO 1503:POKE T,175:NEXT T
140 FOR TT=1 TO 5
150 PRINT@68+M,TT;
160 X=RND(N):Y=LEN(A$(X))
170 FOR T= 1 TO Y
180 B$=MID$(A$(X),T,1)
190 P=ASC(B$)
200 IF P>77 THEN P=P-13 ELSE P=P+13:REM THIS IS WHERE THE SWITCH OCCURS
210 PLAY"L80BAG"
220 PRINT CHR$(P);
230 NEXT T
240 PRINT@81+M,"";:LINEINPUT G$
250 IF G$=A$(X) THEN PLAY"L8CDEFGGG":CR=CR+1
260 IF G$<>A$(X) THEN SOUND 10,3:PRINT@81+M,A$(X)
270 M=M+64
280 NEXT TT
290 FOR T=1 TO 5:PLAY"L20CEG":NEXT T:PRINT@392,"YOU DID";CR;"CORRECT";
300 TM=INT(TIMER/60):PRINT@426,"IN";TM;"SECONDS.";
310 PRINT@488,"PRESS 'e' OR 'c'";
320 EN$=INKEY$
330 IF EN$="E" THEN 340 ELSE IF EN$="C" THEN RUN ELSE 320
340 CLS:END
350 DATA ONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN,EIGHT,NINE,TEN
360 DATA ELEVEN,TWELVE,THIRTEEN,FOURTEEN,FIFTEEN,SIXTEEN,SEVENTEEN,EIGHTEEN,NINETEEN,TWENTY
