0 'MINEFIELD
1 'BY KENNETH REIGHARD, JR.
2 'COPYRIGHT (C) 1992
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
5 GOTO 10
6 HPAINT(H*8+16,V*8+16),0,2:RETURN
7 HPAINT(H*8+16,V*8+16),4,2:RETURN
8 HPAINT(H*8+16,V*8+16),3,2:RETURN
9 HPAINT(H*8+16,V*8+16),1,2:RETURN
10 CLS:INPUT"MONITOR (R/C)";Q$
15 IF Q$="C" THEN PALETTE CMP ELSE PALETTE RGB
20 POKE 65497,0
25 DIM M(29,19), N(29,19), N$(3), T(3), SX(100), SY(100)
30 WIDTH40:CLS3:ATTR 3,2
35 HCOLOR 3,8:HSCREEN 2
40 HPRINT(15,1),"MINEFIELD!":HPRINT(8,3),"By Kenneth Reighard, Jr."
45 HPRINT(16,6),"1. Easy":HPRINT(16,7),"2. Medium":HPRINT(16,8),"3. Hard"
50 Q$=INKEY$:SK=VAL(Q$):IF SK<1 OR SK>3 THEN 50
55 HPRINT(10,20),"One Moment Please..."
60 ON SK GOTO 65,70,75
65 MX=9:MY=9:B=10:GOTO 80
70 MX=19:MY=14:B=45:GOTO 80
75 MX=29:MY=19:B=99
80 L=B
85 F$="C8BD6BR4U5FL":B$="C8BD6BR4H2E2F2G2U4"
90 R=(MX+1)*(MY+1)-B
95 H=RND(-TIMER)
100 FOR Z=1 TO B
105 H=RND(MX+1)-1:V=RND(MY+1)-1
110 IF M(H,V)=-1 THEN 105 ELSE M(H,V)=-1
115 NEXT Z
120 FOR V=0 TO MY:FOR H=0 TO MX
125 IF M(H,V)<>-1 THEN 155
130 FOR Y=-1 TO 1:FOR X=-1 TO 1
135 IF X=0 AND Y=0 THEN 150
140 IF H+X<0 OR H+X>MX OR V+Y<0 OR V+Y>MY THEN 150
145 IF M(H+X,V+Y)<>-1 THEN M(H+X,V+Y)=M(H+X,V+Y)+1
150 NEXT X,Y
155 NEXT H,V
160 HCOLOR 2,8:HCLS
165 HPRINT(34,0),"Mines"
170 HLINE(15,15)-(MX*8+23,MY*8+23),PSET,B
175 HPAINT(16,16),4,2
180 FOR X=0 TO MX
185 HLINE(X*8+15,15)-(X*8+15,23+MY*8),PSET
190 NEXT X
195 FOR Y=0 TO MY
200 HLINE(15,Y*8+15)-(23+MX*8,Y*8+15),PSET
205 NEXT Y
210 H=0:V=0:T=0
215 TIMER=0
220 HCOLOR 7:HLINE(H*8+15,V*8+15)-(H*8+23,V*8+23),PSET,B
225 IF R=0 THEN 440
230 T=T+TIMER:TIMER=0
235 HCOLOR 2:HLINE(288,8)-(304,16),PRESET,BF:HPRINT(35,1),L
240 Q$=INKEY$:IF Q$="" THEN 240
245 HCOLOR 2:HLINE(H*8+15,V*8+15)-(H*8+23,V*8+23),PSET,B
250 IF Q$=CHR$(8) THEN H=H-1:IF H<0 THEN H=MX:GOTO 220 ELSE 220
255 IF Q$=CHR$(9) THEN H=H+1:IF H>MX THEN H=0:GOTO 220 ELSE 220
260 IF Q$=CHR$(10) THEN V=V+1:IF V>MY THEN V=0:GOTO 220 ELSE 220
265 IF Q$=CHR$(94) THEN V=V-1:IF V<0 THEN V=MY:GOTO 220 ELSE 220
270 IF Q$=CHR$(13) THEN IF N(H,V)=0 AND L>0 THEN GOSUB 9:HDRAW"BM"+STR$(H*8+15)+","+STR$(V*8+15)+F$:N(H,V)=1:L=L-1:GOTO 220 ELSE IF N(H,V)=1 THEN GOSUB 7:N(H,V)=0:L=L+1:GOTO 220 ELSE 220
275 IF Q$<>CHR$(32) THEN 220
280 IF N(H,V)<>0 THEN 220
285 C=M(H,V)
290 GOSUB 6
295 IF C=-1 THEN 400
300 R=R-1
305 IF C=0 THEN N(H,V)=3:GOTO 325
310 HCOLOR 3:HPRINT(H+2,V+2),CHR$(C+48)
315 N(H,V)=2
320 GOTO 220
325 OH=H:OV=V
330 ST=1:SX(ST)=H:SY(ST)=V
335 IF ST=0 THEN H=OH:V=OV:GOTO 220
340 X=SX(ST):Y=SY(ST)
345 FOR H=X-1 TO X+1:FOR V=Y-1 TO Y+1
350 IF H<0 OR H>MX OR V<0 OR V>MY THEN 390
355 IF N(H,V)<>0 THEN 390
360 IF M(H,V)>0 THEN N(H,V)=2:R=R-1:GOSUB 6:HCOLOR 3:HPRINT(H+2,V+2),CHR$(M(H,V)+48):GOTO 390
365 IF M(H,V)=-1 THEN 390
370 GOSUB 6
375 N(H,V)=3
380 R=R-1
385 ST=ST+1:SX(ST)=H:SY(ST)=V:GOTO 340
390 NEXT V,H
395 ST=ST-1:GOTO 335
400 GOSUB 8:HDRAW"BM"+STR$(H*8+15)+","+STR$(V*8+15)+B$:PLAY"O1L255V31CDEFGAEGFDCGFV15"
405 FOR V=0 TO MY:FOR H=0 TO MX
410 IF M(H,V)=-1 THEN GOSUB 8:HDRAW"BM"+STR$(H*8+15)+","+STR$(V*8+15)+B$
415 IF N(H,V)=1 AND M(H,V)<>-1 THEN GOSUB 9:HDRAW"C3BM"+STR$(H*8+15)+","+STR$(V*8+15)+"F8BU8G8"
420 NEXT H,V
425 HCOLOR 2:HPRINT(14,23),"Press Key..."
430 Q$=INKEY$:IF Q$="" THEN 430
435 GOTO 480
440 T=T+TIMER:T=INT(T/60)
445 HCOLOR2:HPRINT(15,22),"Time="+STR$(T):HLINE(H*8+15,V*8+15)-(H*8+23,V*8+23),PSET,B
450 FOR V=0 TO MY:FOR H=0 TO MX
455 IF M(H,V)=-1 AND N(H,V)<>1 THEN GOSUB 9:HDRAW"BM"+STR$(H*8+15)+","+STR$(V*8+15)+F$
460 NEXT H,V
465 PLAY"O2L4CEFL2GL4DFGL2O3CP2
470 HCOLOR 2:HPRINT(14,23),"Press Key..."
475 Q$=INKEY$:IF Q$="" THEN 475
480 POKE65496,0
485 OPEN "I",#1,"BESTMINE.DAT"
490 INPUT #1,N$(1),T(1),N$(2),T(2),N$(3),T(3)
495 CLOSE #1
500 HSCREEN 0
505 CLS
510 NS=0:IF R=0 THEN IF T<T(SK) THEN PRINT"You've beat the best time!":LINEINPUT"Enter your name :";N$:T(SK)=T:N$(SK)=N$:NS=1
515 PRINT:PRINT"Best Times":PRINTSTRING$(40,42);
520 PRINTUSING"Easy     #### ";T(1);:PRINTN$(1)
525 PRINTUSING"Medium   #### ";T(2);:PRINTN$(2)
530 PRINTUSING"Hard     #### ";T(3);:PRINTN$(3)
535 IF NS=0 THEN 555
540 OPEN "O",#1,"BESTMINE.DAT"
545 WRITE#1,N$(1),T(1),N$(2),T(2),N$(3),T(3)
550 CLOSE#1
555 PRINTSTRING$(40,42);:PRINT:PRINT"Play Again (Y/N)?"
560 Q$=INKEY$:IF Q$="Y" THEN RUN 20 ELSE IF Q$="N" THEN END ELSE 560
565 END
570 OPEN "O",#1,"BESTMINE.DAT"
575 WRITE#1,"",9999,"",9999,"",9999
580 CLOSE#1
585 END
