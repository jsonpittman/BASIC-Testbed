0 'GRAPHICS EXPERIMENTS
1 'WRITTEN BY KEIRAN KENNY
2 'COPYRIGHT (C) MARCH 1991
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
10 ONBRKGOTO430
20 RGB
30 GOTO180
40 PB=P:VP$=""
50 KY$=INKEY$:IFKY$=""THEN50
60 IFKY$=CHR$(13)THEN170
70 IFKY$=CHR$(8)THEN80ELSE130
80 P=P-1:IFP<PB THENP=PB:GOTO50
90 DR=HPOINT(P*8,C*8+1)
100 HCOLORDR:HPRINT(P,C),RIGHT$(VP$,1)
110 VP$=LEFT$(VP$,LEN(VP$)-1)
120 GOTO50
130 IFP>78THEN50ELSEHCOLOR1:HPRINT(P,C),KY$
140 VP$=VP$+KY$
150 P=P+1
160 GOTO50
170 V=VAL(VP$):RETURN
180 PALETTE0,0
190 HSCREEN4
200 X=80*.7
210 Y=40*.7
220 HCIRCLE(320,96),80
230 HDRAW"BM320,96NU40NM+=X;,-=Y;NR80NM+=X;,+=Y;ND40NM-=X;,+=Y;NL80NM-=X;,-=Y;"
240 HPRINT(33,23),"PRESS ANY KEY"
250 EXEC44539:K$=INKEY$:HCLS
260 HPRINT(0,0),"ENTER AN ANGLE (0 - 88 DEGREES):"
270 P=33:C=0:GOSUB40
280 IFV<0ORV>88THENHCOLOR0:HLINE(264,0)-(288,7),PSET,BF:HCOLOR1:GOTO260
290 X2=COS(V/57.29577951)
300 Y2=SIN(V/57.29577951)
310 X=300*X2
320 Y=150*Y2
330 HPRINT(0,2),"X=300*"+RIGHT$(STR$(X2),LEN(STR$(X2))-1)+" = COS("+VP$+")"
340 HPRINT(0,4),"Y=176*"+RIGHT$(STR$(Y2),LEN(STR$(Y2))-1)+" = SIN("+VP$+")"
350 HPRINT(0,6),"HDRAW"+CHR$(34)+"BM320,176M+=X;,-=Y;"+CHR$(34)
360 HDRAW"BM320,176M+=X;,-=Y;"
370 FORZ=320TO320+X STEP6:HSET(Z,176):NEXT
380 FORW=176TO176-Y STEP-3:HSET(320+X,W):NEXT
390 HPRINT(39+INT(X/2/8),23),INT(X)
400 HPRINT(40+INT(X/8),21-INT(Y/2/8)),INT(Y)
410 HPRINT(0,23),"PRESS ANY KEY."
420 EXEC44539:K$=INKEY$:HCLS:GOTO260
430 RGB:CLS:END
