100 'MKDAT.BAS
105 '
110 CLEAR 200,&H7000
115 WIDTH 32
120 PRINT "MKDAT V1": PRINT
125 INPUT "FILE";F$
130 CLOADM F$
135 IF PEEK(&H1E2)=2 THEN 145
140 PRINT "INVALID FILE": END
145 SZ=PEEK(&H7E)*256
150 SZ=SZ+PEEK(&H7F)-&H7000
155 OPEN "O",#1,F$+".DAT"
160 PRINT #1,"900 '"+F$
165 PRINT #1,"901 '"
170 PRINT #1,"902 DATA "+F$
175 LN=903: AD=&H7000
180 LN$=STR$(LN)
185 LN$=RIGHT$(LN$,LEN(LN$)-1)
190 PRINT #1,LN$;
195 PRINT #1,"DATA ";
200 NB=8
205 IF SZ<8 THEN NB=SZ
210 FOR I=0 TO NB-1
215 B$=HEX$(PEEK(AD+I))
220 IF LEN(B$)=1 THEN B$="0"+B$
225 PRINT #1,B$;
230 IF I<NB-1 THEN PRINT #1,",";
235 NEXT I
240 PRINT #1,CHR$(13)
245 SZ=SZ-NB: AD=AD+NB
250 LN=LN+1
255 IF SZ<>0 THEN 180
260 LN$=STR$(LN)
265 LN$=RIGHT$(LN$,LEN(LN$)-1)
270 PRINT #1,LN$;
275 PRINT #1,"DATA *"
280 CLOSE #1
