10 REM"SCALE DRAWINGS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1987
30 CLS0:PRINT@3,"SCALE DRAWINGS";
40 N=N+1:PRINT@26,"#=";N;
50 PRINT@90,"C=";CR;
60 IF N=11 THEN PRINT@26,"TOTALS";:PRINT@154,CR*10;"%";:PRINT@482,"PRESS G TO GO ON OR E TO END";:GOTO 330
70 B=7+RND(55)
80 FOR T=0 TO 7:SET(T,7,7):NEXT T
90 X1=RND(5):X2=RND(5)
100 X=X1*X2*10
110 R=RND(3):IF R=1 THEN R$="FEET" ELSE IF R=2 THEN R$="MILES" ELSE R$="IN."
120 PRINT@104,"=";
130 PRINT@108,X;R$;
140 FOR T=0 TO B:SET(T,13,8):NEXT T
150 PRINT@256,"THE RED LINE IS ABOUT HOW LONG?"
160 AN=B/8:AN=INT(AN+.5)
170 REM"THE VARIABLE Y IS THE CORRECT ANSWER"
180 Y=X*AN
190 U=RND(2):IF U=1 THEN Y1=Y*2 ELSE Y1=Y/4
200 V=RND(2):IF V=1 THEN Y2=Y/2 ELSE Y2=Y*4
210 R=RND(3):PRINT@320," "
220 IF R=1 THEN PRINT@320,Y;:PRINT@330,Y1;:PRINT@340,Y2;
230 IF R=2 THEN PRINT@320,Y1;:PRINT@330,Y2;:PRINT@340,Y;
240 IF R=3 THEN PRINT@320,Y2;:PRINT@330,Y;:PRINT@340,Y1;
250 PRINT@326,"OR";:PRINT@336,"OR";:PRINT@346,R$;
260 PRINT@384,"";:LINE INPUT EN$
270 PRINT@394,"";:EN=VAL(EN$)
280 IF EN=Y THEN PRINT"CORRECT":CR=CR+1:PLAY"L50CDECDEGGEDC"
290 IF EN<>Y THEN PRINT"THE ANSWER IS"Y:PLAY"L50CCC"
300 PRINT@453,"PRESS ENTER TO GO ON";
310 Q$=INKEY$
320 IF Q$=CHR$(13) THEN 30 ELSE 310
330 PLAY"GGGEEECCCEGEC"
340 Q$=INKEY$
350 IF Q$="G" THEN RUN ELSE IF Q$="E" THEN 360 ELSE 340
360 CLS:END
