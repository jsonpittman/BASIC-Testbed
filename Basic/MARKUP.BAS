0 ' COPYRIGHT 1990 FALSOFT, INC
10 'MARK-UP +-+-+ DISCOUNT'
20 'BY GORDON E. FOUTS'
30 'THIS PROGRAM WAS WRITTEN FOR    THE TRS-80 COLOR COMPUTER'
40 'FOR NON-EXTENDED BASIC,         DELETE LINES 60 THRU 260'
50 'FOR THE MC-10 COMPUTER          CHANGE "PRINT#-2" TO            "LPRINT" IN LINES: 470, 600,    720, AND 850'
60 'GRAPHIC SCREEN (+%-)'
70 PMODE4,1:PCLS:SCREEN1,1
80 CIRCLE(100,50),11,,.75
90 FOR T=0TO1000:NEXT T
100 CIRCLE(102,45),3,,1,.25,.75
110 CIRCLE(115,24),50,,.5,.05,.3
120 FOR T=0TO1000:NEXT T
130 LINE(165,30)-(95,135),PSET
140 FOR T=0TO1000:NEXT T
150 CIRCLE(155,125),11,,.75
160 FOR T=0TO1200:NEXT T
170 FOR D=62TO97STEP1
180 LINE(50,D)-(50,D),PSET
190 NEXT D
200 FOR T=0TO700:NEXT T
210 FOR A=32TO67STEP1
220 LINE(A,79)-(A,79),PSET:NEXT A
230 FOR T=0TO2000:NEXT T
240 FOR M=183TO218STEP1
250 LINE(M,79)-(M,79),PSET:NEXT M
260 FOR T=0TO3000:NEXT T:PCLS
270 'MARK-UP +%- DISCOUNT'
280 CLS:FOR T=0TO1000:NEXT T
290 PRINT@134,"MARK-UP +%- DISCOUNT"
300 FOR T=0TO1000:NEXT T
310 PRINT@224,"WHICH DO YOU WANT TO DO?
320 PRINT@290,"MARK-UP":PRINT@323,"OR"
330 INPUT"  DISCOUNT     <M/D>";MD$
340 PRINT
350 INPUT"  ARE YOU SURE     <Y/N>";YN$
360 IF YN$="Y"THEN380ELSE370
370 CLS:GOTO310
380 CLS:IF MD$="M"THEN390ELSE640
390 FOR T=0TO200:PRINT@106,"MARKING-UP":NEXT T
400 PRINT@192,"WHAT PERCENTAGE OF MARK-UP?":PRINT
410 PRINT@258,"TYPE THE PERCENTAGE":PRINT@290,"AS A DECIMAL":PRINT
420 INPUT"  -- 4%=.04 -- 40%=.40 -- ";MP
430 PRINT:INPUT"  ARE YOU SURE  <Y/N>";YN$
440 IF YN$="Y"THEN470ELSE450
450 CLS:GOTO400
460 'FOR MC-10 COMPUTER, CHANGE      THE TWO "PRINT#-2"'S TO         "LPRINT" IN THE NEXT LINE'
470 PRINT#-2,"MARKING-UP BY  ++++ ";MP:PRINT#-2,""
480 CLS:PRINT@42,"MARKING-UP"
490 PRINT
500 PRINT"INCREASING WHAT AMOUNT?":PRINT
510 PRINT"TYPE <0000>"
520 INPUT"AND <ENTER> TO QUIT ---- ";M:PRINT
530 IF M=0000THEN890
540 AM=M*MP:MS=M+AM
550 PRINT"MARK-UP FROM:          ";M
560 PRINT"MARK-UP PERCENTAGE:    ";MP
570 PRINT"AMOUNT OF MARK-UP:     ";AM
580 PRINT"NEW MARKED-UP AMOUNT:  ";MS
590 'FOR MC-10 COMPUTER CHANGE      THE "PRINT#-2" TO "LPRINT"      IN THE NEXT LINE'
600 PRINT#-2,M;"+++";AM;"="MS:M=0:AM=0:MS=0
610 PRINT:PRINT"HIT ANY KEY TO GO ON"
620 G$=INKEY$
630 IF G$=""THEN620ELSE480
640 FOR T=0TO200:PRINT@106,"DISCOUNTING":NEXT T
650 PRINT@192,"WHAT PERCENTAGE OF DISCOUNT?":PRINT
660 PRINT@258,"TYPE THE PERCENTAGE":PRINT@290,"AS A DECIMAL":PRINT
670 INPUT"  -- 4%=.04 -- 40%=.40 -- ";DP
680 PRINT:INPUT"  ARE YOU SURE       <Y/N>";YN$
690 IF YN$="Y"THEN720ELSE700
700 CLS:GOTO650
710 'FOR MC-10 COMPUTER CHANGE      THE TWO "PRINT#-2"'S TO         "LPRINT" IN THE NEXT LINE'
720 PRINT#-2,"DISCOUNTING BY  ---- ";DP:PRINT#-2,""
730 CLS:PRINT@42,"DISCOUNTING"
740 PRINT
750 PRINT"DECREASING WHAT AMOUNT?":PRINT
760 PRINT"TYPE <0000>"
770 INPUT"AND <ENTER> TO QUIT ---- ";D:PRINT
780 IF D=0000THEN890
790 AD=D*DP:DS=D-AD
800 PRINT"DISCOUNT FROM:       ";D
810 PRINT"DISCOUNT PERCENTAGE: ";DP
820 PRINT"AMOUNT OF DISCOUNT:  ";AD
830 PRINT"NEW REDUCED AMOUNT:  ";DS
840 'FOR MC-10 COMPUTER CHANGE      THE "PRINT#-2" TO "LPRINT"      IN THE NEXT LINE'
850 PRINT#-2,D;"---";AD;"=";DS:D=0:AD=0:DS=0
860 PRINT:PRINT"HIT ANY KEY TO GO ON"
870 O$=INKEY$
880 IF O$=""THEN870ELSE730
890 CLS
900 PRINT@134,"MARK-UP +%- DISCOUNT"
910 PRINT@290,"DO YOU WANT TO START AGAIN?"
920 PRINT:INPUT"   --- <Y/N> ---   ";R$
930 IF R$="Y"THEN270ELSE940
940 CLS
950 PRINT@134,"MARK-UP +%- DISCOUNT"
960 PRINT@320,"TO BEGIN AGAIN --"
970 PRINT"TYPE <RUN> AND <ENTER>"
980 PRINT:PRINT:PRINT
