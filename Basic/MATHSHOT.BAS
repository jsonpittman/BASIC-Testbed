10 REM"MATHSHOOT"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1989"
30 CLS0:TIMER=0:A=5+RND(15):G=5+RND(15):IF A=G THEN 30:REM A=TARGET AND G=RUNNING TOTAL
40 FOR T=1024 TO 1052 STEP 4
50 RR=RND(2):IF RR=1 THEN R=43 ELSE R=45
60 NN=48+RND(9)
70 POKE T,R:POKE T+1,NN:NEXT T
80 FOR T=1056 TO 1087:POKE T,156:NEXT T:FOR T=1440 TO 1471:POKE T,156:NEXT T
90 X=32:Y=24:C=8
100 RESET(X-1,Y):RESET(X+1,Y):SET (X,Y,C)
110 A$=INKEY$
120 IF A$=CHR$(9) THEN X=X+1
130 IF A$=CHR$(8) THEN X=X-1
140 IF X<2 THEN X=1
150 IF X>61 THEN X=62
160 IF A$=CHR$(32) THEN GOSUB 210
170 M=INT(TIMER/60):PRINT@448,A;:PRINT@474,G;:PRINT@460,"TIMER";M;
180 IF M>99 THEN PRINT@458," TIME IS UP ";:GOTO 410
190 IF A$="" THEN 110
200 GOTO 100
210 PLAY"O4L150A"
220 FOR Y=22 TO 6 STEP -2:PLAY"CDC":SET(X,Y,8):NEXT Y
230 FOR Y=24 TO 6 STEP -1
240 RESET(X,Y):RESET(X+1,Y):NEXT Y
250 SET(X,24,8)
260 Y=24
270 H=PEEK(INT(X/2)+1024)
280 IF H<48 THEN 380
290 IF H>58 THEN 380
300 H$=CHR$(H):J=VAL(H$)
310 I=PEEK(INT(X/2)+1023)
320 K=PEEK(INT(X/2)+1022)
330 IF I=45 THEN G=G-J
340 IF K=45 THEN G=G-J
350 IF I=43 THEN G=G+J
360 IF K=43 THEN G=G+J
370 PLAY"L50GFEDC"
380 PRINT@448,A;:PRINT@474,G;
390 IF G=A THEN PRINT@458,"YOU WIN !!!";:GOTO 410
400 RETURN
410 FOR T=1 TO 5:PLAY"CDEFGFEDC":NEXT T
420 E$=INKEY$
430 IF E$=CHR$(13) THEN RUN ELSE IF E$="E" THEN 440 ELSE 420
440 CLS:END
