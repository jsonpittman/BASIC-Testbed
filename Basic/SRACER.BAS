1 'SEASON RACER
2 'BY JOEL MATHEW HEGBERG
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
40 '****************************
50 ' JOEL MATHEW HEGBERG.
60 ' 936 NORTH TWELFTH STREET
70 ' DE KALB, ILLINOIS  60115
80 '****************************
100 CLEAR1000:DIMPL(15)
105 FORT=0TO15:PL(T)=PEEK(&HFFB0+T):NEXTT
110 ON BRK GOTO 210
120 CLS:PRINT"ARE YOU USING A (C)MP OR (R)GB  MONITOR? (R/C)"
130 A$=INKEY$:IFA$=""THEN130
140 IFA$="R"THEN RGB:ELSEIFA$="C"THEN CMP:ELSE140
150 SOUND250,2
160 GOTO220
170 PRINT A$;:RETURN
180 PLAY"V10O1T4L4C":RETURN
190 RETURN
200 FORT=0TO62:SET(T,0,CL):SET(63,T/2,CL):SET(63-T,31,CL):SET(0,31-T/2,CL):NEXTT:RETURN
210 CLS:POKE65496,0:PRINT"THANK YOU FOR PLAYING SEASON":PRINT"RACER.":FORT=0TO15:POKE&HFFB0+T,PL(T):NEXTT:NEW
220 WIDTH32:SCREEN0,0:PALETTE8,12:GOSUB190:POKE65497,0:CLS:PRINT@33,"SEASON RACER":PRINT" (C) JULY 18, 1989 BY":PRINT" JOEL MATHEW HEGBERG.":PRINT:PRINT" ALL RIGHTS RESERVED!"
230 PRINT@321,"LAST SCORE:";SC:PRINT@449,;:A$="PRESS 'P' TO PLAY.":GOSUB170:CL=3:GOSUB200:TIMER=0
240 IFTIMER>600THEN900:ELSEA$=INKEY$:IFA$=""THEN240
250 IFA$="Q"THEN160:ELSEIFA$<>"P"THENGOSUB180:GOTO240
260 GOSUB190:MN=5:ZZ=0:TM=0:TV=0:SC=0:PALETTE13,18:PALETTE0,18:PALETTE8,0:D2=10:D3=11:BK=0:LV=0
270 CLS:GOSUB880
280 GOSUB830:T=RND(-TIMER):CLS:FORT=0TO15:POKE1025+T*32,207:POKE1054+T*32,207:NEXTT:PRINT@462,"spring";
290 CX=15:FORT=0TO2:FORTT=64TO160STEP32:PRINT@TT+CX,"   ";:NEXTTT:SOUND50,5:PRINT@64+CX,C1$;:PRINT@96+CX,CHR$(135)+CHR$(175)+CHR$(139);:PRINT@128+CX,C1$;:PRINT@160+CX,C3$;
300 FORTT=0TO300:NEXTTT,T:SOUND100,10:B$="":PT=0:BC=0:T2=D2:T3=D3
310 FORT=64TO160STEP32:PRINT@T+CX,"   ";:NEXTT:GOTO410
320 TM=TM+1:IFTM<TV THEN350:ELSETM=0:A$=INKEY$:IFA$=""THENA$=B$:ELSEB$=A$
330 IFA$=CHR$(8)THENCX=CX-1:ELSEIFA$=CHR$(9)THENCX=CX+1
340 IFA$="Q"THEN160
350 P(1)=PEEK(1184+CX):P(2)=PEEK(1185+CX):P(3)=PEEK(1186+CX):P(4)=PEEK(1088+CX):P(5)=PEEK(1090+CX):PRINT@64+CX,C1$;:PRINT@96+CX,CHR$(135)+CHR$(175)+CHR$(139);:PRINT@128+CX,C1$;:PRINT@160+CX,C3$;
360 FORT=1TO5:IFP(T)<>96THEN560
370 NEXTT:SC=SC+5
380 ZZ=ZZ+1:IFZZ=100THEN500:ELSEIFZZ=200THEN510:ELSEIFZZ=300THEN520
390 IFZZ=387THEN550:ELSEIFZZ=400THEN530:ELSEIFZZ=500THEN540:ELSEIFZZ>509THEN600
400 GOTO310
410 IFPT>0THENGOSUB480:GOTO420:ELSET3=T3-1:IFT3<1THENT3=D3:PT=4:PX=RND(25)
420 IFBC>0THENGOSUB460:GOTO430:ELSET2=T2-1:IFT2<1THENT2=D2:BC=4:BX=RND(26)
430 IFRND(0)>.95THENPRINT@481+RND(27),CHR$(175);
440 PRINT@481,CHR$(207);:PRINT@510,CHR$(207)
450 GOTO320
460 BC=BC-1:IFBC=3THENPRINT@481+BX,C1$;:RETURN:ELSEIFBC=2THENPRINT@481+BX,CHR$(135);CHR$(239);CHR$(139);:RETURN
470 IFBC=1THENPRINT@481+BX,C1$;:RETURN:ELSEPRINT@481+BX,C3$;:RETURN
480 PT=PT-1:IFPT=3OR PT=0THENPRINT@481+PX+1,STRING$(2,128);:RETURN:ELSEIFPT=2OR PT=1THENPRINT@481+PX,STRING$(4,128);:RETURN
490 PRINT@481+PX+1,STRING$(2,128);:RETURN
500 D2=9-LV:D3=10-LV:PALETTE13,54:PALETTE0,54:PRINT@462,"summer";:GOTO310
510 D2=6-LV:D3=8-LV:PALETTE13,38:PALETTE0,38:PRINT@462,"autumn";:GOTO310
520 D2=4-LV:D3=6-LV:PALETTE13,63:PALETTE0,63:PRINT@462,"winter";:TV=2:GOTO310
530 D2=2-LV:D3=4-LV:PALETTE13,0:PALETTE0,0:PALETTE8,1:PRINT@462,"tunnel";:TV=0:GOTO310
540 D2=0:D3=0:PALETTE13,44:PALETTE8,0:PALETTE0,44:PRINT@462,"finish";:GOTO310
550 PRINT@459,"tunnel ahead";:GOTO310
560 IFP(T)<127THEN370
570 IFP(T)=175THEN810:ELSEIFP(T)=128THEN SC=SC-100:ELSEIFP(T)=207THEN SC=SC-200:ELSESC=SC-150
580 IFSC<0THENSC=0
590 GOTO700
600 FORT=0TO1500:NEXTT
610 CLS:PALETTE13,18:PALETTE0,18:PRINT@65,C1$:PRINT@97,CHR$(135);CHR$(175);CHR$(139):PRINT@129,C1$:PRINT@161,C3$
620 FORT=1TO14:POKE1024+T*32+8,128:IFT<8THENPRINT@T*32+9,STRING$(17,201)
630 NEXTT
640 FORTT=0TO9:FORT=1TO7:PRINT@T*32+9,STRING$(17,201);:NEXTT
650 FORT=0TO200:NEXTT
660 FORT=1TO7:PRINT@T*32+9,STRING$(17,198);:NEXTT
670 FORT=0TO200:NEXTT,TT
680 LV=LV+1:MN=MN+1:IFMN>5THENMN=5
690 D2=10-LV:D3=11-LV:ZZ=0:GOTO280
700 PT=0:BC=0:T2=D2:T3=D3:PRINT@64+CX,S1$;:PRINT@96+CX,S2$;:PRINT@128+CX,S3$;:PRINT@160+CX,S4$;
710 PLAY"O1;L64;T64":FORT=31TO1STEP-2:PLAY"V"+STR$(T):PLAY"1;8;6;9;2;4;6;1;3;6;2":NEXTT
720 MN=MN-1:IFMN>0THENFORT=0TO15:PRINT@T*32+1,CHR$(207);STRING$(28,32);CHR$(207);:NEXTT:CX=15:B$="":GOTO310
730 CLS:PRINT@364,;:A$="game over         ":GOSUB170:PRINT@0,;
740 PALETTE13,18:PALETTE0,18:PRINT"SCORE:";SC:PRINT"LEVEL:";LV+1:PRINT"OIL CANS:";BK
750 FORT=0TO2000:NEXTT:PLAY"L64;T64;O2":IFBK<1THEN780
760 FORT=BK TO1STEP-1:SC=SC+50:BK=BK-1:PRINT@0,"SCORE:";SC:PRINT@64,"OIL CANS:";BK:PLAY"V31CV29CV27CV25CV23CV21CV19CV17CV15CV13CV11CV9CV7CV5CV3CV1C"
770 NEXTT
780 PRINT@480,"PRESS ANY KEY.";:IFINKEY$<>""THEN780
790 IFINKEY$=""THEN790
800 GOSUB190:GOTO220
810 PLAY"L64;T64;O1;V31;1;3;5;7;9;O2;1;3;5;7;9;"
820 BK=BK+1:GOTO370
830 CLS:PRINT"SCORE:";SC:PRINT"CARS LEFT:";MN:PRINT"LEVEL:";LV+1:PRINT"OIL CANS:";BK
840 PRINT:A$="PRESS ANY KEY.    ":GOSUB170
850 IFINKEY$<>""THEN850
860 IFINKEY$=""THEN860
870 GOSUB190:RETURN
880 C1$=CHR$(132)+CHR$(128)+CHR$(136):C2$=CHR$(135)+CHR$(175)+CHR$(139):C3$=CHR$(135)+CHR$(131)+CHR$(139):S1$=CHR$(130)+CHR$(137)+CHR$(141):S2$=CHR$(166)+CHR$(129)+CHR$(132):S3$=CHR$(138)+CHR$(137)+CHR$(134):S4$=CHR$(139)+CHR$(135)+CHR$(135)
890 RETURN
900 PALETTE8,0:GOSUB190:CLS:GOSUB880:PRINT
910 PRINT" ";C1$:PRINT" ";CHR$(135);CHR$(239);CHR$(139);" CRASH INTO A CAR":PRINT" ";C1$;" AND LOSE 150 PTS.":PRINT" ";C3$
920 PRINT:PRINT"  ";STRING$(2,128):PRINT" ";STRING$(4,128);" CRASH INTO A PIT":PRINT" ";STRING$(4,128);" AND LOSE 100 PTS.":PRINT"  ";STRING$(2,128)
930 PRINT:PRINT"  ";CHR$(175);" = 50 BONUS POINTS."
940 PRINT"CRASHING INTO SIDE OF ROAD TAKESAWAY 200 PTS.":PRINT"YOU GET 5 PTS. EACH TIME THE    SCREEN SCROLLS UP.";:PRINT@8,"<<SCORING TABLE>>":PRINT@40,STRING$(17,"=")
950 TIMER=0
960 IFTIMER>900THEN220:ELSEIFINKEY$=""THEN960
970 GOTO220
