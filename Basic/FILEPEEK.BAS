1 'FILE PEEKER
2 'BY GEOFF FRIESEN
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
100 'FPEEK (FILE PEEKER)
110 '
120 ON BRK GOTO 400
130 S=1
140 BF$="........"
150 WIDTH 40
160 PRINT " FPEEK (FILE PEEKER)"
170 PRINT
180 INPUT " FILE NAME"; F$
190 PRINT
200 OPEN "D",#1,F$,1
210 FIELD #1,1 AS A$
220 FOR I=1 TO LOF(1)
230 GET #1
240 IF A$<" " THEN 270
250 IF A$>CHR$(127) THEN 270
260 MID$(BF$,S,1)=A$
270 B$="0"+HEX$(ASC(A$))
280 PRINT " "+RIGHT$(B$,2);
290 S=S+1: IF S<>9 THEN 330
300 PRINT "       "+BF$
310 S=1
320 BF$="........"
330 NEXT I
340 IF S=1 THEN 400
350 FOR I=1 TO 9-S
360 PRINT "   ";
370 NEXT I
380 PRINT "       ";
390 PRINT MID$(BF$,1,S-1)
400 UNLOAD
