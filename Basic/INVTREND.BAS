1 CLS:PRINT@138,"INVTREND":PRINT@138+30,"BY JIM FRANZ":FORX=1TO2000:NEXT
2 CLS:PRINT@96,"THIS PROGRAM PROVIDES ONLY ONE  OF MANY AIDS THAT INVESTORS     SHOULD USE FOR TIMING INVESTMENTDECISIONS. THE AUTHOR IS NOT RE-SPONSIBLE FOR ANY DECISIONS MADETHROUGH USE OF THIS TIMING      METHOD OR USE OF THIS PROGRAM;";
3 PRINT"  FURTHER, THE AUTHOR IS NOT RE-  SPONSIBLE FOR ANY ERRONEOUS CAL-CULATIONS IN THE PROGRAM ITSELF."
4 FORX=1TO15000:NEXT
5 CLEAR1500
10 DIMA(60,30),AV(60,6),PC(60,6),DF(60,6),T(60)
15 DIMA$(60),D$(30)
16 CLS:PRINT"PROGRAM DESCRIPTION"
17 PRINT"THIS PROGRAM COMPUTES AVERAGE   VALUES FOR A 30 WEEK PERIOD."
18 PRINT"IT ALSO COMPUTES % CURRENT VALUEOF AVG VAL ";
19 PRINT"AND SHOWS 6 WEEK     TRENDS.":PRINT"IT CAN TRACK 60 ENTITIES.":FORK=1TO9000:NEXTK
20 CLS:PRINT"INVESTMENT TREND ANALYSIS"
30 PRINT" DO YOU WANT TO:"
31 PRINT"  (1)SET UP A NEW FILE"
32 PRINT"  (2)ADD INV/IND"
33 PRINT"  (3)DELETE INV/IND"
34 PRINT"  (4)CORRECT PAST DATA"
35 PRINT"  (5)UPDATE DATA"
36 PRINT"  (6)GET A PRINTOUT"
37 PRINT"  (7)QUIT"
38 INPUTC
39 IFP=1THENGOTO45
40 IFC>1ANDC<7THENGOSUB910:P=1
45 Y=I
50 ON C GOTO70,80,200,300,430,590,60
60 END
70 I=0
80 I=I+1
90 CLS:INPUT"NAME OF INDICATOR/INVESTMENT";A$(I)
100 PRINT"IS THIS A:"
101 PRINT"  (1)MARKET INDICATOR"
102 PRINT"  (2)PRESENT INVESTMENT"
103 PRINT"  (3)POTENTIAL INVESTMENT"
104 INPUTT(I)
110 AV=0
120 FOR J=1TO30
125 IFC=5ORC=4THEN145
127 IFC=2THENPRINT"VALUE WEEK ENDING "D$(J):INPUTA(I,J):GOTO145
130 INPUT"DATE(WEEK ENDING-MM/DD)";D$(J)
140 INPUT"VALUE";A(I,J)
145 AV=AV+A(I,J)
150 NEXTJ
160 AV(I,1)=AV/30:DF(I,1)=A(I,1)-AV(I,1):PC(I,1)=DF(I,1)/AV(I,1)*100
165 IFC=4ORC=5THENRETURN
170 CLS:INPUT"TYPE'N'FOR NEXT INVESTMENT/     INDICATOR OR 'D'FOR DONE";B$
175 Y=I
180 IFB$="N"THENGOTO80
185 INPUT"DO YOU WANT TO SAVE THIS DATA   NOW(Y/N)";R$
186 IFR$="Y"THENGOSUB750
190 GOTO20
200 INPUT"TYPE NAME YOU WANT TO DROP-IF   DONE TYPE 'D'";N$
210 IFN$="D"THENPRINT"DO YOU WANT TO SAVE THIS DATA NOW(Y/N)":INPUTR$
211 IFN$="D"ANDR$="Y"THENGOSUB750:GOTO20
212 IFN$="D"THEN20
220 K=0
230 FORI=1TOY
260 IFN$=A$(I)THENK=1
265 IFK=0THEN290
270 IFK=1THENT(I)=T(I+1):FORJ=1TO30:A(I,J)=A(I+1,J):NEXTJ
280 IFK=1THENA$(I)=A$(I+1):FORJ=1TO6:AV(I,J)=AV(I+1,J):DF(I,J)=DF(I+1,J):PC(I,J)=PC(I+1,J):NEXTJ
284 CLS:PRINT"PLEASE WAIT"
290 NEXTI
291 IFK=0THEN PRINTN$"IS NOT IN FILE"
292 IF K=0 GOTO200
293 Y=Y-1:PRINTN$" HAS BEEN DELETED":GOTO200
300 K=0:D1=0:D=0
310 CLS:INPUT"NAME TO CORRECT (IF DONE TYPE   'D')";N$
320 IFN$="D"THENPRINT"DO YOU WANT TO SAVE THIS DATA   NOW(Y/N)?":INPUTR$
321 IFN$="D"ANDR$="Y"THENGOSUB750
322 IFN$="D"THEN20
323 PRINT:PRINT:INPUT"IS THIS AN NAV ADJUSTMENT CAUSEDBY MUTUAL FUND DISTRIBUTION(Y/N)";R$
324 IFR$="Y"THEND1=1:PRINT
325 IFD1=1THEN INPUT"AMT OF DISTRIBUTION/SHARE";D
326 PRINT:PRINT"PLEASE WAIT"
327 FORI=1TOY
330 IFK=1THEND1=0
331 K=0
340 IFN$=A$(I)THENK=1:M=1:CLS:PRINTA$(I)
341 IF D1=1 AND K=1 THEN380
345 IFK=0THEN420
350 INPUT"CHANGE NAME Y/N";R$
360 IFR$="Y"THEN PRINT"ENTER CORRECT NAME":INPUTA$(I)
370 PRINT"IS"T(I)"THE CORRECT TYPE INV/IND  (Y/N)?":INPUTR$
371 IFR$="N"THENINPUT"TYPE CORRECTION";T(I)
380 FOR J=1TO30
381 IFD1=1THENA(I,J)=A(I,J)-D
382 IFD1=1THEN CLS:PRINT"NOW CORRECTING THE DATA":GOTO410
390 PRINT"WANT TO CHANGE THIS VALUE?"
391 PRINTD$(J)"-"A(I,J)
392 INPUTR$
400 IFR$="Y"THENINPUT"TYPE NEW VALUE";A(I,J)
410 NEXTJ
415 GOSUB110
420 NEXTI
421 IFM=0THENPRINT"INV/IND NOT IN FILE"ELSEPRINT"CORRECTION COMPLETED"
422 FORK=1TO5000:NEXT:GOTO300
430 FORJ=30TO2STEP-1:D$(J)=D$(J-1):NEXTJ
440 FORI=1TOY
450 CLS:PRINT"PLEASE WAIT"
460 A(I,30)=0:AV(I,6)=0:DF(I,6)=0:PC(I,6)=0
470 FORJ=30TO2STEP-1
480 A(I,J)=A(I,J-1)
490 NEXTJ
500 FORJ=6TO2STEP-1:AV(I,J)=AV(I,J-1):DF(I,J)=DF(I,J-1):PC(I,J)=PC(I,J-1)
510 NEXTJ
520 NEXTI
522 INPUT"CURRENT DATE(WEEK ENDING MM/DD)";D$(1)
523 FORI=1TOY
540 PRINTA$(I)
541 IFA$(I)=""THEN570
560 INPUT"PRES VALUE";A(I,1):GOSUB110
570 NEXTI
575 GOSUB750:GOTO30
590 CLS:INPUT"WHEN PRINTER IS TURNED ON AND   PAPER IS SET, PRESS <ENTER>";R$
591 PRINT#-2,"":PRINT#-2,"":PRINT#-2,"TREND ANALYSIS OF MARKET INDICATORS AND PRESENT/POTENTIAL INVESTMENTS"
592 INPUT"DO YOU WANT TO INCLUDE DETAILED 30 WEEK DATA(Y/N)";F$
600 G=0:PRINT"DO YOU WANT A PRINTOUT OF:"
601 PRINT"  (1)MARKET INDICATORS ONLY"
602 PRINT"  (2)PRESENT INVESTMENTS ONLY"
603 PRINT"  (3)POTENTIAL INVESTMENTS ONLY"
604 PRINT"  (4)ALL OF THE ABOVE"
605 PRINT"  (5)RETURN TO MAIN MENU":INPUTC1
606 FORK=1TO4:PRINT#-2,"":NEXTK:IFC1=1THENPRINT#-2,"MARKET INDICATOR"
607 IFC1=2THENPRINT#-2,"PRESENT INVESTMENT"
608 IFC1=3THENPRINT#-2,"POTENTIAL INVESTMENT"
609 IFC1=4THENC1=1:G=1:GOSUB606:C1=2:GOSUB606:C1=3:GOSUB606:GOTO20
610 IFC1=5THEN20
611 FORI=1TOY
620 IFC1=1ANDT(I)=1THENGOSUB670
630 IFC1=2ANDT(I)=2THENGOSUB670
640 IFC1=3ANDT(I)=3THENGOSUB670
660 NEXTI
661 IFG=1THENRETURNELSEGOTO600
670 PRINT#-2,"":PRINT#-2,"":PRINT#-2,"":PRINT#-2,A$(I)
671 IFF$<>"Y"THEN673
672 FORJ=30TO1STEP-1:PRINT#-2,D$(J)"-"A(I,J);:PRINT#-2,"";:NEXTJ
673 PRINT#-2,"":PRINT#-2,"":PRINT#-2,"6-WEEK TREND        ";D$(6);"       "D$(5);"       ";D$(4);"       ";D$(3);"        ";D$(2);"       "D$(1)
675 PRINT#-2,"":PRINT#-2,"30-WEEK AVG  ";:GOSUB700
676 PRINT#-2,"":PRINT#-2,"DEV FROM AVG ";:GOSUB710
677 PRINT#-2,"":PRINT#-2,"% DEVIATION  ";:GOSUB720:PRINT#-2,"":RETURN
700 FORJ=6TO1STEP-1:PRINT#-2,USING"    ####.##";AV(I,J);:NEXTJ:RETURN
710 FORJ=6TO1STEP-1:PRINT#-2,USING"    ####.##";DF(I,J);:NEXTJ:RETURN
720 FORJ=6TO1STEP-1:PRINT#-2,USING"    ####.##";PC(I,J);:NEXTJ:RETURN
750 PRINT"NOW WE'LL SAVE THIS DATA."
751 INPUT"DATAFILE:(D)DISK OR (T)TAPE";S$
752 IF S$="T"THEN1100
753 IFS$<>"T" AND S$<>"D"THEN750
755 CLS:PRINT"NOW WE'LL SAVE THIS DATA ON DISK.WHEN READY PRESS <ENTER>":INPUTR$
782 OPEN"O",#2,"TRNDDATA/NEW"
790 FORI=1TOY
800 WRITE#2,A$(I)
805 WRITE#2,T(I)
810 FOR J=1TO30
820 WRITE#2,A(I,J)
825 IFI=1THENWRITE#2,D$(J)
830 NEXTJ
840 FORJ=1TO6
850 WRITE#2,AV(I,J),DF(I,J),PC(I,J)
860 NEXTJ
880 NEXTI
889 CLOSE#2
890 IFC=1THEN892
891 KILL"TRNDDATA/DAT"
892 RENAME"TRNDDATA/NEW"TO"TRNDDATA/DAT"
900 RETURN
910 INPUT"IS YOUR DATAFILE (D)DISK OR     (T)TAPE";S$
911 IFS$="T"THEN1200
912 IFS$<>"T" AND S$<>"D" THEN910
915 CLS:PRINT"NOW WE'LL LOAD PAST DATA. WHEN  THE DISK IS READY PRESS <ENTER>":INPUTR$
920 I=0
930 OPEN"I",#1,"TRNDDATA/DAT"
940 IFEOF(1)=-1THEN1040
950 I=I+1
960 INPUT#1,A$(I),T(I)
970 FORJ=1TO30
980 INPUT#1,A(I,J)
985 IFI=1THENINPUT#1,D$(J)
990 NEXTJ
1000 FORJ=1TO6
1010 INPUT#1,AV(I,J),DF(I,J),PC(I,J)
1020 NEXTJ
1030 GOTO940
1040 CLOSE#1
1050 RETURN
1100 CLS:PRINT:PRINT:PRINT:PRINT:PRINT"TO SAVE THIS DATA":PRINT"PLACE A CLEAN TAPE IN THE":PRINT"CASSETTE AND PRESS PLAY":PRINT"AND RECORD"
1110 INPUT"WHEN READY PRESS <ENTER>";R$
1120 OPEN"O",#-1,"TRNDDATA"
1130 FORI=1TOY
1140 PRINT#-1,A$(I),T(I)
1150 FORJ=1TO30
1160 PRINT#-1,A(I,J)
1165 IFI=1THENPRINT#-1,D$(J)
1170 NEXTJ
1175 FORJ=1TO6
1180 PRINT#-1,AV(I,J),DF(I,J),PC(I,J)
1185 NEXTJ
1190 NEXTI
1191 CLOSE#-1
1192 RETURN
1200 CLS:PRINT"INSERT <TRNDDATA> TAPE IN":PRINT"THE CASSETTE AND PRESS PLAY"
1201 INPUT"WHEN READY PRESS <ENTER>";R$
1205 I=0
1210 OPEN"I",#-1,"TRNDDATA"
1215 IFEOF(-1)=-1THEN1297
1220 I=I+1
1230 INPUT#-1,A$(I),T(I)
1240 FORJ=1TO30
1250 INPUT#-1,A(I,J)
1260 IFI=1THENINPUT#-1,D$(J)
1270 NEXTJ
1280 FORJ=1TO6
1285 INPUT#-1,AV(I,J),DF(I,J),PC(I,J)
1290 NEXTJ
1295 GOTO1215
1297 CLOSE#-1
1299 RETURN
