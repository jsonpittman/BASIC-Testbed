0 'A COLOR DOODLER
1 'WRITTEN BY KEIRAN KENNY
2 'COPYRIGHT (C) MAY 1991
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
10 DIMCL(500),H1(500),V1(500),H2(500),V2(500)
20 IFPEEK(&H15F)=196THENDV=1ELSEDV=-1
30 CLS:PRINT@160,"YOU CAN:":PRINT@227,"1 LOAD A SAVED PICTURE, OR":PRINT@291,"2 DRAW A NEW PICTURE."
40 K$=INKEY$:IFK$<>"1"ANDK$<>"2"THEN40
50 IFK$="1"THENLD=1:CLS:PRINT@224,"";:LINEINPUT"LOAD FILENAME: ";F$
60 POKE65497,0
70 PALETTERGB
80 FORSL=8TO15:READPL:PALETTESL,PL:NEXT
90 DATA 24,33,40,43,48,52,56,0
100 POKE41598,255
110 HSCREEN2:HCLS15
120 A$="0123456789ABCDEF"
130 HBUFF1,2552:HBUFF2,2552
140 B$="*COLOR DOODLER*":B=9:CL=0:FORT=1TO15:HCOLORCL:HPRINT(B,3),MID$(B$,T,1):B=B+1:CL=CL+1:NEXT
150 C$="by Keiran Kenny":B=9:CL=0:FORT=1TO15:HCOLORCL:HPRINT(B,5),MID$(C$,T,1):B=B+1:CL=CL+1:NEXT
160 HCOLOR0:HPRINT(11,9),"YOUR COLORS:"
170 HLINE(14,87)-(264,103),PSET,B
180 B=2:FORT=1TO16:HPRINT(B,11),MID$(A$,T,1):B=B+2:NEXT
190 CR=0:FORL=15TO263STEP16:HCOLORCR:HLINE(L,96)-(L+8,102),PSET,BF:CR=CR+1:NEXT
200 HGET(14,87)-(264,103),1
210 HCOLOR0:HPRINT(10,14),"PRESS ANY KEY"::EXEC44539:K$=INKEY$:HCLS15
220 IFLD=0THEN330
230 POKE65496,0
240 OPEN"I",#DV,F$
250 INPUT#DV,NR
260 FORZ=1TONR
270 INPUT#DV,CL(Z),H1(Z),V1(Z),H2(Z),V2(Z)
280 HCOLORCL(Z):HLINE(H1(Z),V1(Z))-(H2(Z),V2(Z)),PSET
290 X=H2(Z):Y=V2(Z)
300 NEXTZ
310 CLOSE#DV
320 POKE65497,0:GOTO340
330 X=160:Y=96
340 PK=PEEK(135)
350 IFPK=80THEN620
360 IFPK=72THENHGET(14,87)-(264,103),2:HPUT(14,87)-(264,103),1:EXEC44539:HPUT(14,87)-(264,103),2
370 IFPK>47ANDPK<58THENK=PK-48:HCOLORK ELSEIFPK>64ANDPK<70THENK=PK-55:HCOLORK
380 IFX=0AND(PK=189ORPK=64)THENPOKE135,0:GOTO340
390 IFX=319AND(PK=87ORPK=83)THENPOKE135,0:GOTO340
400 IFY=0AND(PK=64ORPK=87)THENPOKE135,0:GOTO340
410 IFY=191AND(PK=189ORPK=83)THENPOKE135,0:GOTO340
420 IFPK=94THENY=Y-1
430 IFPK=10THENY=Y+1
440 IFPK=8THENX=X-1
450 IFPK=9THENX=X+1
460 IFPK=64THENY=Y-1:X=X-1
470 IFPK=87THENY=Y-1:X=X+1
480 IFPK=189THENY=Y+1:X=X-1
490 IFPK=83THENY=Y+1:X=X+1
500 IFPK=103THENH1=X:V1=Y:HSET(H1,V1)
510 IFPK=4THENH2=X:V2=Y:HLINE(H1,V1)-(H2,V2),PSET:NR=NR+1:CL(NR)=K:H1(NR)=H1:V1(NR)=V1:H2(NR)=H2:V2(NR)=V2:POKE135,0
520 IFPK=12THEN610
530 IFPK=78THENHCOLOR15:HLINE(H2,V2)-(H1,V1),PSET:NR=NR-1:HCOLORCL(NR):X=H1:Y=V1:POKE135,0
540 IFY<0THENY=0
550 IFY>191THENY=191
560 IFX<0THENX=0
570 IFX>319THENX=319
580 BG=HPOINT(X,Y)
590 HSET(X,Y):FORDL=1TO100:NEXT:HSET(X,Y,BG)
600 GOTO340
610 POKE65496,0:POKE41598,3:RGB:WIDTH32:CLS:END
620 POKE135,0
630 POKE65496,0:POKE41598,3:RGB:WIDTH32:CLS
640 PRINT@224,"";:LINEINPUT"SAVE FILENAME: ";F$
650 OPEN"O",#DV,F$
660 PRINT#DV,NR
670 FORZ=1TONR
680 PRINT#DV,CL(Z),H1(Z),V1(Z),H2(Z),V2(Z)
690 NEXTZ
700 CLOSE#DV
710 CLS:PRINT@224,"FILE "F$" SAVED"
720 END
