10 REM"TACHISTOSCOPE"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1989"
30 DIM A$(50)
40 CLS5:N=1:C$=STRING$(28," ")
50 PRINT@0,"":PRINT@0,"ENTER WORD #";N;:INPUT A$(N)
60 SOUND 200,2:IF A$(N)="END" THEN PRINT@0," ":PLAY"L2A":GOTO 90
70 N=N+1
80 GOTO  50
90 PRINT@0," ":PRINT@0,"ENTER SPEED IN SECONDS";:INPUT S
100 SOUND150,2:IF S<0 OR S>10 THEN 90
110 PRINT@0,"DO YOU WANT TO SEE aLL OR pART  OF THE WORDS ";
120 LINEINPUT Q$
130 IF Q$="A" THEN 170 ELSE IF Q$="P" THEN 140 ELSE 110
140 PRINT@0,"HOW MANY LETTERS OF EACH WORD   WOULD YOU LIKE TO SHOW ";
150 INPUT Q
160 IF Q<2 OR Q>5 THEN SOUND 10,4:GOTO 140
170 PRINT@0,"*****     TACHISTOSCOPE    *****"
180 FOR T=1152 TO 1183:POKE T,128:NEXT T:FOR T=1312 TO 1343:POKE T,128:NEXT T
190 POKE1184,128:POKE1185,128:POKE1216,128:POKE1217,128:POKE1248,128:POKE1249,128:POKE1280,128:POKE1281,128
200 POKE1215,128:POKE1214,128:POKE1247,128:POKE1246,128:POKE1279,128:POKE1278,128:POKE1311,128:POKE1310,128
210 R=RND(N-1):TIMER=0:PRINT@194,C$;:PRINT@226,C$;
220 IF Q$="A" THEN Q=LEN(A$(R))
230 PRINT@204,LEFT$(A$(R),Q);
240 IF TIMER>60*S THEN 250 ELSE 230
250 SOUND240,1:PRINT@194,C$;:PRINT@202,"? ";
260 LINEINPUT B$
270 POKE 1247,128:POKE 1246,128
280 IF B$=A$(R) THEN PRINT@236,"CORRECT";:PLAY"L200CDECDECDEGGG"
290 IF B$<>A$(R) THEN PLAY"D-":PRINT@232,"SORRY, "A$(R);
300 EN$=INKEY$
310 IF EN$=CHR$(13) THEN 210 ELSE IF EN$="E" THEN 320 ELSE 300
320 CLS:END
