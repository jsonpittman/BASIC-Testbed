10 REM"LINEGRAPHS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1987"
30 CLEAR2000
40 GOSUB 500
50 CLS:PRINT@11,"LINEGRAPHS";
60 PRINT@96,"PRESS ENTER TO SEE SEE THE GRAPH                                AND PRESS ENTER AGAIN TO RETURN."
70 PRINT@160,"AND PRESS ENTER AGAIN TO RETURN."
80 PRINT@40,STRING$(16,255);
90 PRINT@419,"PRESS THE 'E' KEY TO END";
100 EN$=INKEY$
110 IF EN$=CHR$(13) THEN 120 ELSE IF EN$="E" THEN 830 ELSE 100
120 PCLS5:SCREEN1,1:PMODE3,1:CIRCLE(90,90),10
130 PCLS5:SCREEN1,1:PMODE3,1:COLOR7
140 LINE(25,170)-(240,172),PSET,BF
150 LINE(25,15)-(27,170),PSET,BF
160 COLOR8:FOR T=30 TO 150 STEP 20
170 LINE(27,T)-(235,T),PSET
180 NEXT T
190 A=40:A1=(3+RND(12))*10
200 B=75:B1=(3+RND(12))*10:IF B1=A1 THEN 190
210 C=110:C1=(3+RND(12))*10:IF C1=A1 OR C1=B1 THEN 190
220 D=145:D1=(3+RND(12))*10:IF D1=A1 OR D1=B1 OR D1=C1 THEN 190
230 E=180:E1=(3+RND(12))*10:IF E1=D1 OR E1=C1 OR E1=B1 OR E1=A1 THEN 190
240 F=215:F1=(3+RND(12))*10:IF F1=A1 OR F1=B1 OR F1=C1 OR F1=D1 OR F1=E1 THEN 190
250 COLOR8
260 CIRCLE(A,A1),2
270 CIRCLE(B,B1),2
280 CIRCLE(C,C1),2
290 CIRCLE(D,D1),2
300 CIRCLE(E,E1),2
310 CIRCLE(F,F1),2
320 COLOR6:LINE(A,A1)-(B,B1),PSET
330 LINE-(C,C1),PSET
340 LINE-(D,D1),PSET
350 LINE-(E,E1),PSET
360 LINE-(F,F1),PSET
370 DRAW"BM45,175;C7A2S12"+A$+SP$+B$+SP$+C$+SP$
380 DRAW D$+SP$+E$+SP$+F$
390 DRAW"BM6,22;C8S8"+Z7$+O$
400 DRAW"BM6,42"+Z6$+O$
410 DRAW"BM6,62"+Z5$+O$
420 DRAW"BM6,82"+Z4$+O$
430 DRAW"BM6,102"+Z3$+O$
440 DRAW"BM6,122"+Z2$+O$
450 DRAW"BM6,142"+Z1$+O$
460 DRAW"BM20,162"+O$
470 EN$=INKEY$
480 IF EN$=CHR$(13) THEN 670 ELSE IF EN$="S" THEN 850 ELSE 470
490 REM"THE STRINGS FOR THE LETTERS AND NUMBERS FOLLOW"
500 A$="BEHUNU2R4NU2DGL2BGBL6"
510 B$="BEHENR3HER3D4L3BGBL6"
520 C$="BU4ER2FD2GL2HBG2BL4"
530 D$="BEHU2ER3D4L3BGBL6"
540 E$="BER3U2NL2U2L4BG5BL2"
550 F$="BUR4U2NL3U2BG5BL5"
560 O$="BEHU2ER2FD2GL2BGBL6"
570 Z1$="BE2NU3DEBFBGBL9"
580 Z2$="BENR3HER3U2L4BG5BL"
590 Z3$="BENR3HENR2HER3BG5BL5"
600 Z4$="BENU4E3L4BG4BL2"
610 Z5$="BER4U2L3HER3BG5BL5"
620 Z6$="BU2FR2EU2NHGL2HER2BG5BL4"
630 Z7$="BUNR4UE3BG5BL4"
640 SP$="BE4BUBG5BL5"' ***SPACER
650 RETURN
660 REM"COMPUTE AND PRINT THE ANSWERS"
670 CLS:PRINT"     HERE ARE THE RESULTS":PRINT
680 A2=(-A1/2)+85
690 B2=(-B1/2)+85
700 C2=(-C1/2)+85
710 D2=(-D1/2)+85
720 E2=(-E1/2)+85
730 F2=(-F1/2)+85
740 PRINT"A="A2,"B="B2
750 PRINT"C="C2,"D="D2
760 PRINT"E="E2,"F="F2
770 AV=(A2+B2+C2+D2+E2+F2)/6
780 AV=INT(AV+.5)
790 PRINT@229,"THE AVERAGE WAS "AV
800 PRINT@456,"PRESS G TO GO ON.";
810 EN$=INKEY$
820 IF EN$="G" THEN RUN ELSE 810
830 CLS:END
840 REM"SAVING THE PICTURE OF THE GRAPH"
850 SAVEM"GRAPH1",3584,9727,3584:GOTO 670
