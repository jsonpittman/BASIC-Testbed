10 'ANIMALS
20 'BY ANDY BLOUNT
30 '
40 'credits
50 PCLEAR1:CLEAR5000:DIMA$(300):CLS0:PRINT@13,"ANIMALS";:PRINT@74,"BY ANDY BLOUNT";:AA$=CHR$(128)+CHR$(147):BB$=CHR$(128)+CHR$(157)+CHR$(157)+CHR$(152):FORX=162TO190:PRINT@X,AA$;:PRINT@X+30,BB$;:PLAY"L32O1CD":NEXT
60 PLAY"O4CABP20CABP20CAB":FORX=1TO1000:NEXT:X=0
70 'load file 
80 CLS:PRINT"DO YOU WISH TO LOAD AN ANIMAL   FILE?"
90 A$=INKEY$:IFA$=""THEN90ELSEIFA$="Y" THEN100ELSE180
100 CLS:PRINT"WHICH DO YOU WANT TO LOAD       ANIMALS OFF OF:",,"1. DISK",,"2. CASSETTE"
110 ONVAL(INKEY$) GOTO120,130:GOTO110
120 CLS:PRINT"INSERT DISK WITH 'ANIMAL/DAT' ONIT AND PRESS [ENTER]":EXEC44539:SF=1:GOTO140
130 CLS:PRINT"POSITION TAPE - PRESS <PLAY>    THEN PRESS [ENTER] WHEN READY":EXEC44539:SF=-1
140 OPEN"I",SF,"ANIMAL"
150 IF EOF(SF) THENCLOSE#SF:GOTO170
160 INPUT#SF,A$(X):X=X+1:GOTO150
170 PRINT"DONE LOADING ANIMALS":FORX=1TO1000:NEXT:GOTO200
180 FORX=0TO3:READA$(X):NEXT
190 'start game  
200 CLS:PRINT@12,"LETS PLAY":PRINT@40,"'GUESS THE ANIMAL'":PRINT
210 PRINT"     THINK OF AN ANIMAL AND I          WILL TRY TO GUESS IT.":T=1:N=VAL(A$(0))
220 IFT=0 THEN CLS
230 T=0:PRINT:PI$=" ARE YOU THINKING OF AN ANIMAL?":GOSUB710
240 A$=INKEY$:IFA$=""THEN240ELSEIFA$="L" THEN250 ELSEIFA$="N" THEN730 ELSEIFA$="Y"THEN260 ELSE240
250 PRINT:PRINTTAB(13)"LIST":GOTO650
260 PRINT:PRINTTAB(15)"YES":PRINT:B=1
270 GOSUB490:IFLEFT$(A$(B),2)="/Q" THEN270
280 'guess animal
290 PI$="IS IT A ":GOSUB710:PI$=RIGHT$(A$(B),LEN(A$(B))-2)+"?":GOSUB710
300 A$=INKEY$:IFA$=""THEN300ELSE310
310 IFA$="Y" THENPRINT" YES":PRINT"    I GUESSED IT! WHY NOT TRY            ANOTHER ANIMAL." ELSE330
320 FORZ=1TO1000:NEXT:GOTO220
330 PRINT" NO":INPUT"THE ANIMAL YOU WERE THINKING OF WAS A ";V$
340 PRINT"ENTERING THE ANIMAL: "V$:FORCC=1TO300:IFLEFT$(A$(CC),2)<>"/A" THENNEXTCC ELSEFORZ=3TO LEN(A$(CC)):IFMID$(A$(CC),Z,1)="/" THENNEXTZ ELSE CC$=CC$+MID$(A$(CC),Z,1):NEXTZ:IFV$=CC$ THEN360 ELSECC$="":NEXTCC
350 GOTO370
360 PI$="SORRY, I ALREADY KNOW A":GOSUB710:PRINT:PI$=V$+", YOU MUST HAVE":GOSUB710:PRINT:PI$="ANSWERED A QUESTION WRONG. WHY  NOT TRY AGAIN.":GOSUB710:FORX=1TO3000:NEXT:GOTO220
370 PRINT"PLEASE TYPE A QUESTION THAT     WOULD DISTINGUISH A "V$:PRINT"FROM A ";RIGHT$(A$(B),LEN(A$(B))-2);":":LINEINPUT"";X$
380 PRINT"FOR A "V$",":LINEINPUT"THE ANSWER WOULD BE: ";A$
390 A$=LEFT$(A$,1):IFA$<>"Y" AND A$<>"N" THEN380
400 IFA$="Y" THENB$="N"
410 IFA$="N" THENB$="Y"
420 D=VAL(A$(0))
430 A$(0)=STR$(D+2)
440 A$(D)=A$(B)
450 A$(D+1)="/A"+V$
460 A$(B)="/Q"+X$+"/"+A$+STR$(D+1)+"/"+B$+STR$(D)+"/"
470 GOTO220
480 'ask questions  
490 Q$=A$(B)
500 FORZ=3TO LEN(Q$)
510 IFMID$(Q$,Z,1)<>"/" THENPRINTMID$(Q$,Z,1);:NEXTZ
520 PRINT"?";
530 C$=INKEY$:IFC$=""THEN530ELSEIFC$="Y" THENPRINT" YES" ELSEIFC$="N" THENPRINT" NO" ELSE530
540 T$="/"+C$
550 FORX=3 TO LEN(Q$)-1
560 IFMID$(Q$,X,2)=T$ THEN580
570 NEXTX
580 FORY=X+1 TO LEN(Q$)
590 IFMID$(Q$,Y,1)="/"THEN610
600 NEXTY
610 B=VAL(MID$(Q$,X+2,Y-X-2))
620 RETURN
630 DATA"4","/QDOES IT FLY/Y2/N3/","/ACROW","/AGOLD FISH"
640 'list animals
650 PRINT:PRINT"ANIMALS I ALREADY KNOW ARE:"
660 FORI=1TO299:IFLEFT$(A$(I),2)<>"/A" THENNEXTI:GOTO690
670 FORZ=3TO LEN(A$(I)):IFMID$(A$(I),Z,1)<>"/" THENPRINTMID$(A$(I),Z,1);:NEXTZ
680 PRINT,;:NEXTI
690 FORX=1TO1000:NEXT:GOTO220
700 'slow printing
710 FORYP=1TOLEN(PI$):PRINTMID$(PI$,YP,1);:FORGH=1TO4:NEXTGH,YP:RETURN
720 'saving animals & quiting
730 CLS:PRINT,,:PI$=" ARE YOU SURE YOU WANT TO QUIT?":GOSUB710
740 A$=INKEY$:IFA$=""THEN740 ELSEIFA$="Y" THEN750 ELSEIFA$="N" THEN220 ELSE740
750 CLS:PI$="DO YOU WISH TO SAVE THE ANIMALS AND QUESTIONS IN MEMORY TO DISK OR CASSETTE?":GOSUB710
760 A$=INKEY$:IFA$=""THEN760ELSEIFA$="N" THENCLS:END ELSEIFA$="Y" THEN770 ELSE760
770 CLS:PRINT"WHICH DO YOU WANT TO SAVE THEM  TO?",,"1. DISK",,"2. CASSETTE"
780 ONVAL(INKEY$)GOTO 790,800:GOTO780
790 CLS:PRINT"INSERT DISK TO SAVE ANIMALS ON  THEN PRESS [ENTER]":SF=1:EXEC44539:GOTO810
800 CLS:PRINT"POSITION TAPE - PRESS PLAY AND  RECORD THEN PRESS [ENTER]":SF=-1:EXEC44539
810 OPEN"O",#SF,"ANIMAL":FORX=0TO300:PRINT#SF,A$(X):NEXT:CLOSE#SF:PRINT"FINISHED":FORX=1TO1000:NEXT:CLS:END
