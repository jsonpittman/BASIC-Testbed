5 '"PRINTUNE"
6 'WRITTEN BY GREG BOOTS
10 GOSUB770:CLEAR1300:DIMDAT(1005):DAT=1:PLAY"T2"
15 '**BASIC VARIABLES**
20 A=1:B=30:C=31:D=1:E=255:X=10:S=0
25 '**MAIN MENU**
30 CLS:PRINT@33,"NOTE=1 ACROSS TO 12"
40 PRINT" OCTAVE=UP OR DOWN"
50 PRINT" LENGTH=LEFT OR RIGHT"
60 PRINT" PAUSE LENGTH=^ (L)OR(R)"
70 PRINT" VOLUME=^ UP OR DOWN"
80 PRINT@1,"X="X
90 GOSUB770:PRINT@7,"SOUND#"INT(DAT/5)+1
100 IFED=0 THENPRINT@193,"PRESS <S> TO SINGLE STEP":PRINT" <SPACEBAR> PLAYS TUNE":PRINT" <ENTER> KEEPS SOUND#"INT(DAT/5)+1:PRINT"   <$> TO SAVE TUNE":PRINT"   <&> TO LOAD A TUNE":PRINT"   <?> TO PRINT TUNE"
110 IFDAT=996THENPRINT@19,"*LAST SOUND*"ELSEIFDAT>996THENS=1
120 PRINT@58,D
130 PRINT@90,A
140 PRINT@122,B
150 PRINT@154,E
160 PRINT@186,C
165 '**KEYBOARD INPUT**
170 A$=INKEY$:IFA$=""THEN170ELSENUM=VAL(A$)
180 IFNUM>=1ANDNUM<10THEND=NUM:GOTO400
190 IFA$="0"THEND=10:GOTO400
200 IFA$=":"THEND=11:GOTO400
210 IFA$="-"THEND=12:GOTO400
220 IFA$="^"ANDA<5THENA=A+1:GOTO130
230 IFA$=CHR$(10)ANDA>1THENA=A-1:GOTO130
240 IFA$=CHR$(9)ANDB<256-X THENB=B+X:GOTO140
250 IFA$=CHR$(8)ANDB>0+X THENB=B-X:GOTO140
260 IFA$=CHR$(93)ANDE<256-X THENE=E+X:GOTO150
270 IFA$=CHR$(21)ANDE>0+X THENE=E-X:GOTO150
280 IFA$=CHR$(95)ANDC<32-X THENC=C+X:GOTO160
290 IFA$=CHR$(91)ANDC>0+X THENC=C-X:GOTO160
300 IFA$="X"THENIFX=1THENX=10:GOTO80
310 IFA$="X"THENIFX=10THENX=1:GOTO80
320 IFA$=CHR$(32)THENFORPL=1 TODAT STEP5:IFPL=DAT THEN90ELSEPRINT@13,INT(PL/5)+1:GOSUB780:PLAY"O"+STR$(DAT(PL))+";L"+STR$(DAT(PL+1))+";V"+STR$(DAT(PL+2))+";"+STR$(DAT(PL+3))+";P"+STR$(DAT(PL+4)):NEXTPL
330 IFA$=CHR$(13)ANDED=1 THENED=0:DAT(DAT)=A:DAT(DAT+1)=B:DAT(DAT+2)=C:DAT(DAT+3)=D:DAT(DAT+4)=E:DAT=PAT:A=DAT(DAT):B=DAT(DAT+1):C=DAT(DAT+2):D=DAT(DAT+3):E=DAT(DAT+4):GOTO20
340 IFA$=CHR$(13)ANDS=0 THENDAT(DAT)=A:DAT(DAT+1)=B:DAT(DAT+2)=C:DAT(DAT+3)=D:DAD(DAT+4)=E:DAT=DAT+5
350 IFA$="S"ANDED=0 THENPRINT@225:PRINTTAB(120):PRINT@191:INPUT" ENTER STARTING SOUND# ";I$:I=VAL(I$):IFI=0ORI>(DAT/5)+1 THEN90ELSEI=I-1:I=(I*5)+1:PRINT@193,"PRESS *S* TO SINGLE STEP":PRINT@225:GOTO410
360 IFA$=CHR$(36)ANDED=0ANDPL>1 THENPRINTTAB(183):PRINT@256:GOSUB590:PRINT" TO SAVE TUNE ENTER":LINEINPUT" LESS THAN 9 LETTERS: ";ND$:IFND$="" THEN30ELSE490
370 IFA$=CHR$(38)ANDED=0 THENPRINTTAB(183):PRINT@225:GOSUB590:LINEINPUT" ENTER NAME OF TUNE: ";ND$:IFND$=""THEN30ELSE540
380 IFA$=CHR$(63)ANDED=0ANDPL>1THEN650
390 GOTO80
395 '**KEYBOARD NOTE**
400 GOSUB780:PLAY"O"+STR$(A)+";L"+STR$(B)+";V"+STR$(C)+";"+STR$(D)+";P"+STR$(E):GOSUB770:GOTO90
405 '**SPACEBAR PLAY**
410 FORPL=I TODAT STEP5:IFPL=DAT THEN90ELSEPRINT@13,INT(PL/5)+1:GOSUB780:PLAY"O"+STR$(DAT(PL))+";L"+STR$(DAT(PL+1))+";V"+STR$(DAT(PL+2))+";"+STR$(DAT(PL+3))+";P"+STR$(DAT(PL+4)):GOSUB770
415 '**SINGLE STEP ROUTINE**
420 A$=INKEY$:PRINT@225,"PRESS *E* TO EDIT SOUND":PRINT" PRESS *P* TO REPEAT SOUND":PRINT" PRESS *R* RETURN TO START SOUND":IFA$=""THEN420
430 IFA$="S"THEN NEXTPL
440 IFA$="E"THEN480
450 IFA$="R"THEN410
460 IFA$="P"THENGOSUB780:PLAY"O"+STR$(DAT(PL))+";L"+STR$(DAT(PL+1))+";V"+STR$(DAT(PL+2))+";"+STR$(DAT(PL+3))+";P"+STR$(DAT(PL+4)):GOSUB770
470 GOTO420
475 '**EDIT ROUTINE**
480 PRINT@193,"*EDIT* PRESS ENTER TO KEEP":PRINT:PRINT:PRINT:ED=1:PAT=DAT:DAT=PL:A=DAT(PL):B=DAT(PL+1):C=DAT(PL+2):D=DAT(PL+3):E=DAT(PL+4):GOTO90
485 '**DISC SAVE ROUTINE**
490 IFLEN(ND$)>8 THENCLS:GOTO360
495 GOSUB780:OPEN"D",#1,ND$,25
500 FIELD#1,5 AS A$,5 AS B$,5 AS C$,5 AS D$,5 AS E$
510 FOROD=1 TODAT STEP5
520 LSETA$=MKN$(DAT(OD)):LSETB$=MKN$(DAT(OD+1)):LSETC$=MKN$(DAT(OD+2)):LSETD$=MKN$(DAT(OD+3)):LSETE$=MKN$(DAT(OD+4))
530 PUT#1,INT((OD/5)+1):NEXTOD:CLOSE#1:PRINT" **"ND$"** IS SAVED:":FORT=1TO1500:NEXTT:GOSUB770:CLS:GOTO20
535 '**DISC LOAD ROUTINE**
540 GOSUB790:GOSUB780:OPEN"D",#1,ND$,25
550 FIELD#1,5 AS A$,5 AS B$,5 AS C$,5 AS D$,5 AS E$
560 FORDAT=1TOLOF(1)-1:GET#1,DAT
570 DAT(DAT*5-4)=CVN(A$):DAT(DAT*5-3)=CVN(B$):DAT(DAT*5-2)=CVN(C$):DAT(DAT*5-1)=CVN(D$):DAT(DAT*5)=CVN(E$)
580 NEXTDAT:DAT=DAT*5-4:CLOSE#1:GOSUB770:CLS:GOTO20
585 '**DISC DIRECTORY SUB**
590 PRINT" TUNES ON FILE:":FORSEC=3TO11
600 GOSUB780:DSKI$0,17,SEC,L$,R$:GOSUB770
610 DI$=L$+LEFT$(R$,127)
620 FORT=0TO7
630 IFMID$(DI$,T*32+9,3)="DAT"ANDMID$(DI$,T*32+1,1)<>CHR$(0) THENPRINT" "MID$(DI$,T*32+1,8)
640 NEXTT:NEXTSEC:DI$="":L$="":R$="":RETURN
645 '**PRINTER ROUTINE**
650 PRINTTAB(180):PRINT@225:LINEINPUT" NAME OF TUNE: ";N$:IFN$=""THEN30ELSEGOSUB780:PRINT#-2,N$:GOSUB770:T=0:S$="PLAY"+CHR$(34)+"T2;":FORP=1TODAT-1 STEP5
660 A=DAT(P):B=DAT(P+1):C=DAT(P+2):D=DAT(P+3):E=INT(DAT(P+4)/13.4+1)
670 IFA<>AA THENS$=S$+"O"+RIGHT$(STR$(A),LEN(STR$(A))-1):AA=A:GOSUB740
680 IFB<>BB THENS$=S$+"L"+RIGHT$(STR$(B),LEN(STR$(B))-1):BB=B:GOSUB740
690 IFC<>CC THENS$=S$+"V"+RIGHT$(STR$(C),LEN(STR$(C))-1):CC=C:GOSUB740
700 S$=S$+RIGHT$(STR$(D),LEN(STR$(D))-1):GOSUB740
710 S$=S$+"P"+RIGHT$(STR$(E),LEN(STR$(E))-1)
720 IFP=DAT-5 THENT=1
730 GOSUB740:NEXTP:AA=0:BB=0:CC=0:S$="":GOTO20
740 IFLEN(S$)<242ANDT=0 THENS$=S$+";":RETURN
750 IFLEN(S$)>=242 OR T=1 THENS$=S$+CHR$(34):GOSUB780:PRINT#-2,S$:GOSUB770:S$=LEFT$(S$,5):T=0
760 RETURN
765 '**KEYBOARD RESPONSE POKES**
770 POKE65495,0:RETURN
780 POKE65494,0:RETURN
785 'ERROR TRAP FOR LOADING TUNE NOT ON FILE
790 L=LEN(ND$):FORSEC=3TO11:GOSUB780:DSKI$0,17,SEC,L$,R$:GOSUB770:DI$=L$+LEFT$(R$,127):FORT=0TO7
800 IFND$=MID$(DI$,T*32+1,L)ANDMID$(DI$,T*32+9,3)="DAT" THENRETURN
810 NEXTT:NEXTSEC:DI$="":L$="":R$="":PRINT" **"ND$"**IS NOT ON FILE:":SOUND60,10:FORN=1TO500:NEXTN:CLS:GOTO360
