10 REM"USING MAP DIRECTIONALS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1990"
30 CLEAR 2000:X=RND(-TIMER)
40 A$="BEHUNU2R4NU2DGL2BGBL6"
50 B$="BEHENR3HER3D4L3BGBL6"
60 C$="BU4ER2FD2GL2HBG2BL4"
70 D$="BEHU2ER3D4L3BGBL6"
80 E$="BER3U2NL2U2L4BG5BL2"
90 F$="BUR4U2NL3U2BG5BL5"
100 G$="BUR4U3HL2GDRBG3BL4"
110 H$="BUU2NU2R4NU2D2BGBL9"
120 I$="BR2BUU4BU2BD7BL8"
130 N$="BUU4F4U4BG5BL5"
140 O$="BEHU2ER2FD2GL2BGBL6"
150 R$="BEHERNH2R2NU2D2L3BGBL6"
160 S$="BU2FR2EHL2HER2FBG4BL6"
170 T$="BUR2NU4R2BDBL10"
180 W$="BUU4F2E2D4BGBL9"
190 X$="BUE2NH2NE2F2BGBL9"
200 SP$="BE4BUBG5BL5"
210 PMODE4,1:PCLS5:SCREEN1,1
220 COLOR0:LINE(50,90)-(100,90),PSET:LINE-(50,50),PSET:LINE-(180,50),PSET:LINE-(115,10),PSET:LINE-(115,70),PSET:LINE(115,10)-(50,50),PSET
230 DRAW"C8A2S4BM53,95"+A$:DRAW"BM100,95"+B$
240 DRAW"BM45,47"+C$:DRAW"BM190,47"+D$
250 DRAW"BM107,6"+E$:DRAW"BM124,70"+F$
260 CIRCLE(50,90),2,0:CIRCLE(100,90),2:CIRCLE(115,10),2:CIRCLE(50,50),2,0:CIRCLE(115,70),2:CIRCLE(180,50),2
270 LINE(0,108)-(255,110),PSET,BF:LINE(0,126)-(255,128),PSET,BF
280 DRAW"BM210,17"+W$+SP$+SP$+SP$+SP$+SP$+E$
290 DRAW"BM227,2"+N$:DRAW"BM227,32"+S$
300 LINE(225,10)-(225,30),PSET:LINE(212,20)-(238,20),PSET
310 DRAW"S4BM40,115"+W$+H$+I$+C$+H$
320 DRAW +SP$+SP$+D$+I$+R$+E$+C$
330 DRAW +T$+I$+O$+N$+SP$+SP$+I$+S$
340 DRAW"S8BM10,136"+A$+SP$+N$:DRAW"BM70,136"+B$+SP$+S$
350 DRAW"BM130,136"+C$+SP$+E$:DRAW"BM190,136"+D$+SP$+W$
360 DRAW"BM10,156"+E$+SP$+N$+E$:DRAW"BM70,156"+F$+SP$+N$+W$
370 DRAW"BM130,156"+G$+SP$+S$+E$:DRAW"BM190,156"+H$+SP$+S$+W$
380 FOR T=15 TO 210 STEP 60:CIRCLE(T,145),2:CIRCLE(T,165),2:NEXT T
390 R=RND(8)
400 IF R=1 THEN A1$=F$:A2$=E$:A3$="A"
410 IF R=2 THEN A1$=E$:A2$=C$:A3$="H"
420 IF R=3 THEN A1$=A$:A2$=B$:A3$="C"
430 IF R=4 THEN A1$=C$:A2$=B$:A3$="G"
440 IF R=5 THEN A1$=E$:A2$=F$:A3$="B"
450 IF R=6 THEN A1$=D$:A2$=C$:A3$="D"
460 IF R=7 THEN A1$=B$:A2$=C$:A3$="F"
470 IF R=8 THEN A1$=C$:A2$=E$:A3$="E"
480 DRAW"C8S4BM194,115"+T$+O$
490 DRAW"S8BM180,112"+A1$:DRAW"BM220,112"+A2$
500 EN$=INKEY$
510 IF EN$="A" OR EN$="B" OR EN$="C" OR EN$="D" OR EN$="E" OR EN$="F" OR EN$="G" OR EN$="H" OR EN$="X" THEN 530
520 IF EN$="" THEN 500
530 IF EN$="X" THEN CLS:END
540 IF EN$=A3$ THEN PLAY"O4L25GFECCC"
550 IF EN$<> A3$ THEN PLAY"O1L4B-B-"
560 AN$=INKEY$
570 IF AN$=CHR$(13) THEN 580 ELSE 560
580 COLOR5:LINE(170,112)-(185,124),PSET,BF:LINE(210,112)-(225,124),PSET,BF:GOTO 390
590 GOTO 590
