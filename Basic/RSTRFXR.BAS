0 ' COPYRIGHT 1989  FALSOFT,INC
100 REM ROSTERFIXER (RSTRFXR)
110 REM RECALCULATION OF THE TOTAL STAT STRING
120 REM COPYRIGHT 1987 DELBERT BAKER
130 REM 32K ECB COCO 1
140 REM
150 CLEAR 5000
160 DIM N$(20),A$(20)
170 DIM C$(20),P$(20)
180 DIM ST$(20),GS$(20)
190 DIM SU$(13),SA$(13)
200 DIM N2$(20),PL$(20)
210 DIM S$(20,13),T$(20,13)
220 DIM S(20,13),T(20,13)
230 DIM U(13)
240 CLS:LINE INPUT"SEASON (YY): ";Y$
250 Y$=RIGHT$(Y$,2):F1$="ROSTER"+Y$
260 OPEN"D",#2,F1$,128:R=LOF(2)
270 FIELD #2,25 AS NF$,30 AS AF$,20 AS CF$,8 AS PF$,45 AS SF$
280 FOR K=1 TO R
290 GET#2,K
300 N$(K)=NF$:A$(K)=AF$:C$(K)=CF$:P$(K)=PF$:FOR Y=1 TO 13:T(K,Y)=0:NEXT Y
310 NEXT K:CLOSE #2
320 PRINT"FINDING THE 2ND NAME STRING":Q$=","
330 FOR K=1 TO R:M=INSTR(1,N$(K),Q$)+1
340 N2$=LEFT$(N$(K),M)
350 NEXT K
360 CLS:PRINT"    THIS PROGRAM IS TO PREPARE  SPECIAL REPORTS AND TO REPAIR A BOTCHED STAT STRING IN A ROSTER FILE."
370 PRINT"ENTER A RANGE OF GAME NUMBERS ATTHE PROMPTS.":PRINT
380 LINE INPUT"START AT GAME NO.: ";SG$:PRINT
390 LINE INPUT"END AT GAME NO.: ";EG$:PRINT
400 PRINT"PLACE THE GAME DATA DISK IN THE DRIVE."
410 LINE INPUT"PRESS <ENTER> WHEN READY.";B$
420 SG=VAL(SG$):EG=VAL(EG$)
430 FOR X=SG TO EG
440 GN$=STR$(X):L=LEN(GN$):GN$=RIGHT$(GN$,L-1)
450 F$="GM"+GN$+Y$
460 OPEN"I",#1,F$
470 INPUT #1,GN$,OT$,HT$,R1$,R2$,D$
480 G=0:CLS:PRINT"WORKING ON GAME";X
490 IF EOF(1)=-1 THEN 580
500 G=G+1:INPUT#1,N2$(G),PL$(G),GS$(G):PRINT N2$(G)
510 FOR Y=1 TO 10:S$(G,Y)=MID$(GS$(G),3*Y-2,3):NEXT Y
530 FOR Z=1 TO 10:T(G,Z)=T(G,Z)+VAL(S$(G,Z)):NEXT Z
540 IF (T(G,1)-T(G,4)-T(G,8))=0 THEN T(G,11)=0 ELSE T(G,11)=T(G,3)/(T(G,1)-T(G,4)-T(G,8))
550 IF (T(G,1)-T(G,8))=0 THEN T(G,12)=0 ELSE T(G,12)=(T(G,3)+T(G,4))/(T(G,1)-T(G,8))
560 IF T(G,3)=0 THEN T(G,13)=1 ELSE T(G,13)=(T(G,3)+T(G,5)+2*T(G,6)+3*T(G,7))/T(G,3)
570 GOTO 490
580 CLOSE #1:NEXT X
590 PRINT"REBUILDING THE STAT STRING":FOR K=1 TO R
600 FOR Y=1 TO 10:T$(K,Y)=STR$(T(K,Y)):NEXT Y
610 FOR Y=1 TO 10:L=LEN(T$(K,Y)):T$(K,Y)="00"+RIGHT$(T$(K,Y),L-1):NEXT Y
620 FOR Y=11 TO 13:T$(K,Y)=STR$(1000*T(K,Y)+.5):NEXT Y
630 IF T(K,11)=1 THEN T$(K,11)="1.000" ELSE IF T(K,11)=0 THEN T$(K,11)="0.000" ELSE T$(K,11)="0."+MID$(T$(K,11),2,3)
640 IF T(K,12)=1 THEN T$(K,12)="1.000" ELSE IF T(K,12)=0 THEN T$(K,12)="0.000" ELSE T$(K,12)="0."+MID$(T$(K,12),2,3)
650 T$(K,13)=MID$(T$(K,13),2,1)+"."+MID$(T$(K,13),3,3)
660 FOR Y=1 TO 10:T$(K,Y)=RIGHT$(T$(K,Y),3):NEXT Y
670 ST$(K)="":FOR Y=1 TO 13:ST$(K)=ST$(K)+T$(K,Y):NEXT Y
680 NEXT K
700 CLS:PRINT"GAMES";SG;" TO";EG;" RECALCULATED.":PRINT
710 PRINT"SAVE TO <R>OSTER FILE.":PRINT
720 PRINT"SAVE TO <S>PECIAL FILE.":PRINT
730 PRINT"SEND TO <P>RINTER.":PRINT
740 PRINT"<Q>UIT":PRINT
750 PRINT"<R>, <S>, <P>, OR <Q>: ";
760 A$=INKEY$:IF A$="R" THEN 780 ELSE IF A$="S" THEN 770 ELSE IF A$="P" THEN 850 ELSE IF A$="Q" THEN 940 ELSE 760
770 CLS:LINE INPUT"INPUT SPECIAL FILE NAME (8 OR   LESS CHARACTERS):";F1$:IF LEN(F1$)>8 THEN 770
780 REM GOSUB 900
790 OPEN"D",#2,F1$,128
800 FIELD#2,25 AS NF$,30 AS AF$,20 AS CF$,8 AS PF$,45 AS SF$
810 FOR X=1 TO R
820 LSET NF$=N$(X):LSET AF$=A$(X):LSET CF$=C$(X):LSET PF$=P$(X):LSET SF$=ST$(X)
830 PUT#2,X:NEXT X
840 CLOSE #2:PRINT"FILE STORED AS ";F1$:GOTO 710
850 CLS:PRINT"PRINTER ON? PAPER POSITIONED?"
860 PRINT"PRESS <ENTER> WHEN READY.":INPUT B$
870 PRINT#-2,TAB(21)"SPECIAL REPORT FROM GAME";SG;" TO";EG
880 PRINT#-2,CHR$(10);CHR$(15);TAB(4)"NAME";TAB(20)"  AB  R   H   BB  2B  3B  HR SAC RBI  EI   BA   OBA   SLA";CHR$(14);CHR$(10)
890 FOR K=1 TO R
900 PRINT#-2,N2$(K);TAB(19)T(K,1);TAB(23)T(K,2);TAB(27)T(K,3);TAB(31)T(K,4);TAB(35)T(K,5);TAB(39)T(K,6);TAB(43)T(K,7);TAB(47)T(K,8);TAB(51)T(K,9);TAB(55)T(K,10);TAB(59)INT(1000*T(K,11)+.5)/1000;TAB(64)INT(1000*T(K,12)+.5)/1000;
901 PRINT#-2,TAB(72)INT(1000*T(K,13)+.5)/1000
910 NEXT K
920 GOTO 700
930 REM A$=INKEY$:IF A$="" THEN 810 ELSE IF A$="Q" THEN 820 ELSE 240
940 CLOSE:END
