100 ' PROGRAM TO GENERATE A DISK FILES LOCATOR REPORT
110 '
120 ' By: A. L. McGarrity
130 '
140 ' Last Modified: 02/03/85
150 '
160 CLEAR 12000
170 DIM N$(20),F$(400),W$(400),P(400),G(20)
180 CLS
190 PRINT:PRINT"   DISK FILES LOCATOR PROGRAM":PRINT:PRINTTAB(7);"DATE: ";
200 DH$=STRING$(15,"-")
210 LINE INPUT D$
220 PRINT
230 TP=INT((78-LEN(D$))/2)
240 PRINT@32*(ND-9*INT(ND/9))+160,"INSERT NEXT DISK, ENTER ##";
250 SOUND 32,2:SOUND 89,4
260 INPUT N$
270 IF N$="" THEN 470 ' NO MORE DISKS
280 ND=ND+1: N$(ND)=N$
290 FOR S=3 TO 11
300 DSKI$ 0,17,S,A$,B$
310 A$=A$+LEFT$(B$,112)
320 FOR F=1 TO 8
330 F$=MID$(A$,32*F-31,11)
340 IF LEFT$(F$,1)=CHR$(255) THEN 440 ' NO MORE FILES THIS DISK
350 IF LEFT$(F$,1)=CHR$(0) THEN 420 ' THIS FILE HAS BEEN DELETED
360 IF NF=0 THEN 400
370 FOR I=1 TO NF
380 IF F$=F$(I) THEN 410
390 NEXT I
400 NF=NF+1:F$(NF)=F$:W$(NF)=STRING$(20,"-"):I=NF
410 MID$(W$(I),ND,1)="X"
420 NEXT F
430 NEXT S
440 G(ND)=FREE(0)
450 PRINT@480,"DISKETTES :";ND;TAB(18);"FILES :";NF;
460 GOTO 240
470 CLS:PRINT"NUMBER OF DISKS :"ND
480 PRINT"NUMBER OF FILES :"NF
490 GOTO 610
500 FOR K=1 TO 500:NEXT:PRINT#-2,CHR$(0)+CHR$(12)+CHR$(0):FOR K=1 TO 800:NEXT
510 NP=NP+1:FOR K=1 TO 3:PRINT#-2,"    ":NEXT:PRINT#-2,TAB(26);"DISK FILES LOCATOR REPORT";TAB(69);"PAGE"NP
520 PRINT#-2,"    ":PRINT#-2,TAB(TP);D$
530 FOR J=1 TO 2:PRINT#-2,"    ":NEXT
540 PRINT#-2,"FILENAME  EXT  ";DH$;" DISKETTE(S) ON WHICH STORED ";DH$
550 PRINT#-2,TAB(16);
560 FOR J=1 TO ND
570 PRINT#-2,N$(J);" ";
580 NEXT
590 FOR J=1 TO 2:PRINT#-2,"    ":NEXT
600 NL=0: RETURN
610 PRINT@128,"SORT FILENAMES (Y/N) ";:INPUT R$:R$=LEFT$(R$,1)
620 IF R$<>"Y" AND R$<>"N" THEN 610
630 M=511: NL=48
640 FOR I=1 TO NF:P(I)=I:NEXT I
650 IF R$="N" THEN 810
660 PRINT@192,"SORTING ... PLEASE WAIT":TIMER=0
670 FOR K=1 TO 8:M=(M-1)/2
680 IF NF-M-1 < 0 THEN 790 ELSE M1=M+1:I=M1
690 I1=P(I):M2=I-M:J=1
700 J1=M2-J+1:I2=P(J1)
710 IF F$(I1)>=F$(I2) THEN 760
720 P(J1+M)=P(J1)
730 IF J+M>M2 THEN 750
740 J=J+M:GOTO 700
750 P(J1)=I1:GOTO 770
760 P(J1+M)=I1
770 IF I+1>NF THEN 790
780 I=I+1:GOTO 690
790 NEXT K
800 PRINT@256,"SORTING TOOK";INT(TIMER/60);"SECONDS"
810 FOR L=1 TO NF: I=P(L)
820 IF NL=48 THEN GOSUB 500
830 PRINT#-2,LEFT$(F$(I),8);"  ";RIGHT$(F$(I),3);"   ";
840 FOR J=1 TO ND
850 PRINT#-2,MID$(W$(I),J,1);"  ";
860 NEXT J
870 PRINT#-2,"    "
880 NL=NL+1
890 NEXT L
900 PRINT#-2,"    "
910 PRINT#-2,"GRANULES USED ";
920 FOR I=1 TO ND: PRINT #-2,USING "###";68-G(I);:NEXT I
930 PRINT#-2,"    ":PRINT#-2,"    "
940 PRINT#-2,"GRANULES FREE ";
950 FOR I=1 TO ND: PRINT #-2,USING "###";G(I);:NEXT I
960 PRINT#-2,"    "
