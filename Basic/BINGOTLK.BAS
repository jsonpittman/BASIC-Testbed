0 'BINGO 2.0; <C> BRUCE BELL 1986,87; 16KECB; this program is nonwarranted!!!
5 POKE3584,0:IFPEEK(33021)=50THENWIDTH32:POKE3584,2
10 FORK=1TO8:PRINT,"BINGO",,"bingo",:NEXT:GOTO1100
100 POKE65495+PEEK(3584),0:PRINT@0,CHR$(181)" ONE MOMENT "CHR$(186);:CLEAR550:DIML$(14),N$(15),A(75),B(2,4,5):R=RND(-TIMER):M1=&HFF00:M2=&HFF7E:GOSUB920:DEF FNL(N)=INT(ABS(N-1)/15)+1
105 FORK=1TO14:FORX=1TO15:READD:L$(K)=L$(K)+RIGHT$(STR$(D),2):NEXTX,K:FORK=0TO15:READN$(K):NEXT:POKE65494+PEEK(3584),0
110 CLS0:FORK=0TO63:SET(K,2,4):SET(K,11,4):NEXT:R=32:L$="TALKING BINGO   BY BRUCE  BELLL":PRINT@19,"by";CHR$(128)"bruce"CHR$(128)"bell";:GOSUB850:GOSUB900:FORK=1TO75:A(K)=0:NEXT:Z=0:F=0
115 PRINT@224,TAB(6)"1. PLAY BINGO"TAB(38)"2. COMPUTER CHALLENGE"TAB(38)"3. PRINT BINGO CARDS"TAB(38)"4. EXIT TO BASIC":L$="WHAT IS YOUR PLEASURE?":PRINT@388,L$;:GOSUB850
120 GOSUB910:CH=Q:ONQ GOTO200,400,500,700:R=RND(7)*16:GOSUB900:GOTO120
200 L$="GGET READY":GOSUB850:CLS0:L$="bingo":GOSUB915:GOSUB925
205 IFQ$="B"THEN300ELSEN=RND(75):IFA(N)>0THEN205ELSER=RND(6)*16:L=FNL(N)
210 P=200:L$=L$(L):GOSUB905:R=R+16:N$=RIGHT$(STR$(N),2)
215 FORK=1TOLEN(N$):X$=MID$(N$,K,1):IFX$=" "THENL$=""ELSEL$=L$(VAL(X$)+5)
220 P=200+6*K:GOSUB905:NEXTK
225 TIMER=0:TALK=1:IFT=0GOSUB800ELSESOUND100,1
230 IFCH=2THEN410
235 GOSUB910:GOSUB930:IFDLAY=5ANDTALK=1GOSUB800:TALK=0:GOTO235ELSEIFDLAY THEN235
240 PRINT@Z,MID$("BINGO",L,1);RIGHT$(STR$(N),2);CHR$(128);
245 A(N)=Z+3:Z=Z+4:IFZ=160THENZ=320
250 GOTO205
300 IFWV GOSUB465:GOTO330ELSEPRINT@251,"bingo";:GOSUB945:PRINT@481," M=MENU   F=FREE   C=CONTINUE ";
305 FORK=0TO4:P=197+K*16:PRINT@P,"";:INPUTQ$
310 PRINT@219+INT(K/2)*32,"bingo";:IFQ$="M"THENK=5:NEXT:GOTO100
315 IFQ$="C"THENK=5:NEXT:GOSUB945:GOSUB925:F=0:FORK=1TO75:IFA(K)>1000THENA(K)=A(K)-1000:PRINT@A(K),CHR$(128);:NEXT:GOTO205ELSENEXT:GOTO205
320 IFQ$="F"THENIFF=1THEN345ELSEF=1:NEXTK:GOTO330
325 N=VAL(MID$(Q$,2)):IFA(N)=0ORA(N)>1000THEN345ELSEPRINT@A(N),CHR$(127);:A(N)=1000+A(N):IFT THENSOUND200,1:NEXTK ELSEL$="CHECK":GOSUB850:NEXTK
330 PRINT@480,STRING$(31,128);:PRINT@480,"GAME OVER! mENU cONTINUE";:L$="BINGO":GOSUB850:IFCH=2THENPRINT" vERIFY";
335 GOSUB910:L$="BINGO":GOSUB915:L$="bingo":GOSUB915:IFQ$="C"THENPRINT@165,STRING$(22,128);:PRINT@293,STRING$(22,128);:CH=2-WV:WV=0:FORK=3TO4:GOTO315ELSEIFCH=2 AND Q$="V"THENUU=U:FORU=0TO2:GOSUB465:PRINT@480,"   CARD"U+1":   PRESS <ENTER>    "; ELSE335
340 GOSUB910:IFQ$=""THEN340ELSENEXTU:U=UU:IFU=3GOSUB945:GOTO330ELSEGOSUB465:GOTO330
345 PRINT@P,"?????";:IFT THENSOUND1,5 ELSEL$="PLEASE RE ENTER":GOSUB850
350 K=K-1:NEXTK
400 L$=" ONE MOMENT PLEASE... ":PRINT@388,L$;:GOSUB850:GOTO600
405 FORK=0TO75:A(K)=0:NEXT:GOTO200
410 FORK=0TO2:FORC=0TO4:FORR=0TO4:IFB(K,C,R)=N THENB(K,C,R)=B(K,C,R)+500
415 GOSUB930:IFDLAY=5ANDTALK=1GOSUB800:TALK=0
420 NEXTR,C,K:GOSUB910
425 WV=0:FORU=0TO2:FORC=0TO4:W=0:V=0:FORR=0TO4:IFB(U,C,R)>499THENW=W+1
430 IFB(U,R,C)>499THENV=V+1
435 NEXTR:GOSUB930:IFW=5THENFORR=0TO4:B(U,C,R)=B(U,C,R)+500:NEXTR:C=4:NEXTC:GOTO460ELSEIFV=5THENFORR=0TO4:B(U,R,C)=B(U,R,C)+500:NEXTR:C=4:NEXTC:GOTO460ELSENEXTC
440 R=4:W=0:V=0:FORC=0TO4:IFB(U,C,C)>499THENW=W+1
445 IFB(U,C,R)>499THENV=V+1
450 R=R-1:NEXTC:GOSUB930:IFW=5THENFORC=0TO4:B(U,C,C)=B(U,C,C)+500:NEXTC:GOTO460ELSEIFV=5THENR=4:FORC=0TO4:B(U,C,R)=B(U,C,R)+500:R=R-1:NEXTC:GOTO460
455 NEXTU:GOTO235
460 WV=5:Q$="B":GOTO240
465 GOSUB945:FORP=165TO293STEP32:PRINT@P,CHR$(165)STRING$(20,32)CHR$(170);:NEXT:FORR=0TO4:P=166+R*32:FORC=0TO4:IFB(U,C,R)>999THENB=B(U,C,R)-1000:D=2ELSEIFB(U,C,R)>499THENB=B(U,C,R)-500:D=1ELSEB=B(U,C,R):D=0
470 IFD=0THENB$="BINGO"ELSEB$="bingo":IFD=2THENPRINT@P,CHR$(186);:IFB THENPRINT@A(B),CHR$(127);
475 IFB=0THENPRINT@P+1,"fre";ELSEPRINT@P+1,USINGMID$(B$,FNL(B),1)+"##";B;:IFD THENFORK=1TO2:POKE1025+P+K,PEEK(1025+P+K)-64:NEXT
480 P=P+4:NEXTC,R:RETURN
500 PRINT@386,"HOW MANY ROWS OF CARDS";:INPUTQ
505 PRINT@416,"PREPARE PRINTER AND PRESS enter";:INPUTQ$:IF(PEEK(65314)AND1)=1THENPRINT@416,"PRINTER IS NOT READY!!!":SOUND100,1:FORZ=1TO1000:NEXT:GOTO505
510 FORZ=1TOQ:PRINT#-2:PRINT#-2:FORK=1TO75:A(K)=0:NEXTK
515 PRINT#-2,CHR$(27)CHR$(14);:'call expanded print mode (DMP200)
520 FORK=1TO3:PRINT#-2,"  B I N G O  ";:NEXT:PRINT#-2
525 FORK=1TO3:PRINT#-2,"  _________  ";:NEXT:PRINT#-2,CHR$(27)CHR$(15):'cancel expanded print mode (DMP200)
600 FORR=0TO4:FORK=0TO2:FORC=0TO4
605 N=RND(15)+15*C:X=2^K:IF(A(N)ANDX)<>0 THEN605ELSEA(N)=(A(N)ORX):IFR=2ANDC=2THENIFCH=2THENB(K,C,R)=500:NEXTC ELSEPRINT#-2,TAB(K*26+12)"**";:NEXTC
610 IFCH=2THENB(K,C,R)=N:NEXTC,K,R:GOTO405ELSEPRINT#-2,TAB(K*26+C*4+3)N;:NEXTC,K:PRINT#-2:PRINT#-2:NEXTR
615 PRINT#-2:PRINT#-2:IFZ/4=INT(Z/4)THENPRINT#-2:PRINT#-2
620 NEXTZ:GOTO100
700 CLSRND(8):L$="SO LONG!":PRINTL$:GOSUB850:END
800 L$=MID$("BINGO",L,1)+" ":IFN<10THENL$=L$+STR$(N)ELSEIFN<21THENL$=L$+N$(N-10)ELSEIFN/10=INT(N/10)THENL$=L$+N$(N/10+8)ELSEL$=L$+N$(INT(N/10)+8)+RIGHT$(STR$(N),1)
850 IFT THENRETURNELSEL$=L$+" "+CHR$(13):FORY=1TOLEN(L$)
855 IFPEEK(M2) AND 128=0 THEN855ELSEPOKEM2,ASC(MID$(L$,Y,1)):NEXT
860 RETURN
900 FORA=1TO5:P=59+6*A:L$=L$(A):GOSUB905:NEXTA:RETURN
905 FORB=1TO21STEP10:PRINT@P,"";:FORC=0TO9STEP2:PRINTCHR$(R+128+VAL(MID$(L$,B+C,2)));:NEXTC:P=P+32:NEXTB:RETURN
910 Q$=INKEY$:IFQ$="M"THEN100 ELSEIFQ$="T"THENT=(T+1)AND1:PRINT@507,CHR$(84+T*32);:GOSUB920:RETURNELSEIFQ$="P"THEN935ELSEIFQ$<>"B"THENQ=VAL(Q$):RETURNELSEIFCH THEN240ELSERETURN
915 FORK=0TO2:PRINT@192+K*32,L$;:PRINT@219+K*32,L$;:NEXT:RETURN
920 POKE&HFF7D,1:POKE&HFF7D,0:POKEM1+1,52:POKEM1+3,63:POKEM1+35,60:RETURN
925 PRINT@480,CHR$(128);"Menu";STRING$(4,128);"Bingo";STRING$(4,128);"Pause";STRING$(4,128);"Talk";:IFT THENPRINT@507,"t";:RETURNELSERETURN
930 DLAY=10-INT(TIMER/60):PRINT@251,RIGHT$(STR$(DLAY),2)"SEC";:RETURN
935 TI=TIMER:PRINT@481,"   PRESS ANY KEY TO CONTINUE  ";
940 IFINKEY$=""THEN940ELSETIMER=TI:GOTO925
945 FORK=197TO261STEP32:PRINT@K,STRING$(22,128);:NEXT:RETURN
1000 DATA15,12,12,15,0,15,12,12,12,15,15,3,3,3,15
1005 DATA4,12,15,12,8,0,0,15,0,0,1,3,15,3,2
1010 DATA15,9,0,0,15,15,0,9,0,15,15,0,0,9,15
1015 DATA15,12,12,12,8,15,0,1,3,3,15,3,3,3,7
1020 DATA15,12,12,12,15,15,0,0,0,15,15,3,3,3,15
1025 DATA0,1,15,0,0,0,0,15,0,0,0,3,15,3,0
1030 DATA0,4,12,12,15,15,12,12,12,12,15,3,3,3,3
1035 DATA12,12,12,15,0,0,4,12,12,15,3,3,3,3,15
1040 DATA15,0,0,15,0,12,12,12,15,12,0,0,0,15,0
1045 DATA15,12,12,0,0,12,12,12,12,15,3,3,3,3,15
1050 DATA15,12,12,0,0,15,12,12,12,15,15,3,3,3,15
1055 DATA0,12,12,12,10,0,0,0,6,0,0,0,6,0,0
1060 DATA0,15,12,15,0,15,12,12,12,15,15,3,3,3,15
1065 DATA15,12,12,12,15,12,12,12,12,15,0,0,0,0,15
1070 DATATENN,EELLEVEN,TWELLVE,THHIRTEEN,FORTEEN,FFIFTEEN,SSIXTEEN,SSEVENTEEN,EIGHT TEEN,NINE TEEN,TWENTEE,THIRTEE,FORTEE,FIFTEE,SSIXTEE,SSEVENTEE
1100 PCLEAR1:GOTO100
