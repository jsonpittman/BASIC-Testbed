1 'ARKITEKT
2 'BY KEIRAN KENNY
3 'COPYRIGHT (C) JANUARY 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 DIMA(3),B(3),C(38),D(38),E(38),F(13),G(38)
20 D$="F3G3H3E3":PK=PEEK(&H15F)
30 PMODE4,1:COLOR0,5:PCLS:SCREEN1,1
40 GET(0,0)-(31,31),G
50 GET(0,0)-(5,6),B
60 DRAW"BM16,31U31R5D31NL5BR21U31R5D31NL5":PAINT(18,30),0,0:PAINT(44,30),0,0:GET(16,0)-(47,47),C
70 DRAW"BM56,16"+D$:PAINT(56,18),0,0:GET(53,16)-(59,23),A,G
80 LINE(80,0)-(111,31),PSET,BF:GET(80,0)-(111,31),D
90 DRAW"BM144,31NR31M+15,-31M+16,+31":PAINT(160,30),0,0:GET(144,0)-(175,31),E
100 CIRCLE(224,15),16,,1,.5,1:CIRCLE(224,15),10,,1,.5,1:LINE(209,15)-(214,15),PSET:LINE(234,15)-(239,15),PSET:PAINT(235,14),0,0:GET(208,0)-(239,15),F
110 FORX=0TO256STEP64:LINE(X,0)-(X,31),PSET:NEXT
120 ST=53:N=1
130 K$=INKEY$:IFK$<>CHR$(8)ANDK$<>CHR$(9)ANDK$<>CHR$(13)ANDK$<>"S"ANDK$<>"L"THEN130
140 IFK$="L"THEN490
150 IFK$="S"THEN450
160 IFK$=CHR$(13)THEN240
170 IFK$=CHR$(9)THENST=ST+64:GOTO180ELSE200
180 IFST>245THENST=245:GOTO230
190 PUT(ST-64,16)-(ST-58,23),B,PSET:PUT(ST,16)-(ST+6,23),A,PSET:N=N+1:GOTO230
200 IFK$=CHR$(8)THENST=ST-64
210 IFST<53THENST=53:GOTO230
220 PUT(ST+64,16)-(ST+70,23),B,PSET:PUT(ST,16)-(ST+6,23),A,PSET:N=N-1
230 GOTO130
240 X=112:Y=32
250 IFN=4THENPUT(X,Y)-(X+31,Y+15),G ELSEPUT(X,Y)-(X+31,Y+31),G,PSET
260 IFN=4THENLL=176ELSELL=160
270 IFPEEK(341)=247THENY=Y-8
280 IFPEEK(342)=247THENY=Y+8
290 IFPEEK(343)=247THENX=X-8
300 IFPEEK(344)=247THENX=X+8
310 IFX<0THENX=0
320 IFX>224THENX=224
330 IFY<32THENY=32
340 IFY>LL THENY=LL
350 IFN=1THENPUT(X,Y)-(X+31,Y+31),C
360 IFN=2THENPUT(X,Y)-(X+31,Y+31),D
370 IFN=3THENPUT(X,Y)-(X+31,Y+31),E
380 IFN=4THENPUT(X,Y)-(X+31,Y+15),F
390 IFINKEY$=CHR$(13)THEN410
400 GOTO250
410 K$=INKEY$:IFK$=""THEN410
420 IFK$=CHR$(12)ANDN<4THENPUT(X,Y)-(X+31,Y+31),G ELSEIFK$=CHR$(12)ANDN=4THENPUT(X,Y)-(X+31,Y+16),G
430 PUT(ST,16)-(ST+6,23),B,PSET:PUT(53,16)-(59,23),A,PSET
440 GOTO120
450 K$=INKEY$:FORT=0TO224STEP32:PUT(T,0)-(T+31,31),G:NEXT:CLS:PRINT@224,"SAVE PIX NAME:";:INPUTF$:SCREEN1,1
460 IFPK=196THEN470ELSE480
470 SAVEMF$,&HE00,PEEK(&HB7)*256,&HADFB:EXEC44539:GOTO60
480 CSAVEMF$,PEEK(&HBA)*256,PEEK(&HB7)*256-1,&HB44A:EXEC44539:GOTO60
490 CLS:PRINT@224,"LOAD PIX NAME:";:INPUTF$:PCLS:SCREEN1,1
500 IFPK=196THEN510ELSE520
510 LOADMF$:EXEC44539:GOTO60
520 CLOADMF$:EXEC44539:GOTO60
