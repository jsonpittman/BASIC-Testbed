10 'EGG TIMER
15 'WRITTEN BY ROBERT C. NUGENT
20 'COPYRIGHT (C) NOVEMBER 1991
25 'BY FALSOFT, INC.
30 ON BRK GOTO 640
40 POKE &HF015,&H21 'OVERWRITE POKE
50 HSCREEN 2:HCOLOR 8:POKE 65497,0 'HIGH SPEED POKE
60 HBUFF 1,2360:HBUFF 2,2360
70 HPRINT (12,1),"*** EGG TIMER ***"
80 HPRINT (2,4),"WOULD YOU LIKE A..."
90 HPRINT (5,20),"MINUTE EGG"
100 '** DRAW EGG TIMER
110 HDRAW "BM190,30;ND5R70D5NL70L5D130R5D5L70U5NR70R5U130R3ND130R54D130L10U63L16U4R16U63L34D63R16D4L16D63
120 '** ESTABLISH & DRAW BUTTONS
130 A$="R15U15L15D15BD3R19U21L23D21R3
140 HDRAW "C9;BM77,74"+A$+"BD29"+A$+"BD29"+A$:HCOLOR 8
150 '** PAINT BUTTONS
160 HPAINT (75,76),3,9
170 HPAINT (74,108),6,9
180 HPAINT (73,140),2,9
190 HPRINT (10,8),"1":HPRINT (10,12),"2":HPRINT (10,16),"3"
200 '** PAINT FRAME
210 HPAINT (192,32),1,8 'TOP
220 HPAINT (192,169),1,8 'BOTTOM
230 HPAINT (196,39),6,8 'LEFT POST
240 HPAINT (253,39),6,8 'RIGHT POST
250 '** PAINT RED & GET TOP TUBE
260 HSET (225,102):HPAINT (210,37),3,8
270 HGET (208,35)-(243,165),1
280 '** PAINT BLUE & GET TOP OF TUBE
290 HSET (225,102):HPAINT (210,37),2,8
300 HGET (208,35)-(243,165),2
310 '** START TIMER
320 HPRINT (4,22),"PLEASE SELECT":PLAY "T255CDEF"
330 S$=INKEY$:IF S$="" THEN 330
340 PLAY "T255FED"
350 IF S$<>"1" AND S$<>"2" AND S$<>"3" THEN 330
360 HPRINT (2,20),VAL(S$)
370 '** ERASE "PLEASE SELECT"
380 HPRINT (4,22),STRING$(13," ")
390 HPRINT (5,22),"GOOD LUCK!"
400 '** SWITCH TUBE COLORS
410 M=M+1:IF M>1 THEN M=0
420 IF M=1 THEN C=3:B=1
430 IF M=0 THEN C=2:B=2
440 HCOLOR C
450 '** MAKE DROPS FROM CENTER TO BOTTOM
460 X=209:Y1=35:Y3=165:X2=225
470 FOR P= 1 TO 62:Y2=Y1+P:Y4=Y3-P
480 HDRAW "C0;BM=X;,=Y2;R32":HCOLOR C
490 FOR DR= 103 TO Y4 STEP 2
500 HSET (225,DR):FOR D= 1 TO 42*VAL(S$):NEXT D
510 HRESET (225,DR):NEXT DR
520 HCOLOR C:HDRAW "BM=X;,=Y4;R32
530 PLAY "T255CDE":HSET (225,Y4):NEXT P
540 HDRAW "C0BM225,98;D3"
550 HPRINT (5,22),"GOOD LUCK!"
560 '** RESET TIMER
570 HCOLOR 8:HPRINT (14,16),"TIMES UP!"
580 HPRINT (6,22),"PRESS <ANYKEY> TO RESET TIMER":SOUND 200,1:IF INKEY$="" THEN 580
590 HPRINT (6,22),STRING$(29," ")
600 HPRINT (14,16),STRING$(9," ")
610 HPRINT (3,20)," "
620 '** SWITCH TUBE COLORS
630 HPUT (208,35)-(243,165),B:GOTO 320
640 '** TAKE OUT OVERWRITE & SLOW DOWN
650 POKE &HF015,&H21:POKE 65496,0
660 WIDTH 40:LOCATE 8,10:ATTR 2,0,B:PRINT "COMPUTER BACK TO NORMAL":ATTR 2,0
670 LOCATE 6,12:PRINT "Hope your egg turned out OK!"
