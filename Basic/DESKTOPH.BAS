0 RGB:WIDTH40:CLS3:LOCATE14,8:ATTR7,2:PRINT"DESKTOP ";:ATTR7,2,U:PRINT"HIGH";:ATTR7,2:LOCATE11,12:PRINT"BY H. Allen Curtis":LOCATE13,14:PRINT"COPYRIGHT 1987":LOCATE24,8:ATTR7,2
5 CLEAR4000:A=&H1600:A$="34761F52313A1E428E7072BFFFA28D188E7A7BBFFFA28E7173BFFFA28D0A":GOSUB600:A=A+30:A$="8E7A7BBFFFA21E4235F68E4000CE6000A684E6C4A7C0E7808C600026F339":GOSUB600
10 CLEAR5000:W=320:H=1
15 HCOLOR3,0:ON BRK GOTO715
20 K$=K$+"C":A1=PEEK(VARPTR(K$)+2):A2=PEEK(VARPTR(K$)+3):IFA2<2THENA2=254:A1=A1-1:GOTO40ELSEA2=A2-2:GOTO40GOTO40
25 GOSUB485:CLS:LOCATE12,8:PRINT"FILENAME: ";:LINEINPUTF$:POKE&HFFA2,&H70:SAVEM"OUT1",&H4000,&H5FFF,&HAC73:POKE&HFFA2,&H71:SAVEM"OUT2",&H4000,&H5FFF,&HAC73:POKE&HFFA2,&H7A:RENAME"OUT1/BIN"TOF$+"/HR1":RENAME"OUT2/BIN"TOF$+"/HR2":RETURN
30 GOSUB485:CLS:LOCATE12,8:PRINT"FILENAME: ";:LINEINPUTF$:RENAMEF$+"/HR1"TO"IN1/BIN":RENAMEF$+"/HR2"TO"IN2/BIN":POKE&HFFA2,&H70:LOADM"IN1":POKE&HFFA2,&H71:LOADM"IN2":POKE&HFFA2,&H7A
35 RENAME"IN1/BIN"TOF$+"/HR1":RENAME"IN2/BIN"TOF$+"/HR2":RETURN
40 L2=1:P=176:DIMF$(84),M(84):GOSUB630:F$(0)=D$(7):M(0)=D(7):K$="1":GOSUB350:POKE&HFFD9,0
45 C1=63:PALETTE0,63:PALETTE1,0:PALETTE2,63:PALETTE3,0
50 HSCREENH:HBUFF1,99:HGET(8,152)-(9,152+D),1
55 HBUFF4,1824:HBUFF5,3624:HGET(16,20)-(111,95),4:HBUFF6,1520:HGET(16,20)-(111,170),5
60 HCLS:T=V:L=U
65 IF(Q=0 OR Z=U) AND L+4>W-1THENL=U:IFT<P THENT=T+D+1
70 IFQ=1 AND Z<>U AND L+4>W-1THENGOSUB195
75 HGET(L,T)-(L+1,T+D),1:HLINE(L,T)-(L+1,T+D),PSET,BF
80 POKE&H23,A1:POKE&H24,A2
85 K$=INKEY$:IFK$=""THEN85
90 K=ASC(K$):IFK>64 AND K<91THENN=K-64:B=N:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
95 IFK>96 AND K<123THENN=K-96:B=N+26:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
100 IFK>47 AND K<58THENN=K-47:B=N+52:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
105 IFK>32 AND K<48THENN=K-32:B=N+62:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
110 IFK>57 AND K<65THENN=K-57:B=N+77:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
115 IFK=32THENZ=L+S:IFL+8<W THENHLINE(L,T)-(L+7,T+D),PRESET,BF:L=L+S:GOTO65ELSEHLINE(L,T)-(L+1,T+D),PRESET,BF:L=U:T=T+1+D:GOTO65
120 IFK=13THENHPUT(L,T)-(L+1,T+D),1:L=U
125 IFK=13 AND T<P THENT=T+1+D:GOTO65
130 IFK=8 AND L>1THENL=2*INT(.5*L):HLINE(L,T)-(L+1,T+D),PRESET,BF:L=L-2:GOTO65
135 IFK=93THENB=0:HLINE(L,T)-(L+1,T+D),PRESET,BF:GOTO180
140 IFK=94THENHPUT(L,T)-(L+1,T+D),1:L=U:IFT>D THENT=T-1-D:GOTO65
145 IFK=91THENZ=U:HPUT(L,T)-(L+1,T+D),1:HDRAW"BM"+STR$(L)+","+STR$(T+1+INT(.75*D))+"R3":L=L+4:IFL>W-5THENL=L-4
150 IFK=9 AND L+4<W THENHPUT(L,T)-(L+1,T+D),1:L=L+4:GOTO65
155 IFK=92THEN60
160 IFK=4THENHPUT(L,T)-(L+1,T+D),1:GOTO230
165 IFK=10THENHPUT(L,T)-(L+1,T+D),1:IFT3=0THENT3=1:L=T1 ELSEIFT3=1THENT3=0:L=T2
170 IFK=21THENHPUT(U,T)-(W-1,T+D),4:L=U:GOTO65
175 GOTO65
180 IFL+M(B)<W THENGOSUB225:L=L+2*INT(.5+M(B)*.5)+2:IFL>W-1THENL2=L-W+1:L=W-1:GOTO65ELSE65
185 IFQ=0 OR Z=U THENL=U:IFT<P THENT=T+D+1:GOTO90ELSE90
190 GOSUB195:GOTO90
195 IFH=3THENZ1=8*INT(.125*Z)
200 IFH=1THENZ1=4*INT(.25*Z)
205 Z=Z1
210 HGET(Z,T)-(L,T+D),6:HPUT(Z,T)-(L,T+D),4:L1=L-Z:L=U:Z=U:IFT<P THENT=T+D+1
215 IFL+L1<0THENL1=L1+2:GOTO215
220 HPUT(L,T)-(L+L1,T+D),6:L=L+L1+L2:L=2*INT(.5*L+.5):L2=0:RETURN
225 HDRAW"BM"+STR$(L)+","+STR$(T)+F$(B):RETURN
230 HSCREEN0:CLS:ATTR0,4:LOCATE10,5:PRINT"B: BACK TO SCREEN":LOCATE10,6:PRINT"C: COLOR SWAP":LOCATE10,7:PRINT"D: DESIGN SELECT
235 LOCATE10,8:PRINT"F: FONT SELECT":LOCATE10,9:PRINT"I: INPUT":LOCATE10,10:PRINT"K: KEYS LISTED":LOCATE10,11:PRINT"M: MARGIN SET":LOCATE10,12:PRINT"O: OUTPUT":LOCATE10,13:PRINT"R: RESOLUTION CHANGE
240 LOCATE10,14:PRINT"S: SCREEN SWITCH":LOCATE10,15:PRINT"T: TAB SET":LOCATE10,16:PRINT"W: WORDWRAP SET":LOCATE10,17:PRINT"X: EXIT DESKTOP":LOCATE14,17:POKE&H23,A1:POKE&24,A2:POKE&HFFD8,0
245 K$=INKEY$:IFK$=""THEN245
250 IFK$="D" OR K$="d"THENGOSUB680:GOTO230
255 IFK$="F" OR K$="f"THENGOSUB325:GOTO380
260 IFK$="B" OR K$="b"THEN320
265 IFK$="I" OR K$="i"THENGOSUB30:GOTO230
270 IFK$="K" OR K$="k"THENGOSUB605:GOTO230
275 IFK$="O" OR K$="o"THENGOSUB25:GOTO230
280 IFK$="M" OR K$="m"THENGOSUB500:GOTO230
285 IFK$="R" OR K$="r"THENIFH=1THENH=3:U=2*U:W=2*W:T1=2*T1:T2=2*T2:GOTO230ELSEH=1:U=.5*U:W=.5*W:T1=.5*T1:T2=.5*T2:GOTO230
290 IFK$="T" OR K$="t"THENGOSUB585:GOTO230
295 IFK$="C" OR K$="c"THENGOSUB710:GOTO230
300 IFK$="S" OR K$="s"THENEXEC&H1600:GOTO230
305 IFK$="W" OR K$="w"THENGOSUB550:GOTO230
310 IFK$="X" OR K$="x"THEN715
315 SOUND60,5:SOUND60,5:GOTO230
320 POKE&HE6E4,&HE6:HSCREENH:POKE&HE6E4,&HE7:POKE&HFFD9,0:GOTO385
325 GOSUB390
330 POKE&H23,A1:POKE&H24,A2
335 K$=INKEY$:IFK$="" THEN335
340 IFK$<"1" OR K$>"2" THENSOUND60,10:RETURN
345 GOSUB485
350 GOSUB495
355 OPEN"I",#1,"FONT"+K$
360 FORI=1TO84:LINEINPUT#1,F$(I):NEXT
365 FORI=1TO84:INPUT#1,M(I):NEXT
370 INPUT#1,D,S:CLOSE#1
375 RETURN
380 HPUT(16,20)-(111,170),5
385 GOSUB545:HGET(L,T)-(L+1,T+D),1:GOTO65
390 POKE&HE6E4,&HE6:HSCREENH:POKE&HE6E4,&HE7:HGET(16,20)-(111,170),5:HPUT(16,20)-(111,95),4:HPUT(16,96)-(111,170),4:HDRAW"BM25,28NGD6NL2RNR2U6BM32,29RDLBD3RDL
395 HDRAW"BM50,24G3ERE2R4GNL3G4D5EU4BR2D5G4UH2LG2ER3FERE2URUE2NFG2U4E4F2DH2DFBM64,29G2ND4LD4NHFRE2NU5RU4FBM72,29G2RD4NHFNEU4NU2E3ND6FNFD6E2BM83,25G3LR3NR2NUD8NE2H2RU5
400 HDRAW"BM24,43NGNDR3ND2FDGL2NG2DG2R5ULBM32,44RDLBD3RDL
405 HDRAW"BM49,43R6NDNGL5D3NR3D3NLR2HU4BM59,45ND3GD2FR3NU3EU2HL2BM66,45D4RU4R3D4RU3BM76,43ND5G2NR4FD2FRE
410 HDRAW"BM24,62NGNDR3ND5FDGNL2FDGL3ULBM32,63RDLBD3RDL
415 HDRAW"BM50,56R5BR2DNLNRDRNRDRE2BL7L5DLNGBR5G2ND8GD8BDBL2L2DR2DFNDRURE2U9NE3RD3ND4REFBM64,61G2ND6LD5LF2RE2NU7RU6FBM72,61NG2D3E3DRFLD7NE2HNU6BL2GNU5LNHU7BM83,57G2RD11NE2H2RU8L2R5
420 HDRAW"BM26,81G3DE4D6NLRNRU2NRNL4U4BM32,82RDLBD3RDL
425 HDRAW"BM46,78R11D2RHL2BL6D6R3D2ENRHL3D6GU13LBM62,82G3D2F3R5E3U2H3L4G3D2F3R3E3U2H2BM72,82R3GNLD7RU5NU2E3R2DRD7LNUE2U4BM88,80DLGR7NR5G5UNE2FDRDRDR4E2
430 HDRAW"BM23,98NR5D2EDR3ND3FD2GL3ULBM32,99RDLBD3RDL
435 HDRAW"BM48,96NR6D8RU4NU3R3BM56,99ND4GD3FR4NU5EU3HL3BM64,99D5RU5R4D5U4BM74,96D7FRHU5NL3NR2U2
440 HDRAW"BM25,110NR2G2D3FR3NU2EUHL3ND2UEBM32,111RDLBD3RDL
445 HDRAW"BM48,109R7NDL6D8NLR2HU3NU3R3NUDBM58,112ND4GD3FR4NU4EU3HL3BM66,112RD5NLR2HU3ER3D5NLR2HU3BM79,109D8REBL3U4NR3NL2U2
450 HDRAW"BM23,124NDNFR5D2HDG2D2RU2EBM32,125RDLBD3RDL
455 HDRAW"BM52,123R4NFL4G2D2NR3D4BM60,125G2D2F2R2E2U2H2LBM68,125ND6FER2F2D4BM78,123DND7GR4
460 HDRAW"BM24,136ND5GDFGDFR3NU5EUHNL2EUHL2BM32,137RDLBD3RDL
465 HDRAW"BM50,136NGR3NR4D4L2NGR3NU3NR2D2HD2GL3BM62,138G2DED2R3E2UGU2L2BM70,138NGD4RUNU3E3RD4RNEU3BM81,136D6R2NEL2HU4GR4
470 HDRAW"BM24,149ND2GDFR3DG2NLRE2U3HND2L2BM32,150RDLBD3RDLBM50,148NG3NR7ND3FD3NR4L3NGR3ND7LD8GL3BM60,148G3NRF2NU3R3E2NU2LU3L2BM68,148G2RD3RU2NU2E3RD5RNE2U4BM79,147NG2D2NR2ND3LD3FR2E2
475 HDRAW"BM24,163ND5GD4FR3U5G2DE3ND4HL2BM32,164RDLBD3RDLBM50,164NR3D2NR2D3BM56,166GDFREUHBM61,166D2NDE2FD2BM68,164D2NLNRD2FE
480 RETURN
485 POKE&H23,A1-1:POKE&H24,A2:RETURN
490 POKE&H23,A1-2:POKE&H24,A2:RETURN
495 POKE&H23,A1-3:POKE&H24,A2:RETURN
500 GOSUB485:CLS:LOCATE6,8:PRINT"ENTER TOP MARGIN (0 - 10): ";:LINEINPUTV$:LOCATE6,12:PRINT"ENTER LEFT MARGIN ";
505 LOCATE24,12:IFH=1THENPRINT"(0 - 200):     ":LOCATE35,12ELSEPRINT"(0 - 400):     ":LOCATE35,12
510 GOSUB490:LINEINPUTU$:V=VAL(V$):U=VAL(U$):IFH=1THENU=4*INT(.25*U):Z=U ELSEU=8*INT(.125*U):Z=U
515 IFV<0THENV=0
520 IFV>10THENV=10
525 IF U>W-50THENSOUND60,5:LOCATE10,15:PRINT"LEFT MARGIN TOO BIG":LOCATE8,16:PRINT"RELATIVE TO RIGHT MARGIN!":LOCATE14,20:PRINT"TRY AGAIN.":GOTO505
530 IFU<0THENU=0
535 IFH=1 AND U>200THENU=200
540 IFH=3 AND U>400THENU=400
545 P=V+(D+1)*(-1+INT((192-V)/(D+1))):L=U:T=V:RETURN
550 CLS:LOCATE6,6:PRINT"VALUE 0 MEANS NO WRAP-AROUND.":LOCATE6,8:PRINT"ANY OTHER VALUE SPECIFIES":LOCATE9,9:PRINT"THE RIGHT MARGIN."
555 GOSUB485:LOCATE6,12:PRINT"ENTER VALUE ( 0 -";320+(H-1)*160;"):     ":LOCATE31,12:LINEINPUTW$:IFVAL(W$)=0THENQ=0:RETURN
560 Q=1:W=VAL(W$):IFW<U+50THENSOUND60,5:LOCATE8,16:PRINT"RIGHT MARGIN TOO SMALL":LOCATE8,17:PRINT"RELATIVE TO LEFT MARGIN.":LOCATE15,19:PRINT"TRY AGAIN.":GOTO555
565 IFH=1 AND W>320THENW=320
570 IFW>640THENW=640
575 IFH=1THENW=4*INT(.25*W)ELSEW=8*INT(.125*W)
580 RETURN
585 GOSUB485:CLS:LOCATE6,8:PRINT"ENTER 1ST TAB VALUE: ";:LINEINPUTT$:T1=2*INT(VAL(T$)*.5):IFH=1 AND T1>320THENT1=320ELSEIFT1>640THENT1=640
590 GOSUB485:LOCATE6,12:PRINT"ENTER 2ND TAB VALUE: ";:LINEINPUTT$:T2=2*INT(VAL(T$)*.5):IFH1 AND T2>320THENT2=320ELSEIFT2>640THENT2=640
595 RETURN
600 FORI=0TO29:B$=LEFT$(A$,2):POKEA+I,VAL("&H"+B$):A$=RIGHT$(A$,LEN(A$)-2):NEXT:RETURN
605 CLS:LOCATE5,3:PRINT"RIGHT ARROW: MOVE CURSOR RIGHT":PRINT"     LEFT ARROW: BACKSPACE":PRINT"     UP ARROW: CARRIAGE RETURN &":PRINT"           MOVE UP 1 LINE":PRINT"     DOWN ARROW: TAB"
610 PRINT"     SHIFT RIGHT ARROW: DRAW DESIGN":PRINT"     SHIFT LEFT ARROW: CLEAR LINE":PRINT"     SHIFT DOWN ARROW: UNDERLINE":PRINT"     SHIFT CLEAR: CLEAR SCREEN
615 PRINT"     ENTER: CARRIAGE RETURN &":PRINT"           MOVE DOWN 1 LINE":PRINT"     SHIFT 0: UPPER/LOWER CASE
620 LOCATE5,22:PRINT"PRESS ";:ATTR0,4,U:PRINT"SPACE";:ATTR0,4:PRINT" TO RETURN TO MENU";:LOCATE12,22
625 K$=INKEY$:IFK$=""THEN625ELSERETURN
630 D(1)=14:D$(1)="D15BR5U15BR5D15BR5U15
635 D(2)=14:D$(2)="R15BD5L15BD5R15BD5L15
640 D(3)=14:D$(3)="R15D15L15U14BR2BDR11D11L11U10BR2BDR7D7L7U6BR2BDR3D3L3U2
645 D(4)=14:D$(4)="BR3G2NUDLD5NF7EDF6NE6RE6NHU5LU2LNL3HL2G2NRNH2DH3DL
650 D(5)=14:D$(5)="BR7BDG3RNE2DG3RNE3DG3RNE3R6ND2R7H3LNF2ENH3RH3UNHRH2
655 D(6)=14:D$(6)="R5G5NU4BD2E7F7G7H7BD2D5R5NH4BR4R5U5NG4BU4U5L5NF4BL2BD3G4F4E4H4BD3GFE
660 D(7)=62:D$(7)="BR31BD4L3GL3NU2D2GNU4GD2GD3R5NFUL2GL2GLGNDL8GLNG2BR8D2LGL2NG2BR4ENFGD3NR2DL2GLBR5FRFR5ER2ER5FR2FR5ERE2R2FRLHL2UNL2U4LNGR2FR2NF2L2H2U2R6FRF2BL10U3L2NDHLHL5NGUR2BR3U2HU2HNU4HU3NUGL2HL3BD13U3LND3U2HUH2RGFDFD
665 D(8)=15:D$(8)="BR5R5FL7ND6GD8HNUD2LGDR7D2RU2R7UH2DNU3HU8LD5
670 D$(9)="D15
675 RETURN
680 CLS:LOCATE15,6:ATTR0,4,U:PRINT"DESIGNS";:ATTR0,4:LOCATE9,9:PRINT"1: VERTICAL STRIPES":LOCATE9,10:PRINT"2: HORIZONTAL STRIPES":LOCATE9,11:PRINT"3: CONCENTRIC SQUARES":LOCATE9,12:PRINT"4: HEART":LOCATE9,13:PRINT"5: XMAS TREE"
685 LOCATE9,14:PRINT"6: DIAMONDS & TRIANGLES":LOCATE9,15:PRINT"7: COCO CAT":LOCATE9,16:PRINT"8: BELL":LOCATE9,17:PRINT"9: VERTICAL BAR":LOCATE15,6
690 POKE&H23,A1:POKE&H24,A2
695 K$=INKEY$:IFK$=""THEN695
700 IFK$<"0" OR K$>"9"THENSOUND60,5:RETURN
705 K=VAL(K$):F$(0)=D$(K):M(0)=D(K):RETURN
710 C3=C1:C1=C2:C2=C3:PALETTE0,C1:PALETTE1,C2:PALETTE2,C1:PALETTE3,C2:RETURN
715 RGB:CLS3:POKE&HFFD8,0
