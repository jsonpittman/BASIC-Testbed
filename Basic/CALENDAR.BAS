10 REM LISTING1
20 REM THE LAST CALENDAR
30 CLEAR1000
40 DIMC(504),M(11)
50 L$=CHR$(10)
60 M1$="JANUARY  FEBRUARY  MARCH    APRIL     MAY     JUNE     JULY    AUGUST   SEPTEMBEROCTOBER  NOVEMBER DECEMBER"
70 S1$="  SUN MON TUE WED THU FRI SAT   ":S1$=S1$+"           "+S1$
80 M$="    %       %   ":M$=M$+"      "+M$
90 M(0)=31:M(1)=28:M(2)=31:M(3)=30:M(4)=31:M(5)=30:M(6)=31:M(7)=31:M(8)=30:M(9)=31:M(10)=30:M(11)=31
100 S$=CHR$(14)
110 E$=CHR$(20)
120 CLS:LINEINPUT"YEAR TO BE PRINTED : ";Y$
130 Y=ABS(INT(VAL(Y$))):IFY<>VAL(Y$)THENGOSUB650:GOTO120
140 IFY>1582THENIFY=INT(Y/4)*4THENM(1)=29:IFY=INT(Y/100)*100ANDY<>INT(Y/400)*400THENM(1)=28ELSEELSEELSEGOSUB650:GOTO120
150 CLS:LINEINPUT"MESSAGE TO BE PRINTED  :        ";MG$
160 IFLEN(MG$)>40THENGOSUB660:GOTO150
170 CLS:LINEINPUT"BIRTH MONTH TO HIGHLIGHT        (1 - 12 0 IF NONE )";BM$:BM=ABS(INT(VAL(BM$))):IFBM<>VAL(BM$)ORBM>12ORBM$<"0"ORBM$>"13"THENGOSUB670:GOTO170
180 IFBM THENELSEBD=0:BM=0:GOTO200
190 CLS:LINEINPUT"BIRTH DAY TO HIGHLIGHT          (1 - 3 , 0 IF NONE )";BD$:BD=ABS(INT(VAL(BD$))):IFBD>M(BM-1)ORBD<>VAL(BD$)ORBM$<"0"ORBM$>"13"THENGOSUB680:GOTO190
200 IFBD THENBD=BD+BM*42
210 CLS:PRINT@224,"SETTING UP CALENDAR YEAR "Y$
220 REM CALENDAR SET-UP
230 J=Y*365:GOSUB620
240 FORML=0TO11:DM=1
250 FORWL=0TO5
260 FORDL=DW TO6
270 C(ML*42+WL*7+DL)=DM:DM=DM+1
280 IFDM>M(ML)THEN300ELSENEXTDL
290 DW=0:NEXTWL
300 DW=DL+1:IFDW=7THENDW=0
310 NEXTML
320 IFBD GOSUB 560
330 CLS:PRINT@224,"PRINTING CALENDAR YEAR "Y$
340 REM CALENDAR PRINTOUT
350 PRINT#-2," "
360 Y$=MID$(STR$(Y),2):PRINT#-2,L$N$S$STRING$(18-LEN(Y$)/2," ")Y$L$L$E$C$
370 FORML=0TO10STEP2
380 PRINT#-2,L$S$;
390 PRINT#-2,USINGM$;MID$(M1$,ML*9+1,9);MID$(M1$,ML*9+10,9)
400 PRINT#-2,E$S1$
410 FORWL=0TO5:W$=" "
420 FORMR=0TO1
430 FORDL=0TO6
440 D=(ML+MR)*42+WL*7+DL:D$=RIGHT$("     "+STR$(C(D)),4):IFVAL(D$)=0THEND$="    "
450 IFC(D)=99THEND$="  **"
460 W$=W$+D$:NEXT
470 IFMR<1THENW$=W$+"               "
480 NEXT
490 PRINT#-2,W$:NEXT
500 NEXT
510 PRINT#-2,L$L$N$S$STRING$(18-LEN(MG$)/2," ")MG$E$L$L$
520 PRINT#-2,L$
530 CLS
540 END
550 REM SET ** ON BIRTHDATE
560 J=0:IFBM=1THEN580
570 FORML=0TOBM-2:J=J+M(L):NEXT
580 J=J+Y*365:GOSUB620
590 C(BD+DW-43)=99
600 RETURN
610 REMDAY OF WEEK CALCUL
620 IFY<>INT(Y/400)*400ANDY=INT(Y/100)*100THENJ=J+1
630 V=J+INT(Y/400)-INT(Y/100)-365+INT((Y-1)/4):V1=1+V-INT(V/7)*7:DW=V1-INT(V1/7)*7
640 RETURN
650 CLS:PRINT"THE YEAR MUST BE FROM 1583 UP":GOTO690
660 CLS:PRINT"THIS PROGRAM CAN ONLY PRINT A   ONE LINE MESSAGE WHICH MUST BE  LESS THEN 40 CHARACTERS LONG .":GOTO690
670 CLS:PRINT"NO SUCH MONTH IN A NORMAL YEAR":GOTO690
680 CLS:PRINT"THIS DATE DOES NOT EXIST IN THE MONTH AND YEAR SPECIFIED ."
690 PRINT@448,"SORRY CHARLY !",,"PRESS ANY KEY TO TRY AGAIN";
700 IFINKEY$=""THEN700ELSERETURN
1000 CLS4
1010 CLEAR 2000
1020 IND=1'INPUT INDEX
1030 UU=0'OUTPUT INDEX
1040 DIM C$(42),D$(31),E(12)'ARRAYS FOR CALENDAR CALCULATIONS
1045 DIM M$(12)'ARRAY TO HOLD MONTH NAMES
1050 DIM CAL$(288)'ARRAY TO HOLD 3 CALENDARS
1060 DIM SPD$(12,12)'ARRAY FOR SPECIAL EVENTS
1070 PRINT@37," APPOINTMENT CALENDAR ";
1080 PRINT@174," BY ";
1090 PRINT@233," BILL HOLDORF ";
1100 PRINT@300," 1-4-86 ";
1110 FOR X=1 TO 2000:NEXT X
1120 CLS3
1130 PRINT@37," APPOINTMENT CALENDAR ";
1140 PRINT@105," FOR ANY YEAR ";
1150 PRINT@196," INPUT FULL YR FIGURES  ";
1160 PRINT@228," FOR BC USE MINUS SIGN  ";
1170 PRINT@260," E.G.  1986,1000,10,-50 ";
1180 PRINT@320:PRINT@328,"WHAT YEAR";:INPUT Y
1190 PRINT@387," WHEN READY FOR INPUT OF  ";
1200 PRINT@419," SPECIAL DATES FOR EACH   ";
1210 PRINT@448:PRINT@452,"MONTH---PRESS <ENTER>";:INPUT AN$
1220 CLS8
1230 PRINT@34," 12 SPECIAL EVENTS PER MO.  ";
1240 PRINT@66," 19 SPACES EA.SPECIAL EVENT ";
1250 PRINT@131," START WITH DATE AND ADD  ";
1260 PRINT@163," NAME OF SPECIAL EVENT    ";
1270 PRINT@230,"      EXAMPLES       ";
1280 PRINT@262," 10-HARRY JOHNSON-BD ";
1290 PRINT@294," 25-CHRISTMAS        ";
1300 PRINT@326," 30-CLUB PICNIC      ";
1310 REM----INPUT SPECIAL EVENTS FOR ALL MONTHS
1320 FOR MON1=1 TO 12
1330 GOSUB 1450
1340 FOR SE=1 TO 12
1350 PRINT@389," MONTH:";:PRINT USING"##";MON1;:PRINT"      S.E.#";:PRINT USING"##";SE;:PRINT" ";
1360 PRINT@421,;:INPUT AN$
1370 IF LEN(AN$)>19 THEN GOTO 1480
1380 IF AN$="END"THEN 1430'IF SPECIAL EVENTS LESS THAN 12 GO TO NEXT MONTH
1390 IF AN$="SKIP" THEN 1530'TO SKIP ALL OR REMAINDER OF MONTHS
1400 SPD$(MON1,SE)=AN$
1410 GOSUB 1450
1420 NEXT SE
1430 NEXT MON1
1440 GOTO 1530
1450 PRINT@416
1460 PRINT@448,"       -------------------"
1470 RETURN
1480 PRINT@417,"DATA OVER 19--PRESS <ENTER>";
1490 INPUT AN2$
1500 GOSUB 1450
1510 GOTO 1360
1520 REM----END SPECIAL EVENTS
1530 CLS5
1540 PRINT@261," INPUT DATA COMPLETED ";
1550 PRINT@387," MOVE PAPER TO FIRST LINE ";
1560 PRINT@451," WHEN READY PRESS <ENTER> ";
1570 AN$=INKEY$:IF AN$="" THEN 1570
1580 FOR I=1 TO 31
1590 READ D$(I)
1600 NEXT I
1610 FOR I=1 TO 12
1620 READ E(I)
1630 NEXT I
1640 FOR I=1 TO 12
1650 READ M$(I)
1660 NEXT I
1670 DATA "  1","  2","  3","  4","  5","  6","  7","  8","  9"," 10"," 11"," 12"," 13"," 14"," 15"," 16"," 17"," 18"," 19"," 20"," 21"," 22"," 23"," 24"," 25"," 26"," 27"," 28"," 29"," 30"," 31"
1680 DATA 31,28,31,30,31,30,31,31,30,31,30,31
1690 DATA "        JANUARY","       FEBRUARY","         MARCH","         APRIL","          MAY","         JUNE","         JULY","        AUGUST","       SEPTEMBER"
1700 DATA "        OCTOBER","       NOVEMBER","       DECEMBER"
1710 CLS6
1720 PRINT@258," CALENDAR CALCULATIONS AND ";
1730 PRINT@358," OUTPUT TO PRINTER ";
1740 REM----BEGINNING CALENDAR ROUTINE
1750 REM----BEGINNING 3 MONTH CALENDAR CONTROL
1760 FOR MZ=1 TO 12
1770 FOR CAL=1 TO 3
1780 IF CAL=1 AND MZ=1 THEN MZ=12:Y=Y-1:GOSUB 1850:MZ=1:Y=Y+1:NEXT CAL
1790 IF CAL=1 AND MZ=>2 THEN MZ=MZ-1:GOSUB 1850:MZ=MZ+1:NEXT CAL
1800 IF CAL=2 AND MZ=>1 THEN GOSUB 1850:NEXT CAL
1810 IF CAL=3 AND MZ=<11 THEN MZ=MZ+1:GOSUB 1850:MZ=MZ-1:GOSUB 2130:NEXT MZ
1820 IF CAL=3 AND MZ=12 THEN MZ=1:Y=Y+1:GOSUB 1850:MZ=12:Y=Y-1:GOSUB 2130:END
1830 REM----END 3 MONTH CALENDAR CONTROL
1840 REM----BEGINNING CALENDAR CALCULATION ROUTINE
1850 CAL$(IND)=M$(MZ):IND=IND+1'NAME OF MONTH
1860 CAL$(IND)=" SU MO TU WE TH FR SA":IND=IND+1
1870 J=367*Y-INT(7*(Y+INT((MZ+9)/12))/4)+INT(275*MZ/9)+1721031
1880 K=0
1890 IF MZ<2 THEN K=-1
1900 J=J-INT(3*(INT((Y+K)/100)+1)/4)
1910 K=E(MZ)
1920 IF MZ<>2 THEN 1990
1930 W=INT(Y-100*INT(Y/100))
1940 X=INT(Y-4*INT(Y/4))
1950 Z=INT(Y-400*INT(Y/400))
1960 IF X<>0 THEN 1990
1970 IF W=0 AND Z<>0 THEN 1990
1980 K=29
1990 X=J-7*INT(J/7)
2000 FOR I=1 TO 42
2010 C$(I)="   "
2020 NEXT I
2030 FOR I=1 TO K
2040 C$(I+X)=D$(I)
2050 NEXT I
2060 FOR I=1 TO 6
2070 J=7*I
2080 REM----WEEKLY DATES INPUTED TO ARRAY
2090 CAL$(IND)=C$(J-6)+C$(J-5)+C$(J-4)+C$(J-3)+C$(J-2)+C$(J-1)+C$(J):IND=IND+1
2100 NEXT I
2110 RETURN
2120 REM----END CALENDAR ROUTINE
2130 REM----OUTPUT TO PRINTER
2140 PRINT#-2:PRINT#-2
2150 PRINT#-2,TAB(35);
2160 PRINT#-2,CHR$(27)CHR$(14);'START ELONGATION
2170 PRINT#-2,Y
2180 PRINT#-2,CHR$(27)CHR$(15);'END ELONGATION
2190 FOR I=1 TO 40
2200 PRINT#-2,CHR$(253)CHR$(251);
2210 NEXT I
2220 PRINT#-2
2230 PRINT#-2,CHR$(27)CHR$(20);'CONDENSED
2240 FOR I=1 TO 8
2250 PRINT#-2,CAL$(I+UU);
2260 UU=UU+8
2270 PRINT#-2,CHR$(27)CHR$(19);'STANDARD
2280 PRINT#-2,CHR$(27)CHR$(14);'START ELONGATION
2290 PRINT#-2,CHR$(27);CHR$(16);CHR$(0);CHR$(110);
2300 PRINT#-2,CAL$(I+UU);
2310 PRINT#-2,CHR$(27)CHR$(15);'END ELONGATION
2320 UU=UU+8
2330 PRINT#-2,CHR$(27)CHR$(20);'CONDENSED
2340 PRINT#-2,CHR$(27);CHR$(16);CHR$(3);CHR$(45);
2350 PRINT#-2,CAL$(I+UU)
2360 UU=0
2370 NEXT I
2380 PRINT#-2,CHR$(27)CHR$(19);'STANDARD
2390 FOR G=1 TO 40
2400 PRINT#-2,CHR$(252);CHR$(254);
2410 NEXT G
2420 PRINT#-2
2430 IND=1
2440 REM----END CALENDAR OUTPUT
2450 REM----OUTPUT MONTHLY SPECIAL EVENTS
2460 FOR SE=1 TO 9 STEP 4
2470 IF SPD$(MZ,SE)=" "THEN PRINT#-2:PRINT#-2:PRINT#-2:PRINT#-2:GOTO 2530
2480 PRINT#-2,SPD$(MZ,SE);
2490 PRINT#-2,TAB(20);SPD$(MZ,SE+1);
2500 PRINT#-2,TAB(40);SPD$(MZ,SE+2);
2510 PRINT#-2,TAB(60);SPD$(MZ,SE+3)
2520 NEXT SE
2530 REM----END SPECIAL EVENTS
2540 PRINT#-2,STRING$(80,(CHR$(241)))
2550 REM----BEGINNING DATE/LINES OUTPUT
2560 FOR DT=1 TO 16
2570 PRINT#-2:PRINT#-2
2580 PRINT#-2,USING"##";DT;:PRINT#-2,STRING$(37,(CHR$(241)));
2590 IF DT+16=32 THEN 2630
2600 PRINT#-2,TAB(40);:PRINT#-2,USING"##";DT+16;:PRINT#-2,STRING$(37,(CHR$(241)))
2610 NEXT DT
2620 REM----END DATE/LINE OUTPUT
2630 REM----MOVE TO END OF PAGE
2640 PRINT#-2:PRINT#-2
2650 RETURN'READY FOR NEXT PAGE AND NEXT 3 MONTHS
0 'calendar' by: fred kolesar      7 ladd road,  westfield,pa      16950         4/87
1 L=22:M=0:GOSUB5:M$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
2 FORP=L TO26STEP4:D=D+1:PRINTTAB(P)D;:IFD=29ANDM=2GOSUB4ELSEIFD=30THENIFM=4ORM=6ORM=9ORM=11GOSUB4ELSEELSEIFD=31GOSUB4
3 NEXT:PRINT:L=2:GOTO2
4 EXEC44539:IFPEEK(135)=12THENEND
5 IFM=12THEN1ELSECLS0:D=0:PRINT@76,MID$(M$,M*3+1,3)" 1988";:M=M+1:PRINT@192,"  SUN MON TUE WED THU FRI SAT":RETURN
1 REM***************************
2 REM*      THE 12 MONTHS      *
3 REM*   COPYRIGHT (C) 1989    *
4 REM*    BY FRED B. SCERBO    *
5 REM*    60 HARDING AVENUE    *
6 REM*  NORTH ADAMS, MA 01247  *
7 REM***************************
10 CLEAR3000
15 CLS0:PRINTSTRING$(32,220);STRING$(32,156);:FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
20 PRINTSTRING$(32,195);STRING$(32,211);
25 PRINT@358," AN INTRODUCTION TO ";:PRINT@390,"   THE 12 MONTHS    ";
30 PRINT@422,"  BY FRED B.SCERBO  ";:PRINT@454," COPYRIGHT (C) 1989 ";
35 DATA110,108,109,101,108,108,109,100,110,,96,109,108,109,100,111,,100,110,100,110,108,105,101,108,108,109,100,110,108,108,109
40 DATA106,,,101,,,101,,106,,,101,,,,110,106,,106,,106,,101,101,,,101,,106,,,101
45 DATA106,,,101,99,99,103,,106,,,101,99,103,,106,109,,106,,106,,101,101,99,99,103,,107,99,99,103
50 DATA106,,,101,,,101,,106,,,101,,100,,106,100,106,106,,106,,101,101,,,101,,106,100,98,
55 DATA106,,,101,,,101,,106,,,101,,96,,106,,108,106,,106,,101,101,,,101,,106,,100,98
60 DATA107,99,103,101,98,,103,97,107,99,106,103,99,103,97,107,,97,107,97,107,99,102,101,98,96,103,97,107,,,101
65 X$=INKEY$:IFX$<>CHR$(13)THEN65
70 DIM P$(12,3),A$(6),B$(20),C$(20),A(20),N(20),B(4),C(4),D(4),E(4),F(4),AO(20)
75 FORI=1TO3:READ C(I),D(I),E(I),F(I):NEXT:FORI=1TO6:READA$(I):NEXT:FORI=1TO12:READP$(I,1),B$(I),P$(I,2),C$(I):NEXT
80 FORI=1TO12:P$(I,3)="BR12ND8R4ND8R4D8BR4U8R6D8NL6BR4U8F8U8BR4R4ND8R4BR4D8U4R6U4D8":NEXTI
85 COLOR1,0
90 CLS:PRINTSTRING$(32,"=");:PRINT@72,"THE 12 MONTHS":PRINT@134,"A) REVIEW MONTHS":PRINT@198,"B) QUIZ GRAPHICS":PRINT@262,"C) QUIZ WRITTEN"
95 PRINT@324,"<<<SELECT YOUR CHOICE>>>"
100 PRINT:PRINTSTRING$(32,"=");:PRINT@420,"DEDICATED TO THE STUDENTS":PRINTTAB(6)"OF CONTE MIDDLE SCHOOL"
105 X$=INKEY$:X=RND(-TIMER):IFX$="A"THEN360ELSEIFX$="B"THEN110ELSEIFX$="C"THEN630ELSE105
110 CLS0:PMODE0,1:PCLS1
115 LINE(0,0)-(254,170),PRESET,B
120 LINE(6,4)-(122,82),PRESET,BF
125 LINE(128,4)-(248,82),PRESET,B
130 LINE(6,86)-(122,164),PRESET,B
135 LINE(128,86)-(248,164),PRESET,B
140 DRAW"BM26,188C0NU10R10NU10BR6R10U6L10U4R10BR6NR10D4NR10D6R10BR12BU6NE4D2F4BR6R10U6L10U4R10BR6ND10R10D4NL10BR6NR10D6U10R10D10BR6NR10U10R10BR6NR10D4NR10D6R10BR10U10NL4R10D4NL10D6NL14BR6U10R10D4NL10D6BR6U10R10D4L10R4F6BR6E4U2H4"
145 DATA130,6,246,80,6,86,120,162,130,86,246,162
150 PAINT(2,2),0,0:PCOPY1TO3
155 PMODE0,4:PCLS1
160 LINE(0,0)-(254,170),PRESET,BF
165 LINE(8,6)-(120,80),PSET,BF
170 PCOPY4TO2:PMODE0,1:SCREEN1,1
175 DATA"BM2,8C1","BM130,8C0","BM2,90C0","BM130,90C0","BM2,48C0","BM130,48C0"
180 FORI=1TO12
185 A(I)=RND(12):IFN(A(I))=1THEN185
190 N(A(I))=1:NEXTI:FORY=1TO12:COLOR1,0
195 FORI=2TO4
200 B(I)=RND(3)+1:IFN(B(I))=0THEN200
205 N(B(I))=0:NEXTI:FORI=1TO4:N(I)=1:NEXT
210 B=RND(10):IFB=A((Y))THEN210
215 C=RND(10):IFC=B OR C=A((Y))THEN215
220 DRAW A$(1):DRAWP$(A(Y),1)
225 DRAW A$(B(2)):DRAWP$(B,2):DRAWP$(B,3)
230 DRAW A$(B(3)):DRAWP$(C,2):DRAWP$(C,3)
235 DRAW A$(B(4)):DRAWP$(A(Y),2):DRAWP$(A(Y),3)
240 COLOR1,0
245 Z=0
250 PMODE0,4
255 DRAW A$(1)+"C0":DRAWP$(A(Y),1)
260 DRAW A$(B(2))+"C1":DRAWP$(B,2):DRAWP$(B,3)
265 DRAW A$(B(3))+"C1":DRAWP$(C,2):DRAWP$(C,3)
270 DRAW A$(B(4))+"C1":DRAWP$(A(Y),2):DRAWP$(A(Y),3)
275 PMODE0,1:SCREEN1,1
280 LINE(8,6)-(120,80),PSET,B
285 X$=INKEY$:IFX$=" "THEN295ELSEIFX$="@"THEN800
290 COLOR1,0:LINE(8,6)-(120,80),PRESET,B:GOTO280
295 Z=Z+1:IFZ=4THENZ=1
300 COLOR1,0:LINE(C(Z),D(Z))-(E(Z),F(Z)),PSET,B
305 X$=INKEY$:IFX$=" "THEN295ELSEIFX$=CHR$(13)THEN315ELSEIFX$="@"THEN800
310 COLOR1,0:LINE(C(Z),D(Z))-(E(Z),F(Z)),PRESET,B:GOTO300
315 IFZ+1=B(4)THEN325
320 NW=NW+1:FORK=1TO5:PMODE0,4:SCREEN1,1:SOUND10,3:PMODE0,1:SCREEN1,1:SOUND1,3:NEXTK:GOTO300
325 NC=NC+1:PMODE0,4:PCLS1:LINE(0,40)-(256,126),PRESET,B:LINE(6,44)-(124,122),PRESET,B:LINE(130,44)-(248,122),PRESET,B:PAINT(2,42),0,0
330 DRAW A$(5):DRAWP$(A(Y),1)
335 DRAW A$(6):DRAWP$(A(Y),2):DRAWP$(A(Y),3)
340 SCREEN1,1
345 X$=INKEY$:IFX$<>CHR$(13)THEN345
350 PMODE0,1
355 PCOPY3TO1:SCREEN1,1:PCOPY2TO4:NEXTY:GOTO800
360 PMODE0,2:PCLS1:SCREEN1,1:LINE(0,40)-(256,126),PRESET,B:LINE(6,44)-(124,122),PRESET,B:LINE(130,44)-(248,122),PRESET,B:PAINT(2,42),0,0
365 FORI=1TO12:DRAW A$(5):DRAWP$(I,1)
370 DRAW A$(6):DRAWP$(I,2):DRAWP$(I,3)
375 X$=INKEY$:IFX$<>CHR$(13)THEN375
380 COLOR1,0:LINE(8,46)-(122,120),PSET,BF:LINE(132,46)-(246,120),PSET,BF:NEXTI
385 RUN
390 DATA "BR14BD28R12L6D12NL6BR10U12R8D6NL8D6BR6U12M+8,+12U12BR6D12R8U12BR6ND12R8D6NL8D6BR6U12R8D6L6F6BR12U6NH6E6BE4BU4NL104BD28L104"
395 DATA JANUARY
400 DATA"BR10BD46R100L58H4U6E4R12F4D6NG4BU10BL6E4U4H4L8G4D4F2BU10BR2H2U4E2R8F2D4G2BU10NR4NU8L2NU8L2NU8L2NU8L2NU8L4BD16NG8BR16F8BD30BL58U8NG2BR6NR6D4R6D4NL6BR8U8L4R8"
405 DATA DECEMBER AND FEBRUARY
410 DATA "BR14BD28NR8D6NR8D6BR12NR8U6NR8U6R8BR4R10D6L8U6D12L2R10NU4BR4U12R8D6L6F6BR4BU12D12R8U12BR4ND12R8D6NL8D6BR4U12R8D6L6F6BR10U6NH6E6BE4BU4NL104BD28L104"
415 DATA FEBRUARY
420 DATA"BR60BD46M+18,-22U4H2U2H2L2H2L4G2L2G4ND2H4L2H2L4G2L2G2D2G2D4M+18,+22FEBH16BR6M-12,+6NH4ND4L2NH4ND4BM+36,-18BD4M+16,-10NL6ND4BD44BL74R6D4L6D4R6BR6U8F8U8BR4R2D8L2R6U8NL4"
425 DATA JANUARY AND MARCH
430 DATA "BR26BD28ND12R6ND12R6D12BR6U12R8D6NL8D6BR6U12R8D6L6F6BR6NR8U12R8BR6D12U6R8U6ND12BR22BU8NL104BD28L104"
435 DATA MARCH
440 DATA"BR58BD20U8E4R10F8D4G4NL12F4D4G8L10H4NU8G4L10H8U4E4NR12H4U4E8R10NF4BD32G6L4BL28BD10R6D4NL6D4NL6BR6U8R6D4L4F4BR4R2NU8R4U8NL6"
445 DATA FEBRUARY AND APRIL
450 DATA "BR32BD28ND12R8D6NL8D6BR6U12R8D6NL6BR6D6U12R8D6L6F6BR8U12BR8D12R8BR24BU20NL104BD28L104"
455 DATA APRIL
460 DATA"BR58BD28F4R20E4NU4F2R8E6U10H6L20G8ND6E4H8L16G8E2L10G6L6G6D8F4R18E8G4R12E6BG10BR4NG12BL8G14BL8E10BR24NG12E2BR8NG10BR8NG12BR8NG10BR8G12BL66BD8D4R6D4U8BR6R4ND8R4BR4D8U4R6U4ND8"
465 DATA MARCH AND MAY
470 DATA "BR38BD28ND12R8ND12R8ND12BR6ND12R8D6NL8D6BR12U6NH6E6BR30BU8NL104BD28L104"
475 DATA MAY
480 DATA"BR10BD46R100L74U10H14U4D10F16R2U8E14U4D10G16U20E6U6G2H2G2H2G2H2D6F6BD22BR42U10H14U4D10F16R2U8E14U4D10G16U20E6U6G2H2G2H2G2H2D6F6BD32BL68NR6D4R6D4NL6BR10U8L4R8BR4D8U4R6D4U8"
485 DATA APRIL AND JUNE
490 DATA "BR32BD28R12L6D12NL6BR12NU12R8NU12BR6U12F12U12BR6NR8D6NR8D6R8BR26BU20NL104BD28L104"
495 DATA JUNE
500 DATA"BR28BD20R64D2L64R6D12NG2NF2BD4NG2NF2BD4NG2F2BE12R30U2L30U2R30U2L30U2R30BD32BL58L6D8U4R6D4NL6BR10U8L4R8BR4D8U4R6D4U8"
505 DATA MAY AND JULY
510 DATA "BR32BD28R12L6D12NL6BR12NU12R8NU12BR8NU12R8BR8U6NH6E6BR28BU8NL104BD28L104"
515 DATA JULY
520 DATA"BR20BD22D16R50U16NL50D8R10E2R2E2R2E2U2E2U2BU6NU4BF2BR2NE4BD2BR4NR4BG2BD2NF4BL12NG2BU4NL4BE2BU2NH2BD44BL74ND2R6D4G2D2BR12U8L4R8BR4D8U4R6D4U8"
525 DATA JUNE AND AUGUST
530 DATA "BR22BD28ND12R8D6NL8D6BR6NU12R8NU12BR6NR8U12R8BD6NL4D6BR6NU12R8NU12BR6R8U6L8U6R8BR4R6ND12R6BR14BU8NL104BD28L104"
535 DATA AUGUST
540 DATA"BR32BD46H6R32U38G28R28U28R4ND28D8F22L22D8L4R36G6BR20G4L4H4G4L4H4G4L4H4G4L4H4G4L4H4G4L4H4G4L4H4G4L4H4BD10BR4ND8R6D4NL6D4NL6BR8U8L4R8BR4D8U4R6D4U8"
545 DATA JULY AND SEPTEMBER
550 DATA "BR12BD28NR6D6R6D6NL6BR6NR6U6NR6U6R6BR6D12U6NR6U6R6ND6BR4R4ND12R4BR4NR6D6NR6D6R6BR6U12R4ND12R4ND12BR6NL2ND12R6D6NL6D6NL8BR6NR6U6NR6U6R6BR6ND12R6D6L6F6BU20NL104BD28L104"
555 DATA SEPTEMBER
560 DATA "BR40BD20L4D4R4D4NL4BR4NR4U8R4BR4D8U4R4U4D8BR4U8R4D8NL4BR4U8R4D8NL4BR4NU8R4BR8R6H20L32G20R6D16R58NU16L8U10L6ND10BL8L4D4R4U4BL12L4D4R4U4BL12L4D4R4U4BD22BL24ND4R6D4NL4D4BR8U8L4R8BR4D8U4R6D4U8"
565 DATA AUGUST AND OCTOBER
570 DATA "BR16BD28ND12R8D12NL8BR6NR8U12R8BR4R6ND12R6BR4NR8D12R8U12BR4R2ND12R8D6NL8D6NL10BR6NR8U6NR8U6R8BR6ND12R8D6L6F6BR8BU20NL104BD28L104"
575 DATA OCTOBER
580 DATA"BR56BD14ND30R8F4D22G4L16H4U22E4NR8G2L6G4D18F4R6L12H6U14E6R6BR28R6F4D18G4NL6R6E6U14H6L6BL16BU4U4E6L4G6D4BD14L8E4ND2F4BR4R8H4ND2G4L2D2NG4F4NL8BD6L10NH2R12E2BD20BL52NG2ND8BR4ND8R6D8NL6BR10U8L4R8BR4D8U4R6D4U8"
585 DATA SEPTEMBER AND NOVEMBER
590 DATA"BR14BD28ND12M+8,+12U12BR4NR8D12R8U12BR4D8F4E4U8BR4NR6D6NR6D6R6BR4U12R6ND12R6D12BR4R2U12L2R10D6NL8D6NL8BR4NR8U6NR8U6R8BR4ND12R8D6L6F6BR4BU20NL104BD28L104"
595 DATA NOVEMBER
600 DATA"BR20BD40R80U4L80ND4R8U6R4E2U2E2U2E2R2E2R2E2R16NE6G8D4F4R8E4U4H4E8NG4H4U4R4F4D2G2L2BD4NL4F4R2F2R2F2D2F2D2F4BL80BD22NG2ND8BR8NG2ND8BR6R4ND8R4BR4D8U4R6D4U8"
605 DATA OCTOBER AND DECEMBER
610 DATA"BR14BD28R2ND12R8D12NL10BR4NR6U6NR6U6R6BR4NR8D12R8BR4NR6U6NR6U6R6BR6ND12R6ND12R6D12BR4R2U12L2R10D6NL8D6NL8BR4NR8U6NR8U6R8BR4ND12R8D6L6F6BR4BU20NL104BD28L104"
615 DATA DECEMBER
620 DATA"BR34BD12G6R4G6R4G6R4G6R26H6R4H6R4H6R4H6L2BD8NR2BD4NR2BD4NR2BD4NR2BD4NR2BD2D4R2NU4BR36R8L2U2L4R2U12NU12L4NU10L4NU10L4NU10L4NU10R20NU10R4NU10R4NU10R4NU10BD28BL82NG2ND8BR6R6D4L6D4R6BU8BR6R4ND8R4BR4D8U4R6D4U8"
625 DATA NOVEMBER AND JANUARY
630 CLS:V=1
635 FORI=1TO12
640 AO(I)=RND(12)
645 IF N(AO(I))=1 THEN 640
650 N(AO(I))=1:NEXTI
655 FOR P=1TO12
660 CLS
665 PRINT@68,"WHICH MONTH COMES BETWEEN"
670 PRINT@132,C$(AO(P))+" ?"
675 FOR Q=1TO2
680 C(Q)=RND(10):IF C(Q)=AO(P) THEN680
685 FOR K=Q-1 TO 0STEP-1:IF C(K)=C(Q) THEN680
690 NEXTK
695 NEXTQ:C(3)=AO(P)
700 FOR E=1TO3
705 F(E)=RND(3)
710 FOR K=E-1 TO 0 STEP-1:IF F(K)=F(E) THEN705
715 NEXTK:NEXTE
720 PRINT
725 PRINTTAB(8)"A-"+B$(C(F(1))):PRINT
730 PRINTTAB(8)"B-"+B$(C(F(2))):PRINT
735 PRINTTAB(8)"C-"+B$(C(F(3))):PRINT
740 G$=INKEY$:IFG$="@"THEN800
745 IF G$=""THEN740
750 G=ASC(G$)-64
755 IF G<1 THEN 740
760 IF G>5 THEN 740
765 IF C(F(G))<>AO(P) THEN780
770 PRINT:PRINT" RIGHT! THE ANSWER IS: "+B$(AO(P))
775 NC=NC+1:GOTO790
780 PRINT:PRINT" SORRY! THE ANSWER IS: "+B$(AO(P))
785 NW=NW+1
790 X$=INKEY$:IFX$<>CHR$(13)THEN790
795 NEXT P
800 CLS:PRINT@101,"YOU TRIED"NC+NW"TIMES &":PRINT@165,"ANSWERED"NC"CORRECTLY"
805 PRINT@229,"WHILE DOING"NW"WRONG."
810 NQ=NC+NW:IF NQ=0THEN NQ=1
815 MS=INT(NC/NQ*100)
820 PRINT@293,"YOUR SCORE IS"MS"%."
825 PRINT@357,"ANOTHER TRY (Y/N/C) ?";
830 X$=INKEY$:IFX$="Y"THEN RUN
835 IFX$="N"THENCLS:END
840 IFX$="C"THEN850
845 GOTO830
850 IFV=1THEN660
855 IFV=0THEN275
10 REM"READING A CALENDAR"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1990"
30 CLS5:R=RND(-TIMER)
40 PRINT@3,"calendar for november,1990";
50 PRINT@64,"   S   M   T   W   T   F   S    "
60 PRINT@115,"1   2   3      "
70 PRINT@131,"4   5   6   7   8   9  10      "
80 PRINT@162,"11  12  13  14  15  16  17     "
90 PRINT@194,"18  19  20  21  22  23  24     "
100 PRINT@226,"25  26  27  28  29  30        ";
110 SOUND 250,1:PRINT@288,STRING$(64," ");:PRINT@384," ":PRINT@288,"";
120 A=1+RND(23):B=1+RND(17):C=1+RND(20):D=RND(8)+1:E=RND(19)+1:F=RND(10)+20:G=RND(2)+1
130 S=RND(6):ON S GOTO 140,170,200,230,260,290
140 PRINT"ELECTION DAY IS ON NOVEMBER 4.  WHAT DATE IS";A;"DAYS LATER";
150 INPUT Z
160 Y=4+A:GOTO 320
170 PRINT"VETERANS DAY IS ON NOVEMBER 12. WHAT DATE IS";B;"DAYS LATER";
180 INPUT Z
190 Y=12+B:GOTO 320
200 PRINT"THANKSGIVING IS ON NOVEMBER 22. WHAT DATE IS";C;"DAYS EARLIER";
210 INPUT Z
220 Y=22-C:GOTO 320
230 PRINT"WHAT DATE IS";D;"DAYS AFTER":PRINT"NOVEMBER";E;
240 INPUT Z
250 Y=D+E:GOTO 320
260 PRINT"WHAT DATE IS";E;"DAYS BEFORE":PRINT"NOVEMBER";F;
270 INPUT Z
280 Y=F-E:GOTO 320
290 PRINT"WHAT DATE IS";G;"WEEKS AFTER":PRINT"NOVEMBER";D;
300 INPUT Z
310 Y=D+(7*G):GOTO 320
320 IF Z=Y THEN PRINT@396,"CORRECT":PLAY"L30CDEFGGG"
330 IF Z<>Y THEN PRINT@384,"SORRY,";Y;"IS THE ANSWER.":PLAY"L4B-"
340 EN$=INKEY$
350 IF EN$=CHR$(13) THEN 110 ELSE IF EN$="E" THEN 360 ELSE 340
360 CLS:END
1 'PERPETUAL CALENDAR
2 'BY HAROLD H. BRITTEN
3 'COPYRIGHT (C) 1993
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 WIDTH40:DIMM$(12),D(12):FOR I=1TO12:READM$(I),D(I):NEXT
20 PRINT:INPUT"     MONTH,YEAR";M$,Y:FORI=1TO12:IFM$=LEFT$(M$(I),LEN(M$))THENM=I ELSENEXT:GOTO20
30 D=INT(Y-2000):D(2)=1-SGN(D-INT(D/4)*4)+SGN(D-INT(D/100)*100)-SGN(D-INT(D/400)*400):D=INT((D+3)/4)-INT((D+99)/100)+INT((D+399)/400)+D:FORI=0TOM-1:D=D+D(I):NEXT:D=D+6:D=D-INT(D/7)*7
40 CLS:M$=M$(M)+STR$(Y):PRINTTAB(21-LEN(M$)/2);M$:LOCATE0,3:PRINT"  SUN  MON  TUE  WED  THU  FRI  SAT":LOCATE5*D,6:FORI=1TOD(M)+28:M$=STR$(I):PRINTLEFT$("   ",5-LEN(M$))+M$;:D=D+1:IFD=7THENPRINT:PRINT:PRINT:D=0:NEXTELSENEXT
50 A$=INKEY$:IFA$=""THEN50ELSEPRINT:IFA$=" "THENIFM<12THENM=M+1:GOTO30ELSEM=1:Y=Y+1:GOTO30ELSE20
60 DATA JANUARY,3,FEBRUARY,0,MARCH,3,APRIL,2,MAY,3,JUNE,2,JULY,3,AUGUST,3,SEPTEMBER,2,OCTOBER,3,NOVEMBER,2,DECEMBER,3
