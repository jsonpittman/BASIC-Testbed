1 'COCOFONE
2 'BY TREVOR BOEHM
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
9 'PALETTE 13,0:PALETTE 12,63
10 CLEAR 5000:DIM N$(100),A$(100),B$(100),H$(100):HN=1:N$(1)="TREVOR BOEHM":A$(1)="77 INWOOD CRESC.":H$(1)="(204) 832-4495":B$(1)="N/A":C=1:CC=1
20 CLS:PRINT:PRINTTAB(8)"COCO PHONE BOOK":PRINTTAB(8)"==============="
30 PRINT:PRINT"  NAME: ";N$(C)
40 PRINT"  ADDRESS: ";A$(C)
50 PRINT"  BUSINESS NUMBER: ";B$(C)
60 PRINT"  HOME NUMBER: ";H$(C)
70 PRINT:PRINT:PRINT"MENU:":PRINT:PRINT"<A>DD <D>ELETE <C>HANGE <S>AVE":PRINT"<L>OAD <F>IND <N>EXT <P>REV":PRINT"<H>ARDCOPY <Q>UIT"
80 A$=INKEY$:IF A$="" THEN 80
90 IF A$="P" THEN C=C-1:IF C=0 THEN C=1:GOTO 20 ELSE GOTO 20
100 IF A$="N" THEN C=C+1:IF C>CC THEN C=CC:GOTO 20 ELSE GOTO 20
110 IF A$="Q" THEN CLS:PRINT"TYPE GOTO 20 TO RESUME":END
120 IF A$="A" THEN CC=CC+1:C=C+1:CLS:LINEINPUT"NAME: ";N$(C):LINEINPUT"ADDRESS: ";A$(C):LINEINPUT"BUSINESS NUMBER: ";B$(C):LINEINPUT"HOME NUMBER: ";H$(C):GOTO 20
130 IF A$="C" THEN CLS:LINEINPUT"NAME: ";N$(C):LINEINPUT"ADDRESS: ";A$(C):LINEINPUT"BUSINESS NUMBER: ";B$(C):LINEINPUT"HOME NUMBER: ";H$(C):GOTO 20
140 IF A$="S" THEN CLS:PRINT"READY DISK...":EXEC44539:OPEN"O",1,"PHONE#S/PHN":FOR X=1 TOCC:PRINT#1,N$(X):PRINT#1,A$(X):PRINT#1,B$(X):PRINT#1,H$(X):NEXT:CLOSE:PRINT"SAVED...":EXEC44539:GOTO20
150 IF A$="L" THEN CLS:PRINT"READY DISK...":EXEC44539:OPEN"I",1,"PHONE#S/PHN":FOR X=1 TO CC:LINEINPUT#1,N$(X):LINEINPUT#1,A$(X):LINEINPUT#1,B$(X):LINEINPUT#1,H$(X):NEXT:CLOSE:PRINT"LOADED...":EXEC44539:GOTO20
160 IF A$="F" THEN CLS:LINEINPUT"NAME: ";F$:FOR X=1 TO CC:IF INSTR(N$(X),F$)<>0 THEN C=X:GOTO 20 ELSE NEXT:PRINT"NOT FOUND...":EXEC 44539:GOTO20
170 IF A$="D" THEN N$(C)="":A$(C)="":B$(C)="":H$(C)="":GOTO20
180 IF A$="H" THEN CLS:PRINT"READY PRINTER...":EXEC44539:PRINT#-2,"NAME: ";N$(C):PRINT#-2,"ADDRESS: ";A$(C):PRINT#-2,"BUSINESS NUMBER: ";B$(C):PRINT#-2,"HOME NUMBER: ";H$(C):GOTO20
190 GOTO 80
