1 'NOTE RIGHT 2X40
2 'WRITTEN BY JOHN MUSUMECI
3 'COPYRIGHT 1991 FALSOFT, INC.
10 CLEAR 3000
20 POKE150,7:'*BAUD RATE*
30 DIMB$(22)
40 WIDTH 40:T=0
50 CLS5:LOCATE5,10:PRINT"ADJUST PAPER TO PERFORATION THEN      PRESS <ANY KEY> WHEN READY."
55 ON BRK GOTO 990
60 A$=INKEY$:IF A$="" THEN 60
80 CLS:FOR A=1 TO 22:B$(A)=STRING$(80,32):NEXT A
90 AA$=STRING$(13,45)+"*LEFT SCREEN*"+STRING$(14,45)
100 MID$(B$(22),1,40)=AA$
110 AB$=STRING$(13,45)+"*RIGHT SCREEN*"+STRING$(13,45)
120 MID$(B$(22),41,40)=AB$
130 LOCATE0,21:PRINT AA$;
140 LOCATE8,22:PRINT"PRESS <CLEAR> FOR OPTIONS"
150 LOCATE8,23:PRINT"  PRESS <BREAK> TO QUIT       ";
155 POKE65497,0
160 X=0:Y=0:A=1:Z=1
170 LOCATEX,Y
180 A$=INKEY$:IF A$="" THEN 180
200 IF A$=CHR$(12) THEN 410
210 IF A$=CHR$(13) AND Z>40 THEN 590 ELSE IF A$=CHR$(13) AND Z<41 THEN 610
220 IF A$=CHR$(8) AND Z=41 THEN 630 ELSE IF A$=CHR$(8) AND X=0 THEN 170 ELSE IF A$=CHR$(8) THEN X=X-1:Z=Z-1:GOTO 170
230 IF A$=CHR$(9) AND Z=80 THEN 590 ELSE IF A$=CHR$(9) THEN 640
240 IF A$=CHR$(10) THEN Y=Y+1:A=A+1:GOSUB 730:GOTO 170
250 IF A$=CHR$(94) THEN Y=Y-1:A=A-1:IF Y<0 THEN Y=0:A=1:GOTO 170 ELSE 170
260 IF A$=CHR$(21) AND Z>40 THEN X=0:Z=41:GOTO 170 ELSE IF A$=CHR$(21) AND Z<41 THEN X=0:Z=1:GOTO 170
270 IF A$=CHR$(93) AND Z>40 THEN X=39:Z=80:GOTO 170 ELSE IF A$=CHR$(93) AND Z<41 THEN X=39:Z=40: GOTO 170
280 IF A$=CHR$(91) THEN Y=20:A=21:GOTO 170
290 IF A$=CHR$(95) THEN Y=0:A=1:GOTO 170
300 IF A$=CHR$(189) AND Z>40 THEN 680 ELSE IF A$=CHR$(189) AND Z<41 THEN 700
310 GOSUB 730
320 MID$(B$(A),Z,1)=A$
330 IF Z<41 THEN S=1 ELSE IF Z>40 THEN S=41
340 LOCATE0,Y:PRINT MID$(B$(A),S,40);
350 X=X+1:Z=Z+1
360 IF Z=41 THEN 720
370 IF Z=81 THEN 590
380 IF Z=69 THEN SOUND200,2
390 IF Z>68 AND A$=CHR$(32) THEN 590
400 GOTO 170
410 LOCATE0,21:PRINT STRING$(15,42)+" OPTIONS "+STRING$(16,42);
413 LOCATE8,22:PRINT"  L-LOAD. S-SAVE. P-PRINTER."
416 LOCATE8,23:PRINT"R-RETURN. C-CONT. N-NEW. Q-QUIT";:LOCATEX,Y
418 POKE65496,0
420 A$=INKEY$:IF A$="" THEN 420
430 IF A$="L" OR A$="l" THEN 810
440 IF A$="S" OR A$="s" THEN 900
450 IF A$="P" OR A$="p" THEN 540
460 IF A$="R" OR A$="r" THEN 890
470 IF A$="C" OR A$="c" THEN 80
480 IF A$="N" OR A$="n" THEN 1000
490 IF A$="Q" OR A$="q" THEN 990
530 GOTO 410
540 CLS:LOCATE7,2:PRINT"P R I N T I N G . . ."
550 IF T=0 THEN FOR X=1 TO 12:PRINT#-2:NEXT X:T=1:AA=0:GOTO 570
560 IF AA=1 THEN FOR X=1 TO 24:PRINT#-2:NEXT X:AA=0 ELSE AA=1
570 FOR X=1 TO 21:PRINT#-2,B$(X);:NEXT X
580 GOTO 890
590 Y=Y+1:A=A+1:Z=1:X=0
600 GOSUB 730:GOSUB 780:GOTO 170
610 Y=Y+1:X=0:Z=1:A=A+1
620 GOSUB 730:GOTO 170
630 GOSUB 780:Z=Z-1:X=39:GOTO 170
640 IF X=39 THEN 650 ELSE 670
650 GOSUB 750
660 X=0:Z=41:GOTO 170
670 X=X+1:Z=Z+1:GOTO 170
680 GOSUB 780
690 Z=Z-40:GOTO 170
700 GOSUB 750
710 Z=Z+40:GOTO 170
720 GOSUB 750:X=0:GOTO 170
730 IF Y>20 THEN Y=20:A=21
740 RETURN
750 LOCATE0,0:FOR C=1 TO 22
760 PRINT MID$(B$(C),41,40);
770 NEXT C:RETURN
780 LOCATE0,0:FOR C=1 TO 22
790 PRINT MID$(B$(C),1,40);
800 NEXT C:RETURN
810 CLS
815 PRINT:PRINT"USE UPPER-CASE LETTERS"
820 LOCATE10,4:LINEINPUT"NAME OF FILE: ";F$
830 F$=F$+"/TXT"
840 CLS:LOCATE7,2:PRINT"L O A D I N G"
850 OPEN "I",#1,F$
860 FOR A=1 TO 21
870 LINEINPUT #1,B$(A)
880 NEXT A:CLOSE #1
890 LOCATE0,0:FOR C=1 TO 22:PRINT MID$(B$(C),1,40);:NEXT C:GOTO 140
900 CLS:PRINT:PRINT"USE UPPER-CASE LETTERS":LOCATE10,4:LINEINPUT"NAME OF FILE: ";F$
910 F$=F$+"/TXT"
920 CLS:LOCATE7,2:PRINT"S A V I N G"
930 OPEN "O",#1,F$
940 FOR C=1 TO 21
950 PRINT #1,B$(C)
960 NEXT C
970 CLOSE #1
980 GOTO 890
990 POKE65496,0:WIDTH32:END
1000 RUN
