1 'CARD-PLAYER'S SORT DEMO
2 'BY GEORGE R. MABRY
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW
40 CLS
50 PRINT "CARD PLAYER'S SORT PROGRAM"
60 PRINT
70 FOR A=1 TO 10
80 X(A)=RND(1000)
90 PRINT X(A)
100 NEXT A
110 GOSUB 2000
120 FOR A=1 TO 10
130 PRINT @(32*A+48),X(A)
140 NEXT A
150 PRINT
160 PRINT "RUN THE PROGRAM AGAIN"
170 PRINT "(Y OR N)?"
180 Z$=INKEY$:IF Z$="" THEN 180
190 IF Z$="Y" THEN 40
200 IF Z$<>"N" THEN 180
210 END
2000 REM* CARD PLAYER'S SORT *
2010 FOR A=2 TO 10
2020 S=0:X(0)=X(A)
2030 IF X(0)<X(A+S-1) THEN X(A+S)=X(A+S-1):S=S-1:IF (A+S)>1 THEN 2030
2040 X(A+S)=X(0)
2050 NEXT A
2060 RETURN
