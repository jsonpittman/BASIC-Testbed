1 'MAKE OR BREAK
2 'WRITTEN BY DENNIS HOIN
3 'COPYRIGHT JUNE 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 '******LISTING FOR MAKE OR BREAK****
20 '********LOAD 51 CHR.SCREEN & DIM******
30 IF PEEK(&H7CC2)<>142 THEN 40 ELSE 70
40 CLEAR1,&H7CC1:CLEAR300:LOADM"51":EXEC&H7CC2
50 PMODE4,1:PCLS1:SCREEN1,0
60 CLS
70 DIM PP(318),P1(318),P2(318),P3(318),BB(318),A$(26),B$(26),C(26),D1(17),D2(17),D3(17),D4(17),N$(32),NN$(32),N(32),OW(32),O(32)
80 M(1)=1:M(2)=180:M(3)=1:M(4)=180:M(5)=1:M(6)=180:M(7)=1:M(8)=180:P$="#####.##"
90 FOR X=1TO4:P(X)=1:PAY(X)=1500:NEXT
100 PL$(0)="None":POT=200
110 COLOR0
120 GOSUB 690
130 '****************GET NUM.OF PLAYERS & NAMES***
140 GOSUB1230:PRINT@368,"HOW MANY";:PRINT@419,"PLAYERS";:GOSUB1320:INPUTZZ:PRINT@419,"";
150 IF ZZ<0 OR ZZ>4 THEN GOTO140 ELSE GOSUB 1220
160 FOR X=1TOZZ:GOSUB1230:PRINT@368,"ENTER NAME";:PRINT@419,"OF PLAYER";X
170 PRINT@470," ";:GOSUB1320:INPUTPL$(X):NEXT:GOSUB1220
180 '************PLAY GAME************************
190 FOR T=1TO ZZ
200 IF PAY(T)=0 THEN 440
210 PRINT@726,STRING$(27,"*");
220 GOSUB1340:PRINT@729,"IT'S YOUR TURN ";PL$(T);: PRINT@386,"Player ";T;:PRINT@485,"You Have";:PRINT@536,"$";:PRINTUSING P$;PAY(T);
230 LINE(0,0)-(256,191),PSET,B
240 GOSUB1220:GOSUB1320:GOSUB830
250 PRINT@1114,"$";POT;
260 IF T=1 THEN TT=1 ELSE IF T=2 THEN TT=3 ELSE IF T=3 THEN TT=5 ELSE IF T=4 THEN TT=7
270 GOSUB450
280 IF P(T)=8 THEN GOSUB1410:GOTO380
290 GOSUB1230:PRINT@ 368,N$(P(T));:PRINT@419,NN$(P(T));:PRINT@470,"$";:PRINTUSING P$;N(P(T));:PRINT@521,"OWNER ";PL$(OW(P(T)));
300 IF O(P(T))>0 THEN SOUND 100,1:PRINT@623,O(P(T));" STORIES";
310 IF OW(P(T))>0 AND OW(P(T))<>T AND O(P(T))<4THEN N=N(P(T))*(O(P(T))*10)/100:GOSUB1360:GOSUB1370:GOSUB1400:GOSUB1340:GOSUB1350:GOTO380
320 IF OW(P(T))>0 AND OW(P(T))<>T AND O(P(T))>3 THEN N=N(P(T))*(O(P(T))*10)/100+100*O(P(T)):GOSUB1360:GOSUB1370:GOSUB 1400:GOSUB1340:GOSUB1350:GOTO380
330 IF P(T)=24 THEN PAY(T)=PAY(T)+POT:POT=200:SOUND230,5:GOSUB1350:GOTO380
340 IF P(T)=1 OR P(T)=6 OR  P(T)=19 OR P(T)=24 OR P(T)=26 THEN GOSUB1340:GOSUB1350:GOTO380
350 IF P(T)=17 THEN GOSUB 1420:GOSUB1350:GOTO380
360 GOSUB 1340:PRINT@383,"1=Buy";:PRINT@434,"2=Build";:PRINT@485,"Space=Pass";:PRINT@536,"$";:PRINTUSING P$;PAY(T);:GOSUB1390
370 IF MID$(PL$(T),1,4)="COMP" THEN GOTO 1480
380 IF MID$(PL$(T),1,4)="COMP" THEN A$=" ":GOTO 390 ELSE A$=INKEY$:IF A$="" THEN 380
390 IF P(T)=1 OR P(T)=6 OR P(T)=8 ORP(T)=19 OR P(T)=17 ORP(T)=19 OR P(T)=24 OR P(T)=26 THEN GOTO430
400 IF A$="1"AND O(P(T))=0 AND PAY(T)>N(P(T)) THEN O(P(T))=1:OW(P(T))=T:PAY(T)=PAY(T)-N(P(T)):PLAY"T20L200O3;1;5;12":PRINT@588,"+++BUY+++";:GOTO430
410 IF A$="2" AND OW(P(T))=T AND PAY(T)>N(P(T)) THEN PAY(T)=PAY(T)-N(P(T)):O(P(T))=O(P(T))+1:PLAY"T200L200O4;1;5;12":PRINT@588,"###BUILD###";:GOTO430
420 GOSUB1390
430 IF D3=1 THEN SOUND 200,3:PLAY"P8":SOUND200,3:GOTO200
440 NEXT T:GOTO190
450 '**********************MOVE PIECES*********
460 FOR X=1 TO D1
470 IF OW(P(T))<>T THEN GOSUB 1380
480 IF OW(P(T))=T THEN ON T GOSUB1250,1270,1290,1310
490 IF M(TT)<36 THEN M(TT+1)=M(TT+1)-15:IF T=1THEN M(TT)=1 ELSE IF T=2 THEN M(TT)=13 ELSE IF T=3 THEN M(TT)=24 ELSE IF T=4 THEN M(TT)=35
500 IF M(TT+1)>141 AND M(TT)<50 AND M(TT+1)<179 THEN M(TT+1)=128
510 IF M(TT+1)=38 AND M(TT)<36 THEN M(TT)=16:M(TT+1)=1
520 IF M(TT+1)<36 THEN M(TT)=M(TT)+20:IF T=1 THEN M(TT+1)=1 ELSE IF T=2 THEN M(TT+1)=13 ELSE IF T=3 THEN M(TT+1)=24 ELSE IF T=4 THEN M(TT+1)=35
530 IF M(TT)=56 THEN M(TT)=54
540 IF M(TT)>205 AND M(TT+1)<36 THEN M(TT+1)=23
550 IF M(TT)>206 THEN M(TT+1)=M(TT+1)+15:IF T=1 THEN M(TT)=207 ELSE IF T=2 THEN M(TT)=218 ELSE IF T=3 THEN M(TT)=229 ELSE IF T=4 THEN M(TT)=240
560 IF M(TT+1)>141 AND M(TT)>206 THEN M(TT)=226
570 IF M(TT+1)>141 AND M(TT)>50 THEN M(TT)=M(TT)-20:IF T=1 THEN M(TT+1)=142 ELSE IF T=2 THEN M(TT+1)=153 ELSE IF T=3 THEN M(TT+1)=164 ELSE IF T=4 THEN M(TT+1)=175
580 IFM(TT)=186 THEN M(TT)=193
590 IF M(TT)<50 AND M(TT+1)>141 THEN M(TT+1)=180:IFT=1 THEN M(TT)=1 ELSE IF T=2 THEN M(TT)=13 ELSE IF T=3 THEN M(TT)=24 ELSE IF T=4 THEN M(TT)=35
600 IF P(T)=1 AND PAY(T)>0 THEN PAY(T)=PAY(T)+400:PLAY"T200L200O5;1;3;6;9"
610 IF P(T)=24 THEN POT=POT+75 
620 P(T)=P(T)+1:IF P(T)=33 THEN P(T)=1
630 ON T GOSUB 1240,1260,1280,1300
640 PLAY"T200;L200;O1;5;O3;5;O5;5"
650 NEXTX
660 RETURN
670 CLS'********************GET STATEMENTS******
680 LINE(50,50)-(100,100),PSET,B:RETURN
690 CLS:PRINT@367,"   PAY":PRINT@418,"   DAY":PRINT@469,"  $400":GOSUB680:GET(50,50)-(100,100),PP,G
700 CLS:PRINT@367,"  GENERAL";:PRINT@418,"   STORE":GOSUB680:GET(50,50)-(100,100),P2,G
710 CLS:PRINT@367,"   BONUS":PRINT@418,"   POT":GOSUB680:GET(50,50)-(100,100),P3,G
720 CLS:PRINT@367,"  LUCKY":PRINT@418,"   DAY":GOSUB680:GET(50,50)-(100,100),P1,G
730 PRINT@52,"1":LINE(2,6)-(12,16),PSET,B:GET(2,6)-(12,16),D1,G
740 PRINT@52,"2":LINE(2,6)-(12,16),PSET,B:GET(2,6)-(12,16),D2,G
750 PRINT@52,"3":LINE(2,6)-(12,16),PSET,B:GET(2,6)-(12,16),D3,G
760 PRINT@52,"4":LINE(2,6)-(12,16),PSET,B:GET(2,6)-(12,16),D4,G
770 '*****************DATA*********************
780 GOSUB1430
790 DATA PAY,DAY,400,Skid,Road,200,City,Dump,220,Train,Station,300,Creek,Side,250,FREE,SPACE,0,River,View,275,LUCKY,DAY,,Park,Street,300,Burbon,Ave.,320,Apple,Blvd.,330,Power,House,300
800 DATA Subway,Terminal,300,Grand,View,350,Mountain,Side,375,Lake,Side,390,GENERAL,STORE,,Magnola,Drive,400,FREE,SPACE,0,Rosewood,Ave.,440,Bus,Station,300,Colonial,Park,470,Ranch,Drive,490,BONUS,POT,,Thunderbird,Terace,500,FREE,SPACE,0
810 DATA Bently,Drive,520,Hudson,Blud.,540,Airline,Terminal,300,Platinum,Place,560,Silver,Lake,580,Diamond,Aces,600
820 GOTO880
830 '**************** ROLE DIE***************
840 COLOR0:LINE(80,127)-(154,137),PSET,BF:DRAW"C1BM52,127E5R10G5BM64,127E5R10G5E5D10G5":FOR Z=1TO RND(5)+5:LINE(52,127)-(62,137),PSET,BF:LINE(64,127)-(74,137),PSET,BF:D1=0:Y=0
850 FORX=54TO66STEP12:A=RND(6):Y=Y+1:D(Y)=A:D2=7-A:D1=D1+D2:B=A>1:C=A>3:D=A=6:E=A/2=INT(A/2):PSET(X,128,-D):PSET(X+6,128,-C):PSET(X,132,-B):PSET(X+3,132,1-E):PSET(X+6,132,-B):PSET(X,136,-C):PSET(X+6,136,-D):NEXTX:EXEC43345
860 NEXTZ:PRINT@833,"="D1;:IF D(1)=D(2)THEN D3=1:PRINT" DOUBLES"; ELSE D3=0
870 RETURN
880 CLS'**************DRAW BOARD**************
890 PRINT@0,""
900 LINE(0,0)-(256,191),PSET,B
910 LINE(50,50)-(206,141),PSET,BF
920 LINE(0,0)-(50,50),PSET,B
930 LINE(206,141)-(256,191),PSET,B
940 LINE(0,141)-(50,191),PSET,B
950 LINE(206,50)-(256,0),PSET,B
960 LINE(0,50)-(50,65),PSET,B
970 LINE(206,50)-(256,65),PSET,B
980 LINE(0,65)-(50,80),PSET,B
990 LINE(206,65)-(256,80),PSET,B
1000 LINE(0,80)-(50,95),PSET,B
1010 LINE(206,80)-(256,95),PSET,B
1020 LINE(0,95)-(50,110),PSET,B
1030 LINE(206,95)-(256,110),PSET,B
1040 LINE(0,110)-(50,125),PSET,B
1050 LINE(206,110)-(256,125),PSET,B
1060 LINE(70,0)-(90,50),PSET,B
1070 LINE(70,141)-(90,191),PSET,B
1080 LINE(110,0)-(130,50),PSET,B
1090 LINE(110,141)-(130,191),PSET,B
1100 LINE(150,0)-(170,50),PSET,B
1110 LINE(150,141)-(170,191),PSET,B
1120 LINE(190,0)-(190,50),PSET,B
1130 LINE(190,141)-(190,191),PSET
1140 PUT(0,141)-(50,191),PP,PSET
1150 PUT(205,0)-(255,50),P2,PSET
1160 PUT(205,141)-(255,191),P3,PSET
1170 PUT(0,0)-(50,50),P1,PSET
1180 X=0
1190 EXEC43345:X=X+1:READ N$(X),NN$(X),N(X):IF X=32 THEN 1200 ELSE 1190
1200 GOTO140
1210 '*****************GOSUB STATEMENTS***********
1220 LINE(52,52)-(124,110),PSET,BF:COLOR0:RETURN
1230 COLOR1:LINE(52,52)-(120,110),PSET,BF:COLOR0:RETURN
1240 PUT(M(1),M(2))-(M(1)+10,M(2)+10),D1,PSET:RETURN
1250 PUT(M(1),M(2))-(M(1)+10,M(2)+10),D1,PRESET:RETURN
1260 PUT(M(3),M(4))-(M(3)+10,M(4)+10),D2,PSET:RETURN
1270 PUT(M(3),M(4))-(M(3)+10,M(4)+10),D2,PRESET:RETURN
1280 PUT(M(5),M(6))-(M(5)+10,M(6)+10),D3,PSET:RETURN
1290 PUT(M(5),M(6))-(M(5)+10,M(6)+10),D3,PRESET:RETURN
1300 PUT(M(7),M(8))-(M(7)+10,M(8)+10),D4,PSET:RETURN
1310 PUT(M(7),M(8))-(M(7)+10,M(8)+10),D4,PRESET:RETURN
1320 PLAY"T100L100O3;1;12;1":RETURN
1330 LINE(125,52)-(204,110),PSET,BF:RETURN
1340 COLOR1:LINE(125,52)-(204,110),PSET,BF:COLOR0:RETURN
1350 GOSUB1390:PRINT@638,"PRESS ANY KEY";:RETURN
1360 PRINT@521,"OWNER ";PL$(OW(P(T)));:RETURN
1370 PRINT@572,"RENT$";:PRINTUSING"####.##";N;:RETURN 
1380 COLOR1:LINE(M(TT),M(TT+1))-(M(TT)+10,M(TT+1)+10),PSET,BF:RETURN
1390 PLAY"T200L200O5;1;12":RETURN
1400 PAY(T)=PAY(T)-N:PAY(OW(P(T)))=PAY(OW(P(T)))+N:IF PAY(T)<=0 THEN 1500 ELSE RETURN
1410 N=RND(200):PAY(T)=PAY(T)+N:GOSUB1340:PRINT@383,"YOU WIN $";N;:GOSUB 1350:RETURN
1420 N=RND(200):PAY(T)=PAY(T)-N:GOSUB1340:PRINT@383,"YOU PAY $";N;:IF PAY(T)=<0THENGOTO1500ELSE GOSUB1350:RETURN
1430 CLS:PMODE2,1:SCREEN1,0:PRINT@61,"Make":PRINT@113,"or":PRINT@163,"Break"
1440 COLOR0:FOR X=1TO90: EXEC43345:LINE(X,12)-(255-X,62),PSET,B:NEXT
1450 PRINT@500,"By  Dennis";:PRINT@557,"Hoin";
1460 LINE(0,0)-(255,190),PSET,B:FOR X=1TO900:NEXT
1470 CLS:PMODE4,1:SCREEN1,0:COLOR0,1:RETURN
1480 IF PAY(T)-N(P(T))>600 AND O(P(T))=0 THEN A$="1":PRINT@588,"+++BUY+++";:GOTO390 ELSE IF PAY(T)-N(P(T))>600 THEN A$="2": PRINT@588,"###BUILD###";:GOTO390 ELSE A$=" ":PRINT@588,"%%%PASS%%%";:GOTO 390
1490 '************ CHECK IF GAME OVER********
1500 PAY(T)=0:FOR X=1TO32:IF OW(X)=T THEN OW(X)=0:O(X)=0
1510 NEXT
1520 N=0:GOSUB1220:GOSUB1330:GOSUB1230:GOSUB1340:PRINT@419,"   SORRY";:PRINT@433,"  ";PL$(T);:PRINT@470,"   YOUR";:PRINT@484,"   OUT";:SOUND1,50:SOUND100,1:FOR X= 1TO ZZ:IF PAY(X)>0 THEN N=N+1
1530 NEXT X
1540 IF N>1 THEN D1=32:GOSUB460:GOSUB1380:RETURN  ELSE CLS:PMODE2,1:SCREEN1,0: FOR X=1 TO ZZ:IF PAY(X)>0 THEN  SOUND 200,10:PRINT@61,"WINNER ";:PRINT@163,PL$(X) ELSE NEXT X
1550 FOR X=1TO 200 STEP3:SOUNDX,1:SOUND 210-X,1:NEXT:PMODE4,1:SCREEN1,0:END
1560 '*************************
1570 '***  MAKE OR BREAK    ***
1580 '***  BY DENNY HOIN    ***
1590 '***     02/27/85      ***
1600 '*************************
