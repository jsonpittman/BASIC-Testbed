10 '***************************
20 '******* SONG WRITER *******
30 '**** BY GARRY L SHELTON ***
40 '******* MARCH 1986 ********
49 '*** INTRODUCTORY SCREEN ***
50 CLS:PRINT@33,STRING$(30,214);:PRINT@449,STRING$(30,214);:FORX=65TO417STEP32:PRINT@X,CHR$(214);:PRINT@X+29,CHR$(214);:NEXTX:PRINT@138,"song writer";:PRINT@206,"by";:PRINT@264,"garry l shelton";
60 P$="O2;L4;T3;GGAL3;F+;L16;GL4;ABBO3;CO2;L3;BL16;AL4GAGF+L2;G":PLAYP$
99 '*** PROGRAM INITIALIZATION ***
100 POKE65495,0:CLEAR5000:DIMK$(12),N$(21),S$(25):FORX=1TO12:READK$(X):NEXTX:FORX=1TO21:READN$(X):NEXTX:FORX=1TO7:READV$(X):NEXTX:C$=STRING$(12,32):D$=STRING$(90,32)
110 DATA Z,S,X,D,C,V,G,B,H,N,J,M
120 DATA C,C+,D,D+,E,F,F+,G,G+,A,A+,B,94,10,8,9,32,12,64,45,95
130 DATA C,D,F,G,A,E,B
499 '*** SET INITIAL "PLAY" VALUES ***
500 OCT=2:NL=4:TP$="2":N=1:NV$="0":FL$="0"
509 '*** SET UP MAIN SCREEN ***
510 CLS:PRINT@0,"SONG:";:PRINT@96,"OCTAVE:";:PRINT@108,"NOTE LENGTH:";:PRINT@128,"LEARN MODE:":PRINT@160,"STANZA NUMBER:":PRINT@179,"TEMPO:":FORX=16TO27:FORY=10TO51:SET(Y,X,5):NEXTY:FORY=15TO45STEP6:RESET(Y,X):NEXTY,X
520 FORX=16TO21:FORY=14TO20STEP6:RESET(Y,X):RESET(Y+2,X):NEXTY:FORY=32TO44STEP6:RESET(Y,X):RESET(Y+2,X):NEXTY,X:PRINT@192,STRING$(32,230);:PRINT@327,"s";:PRINT@330,"d";:PRINT@336,"g";:PRINT@339,"h";:PRINT@342,"j";:PRINT@422,"z";:PRINT@425,"x";
530 PRINT@428,"c";:PRINT@431,"v";:PRINT@434,"b";:PRINT@437,"n";:PRINT@440,"m";
540 PRINT@6,D$;:PRINT@6,S$(N);:PRINT@103,"   ";:PRINT@103,OCT;:PRINT@120,"   ";:PRINT@120,NL;:PRINT@145,C$;:PRINT@175,"   ";:PRINT@175,N;:PRINT@186,"    ";:PRINT@186,TP$;:IFFL$="1" THENPRINT@140,"ON ";ELSEPRINT@140,"OFF";
550 PLAY"O"+STR$(OCT)+";"+"L"+STR$(NL)+";"+"T"+TP$
999 '*** INPUT A COMMAND ***
1000 E$=INKEY$:IFE$="" THEN1000ELSEE=ASC(E$)
1010 IFE=92 THEN1100ELSEGOSUB1130:IFNV$="0" THEN1000ELSENV$="0"
1020 IFE=64 THENGOSUB1170:GOTO1000
1030 IFE=12 THEN1180ELSEIFE=94 THEN2000ELSEIFE=10THEN2010ELSEIFE=8 THEN2040ELSEIFE=9 THEN2050ELSEIFE=32 THEN2070ELSEIFE=45 THENTP$="":GOTO2080ELSEIFE=95 THEN3010
1040 IFFL$="1" THENS$(N)=S$(N)+E$:PRINT@6,S$(N);:IFLEN(S$(N))>80 THENGOSUB3000
1050 POKE65494,0:PLAYE$:POKE65495,0:GOTO1000
1099 '*** END ***
1100 PRINT@145,"end? y/n";
1110 E$=INKEY$:IFE$="" THEN1110ELSEIFE$="Y" THENPOKE65494,0:END
1120 PRINT@145,C$;:GOTO1000
1129 '*** CHECK FOR VALID CHARACTER ***
1130 FORX=1TO12:IFE$=K$(X) THENE$=N$(X):X=12:NV$="1"
1140 NEXTX:IFNV$="1" THENRETURN
1150 FORX=13TO21:IFE=VAL(N$(X)) THENX=21:NV$="1"
1160 NEXTX:RETURN
1169 '*** CHANGE LEARN FLAG ***
1170 IFFL$="1" THENFL$="0":PRINT@140,"OFF";:RETURNELSEFL$="1":PRINT@140,"ON ";:RETURN
1179 '*** SET UP SUB-MENU ***
1180 PRINT@145,"e p n s l r ";
1190 E$=INKEY$:IFE$="" THEN1190ELSEIFE$="P" THEN1400ELSEIFE$="E" THEN1210ELSEIFE$="N"THEN1410ELSEIFE$="S"THEN1440ELSEIFE$="L"THEN1460ELSEIFE$="R" THENPRINT@145,C$;:GOTO1000
1200 GOTO1190
1209 '*** EDIT SONG ***
1210 CLS:S=1:CP=1:PRINT@296,"EDIT COMMANDS":PRINT@36,"STANZA: ";S:PRINT@56,"edit":PRINT@320,"clear TO QUIT    space TO EDIT":PRINT@352,"UP ARROW - DISPLAY PREV. STANZA.":PRINT@384,"DN ARROW - DISPLAY NEXT STANZA.":GOSUB1260
1220 E$=INKEY$:IFE$="" THEN1220ELSEE=ASC(E$)
1230 IFE=10 THEN1240ELSEIFE=94 THEN1250ELSEIFE=32 THENPRINT@448,STRING$(20,32);:GOTO1270ELSEIFE=12 THEN510ELSE1220
1239 '*** NEXT STANZA ***
1240 IFS<N THENS=S+1:GOSUB1260:GOTO1220ELSE1220
1249 '*** PREVIOUS STANZA ***
1250 IF S>1 THENS=S-1:GOSUB1260:GOTO1220ELSE1220
1259 '*** DISPLAY STANZA ***
1260 PRINT@44,S;:CC=LEN(S$(S)):PRINT@96,D$;:PRINT@96,S$(S);:RETURN
1269 '*** EDIT STANZA ***
1270 A$=MID$(S$(S),CP,1):E$=INKEY$:PRINT@95+CP,CHR$(191);:PRINT@95+CP,CHR$(191);:PRINT@95+CP,A$;:IFE$="" THEN1270ELSEE=ASC(E$)
1280 IFE=8 THEN1320ELSEIFE=9 THEN1330ELSEIFE=12 THEN1340ELSEIFE=13 THEN1370
1289 '*** PLACE INPUT CHAR. INTO STANZA ***
1290 IF CC>=90 THENSOUND5,5:GOTO1270
1300 IFCP=1 THENS$(S)=E$+S$(S) ELSES$(S)=LEFT$(S$(S),CP-1)+E$+RIGHT$(S$(S),CC-(CP-1))
1310 PRINT@96,S$(S);:CC=CC+1:CP=CP+1:GOTO1270
1319 '*** MOVE EDIT CURSOR LEFT ***
1320 IFCP=1 THEN1270ELSECP=CP-1:GOTO1270
1329 '*** MOVE EDIT CURSOR RIGHT ***
1330 IFCP=CC THEN1270ELSECP=CP+1:GOTO1270
1339 '*** DELETE CHAR ***
1340 IFCC<=1 THENSOUND5,5:GOTO1270
1350 IFCP=1 THENS$(S)=RIGHT$(S$(S),CC-1) ELSEIFCP=CC THENS$(S)=LEFT$(S$(S),CC-1) ELSES$(S)=LEFT$(S$(S),CP-1)+RIGHT$(S$(S),CC-CP)
1360 PRINT@96,D$;:PRINT@96,S$(S);:IFCP=CC THENCP=CP-1:CC=CC-1:GOTO1270ELSECC=CC-1:GOTO1270
1369 '*** CHECK FOR VALID EDIT CHARS ***
1370 CP=1:Y=LEN(S$(S)):FG$="0"
1380 IFCP>Y THENPRINT@448,STRING$(20,32);:PRINT@448,"edit complete";:CP=1:GOTO1220ELSEE$=MID$(S$(S),CP,1)
1390 GOTO1500
1399 '*** PLAY THE SONG ***
1400 POKE65494,0:PLAY"O2;L4;T2;V15":FORX=1TO N:PRINT@6,D$;:PRINT@6,S$(X);:PRINT@175,X;:PLAYS$(X):NEXTX:POKE65495,0:GOTO1190
1409 '*** NEW SONG? ***
1410 PRINT@145,"new song? yn";
1420 E$=INKEY$:IFE$="" THEN1420ELSEIFE$="Y" THENFORX=1TO N:S$(X)="":NEXT:OCT=2:NL=4:TP$="2":N=1:NV$="0":FL$="0":GOTO540
1430 GOTO1180
1439 '*** SAVE "SONG" ***
1440 POKE65494,0:PRINT@145,"recorder    ";:IFINKEY$="" THEN1440
1450 OPEN"O",#-1,"SONG":PRINT#-1,OCT,NL,TP$:FORX=1TO N:PRINT#-1,S$(X):NEXT:CLOSE:POKE65495,0:GOTO1180
1459 '*** LOAD "SONG" ***
1460 POKE65494,0:PRINT@145,"recorder    ";:IFINKEY$="" THEN1460
1470 N=0:OPEN"I",#-1,"SONG":INPUT#-1,OCT,NL,TP$
1480 IFEOF(-1) THENCLOSE:FL$="0":NV$="0":POKE65495,0:GOTO540
1490 N=N+1:INPUT#-1,S$(N):GOTO1480
1499 '*** EDIT SUBROUTINE ***
1500 L=255:IFE$="T" OR E$="P" ORE$="L" THEN1570ELSEIFE$="O" THENL=5:GOTO1570ELSEIFE$="V" THENL=31:GOTO1570
1509 '*** CHECK FOR CORRECT NOTES ***
1510 FORX=1TO7:IFE$=V$(X) THENX=7:FG$="1"
1520 NEXTX:IFFG$="1" THEN1560
1530 IFE$="+" THEN1540 ELSEPRINT@448,STRING$(20,32);:PRINT@448,"invalid note";:SOUND5,5:GOTO1270
1539 '*** CHECK FOR CORRECT SHARPS ***
1540 FORX=1TO5:IFMID$(S$(S),CP-1,1)=V$(X) THENFG$="1":X=5
1550 NEXTX:IFFG$="1" THEN1560ELSEPRINT@448,STRING$(20,32);:PRINT@448,"invalid use of sharp";:SOUND5,5:GOTO1270
1559 '*** UP CURS POS. BY ONE - CONTINUE EDIT ***
1560 FG$="0":CP=CP+1:GOTO1380
1569 '*** CHECK FOR CORRECT FORMAT OF FUNCTIONS ***
1570 FORX=1TO L:A$=E$+RIGHT$(STR$(X),LEN(STR$(X))-1)+";":PRINT@416,A$,E$;:IFA$=MID$(S$(S),CP,LEN(A$)) THENX=255:FG$="1"
1580 NEXTX:IFFG$="1" THENCP=CP+LEN(A$):FG$="0":GOTO1380 ELSEPRINT@448,STRING$(20,32);:PRINT@448,"invalid format";:SOUND5,5:GOTO1270
1999 '*** INCREASE OCTAVE ***
2000 IFOCT=5 THEN1000ELSEOCT=OCT+1:GOTO2020
2009 '*** DECREASE OCTAVE ***
2010 IFOCT=1 THEN1000ELSEOCT=OCT-1
2020 ITM$=RIGHT$(STR$(OCT),1):PLAY"O"+ITM$:PRINT@103,OCT;:H$="O":IFFL$="1" THENGOSUB2030:GOTO1000ELSE1000
2029 '*** PLACE INTO SONG IF FLAG IS SET ***
2030 S$(N)=S$(N)+H$+ITM$+";":PRINT@6,S$(N);:IFLEN(S$(N))>80 THENGOSUB3000:RETURNELSERETURN
2039 '*** INCREASE NOTE LENGTH ***
2040 IFNL=1 THEN1000ELSEIFNL<8 THENNL=NL-1:GOTO2060ELSENL=NL/2:GOTO2060
2049 '*** DECREASE NOTE LENGTH ***
2050 IFNL=64 THEN1000ELSEIFNL>3 THENNL=NL*2 ELSENL=NL+1
2060 ITM$=RIGHT$(STR$(NL),LEN(STR$(NL))-1):PLAY"L"+ITM$:PRINT@120,NL;:H$="L":IFFL$="1" THENGOSUB2030:GOTO1000ELSE1000
2069 '*** REST ***
2070 IFFL$="0" THEN1000ELSEH$="P":ITM$=RIGHT$(STR$(NL),LEN(STR$(NL))-1):GOSUB2030:IFLEN(S$(N))>80 THENGOSUB3000:GOTO1000ELSE1000
2079 '*** CHANGE TEMPO ***
2080 E$=INKEY$:IFE$=""THEN2080 ELSEIFASC(E$)=13 THEN2090ELSEIFASC(E$)=8 THENTP$=LEFT$(TP$,LEN(TP$)-1):PRINT@186,"    ";:PRINT@186,TP$;:GOTO2080ELSETP$=TP$+E$:PRINT@186,"    ";:PRINT@186,TP$;:GOTO2080
2090 IFVAL(TP$)<1 OR VAL(TP$)>255 THENSOUND5,5:GOTO2080ELSEPLAY"T"+TP$:IFFL$="1" THENITM$=TP$:H$="T":GOSUB2030:GOTO1000ELSE1000
2999 '*** CHANGE STANZA ***
3000 N=N+1:PRINT@6,D$;:PRINT@175,N;:RETURN
3009 '*** HELP SCREEN ***
3010 CLS:PRINT@0,"***** SONG WRITER COMMANDS *****";:PRINT"UP ARROW     INCREASE OCTAVE":PRINT"DN ARROW     DECREASE OCTAVE":PRINT"<-           INCREASE NOTE":PRINT"->           DECREASE NOTE":PRINT"SPACE BAR    REST"
3020 PRINT"-            CHANGE TEMPO":PRINT"@            TOGGLE ON/OFF LEARN";:PRINT"SHIFT CLEAR  END PROGRAM":PRINT"CLEAR        DISPLAY SUB-MENU":PRINT" eDIT SONG     sAVE SONG":PRINT" pLAY SONG     lOAD SONG":PRINT" nEW SONG      rETURN"
3030 PRINT:PRINT"SHIFT UP ARROW  HELP SCREEN":PLAY"T2;O3;L8;CO2;EEO3;CL2;CL8;CO2;EEO3;CCO2;EFEDDDBL2;BL8;BDDBBDEDCCCAL2;AL8;ACCAACDCO1;BBBO2;G+L2;G+L8;G+ABFL1;E":PRINT@484,"press any key to return";:PRINT@0,;
3040 IFINKEY$="" THEN3040ELSE510
