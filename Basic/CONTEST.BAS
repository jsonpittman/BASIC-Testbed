10 'BASIC PROGRAMMING CONTEST
20 'by Bill Bernico
30 'AND (YOUR NAME HERE)
40 '
50 CLEAR1000:DIM A(12,12)
60 D$="BM=H;,=V;":X$="BM=X;,=Y;
70 N1$="BR3R2U6NGD6R2":N2$="BR3BU5ER2FDGL2GD2R4":N3$="BR3BU5ER2FDGNLFDGL2NH":N4$="BR6U6G3R4":N5$="BR3BUFR2EU2HL3U2R4":N6$="BR3BU3R3FDGL2HU4ER2":N7$="BR3BU6R4DG3D2":N8$="BR4HUER2EUHL2GDFR2FDGNL2"
80 PMODE4,1:PCLS1:SCREEN1,1:COLOR0,1
90 DRAW"BM8,0R80D73L80U73"
100 CIRCLE(48,36),25
110 DRAW"BM42,8"+N1$+"BM64,17"+N2$+"BM74,39"+N3$+"BM65,59"+N4$+"BM43,70"+N5$+"BM21,59"+N6$+"BM12,39"+N7$+"BM22,18"+N8$
120 GET(8,0)-(88,73),A
130 PUT(88,0)-(168,73),A:PUT(168,0)-(248,73),A
140 DRAW"BM8,0R240D191L240U191
150 RI$=D$+"R20NH2G2":LE$=D$+"L20NE2F2":UP$=D$+"U20NG2F2":DO$=D$+"D20NH2E2":UR$=D$+"E14NL3D3":LR$=D$+"F14NU3L3":LL$=D$+"G14NR3U3":UL$=D$+"H14NR3D3
160 H=48:V=36:GOSUB 270
170 IF INKEY$<>"1"THEN 160
180 X=110:Y=90:GOSUB 360
190 H=128:V=36:GOSUB 270
200 IF INKEY$<>"2"THEN 190
210 X=120:Y=90:GOSUB 360
220 H=208:V=36:GOSUB 270
230 IF INKEY$<>"3"THEN 220
240 X=130:Y=90:GOSUB 360
250 IF INKEY$<>CHR$(32)THEN 250
260 GOTO 80
270 DRAW"C0"+RI$+"C1"+RI$
280 DRAW"C0"+LR$+"C1"+LR$
290 DRAW"C0"+DO$+"C1"+DO$
300 DRAW"C0"+LL$+"C1"+LL$
310 DRAW"C0"+LE$+"C1"+LE$
320 DRAW"C0"+UL$+"C1"+UL$
330 DRAW"C0"+UP$+"C1"+UP$
340 DRAW"C0"+UR$+"C1"+UR$
350 RETURN
360 S=RND(8):ON S GOSUB370,380,390,400,410,420,430,440:RETURN
370 DRAW"C0"+RI$+X$+N3$:RETURN
380 DRAW"C0"+DO$+X$+N5$:RETURN
390 DRAW"C0"+LE$+X$+N7$:RETURN
400 DRAW"C0"+UP$+X$+N1$:RETURN
410 DRAW"C0"+UR$+X$+N2$:RETURN
420 DRAW"C0"+LR$+X$+N4$:RETURN
430 DRAW"C0"+LL$+X$+N6$:RETURN
440 DRAW"C0"+UL$+X$+N8$:RETURN
