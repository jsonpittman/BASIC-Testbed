10 CLS 8:PRINT"COCO SIMON"
20 PRINT"        BY W.MASSIE"
30 FORX=1TO20:SOUND 100,1:SOUND90,1:NEXTX
40 PMODE 3,1:SCREEN 1,0:PCLS
50 '***************************
60 P=1:' FOR SOUND DELAY
70 DIM M$(99):C=0:CH=0
80 R=RND(4):A$="QWAS":C=C+1
90 M$(C)=MID$(A$,R,1)
100 C1=0:C2=0
110 '**************************
120 C1=C1+1:PCLS
130 IF M$(C1)="Q" THEN 190
140 IF M$(C1)="W" THEN 220
150 IF M$(C1)="A" THEN 250
160 IF M$(C1)="S" THEN 280
170 IF C1=C THEN 320 
180 GOTO 120
190 COLOR 3,5
200 LINE(0,0)-(100,75),PSET,B
210 SOUND 100,P:GOTO 170
220 COLOR 2,5
230 LINE(100,0)-(200,75),PSET,BF
240 SOUND 125,P:GOTO 170
250 COLOR 3,5
260 LINE(0,75)-(100,150),PSET,BF
270 SOUND 150,P:GOTO 170
280 COLOR 4,5
290 LINE(100,75)-(200,150),PSET,BF
300 SOUND 200,P:GOTO 170
310 '***************************
320 C2=C2+1:PCLS
330 K$=INKEY$
340 IF CH<C2 THEN CH=C2
350 IF C2=C+1 THEN PCLS 2:FORX=1TO20:NEXTX:GOTO80
360 IF K$="Q" OR K$="W" OR K$="A" OR K$="S" THEN 370 ELSE 330
370 IF K$<>M$(C2) THEN CLS:GOTO430
380 IF K$="Q"THEN COLOR 3,5:LINE(0,0)-(100,75),PSET,B:SOUND100,P
390 IF K$="W"THEN COLOR 2,5:LINE(100,0)-(200,75),PSET,BF:SOUND125,P
400 IF K$="A"THEN COLOR 3,5:LINE(0,75)-(100,150),PSET,BF:SOUND150,P
410 IF K$="S"THEN COLOR 4,5:LINE(100,75)-(200,150),PSET,BF:SOUND200,P
420 PCLS:GOTO 320
430 PRINT"WRONG, IT WAS THE ";M$(C2);"-KEY.":SOUND 1,15
440 PRINT:PRINT"HIGHEST NUMBER OF SEQUENCE(S)"
450 PRINT"REMEMBERED IS:";CH-1
460 PRINT:PRINT"DO YOU WANT TO PLAY AGAIN (Y/N)?";
470 R$=INKEY$:IF R$="Y" THEN C=0:CH=0:PMODE 3,1:SCREEN 1,0:PCLS:GOTO80
480 IF R$="N" THEN END ELSE 470
