0 'COPYRIGHT 1989  FALSOFT,INC
1 ' Graphics Pro.
2 ' By Michael J. Vandall
3 ' 20985 Cindy Ct.
4 ' Poulsbo, WA 98370
5 ' December 1986
6 '
7 '
8 ' * Initialization *
9 '
10 GOSUB 2230:HSCREEN 2:HBUFF 1,2416:HBUFF 2,799:HBUFF 3,44:HGET(0,0)-(300,15),1:HSCREEN0:PALETTE RGB:CLEAR 2000:ON BRK GOTO 440
20 LN=120:V=96:H=160:Z=1:DIMPL(15):WIDTH 40
30 INPUT"NEW DRAWING OR OLD (N/O)";N$:INPUT"PROGRAM NAME ";NN$:IF N$="O" AND NN$<>"?" THEN NN$=NN$+".BAS":FG=1:GOTO 70:ELSE IF NN$="?" THEN DIR:PRINT:GOTO 30
40 NN$=LEFT$(NN$,8)+".DAT":OPEN "O",#1,NN$
50 GOSUB 1520
60 PRINT#1,"110HSCREEN2:HCLS0"
70 HSCREEN2:HCLS0:IF FG=1 THEN GOSUB 1730:GOSUB 630:ELSE GOSUB 630   
77 '
78 ' * Main Inkey$ *
79 '
80 HGET(0,V-2)-(319,V+2),2
90 HDRAW"BM"+STR$(INT(H))+","+STR$(INT(V))+";C"+STR$(Z)+"ND2NU2NL2NR2"
100 C$=INKEY$:HPUT(0,V-2)-(319,V+2),2:VC=V:HC=H
110 IF PEEK(341)=247 THEN V=V-1
120 IF PEEK(339)=191 AND PEEK(341)=247 THEN V=V-4
130 IF PEEK(342)=247 THEN V=V+1
140 IF PEEK(339)=191 AND PEEK(342)=247 THEN V=V+4
150 IF PEEK(343)=247 THEN H=H-1
160 IF PEEK(339)=191 AND PEEK(343)=247 THEN H=H-4
170 IF PEEK(344)=247 THEN H=H+1
180 IF PEEK(339)=191 AND PEEK(344)=247 THEN H=H+4
190 GOSUB 1080
197 '
198 ' * Cursor Subroutines *
199 '
200 IF C$="" THEN 80 ELSE IF  C$="*" THEN END
210 IF C$="S" AND FLG=1 THEN FLG=0:RETURN
220 IF C$="F" AND FLG=2 THEN FLG=0:RETURN
230 IF C$="X" AND FLG=3 THEN FLG=0:RETURN
240 IF C$="E" AND FLG=4 THEN FLG=0:RETURN
250 IF C$="I" AND FLG=5 THEN FLG=0:RETURN
260 IF C$="M" AND FLG=6 THEN FLG=0:RETURN
270 IF C$="M" AND FLG=7 THEN FLG=0:RETURN
280 IF ASC(C$)=4 THEN GOSUB 1640:ELSE IF ASC(C$)=189 AND TG<>1 THEN TG=1:GOSUB 1080:ELSE IF ASC(C$)=189 AND TG=1 THEN TG=0:GOSUB 1130
290 IF ASC(C$)=64 THEN RS=1:HPRINT(1,0),"REDRAW":PRINT #1,STR$(LN)+"GOTO"+STR$(LN):CLOSE:GOSUB 1370:RS=0:GOSUB 1130:GOSUB 1140
300 IF ASC(C$)=103 THEN HPRINT(1,0),"ERASE LAST":GOSUB 1150:GOSUB 1130
310 IF C$="L" THEN 400
320 IF C$="?" THEN GOSUB 1710
330 IF C$="C" THEN GOSUB 630
340 IF C$="E" THEN 450
350 IF C$="P" THEN 580
360 IF C$="D" THEN 700
370 IF C$="T" THEN 930
380 IF C$="Q" THEN HPRINT(1,0),"REALLY WANT TO QUIT? (Y/N)":INPUT Q$:IF Q$<>"Y" THEN GOSUB 1130:GOTO 80 ELSE GOSUB1130:HPRINT(1,0),"QUIT":PRINT#1,STR$(LN)+"GOTO"+STR$(LN):CLOSE #1:HSCREEN0:CLS:PALETTE RGB:GOTO 1370
390 GOTO 80
397 '
398 ' * Line Command *
399 '
400 HPRINT(1,0),"LINE: Mark Start <S>":FLG=1:GOSUB 80:HSET(H,V,6):V1=V:H1=H:GOSUB 1130:HPRINT(1,0),"LINE: Mark Finish <F>":FLG=2:GOSUB 80:GOSUB 1130:HPRINT(1,0),"LINE: Box Option 1.None, 2.B, 3.BF"
410 H2=H:V2=V:A$=INKEY$:IF A$=""THEN410ELSE IFINSTR("123",A$)=0 THEN 410:ELSE IF A$="1" THEN O$="PSET":HLINE(H1,V1)-(H2,V2),PSET:ELSE IF A$="2" THEN O$="PSET,B":HLINE(H1,V1)-(H2,V2),PSET,B:ELSE IF A$="3" THEN O$="PSET,BF":HLINE(H1,V1)-(H2,V2),PSET,B
420 E1=H1:E2=V1:E3=H2:E4=V2:IF A$="1" THEN E$="L":ELSE IF A$="2" THEN E$="LB":ELSE IF A$="3" THEN E$="LF"
430 PRINT#1,STR$(LN)+"HLINE("+STR$(H1)+","+STR$(V1)+")-("+STR$(H2)+","+STR$(V2)+"),"+O$:LN=LN+10
440 GOSUB 1130:FLG=0:GOTO 80
447 '
448 ' * Ellipse Command *
449 '
450 HPRINT(1,0),"ELLIPSE: Mark Center <X>":FLG=3:B=TG:TG=0:GOSUB 80:HSET(H,V,6):H1=H:V1=V:GOSUB 1130:HPRINT(1,0),"ELLIPSE: Mark Radius <R>"
460 IF PEEK(343)=247 THEN H=H-1 ELSE IF PEEK(339)=191 AND PEEK(343)=247 THEN H=H-5
470 IF PEEK(344)=247 THEN H=H+1 ELSE IF PEEK(339)=191 AND PEEK(344)=247 THEN H=H+5
480 GOSUB 1080:HSET(H,V):FOR X=1 TO 5:NEXT:HRESET(H,V)
490 C$=INKEY$:IF C$<>"R" THEN HRESET(H1,V1):GOTO 460
500 IF H>H1 THEN R=H-H1 ELSE R=H1-H:HSET(H,V,6)
510 GOSUB 1130:HPRINT(1,0),"ELLIPSE: Color (1-15)":INPUT K$:HPRINT(23,0),K$:IF VAL(K$)<1 OR VAL(K$)>15 THEN 510 ELSE K=VAL(K$)
520 GOSUB 1130:HPRINT(1,0),"ELLIPSE: Height/Width Ratio (0-255)":INPUT H$:HPRINT(37,0),H$:IF VAL(H$)>255 THEN 520 ELSE H=VAL(H$)
530 GOSUB 1130:HPRINT(1,0),"ELLIPSE: Start (0-1)":INPUT S$:HPRINT(22,0),S$:IF VAL(S$)>1 THEN 530 ELSE S=VAL(S$)
540 GOSUB 1130:HPRINT(1,0),"ELLIPSE: End (0-1)":INPUT E$:HPRINT(20,0),E$:IF VAL(E$)>1 THEN 540 ELSE E=VAL(E$)
550 HCIRCLE(H1,V1),R,K,H,S,E:E1=H1:E2=V1:E3=R:E4=H:E5=S:E6=E:E$="E"
560 PRINT#1,STR$(LN)+"HCIRCLE("+STR$(H1)+","+STR$(V1)+"),"R","K","H","S","E
570 LN=LN+10:H=H1:V=V1:TG=B:GOSUB 1130:GOTO 80
577 '
578 ' * Paint Command *
579 '
580 HPRINT(1,0),"PAINT: Mark Edge <E>":FLG=4:GOSUB 80:H1=H:V1=V:E=HPOINT(H,V):GOSUB1130
590 HPRINT(1,0),"PAINT: Mark Interior <I>":FLG=5:GOSUB 80:H2=H:V2=V:HSET(H1,V1,E):GOSUB 1130:E3=HPOINT(H2,V2)
600 HPRINT(1,0),"PAINT: Paint Color (1-15)":INPUT K$:HPRINT(27,0),K$:IF VAL(K$)<1 OR VAL(K$)>15 THEN 600 ELSE K=VAL(K$)
610 HPAINT(H2,V2),K,E:E1=H2:E2=V2:E4=E:E$="P"
620 PRINT#1,STR$(LN)+"HPAINT("H2","V2"),"K","E:LN=LN+10:GOSUB 1130:GOTO 80
627 '
628 ' * Color Command *
629 '
630 HPRINT(0,0),"COLOR: Select Co. <C>"
640 FOR X=169 TO 309 STEP 10:HCOLOR X/10-15.9:HLINE(X,0)-(X+10,10),PSET,BF:NEXT:HC=174
650 HGET(0,3)-(319,7),2:HDRAW"BM"+STR$(INT(HC))+",5C0ND2NU2NL2NR2":C$=INKEY$:HPUT(0,3)-(319,7),2
660 IF PEEK(343)=247 AND HC>174 THEN HC=HC-10
670 IF PEEK(344)=247 AND HC<314 THEN HC=HC+10
680 IF C$<>"C" THEN 650 ELSE Z=(HC/10-16.4):HCOLORZ:GOSUB 1140:IF FLG=8 THEN FLG=0:RETURN
690 PRINT#1,STR$(LN)+"HCOLOR"+STR$(Z):LN=LN+10:GOSUB 1130:RETURN
697 '
698 ' * Draw Command *
699 '
700 HPRINT(1,0),"DRAW: Mark Start <M>":FLG=6:GOSUB 80:HSET(H,V,6):H1=H:V1=V
710 T$=STR$(LN)+"HDRAW"+CHR$(34)+"BM"+RIGHT$(STR$(H1),LEN(STR$(H1))-1)+","+RIGHT$(STR$(V1),LEN(STR$(V1))-1)
720 GOSUB1130:HPRINT(1,0),"DRAW: Press Dir.(U,D,L,R,E,F,G,H,M,C)"
730 D$=INKEY$:IF D$="" THEN 730 ELSE IF ASC(D$)=103 THEN 1260 ELSE IF D$="Q" THEN 920 ELSE IF INSTR("UDLREFGHMC",D$)=0 THEN 720
740 IF ASC(D$)=103 THEN GOSUB 1260
750 IF D$="M" OR D$="C" THEN 770
760 GOSUB 1130:HPRINT(1,0),"DRAW: Number of Dots ":HPRINT(22,0),D$:INPUT N$:HPRINT(24,0),N$:IF VAL(N$)<1 THEN 760
770 ON INSTR("UDLREFGHMC",D$) GOSUB 790,800,810,820,830,840,850,860,870,900
780 GOTO 720
789 ' Draw Up
790 IF V1-VAL(N$)<0 THEN RETURN ELSE HLINE(H1,V1)-(H1,V1-VAL(N$)),PSET:T$=T$+"U"+N$:V1=V1-VAL(N$):GOSUB 910:E$="U":RETURN
799 ' Draw Down
800 IF V1+VAL(N$)>191 THEN RETURN ELSE HLINE(H1,V1)-(H1,V1+VAL(N$)),PSET:T$=T$+"D"+N$:V1=V1+VAL(N$):GOSUB 910:E$="D":RETURN
809 ' Draw Left
810 IF H1-VAL(N$)<0 THEN RETURN ELSE HLINE(H1,V1)-(H1-VAL(N$),V1),PSET:T$=T$+"L"+N$:H1=H1-VAL(N$):GOSUB 910:E$="L":RETURN
819 ' Draw Right
820 IF H1+VAL(N$)>319 THEN RETURN ELSE HLINE(H1,V1)-(H1+VAL(N$),V1),PSET:T$=T$+"R"+N$:H1=H1+VAL(N$):GOSUB 910:E$="R":RETURN
829 ' Draw 45 Degree
830 IF H1+VAL(N$)>319 OR V1-VAL(N$)<0 THEN RETURN ELSE HLINE(H1,V1)-(H1+VAL(N$),V1-VAL(N$)),PSET:T$=T$+"E"+N$:H1=H1+VAL(N$):V1=V1-VAL(N$):GOSUB 910:E$="E":RETURN
839 ' Draw 135 Degree
840 IF H1+VAL(N$)>319 OR V1+VAL(N$)>191 THEN RETURN ELSE HLINE(H1,V1)-(H1+VAL(N$),V1+VAL(N$)),PSET:T$=T$+"F"+N$:H1=H1+VAL(N$):V1=V1+VAL(N$):GOSUB 910:E$="F":RETURN
849 ' Draw 225 Degree
850 IF H1-VAL(N$)<0 OR V1+VAL(N$)>191 THEN RETURN ELSE HLINE(H1,V1)-(H1-VAL(N$),V1+VAL(N$)),PSET:T$=T$+"G"+N$:H1=H1-VAL(N$):V1=V1+VAL(N$):GOSUB 910:E$="G":RETURN
859 ' Draw 315 Degree
860 IF H1-VAL(N$)<0 OR V1-VAL(N$)<0 THEN RETURN ELSE HLINE(H1,V1)-(H1-VAL(N$),V1-VAL(N$)),PSET:T$=T$+"H"+N$:H1=H1-VAL(N$):V1=V1-VAL(N$):GOSUB 910:E$="H":RETURN
869 ' Draw Move
870 GOSUB 1130:HPRINT(1,0),"DRAW: Mark Point <M>":FLG=7:H=H1:V=V1:GOSUB 80:H2=H:V2=V
880 H3=H1:V3=V1:E$="M"
890 HLINE(H1,V1)-(H2,V2),PSET:T$=T$+"M"+RIGHT$(STR$(H2),LEN(STR$(H2))-1)+","+RIGHT$(STR$(V2),LEN(STR$(V2))-1):H1=H2:V1=V2:GOSUB 910:RETURN
899 ' Draw Color
900 GOSUB 1130:FLG=8:GOSUB 630:T$=T$+"C"+STR$(Z):GOSUB 910:RETURN
910 IF LEN(T$)>240 THEN PRINT#1,T$+CHR$(34):LN=LN+10:GOSUB 1130:GOTO 700 ELSE RETURN
920 PRINT#1,T$+CHR$(34):LN=LN+10:GOSUB 1130:GOTO 80
927 '
928 ' * Text Command *
929 '
930 TS$="":HPRINT(1,0),"TEXT: Mark Starting Point <P>":H1=0:V1=8
940 HGET(H1,V1)-(H1+8,V1+8),3:HLINE(H1,V1)-(H1+8,V1+8),PSET,BF:C$=INKEY$:HPUT(H1,V1)-(H1+8,V1+8),3
950 IF PEEK(341)=247 THEN V1=V1-8 ELSE IF PEEK(342)=247 THEN V1=V1+8
960 IF PEEK(343)=247 THEN H1=H1-8 ELSE IF PEEK(344)=247 THEN H1=H1+8
970 IF H1>311 THEN H1=0 ELSE IF H1<0 THEN H1=311
980 IF V1>183 THEN V1=0 ELSE IF V1<0 THEN V1=183
990 IF C$<>"P" THEN 940
1000 H2=INT(H1/8):V2=INT(V1/8):GOSUB 1130:HPRINT(1,0),"Text ? ":H1=64
1010 HGET(H1,0)-(H1+8,8),3:HLINE(H1,0)-(H1+8,8),PSET,BF:C$=INKEY$:HPUT(H1,0)-(H1+8,8),3:IF C$="" THEN 1010 ELSE C=ASC(C$)
1020 IF C>13 GOTO 1050
1030 IF LEN(TS$)>0 AND C=8 THEN HCOLOR 0:HPRINT((H1+8)/8-2,0),RIGHT$(TS$,1):H1=H1-8:HCOLOR Z:TS$=LEFT$(TS$,LEN(TS$)-1):GOTO1010:ELSE IF C=8 THEN 1010
1040 IF C=13 THEN 1060
1050 IF LEN(TS$)<=40 THEN TS$=TS$+C$:HPRINT(H1/8,0),C$:H1=H1+8:GOTO 1010:ELSE SOUND 250,3:GOTO 1010
1060 HPRINT(H2,V2),TS$
1070 PRINT#1,STR$(LN)+"HPRINT("+STR$(H2)+","+STR$(V2)+"),"+CHR$(34)+TS$+CHR$(34):LN=LN+10:GOSUB 1130:E1=H2:E2=V2:E$="T":GOTO 80
1077 '
1078 ' * Cursor Limiter *
1079 '
1080 IF H>317 THEN H=2:FG=3:ELSE IF H<2 THEN H=317:FG=3
1090 IF V>189 THEN V=2:FG=3:ELSE IF V<2 THEN V=189:FG=3
1100 IF TG=1 AND FG=3 THEN GOSUB 1130:FG=0:ELSE IF TG=1 AND HC=H AND VC=V THEN RETURN
1110 IF TG=1 THEN HC$=STR$(HC):VC$=STR$(VC):CO$=RIGHT$(HC$,LEN(HC$)-1)+","+RIGHT$(VC$,LEN(VC$)-1):HCOLOR 0:HPRINT(30,0),CO$:HCOLOR Z:H$=STR$(H):V$=STR$(V):CN$=RIGHT$(H$,LEN(H$)-1)+","+RIGHT$(V$,LEN(V$)-1):HPRINT(30,0),CN$
1120 RETURN
1127 '
1128 ' * Status Clear & Co. Box Update *
1129 '
1130 HPUT(0,0)-(300,15),1:RETURN
1140 HCOLOR Z:HLINE(300,0)-(319,10),PSET,BF:RETURN
1146 '
1147 ' * Erase Last Function *
1148 '
1149 ' Erase Line
1150 IF E$="L" THEN HLINE(E1,E2)-(E3,E4),PRESET:ELSE IF E$="LB" THEN HLINE(E1,E2)-(E3,E4),PRESET,B:ELSE IF E$="LF" THEN HLINE(E1,E2)-(E3,E4),PRESET,BF
1159 ' Erase Ellipse
1160 IF E$="E" THEN K=HPOINT(E1,E2):HCIRCLE(E1,E2),E3,K,E4,E5,E6
1169 ' Erase Paint
1170 IF E$="P" THEN HPAINT(E1,E2),E3,E4
1179 ' Erase Text
1180 IF E$="T" THEN HCOLOR 0:HPRINT(E1,E2),TS$:HCOLOR Z
1190 IF E$="" THEN GOSUB 1130:HPRINT(0,0),"LAST ENTRY ALREADY ERASED":SOUND 50,5:FOR T=1 TO 500:NEXT:GOSUB 1130,1140:RETURN
1199 ' Disk File Fix
1200 E$="":CLOSE:RENAME NN$ TO "EL.DAT":OPEN "I",#2,"EL.DAT":OPEN "O",#1,NN$
1210 LINE INPUT #2,K$
1220 IF EOF(2)=-1 THEN 1240
1230 PRINT#1,K$:GOTO 1210
1240 CLOSE#2:KILL"EL.DAT"
1250 LN=LN-10:RETURN
1259 ' Erase Draw
1260 IF E$="" THEN 720 ELSE IF E$="M" THEN 1360
1270 T$=LEFT$(T$,(LEN(T$)-(LEN(N$)+1)))
1279 ' Erase Up
1280 IF E$="U" THEN HLINE(H1,V1)-(H1,V1+VAL(N$)),PRESET:V1=V1+VAL(N$):E$="":GOTO 720
1289 ' Erase Down
1290 IF E$="D" THEN HLINE(H1,V1)-(H1,V1-VAL(N$)),PRESET:V1=V1-VAL(N$):E$="":GOTO 720
1299 ' Erase Left
1300 IF E$="L" THEN HLINE(H1,V1)-(H1+VAL(N$),V1),PRESET:H1=H1+VAL(N$):E$="":GOTO 720
1309 ' Erase Right
1310 IF E$="R" THEN HLINE(H1,V1)-(H1-VAL(N$),V1),PRESET:H1=H1-VAL(N$):E$="":GOTO 720
1319 ' Erase 45 Degree
1320 IF E$="E" THEN HLINE(H1,V1)-(H1-VAL(N$),V1+VAL(N$)),PRESET:H1=H1-VAL(N$):V1=V1+VAL(N$):E$="":GOTO 720
1329 ' Erase 135 Degree
1330 IF E$="F" THEN HLINE(H1,V1)-(H1-VAL(N$),V1-VAL(N$)),PRESET:H1=H1-VAL(N$):V1=V1-VAL(N$):E$="":GOTO 720
1339 ' Erase 225 Degree
1340 IF E$="G" THEN HLINE(H1,V1)-(H1+VAL(N$),V1-VAL(N$)),PRESET:H1=H1+VAL(N$):V1=V1-VAL(N$):E$="":GOTO 720
1349 ' Erase 315 Degree
1350 IF E$="H" THEN HLINE(H1,V1)-(H1+VAL(N$),V1+VAL(N$)),PRESET:H1=H1+VAL(N$):V1=V1+VAL(N$):E$="":GOTO 720
1359 ' Erase Move
1360 HLINE(H3,V3)-(H2,V2),PRESET:H1=H3:V1=V3:W$=STR$(H2)+STR$(V2):L=LEN(W$)+2:T$=LEFT$(T$,LEN(T$)-L):E$="":GOTO 720
1367 '
1368 ' * Space Remover & Print Sub. *
1369 '
1370 OPEN "I",#1,NN$:Q=LEN(NN$):Q=Q-4:NN$=LEFT$(NN$,Q)+".BAS":OPEN "O",#2,NN$
1380 IF RS=0 THEN PRINT"PROGRAM LISTING OF ";NN$:PRINT
1390 LINE INPUT#1, L$:L1$=""
1400 P=INSTR(L$," "):L=LEN(L$)
1410 IF INSTR(L$,CHR$(34))>0 AND P>INSTR(L$,CHR$(34)) THEN P=0
1420 IF P=0 THEN 1450
1430 L1$=L1$+LEFT$(L$,P-1):L$=RIGHT$(L$,L-P)
1440 GOTO 1400
1450 L1$=L1$+L$:IF VAL(LEFT$(L1$,4))<>VAL(LEFT$(L2$,4)) AND RS=0 THEN PRINTL2$
1460 L2$=L1$
1470 PRINT#2,L1$
1480 IF EOF(1)<>-1 THEN 1390
1490 IF RS=0 AND F<>0 THEN PRINTL1$
1500 CLOSE#1:CLOSE#2:Q=LEN(NN$)-4:NN$=LEFT$(NN$,Q)+".DAT":KILLNN$
1510 IF RS=1 THEN NN$=LEFT$(NN$,LEN(NN$)-4):NN$=NN$+".BAS":GOTO 1730:ELSE END
1517 '
1518 ' * Palette Setup Subroutine *
1519 '
1520 CLS:PRINT"PALETTE SETUP":PRINT
1530 PRINT:INPUT "NEW PALETTE (Y/N)";P$:IF P$<>"Y" AND P$<>"N" THEN 1530:ELSE IF P$="Y" THEN PRINT:GOTO 1550
1540 OPEN "I",#2,"PALETTE.DBL":FOR X=1 TO 15:INPUT#2,PL(X):NEXT X:CLOSE#2:FOR X=1 TO 15:PALETTE X,PL(X):NEXT X:PALETTE 0,0:GOTO 1610
1550 FOR X=1 TO 15
1560 PRINT"COLOR";X;" Color Code";:INPUT P:IF P>63 THEN 1560
1570 PL(X)=P:NEXT X
1580 FORX=1 TO 15:PALETTE X,PL(X):NEXT X:PALETTE 0,0
1590 CLS:INPUT"SAVE NEW PALETTE? (Y/N)";P$:IF P$<>"Y" THEN 1610
1600 OPEN "O",#2,"PALETTE.DBL":FOR X=1 TO 15:WRITE #2,PL(X):NEXT X:CLOSE #2
1610 FOR X=1 TO 15:Q$=Q$+","+STR$(PL(X)):NEXT X
1620 Q$="100FORX=0TO15:READW:PALETTE X,W:NEXT X:DATA 0"+Q$
1630 PRINT#1,Q$:RETURN
1637 '
1638 ' * Grid Function *
1639 '
1640 FOR X=0 TO 320 STEP 10
1650 FOR Y=0 TO 191 STEP 10
1660 IF G=0 THEN HSET(X,Y,Z)
1670 IF G=1 THEN HRESET(X,Y)
1680 NEXT Y,X
1690 IF G=0 THEN G=1 ELSE G=0
1700 RETURN
1707 '
1708 ' * Help Command *
1709 '
1710 FOR X=1 TO 12:READ H$:HPRINT(1,0),H$:FOR Y=1 TO 400:NEXT Y:GOSUB 1130:NEXT X:RESTORE:RETURN:DATA "COLOR: Press <C>","LINE: Press <L>","ELLIPSE: Press <E>"
1720 DATA"DRAW: Press <D>","PAINT: Press <P>","TEXT: Press <T>","COORDINATES: On/Off Press <CTRL>","GRID: On/Off Press <F2>","ERASE LAST: Press <F1>","REDRAW: Press <ALT>","HELP: Press <?>","QUIT: Press <Q>"
1727 '
1728 ' * Redraw Function & Old File *
1729 '
1730 HCLS0:OPEN "I",#1,NN$
1740 LINE INPUT #1,I$
1750 IF INSTR(I$,"GOTO")<>0 THEN 2170
1760 IF VAL(LEFT$(I$,4))=100 THEN A$=MID$(I$,42):FOR X=0 TO 14:A=LEN(A$):P$=LEFT$(A$,INSTR(A$,",")):P=VAL(P$):PL=LEN(P$):A$=RIGHT$(A$,A-PL):PALETTE X,P:NEXT X:PALETTE 15,VAL(A$):GOTO2170
1770 IF VAL(LEFT$(I$,4))=110 THEN 2170
1780 I=INSTR(I$,"H"):IF I>8 OR I=0 THEN 2170
1789 ' Color
1790 IF MID$(I$,I,3)="HCO" THEN HCOLOR VAL(MID$(I$,I+6,2)):GOTO 2170
1799 ' Ellipse
1800 IF MID$(I$,I,3)<>"HCI" THEN 1830 ELSE X=VAL(MID$(I$,I+8,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):Y=VAL(LEFT$(I$,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):R=VAL(LEFT$(I$,INSTR(I$,",")))
1810 I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):C=VAL(LEFT$(I$,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):HW=VAL(LEFT$(I$,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):S$=LEFT$(I$,INSTR(I$,",")-1):E$=RIGHT$(I$,LEN(I$)-INSTR(I$,","))
1820 HCIRCLE(X,Y),R,C,HW,VAL(S$),VAL(E$):GOTO 2170
1829 ' Text
1830 IF MID$(I$,I,3)="HPR" THEN X=VAL(MID$(I$,I+7,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):Y=VAL(LEFT$(I$,INSTR(I$,","))):I$=RIGHT$(I$,LEN(I$)-INSTR(I$,",")):I$=LEFT$(I$,LEN(I$)-1):I$=RIGHT$(I$,LEN(I$)-1):HPRINT(X,Y),I$:GOTO 2170
1839 ' Draw
1840 IF MID$(I$,I,3)<>"HDR" THEN 2090 ELSE 
1850 L=LEN(I$):I$=RIGHT$(I$,L-(INSTR(I$,"M"))):L=LEN(I$):H=VAL(LEFT$(I$,INSTR(I$,","))):I$=RIGHT$(I$,L-INSTR(I$,",")):L=LEN(I$)
1860 Q1$="":FOR Q=1 TO 3:Q$=MID$(I$,Q,1):IF INSTR("UDLREFGHMC",Q$)=0 THEN Q1$=Q1$+Q$:NEXT Q
1870 IF Q1$<>"" THEN Q$=Q1$:V=VAL(Q$)
1880 I$=RIGHT$(I$,L-LEN(Q$)):IF LEN(I$)=1 THEN 2170
1890 L1=LEN(I$):FORX=1 TO L1:A$=MID$(I$,X,1):IF INSTR("UDLREFGHMC",A$)=0 THEN A1$=A1$+A$:GOTO 1910
1900 W=INSTR("UDLREFGHMC",A$):IF A1$="" THEN NEXT X
1910 IF INSTR("UDLREFGHMC,",MID$(I$,X+1,1))=0 THEN NEXT X
1920 IF LEN(I$)=0 THEN 2170
1930 L=LEN(I$):A=VAL(A1$):I$=RIGHT$(I$,L-(LEN(A1$)+1)):A1$="":L=LEN(I$)
1940 ON W GOSUB 2000,2010,2020,2030,2040,2050,2060,2070,1960,2080
1950 IF LEN(I$)<>1 THEN 1890 ELSE2170 
1959 ' Draw Move
1960 H1=A:I$=RIGHT$(I$,L-1):L=LEN(I$):Z1$="":FOR Z=1 TO 3:Z$=MID$(I$,Z,1):IF INSTR("UDLREFGHMC",Z$)=0 THEN Z1$=Z1$+Z$:NEXT Z
1970 IF Z1$<>"" THEN Z$=Z1$
1980 V1=VAL(Z$):I$=RIGHT$(I$,L-LEN(Z$)):HLINE(H,V)-(H1,V1),PSET:H=H1:V=V1:L=LEN(I$)
1990 RETURN
1999 ' Draw Up
2000 HLINE(H,V)-(H,V-A),PSET:V=V-A:RETURN
2009 ' Draw Down
2010 HLINE(H,V)-(H,V+A),PSET:V=V+A:RETURN
2019 ' Draw Left
2020 HLINE(H,V)-(H-A,V),PSET:H=H-A:RETURN
2029 ' Draw Right
2030 HLINE(H,V)-(H+A,V),PSET:H=H+A:RETURN
2039 ' Draw 45 Degree
2040 HLINE(H,V)-(H+A,V-A),PSET:H=H+A:V=V-A:RETURN
2049 ' Draw 135 Degree
2050 HLINE(H,V)-(H+A,V+A),PSET:H=H+A:V=V+A:RETURN
2059 ' Draw 225 Degree
2060 HLINE(H,V)-(H-A,V+A),PSET:H=H-A:V=V+A:RETURN
2069 ' Draw 315 Degree
2070 HLINE(H,V)-(H-A,V-A),PSET:H=H-A:V=V-A:RETURN
2079 ' Draw Color
2080 HCOLOR A:RETURN
2089 ' Paint
2090 IF MID$(I$,I,3)<>"HPA" THEN 2120 ELSE I$=RIGHT$(I$,LEN(I$)-6-I):H$=LEFT$(I$,INSTR(I$,",")):I$=RIGHT$(I$,LEN(I$)-LEN(H$)):H=VAL(H$)
2100 V$=LEFT$(I$,INSTR(I$,",")):I$=RIGHT$(I$,LEN(I$)-LEN(V$)):V=VAL(V$)
2110 K$=LEFT$(I$,INSTR(I$,",")):K=VAL(K$):I$=RIGHT$(I$,LEN(I$)-LEN(K$)):E=VAL(I$):HPAINT(H,V),K,E:GOTO2170
2119 ' Line
2120 IF MID$(I$,I,3)<>"HLI" THEN 2170 ELSE I$=RIGHT$(I$,LEN(I$)-5-I):H1$=LEFT$(I$,INSTR(I$,",")):I$=RIGHT$(I$,LEN(I$)-LEN(H1$)):H1=VAL(H1$)
2130 V1$=LEFT$(I$,INSTR(I$,"-")):I$=RIGHT$(I$,LEN(I$)-LEN(V1$)):V1=VAL(V1$):H$=LEFT$(I$,INSTR(I$,",")):H$=RIGHT$(H$,LEN(H$)-1):I$=RIGHT$(I$,LEN(I$)-LEN(H$)-1):V$=(LEFT$(I$,LEN(I$)-6)):H=VAL(H$):V=VAL(V$)
2140 IF INSTR(I$,"BF")<>0 THEN HLINE(H1,V1)-(H,V),PSET,BF:GOTO 2170
2150 IF INSTR(I$,"B")<>0 THEN HLINE(H1,V1)-(H,V),PSET,B:GOTO 2170
2160 HLINE(H1,V1)-(H,V),PSET:GOTO 2170
2170 IF EOF(1)<>-1 THEN 1740
2179 ' Disk File Fix
2180 CLOSE #1:OPEN "I",#2,NN$:Q=LEN(NN$)-4:NN$=LEFT$(NN$,Q)+".DAT":OPEN "O",#1,NN$
2190 LINE INPUT#2,L$:PRINT#1,L$
2200 IF EOF(2)<>-1 THEN 2190
2210 LN=VAL(LEFT$(L$,4)):CLOSE #2
2220 RETURN
2227 '
2228 ' * Title Page *
2229 '
2230 HSCREEN2:PALETTE 0,0:HCLS0:HCOLOR 8
2240 HLINE(0,0)-(159,191),PSET:HLINE-(319,0),PSET
2250 HCOLOR2:HLINE(319,85)-(159,191),PSET:HLINE-(0,85),PSET
2260 HCOLOR3:HLINE(0,152)-(159,191),PSET:HLINE-(319,152),PSET
2270 HCOLOR3:HPRINT(10,5),"GRAPHICS PROGRAMMER"
2280 HCOLOR2:HPRINT(19,7),"by"
2290 HCOLOR8:HPRINT(11,9),"Michael J Vandall"
2300 FOR T=1 TO 10:FOR X=1 TO 75:NEXT:PALETTE 2,9:PALETTE 3,36:PALETTE 8,18:FOR X=1 TO 75:NEXT:PALETTE 2,18:PALETTE 3,9:PALETTE 8,36:FOR X=1 TO 75:NEXT:PALETTE 2,36:PALETTE 3,18:PALETTE 8,9:NEXT
2310 RETURN
