0 'NEAT LABELS
1 'WRITTEN BY KEIRAN KENNY
2 'COPYRIGHT (C) JANUARY 1991
3 'BY FALSOFT, INC.
10 CLS:DL=1200
20 TB=20 'TAB(20)
30 POKE150,18 '2400 baud.
40 PRINT@64,"DO YOU WANT TO:":PRINT
50 PRINTTAB(2)"1. PRINT ADDRESSES IN DATA?":PRINT
60 PRINTTAB(2)"2. TYPE AND PRINT ONE-OFF",TAB(5)"ADDRESSES?":PRINT
70 PRINTTAB(2)"3. END PROGRAM?":PRINT
80 PRINTTAB(8)"PRESS 1, 2 OR 3."
90 K$=INKEY$:IFK$=""THEN90
100 IFK$="1"THENPOKE282,255:GOTO140
110 IFK$="2"THENPOKE282,0:GOTO140
120 IFK$="3"THENPOKE282,255:CLS:END
130 GOTO90
140 PRINT:PRINT"PRINTER READY? - PRESS ANY KEY.":EXEC44539
150 PRINT#-2,CHR$(27);CHR$(18);CHR$(27);CHR$(31); 'NLQ Bold
160 IFK$="2"THENCLS:PRINT@32,"TYPE ADDRESS:":PRINT@64:GOTO250
170 N=N+1:READN$(N):IFN$(N)="ZZ"THENCLS:RESTORE:N=0:PRINT"DATA END.":GOTO40ELSEIFN$(N)="XX"THENN=N-1:CLS:GOTO180ELSE170
180 PRINT@128
190 FORT=1TON
200 PRINTN$(T)
210 NEXT
220 PRINT:PRINTTAB(3)"PRESS SPACEBAR TO PRINT."
230 FORD=1TODL:NEXT:IFINKEY$=CHR$(32)THEN290ELSEN=0:CLS:GOTO170
240 PRINT:PRINTTAB(3)"PRINT? Y/N"
250 FORN=1TO7
260 PRINT"LINE"N;CHR$(8)":";:LINEINPUTN$(N)
270 IFN=7THEN290ELSEIFN$(N)=CHR$(95)THENN=N-1:GOTO290
280 NEXT
290 TM=INT((9-N)/2)
300 FORT=1TON
310 IFLEN(N$(T))>Z THEN320ELSE330
320 Z=LEN(N$(T))
330 NEXT
340 FORD=1TOTM:PRINT#-2:NEXT
350 FORT=1TON
360 PRINT#-2,TAB(TB-(Z/2))N$(T)
370 NEXT
380 FORD=1TO9-N-TM:PRINT#-2:NEXT
390 Z=0:N=0:IFK$="1"THEN170ELSE40
1000 DATA The Tandy Corporation,300 One Tandy Center,Fort Worth TX,U.S.A. 76102,XX
1010 DATA The Submissions Editor,RAINBOW,P.O. Box 385,Prospect KY,U.S.A. 40059,XX
1020 DATA MR. William P. Nutt,XX
1030 DATA Keiran Kenny,2/45 Cremorne Road,Cremorne NSW,Australia 2090,XX
2000 DATA ZZ
