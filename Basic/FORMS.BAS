1 'FORMS
2 'BY JOHN MUSUMECI
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR7500:WIDTH40:CLS5
20 PRINT"DO YOU WANT INSTRUCTIONS?     PRESS (Y)       IF NOT,  PRESS ANOTHER KEY"
30 ONBRKGOTO1110
40 I$=INKEY$:IF I$="Y" THEN 1200 ELSE IF I$="" THEN 40 ELSE 
50 CLS:PRINT"ADJUST PRINTER-PAPER PERFORATION    THEN          PRESS <ANY KEY>"
60 DIMA$(101):TT=1
70 FORA=1 TO 101:A$(A)=STRING$(52,32):NEXTA
80 I$=INKEY$:IF I$=""THEN80
90 IF I$=CHR$(3)THEN1110
100 HSCREEN1:HCLS5:HCOLOR2:S=1:C=1:D=26
110 GOTO 980
120 GOSUB1000:GOSUB1010:GOSUB1020:GOSUB1030:GOSUB1040:GOSUB1060:GOSUB1070:GOSUB1080:GOSUB1090
130 X=10:Y=10:Z=1:A=C
140 HDRAW"BM"+STR$(X)+",0;D5"
150 HDRAW"BM0,"+STR$(Y)+"R6"
160 GOTO450
170 I$=INKEY$:IF I$=""THEN170
180 IF I$=CHR$(3) THEN 1110
190 IF I$="A" OR I$="a" THEN GOSUB1010:HPRINT(1,21),"AUTO":GOTO 1120
200 IF I$="P" OR I$="p" THEN HDRAW"BM"+STR$(X)+",0;C5D5C2":HDRAW"BM0,"+STR$(Y)+"C5R6C2":GOTO 910
210 IF I$="R" OR I$="r" THEN B=240:B$="ND2NR3":GOTO 430
220 IF I$="T" OR I$="t" THEN B=243:B$="ND2NL2NR3":GOTO 430
230 IF I$="Y" OR I$="y" THEN B=242:B$="ND2NL2":GOTO 430
240 IF I$="F" OR I$="f" THEN B=244:B$="ND2NU3NR3":GOTO 430
250 IF I$="G" OR I$="g" THEN B=250:B$="ND2NL2NU3NR3":GOTO 430
260 IF I$="H" OR I$="h" THEN B=249:B$="ND2NL2NU3":GOTO 430
270 IF I$="V" OR I$="v" THEN B=246:B$="NU3NR3":GOTO 430
280 IF I$="B" OR I$="b" THEN B=248:B$="NL2NU3NR3":GOTO 430
290 IF I$="N" OR I$="n" THEN B=247:B$="NL2NU3":GOTO 430
300 IF I$="U" OR I$="u" THEN B=241:B$="NL2NR3":GOTO 430
310 IF I$="I" OR I$="i" THEN B=245:B$="ND2NU3":GOTO 430
320 IF I$="S" THEN 620
330 IF I$="E" THEN B$="C5G2U5RD5RU5RD5RU5RD5L3U2C2":B=32:GOTO 430
340 IF I$=CHR$(189) THEN U=0:GOTO 470
350 IF I$=CHR$(9) THEN GOSUB600:Z=Z+1:X=X+6:IF X>316 THEN X=316:Z=52:GOTO 140 ELSE 140
360 IF I$=CHR$(93) THEN GOSUB600:X=316:Z=52:GOTO 140
370 IF I$=CHR$(8) THEN GOSUB600:Z=Z-1:X=X-6:IF X<10 THEN X=10:Z=1:GOTO 140 ELSE 140
380 IF I$=CHR$(21) THEN GOSUB600:X=10:Z=1:GOTO 140
390 IF I$=CHR$(94) THEN GOSUB610:A=A-1:Y=Y-6:IF Y<10 THEN Y=10:A=C:GOTO 140 ELSE 140
400 IF I$=CHR$(95) THEN GOSUB610:A=C:Y=10:GOTO 140
410 IF I$=CHR$(10) THEN GOSUB610:A=A+1:Y=Y+6:IF Y>160 THEN Y=160:A=D:GOTO 140 ELSE 140
420 IF I$=CHR$(91) THEN GOSUB610:A=D:Y=160:GOTO 140
430 HDRAW "BM"+STR$(X)+","+STR$(Y)+B$
440 MID$(A$(A),Z,1)=CHR$(B)
450 GOSUB 1030:GOSUB 1040:HPRINT(15,21),S:HPRINT(26,21),Z:HPRINT(36,21),A
460 GOTO 170
470 GOSUB 1010:HPRINT(1,21),"TEXT"
480 I$=INKEY$:IF I$="" THEN 480
490 IF I$=CHR$(189) OR I$=CHR$(13) THEN GOSUB1050:GOSUB 1010:HPRINT(0,21),"GRAPHIC":GOTO 170
500 IF I$=CHR$(94) OR I$=CHR$(10) THEN 480
510 IF I$=CHR$(9) THEN GOSUB600:Z=Z+1:X=X+6:HDRAW"BM"+STR$(X)+",0;D5":IF X>316 THEN X=316:Z=52:GOTO 590 ELSE 590
520 IFI$=CHR$(8)ANDZ>1THENHDRAW"BM"+STR$(X-6)+","+STR$(Y)+"C5G2U5RD5RU5RD5RU5RD5L3U2C2":GOSUB600:MID$(A$(A),Z-1,1)=CHR$(32):U=U-1:Z=Z-1:X=X-6:HDRAW"BM"+STR$(X)+",0;D5":GOSUB 1100:GOTO 590
530 IF I$=CHR$(8) THEN HDRAW"BM"+STR$(X)+","+STR$(Y)+"C5G2U5RD5RU5RD5RU5RD5L3U2C2":HDRAW"BM4,0;C5D5C2":Z=1:X=10:GOTO 600
540 MID$(A$(A),Z,1)=I$:GOSUB600:HPRINT(U,23),I$
550 IF I$<>CHR$(32) THEN HDRAW"BM"+STR$(X)+","+STR$(Y)+"L2E2F2L2NU2"
560 X=X+6:Z=Z+1:IF X>316 THEN X=316:Z=52
570 HDRAW"BM"+STR$(X)+",0;D5"
580 U=U+1:IF U>39 THEN U=39
590 GOSUB 1030:HPRINT(26,21),Z:GOTO480
600 HDRAW"BM"+STR$(X)+",0;C5D5C2":RETURN
610 HDRAW"BM0,"+STR$(Y)+"C5R6C2":RETURN
620 HCLS5
630 IF S=1 THEN C=26:D=51:S=2:GOTO 670
640 IF S=2 THEN C=51:D=76:S=3:GOTO 670
650 IF S=3 THEN C=76:D=101:S=4:GOTO 670
660 IF S=4 THEN C=1:D=26:S=1:GOTO 670
670 XX=10:YY=10
680 IFTT>3THEN690ELSEAA=C:GOTO700
690 FOR AA=C TO D
700 FORZZ=1TO52
710 II$=MID$(A$(AA),ZZ,1):N=ASC(II$)
720 IF N=32 THEN870
730 IF N>32 AND N<128 THEN B$="L2E2F2L2NU2":GOTO860
740 IF N<240 OR N>250 THEN870
750 IF N=240 THEN B$="ND2NR3":GOTO860
760 IF N=243 THEN B$="ND2NL2NR3":GOTO860
770 IF N=242 THEN B$="ND2NL2":GOTO860
780 IF N=244 THEN B$="ND2NU3NR3":GOTO860
790 IF N=250 THEN B$="ND2NL2NU3NR3":GOTO860
800 IF N=249 THEN B$="ND2NL2NU3":GOTO860
810 IF N=246 THEN B$="NU3NR3":GOTO860
820 IF N=248 THEN B$="NL2NU3NR3":GOTO860
830 IF N=247 THEN B$="NL2NU3":GOTO860
840 IF N=241 THEN B$="NL2NR3":GOTO860
850 IF N=245 THEN B$="ND2NU3"
860 HDRAW"BM"+STR$(XX)+","+STR$(YY)+B$
870 XX=XX+6:NEXT ZZ:YY=YY+6:XX=10
880 IF TT<4 THEN 900
890 NEXT AA
900 TT=TT+1:GOTO120
910 POKE150,7:'***BAUD RATE***
920 POKE65496,0:'***SLOW-DOWN POKE***
930 FOR AA=1 TO 7:PRINT#-2:NEXT AA
940 PRINT#-2,CHR$(27);CHR$(28):'***HALF-FORWARD LINE FEED***
950 FOR AA=1 TO 101:PRINT#-2,TAB(14);A$(AA):NEXT AA
960 PRINT#-2:PRINT#-2,CHR$(27);CHR$(54)
970 FOR AA=1 TO 7:PRINT#-2:NEXT AA
980 POKE65497,0:'***HIGH SPEED POKE***
990 GOTO 120
1000 HDRAW"BM0,164;R319DL319":RETURN
1010 HLINE(0,166)-(55,175),PRESET,BF:RETURN
1020 HLINE(126,166)-(136,177),PRESET,BF:RETURN
1030 HLINE(210,166)-(234,177),PRESET,BF:RETURN
1040 HLINE(290,166)-(319,177),PRESET,BF:RETURN
1050 HDRAW"BM0,184;C5R319DL319DR319DL319DR319DL319DR319DL319C2":RETURN
1060 HPRINT(0,21),"GRAPHIC":RETURN
1070 HPRINT(9,21),"SCREEN":RETURN
1080 HPRINT(20,21),"ACROSS":RETURN
1090 HPRINT(32,21),"DOWN":RETURN
1100 HDRAW"BM"+STR$(U*8)+",191;C5U8RD8RU8RD8RU8RD8C2":RETURN
1110 POKE65496,0:WIDTH 32:END
1120 IF MID$(A$(A),Z,1)<>CHR$(32) THEN GOSUB1010:GOSUB1060:GOTO170
1130 I$=INKEY$:IF I$="" THEN 1130
1140 IF I$="A" OR I$="a" THEN GOSUB 1010:GOSUB1060:GOTO170
1150 IF I$=CHR$(9) AND X<>316 THEN HDRAW"BM"+STR$(X)+","+STR$(Y)+"NL2NR3BR6":MID$(A$(A),Z,1)=CHR$(241):GOSUB600:Z=Z+1:X=X+6:HDRAW"BM"+STR$(X)+",0;D5":GOTO 1180 ELSE IF I$=CHR$(9) THEN GOSUB 1010:GOSUB1060:GOTO170
1160 IF I$=CHR$(10) THEN HDRAW"BM"+STR$(X)+","+STR$(Y)+"ND2NU3BD6":MID$(A$(A),Z,1)=CHR$(245):HDRAW"BM0,"+STR$(Y)+"C5R6C2":A=A+1:Y=Y+6:GOTO1190
1170 GOTO1130
1180 IF MID$(A$(A),Z,1)<>CHR$(32) OR X=316 THEN GOSUB 1010:GOSUB 1060:GOTO 170 ELSE 1150
1190 IF MID$(A$(A),Z,1)<>CHR$(32) OR A=D THEN GOSUB 1010:GOSUB1060:Y=10:A=C:GOTO150 ELSE1160
1200 CLS:LOCATE11,0:PRINT"GRAPHIC MODE":PRINT
1210 PRINT"PRESS":LOCATE25,2:PRINT"FUNCTION"
1220 PRINT:PRINT"ARROWS":LOCATE15,4:PRINT"MOVE AROUND SCREEN"
1230 PRINT"SHIFT/ARROWS":LOCATE15,5:PRINT"MOVE TO EDGES"
1240 PRINT"E":LOCATE15,6:PRINT"TO ERASE"
1250 PRINT"P":LOCATE15,7:PRINT"TO PRINTER"
1260 PRINT"S":LOCATE15,8:PRINT"GOTO NEXT SCREEN"
1270 PRINT"CTRL":LOCATE15,9:PRINT"ENTER TEXT MODE"
1280 PRINT"A":LOCATE15,10:PRINT"ENTER AUTO MODE"
1290 PRINT:PRINT"R,Y,V,N":LOCATE15,12:PRINT"DRAW CORNERS"
1300 PRINT"T,F,H,B":LOCATE15,13:PRINT"DRAW T's"
1310 PRINT"G":LOCATE15,14:PRINT"DRAW CROSS"
1320 PRINT"I":LOCATE15,15:PRINT"DRAW VERT. LINE"
1330 PRINT"U":LOCATE15,16:PRINT"DRAW HORIZ. LINE"
1340 PRINT:PRINT"DO NOT DRAW A GRAPHIC OR CHARACTER OVER ANOTHER GRAPHIC OR CHARACTER WITHOUT    ERASING FIRST."
1350 LOCATE12,22:PRINT"PRESS ANY KEY"
1360 I$=INKEY$:IF I$="" THEN 1360
1370 HSCREEN1:HCLS5:HCOLOR2
1380 HPRINT(8,1),"DRAWING IN GRAPHIC MODE"
1390 H=83:V=44:FOR X=1 TO 5:HCIRCLE(H,V),10:H=H+40:NEXT X
1400 H=92:V=91:FOR X=1 TO 3:HCIRCLE(H,V),10:H=H+40:NEXT X
1410 H=100:V=147:FOR X=1 TO 3:HCIRCLE(H,V),10:H=H+40:NEXT X
1420 HPRINT(10,5),"R":HPRINT(15,5),"T":HPRINT(20,5),"Y":HPRINT(25,5),"U":HPRINT(30,5),"I"
1430 HPRINT(11,11),"F":HPRINT(16,11),"G":HPRINT(21,11),"H"
1440 HPRINT(12,18),"V":HPRINT(17,18),"B":HPRINT(22,18),"N"
1450 HPRINT(12,21),"(KEYBOARD)":HPRINT(9,23),"PRESS ANY KEY TO CONT."
1460 HPRINT(0,3),"draws ->":HDRAW"BM80,30;U6R6BR37ND6NL3R3BR40NL6LD6BU3BR35R6BR37NU3D3"
1470 HPRINT(0,9),"draws ->":HDRAW"BM92,75;NU3ND3NR6BR40NL3NU3NR3ND3BR40NL3NU3ND3"
1480 HPRINT(0,16),"draws ->":HDRAW"BM100,133;NU6R6BL3BR37NL3NR3NU6BR37R6U6"
1490 I$=INKEY$:IF I$="" THEN 1490
1500 WIDTH40
1510 LOCATE13,0:PRINT"TEXT MODE"
1520 PRINT:PRINT"ENTER TEXT AS YOU NEED TO, AT ANY TIME.":PRINT"BACK-SPACE, TO CORRECT."
1530 PRINT"WHEN DONE, PRESS <ENTER> OR <CTRL>."
1540 PRINT:PRINT"DO NOT ENTER TEXT DIRECTLY BELOW OTHER  TEXT WITHOUT SKIPPING A LINE BECAUSE THEPRINTER IS IN HALF-FORWARD LINE FEED AND WILL PRINT OVER PART OF CHARACTERS."
1550 PRINT"GRAPHICS CAN BE ENTERED BETWEEN TEXT    WITHOUT PROBLEMS."
1560 LOCATE8,21:PRINT"PRESS ANY KEY TO CONT."
1570 I$=INKEY$:IF I$="" THEN 1570
1580 CLS:LOCATE13,0:PRINT"AUTO MODE"
1590 PRINT:PRINT"PRESS (A) TO RETURN, BEFORE USING, ELSE:"
1600 PRINT:PRINT"PRESSING RIGHT/ARROW KEY WILL DRAW A    HORIZ. LINE FROM THAT POINT TO NEXT TO  LAST COLUMN."
1610 PRINT:PRINT"PRESSING DOWN/ARROW KEY WILL DRAW A     VERT. LINE FROM THAT POINT, DOWN THE    SCREEN, STOPPING ROW BEFORE LAST."
1620 PRINT:PRINT"AUTO WILL NOT OPERATE IF NOT STARTED    FROM A BLANK SPACE. IT WILL STOP DRAWING WHEN IT DOES NOT ENCOUNTER A BLANK     SPACE."
1630 LOCATE12,21:PRINT"PRESS ANY KEY"
1640 I$=INKEY$:IF I$="" THEN 1640
1650 GOTO 50
