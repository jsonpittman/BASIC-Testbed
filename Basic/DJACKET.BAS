1 'DISK-JACKET PRINTER
2 'BY RON DAHLKE
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR 5000
20 DIM F0$(75),D0$(11)
30 POKE 150,18    ' 1200 BAUD
40 WIDTH32:PALETTE 12,63:PALETTE 13,0:CLS:PRINT
50 PRINT"      PUT DISK IN DRIVE 0 "
60 PRINT"LINE UP PAPER AND PRESS ANY KEY"
70 EXEC 44539:PRINT#-2:GOSUB 560
80 CLS:PRINT"           WORKING"
90 GOSUB 450:FOR X=1 TO 3:GOSUB 460:GOSUB 470:NEXT X
100 GOSUB 460
110 GOSUB 510:PRINT#-2,TAB(28);"Free Space :";FREE(0);:GOSUB 560
120 GOSUB 470
130 GOSUB 460:GOSUB 470
140 GOSUB 350:GOSUB 220
150 PRINT#-2,"+     ";CHR$(124);STRING$(56,32);CHR$(124);"     +"
160 PRINT#-2,"  +   ";CHR$(124);STRING$(56,32);CHR$(124);"   +  "
170 PRINT#-2,"    + ";CHR$(124);STRING$(56,32);CHR$(124);" +    "
180 GOSUB 500
190 FOR X=1 TO 27:GOSUB 490:NEXT X
200 GOSUB 500
210 GOTO 40
220 N=0:R=1
230 FOR L=1 TO 15
240 GOSUB 460
250 IF R>68 THEN 340
260 IF LEFT$(F0$(R),1)=CHR$(255) THEN R=R+1:GOTO 250
270 IF LEFT$(F0$(R),1)=CHR$(0) THEN R=R+1:GOTO 250
280 IF N=0 THEN PRINT#-2,TAB(13);
290 IF N=1 THEN PRINT#-2,TAB(29);
300 IF N=2 THEN PRINT#-2,TAB(45);
310 GOSUB 510:PRINT#-2,F0$(R);:GOSUB 560
320 R=R+1:N=N+1
330 IF N<3 THEN 250 ELSE N=0:GOTO 340
340 GOSUB 470:NEXT L:RETURN
350 Y=1:FOR X=3 TO 11
360 DSKI$0,17,X,A$,B$
370 D0$(X)=A$+LEFT$(B$,127)
380 F0$(Y)=LEFT$(D0$(X),8)+"."+MID$(D0$(X),9,3)
390 Y=Y+1
400 FOR N=1 TO 7
410 F0$(Y)=MID$(D0$(X),N*32+1,8)+"."+MID$(D0$(X),9+N*32,3)
420 Y=Y+1
430 NEXT N,X
440 RETURN
450 PRINT#-2,STRING$(70,95):RETURN
460 PRINT#-2,CHR$(124);STRING$(5,32);CHR$(124);:RETURN
470 PRINT#-2,TAB(63);CHR$(124);STRING$(5,32);CHR$(124):RETURN
480 PRINT#-2,TAB(7);CHR$(124);STRING$(56,32);CHR$(124):RETURN
490 PRINT#-2,STRING$(6,32);CHR$(124);STRING$(56,32);CHR$(124):RETURN
500 PRINT#-2,STRING$(6,32);CHR$(124);STRING$(56,95);CHR$(124):RETURN
510 A=PEEK(&H9C)
520 PRINT#-2,CHR$(27);CHR$(18);           ' NLQ 10 CPI
530 PRINT#-2,CHR$(27);CHR$(66);CHR$(1);   ' START ITALICS
540 PRINT#-2,CHR$(27);CHR$(31);           ' START BOLD
550 POKE &H9C,A:RETURN
560 A=PEEK(&H9C)
570 PRINT#-2,CHR$(27);CHR$(19);           ' STANDARD 10 CPI
580 PRINT#-2,CHR$(27);CHR$(66);CHR$(0);   ' END ITALICS
590 PRINT#-2,CHR$(27);CHR$(32);           ' END BOLD
600 POKE&H9C,A:RETURN
