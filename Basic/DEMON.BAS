1 'THE DEMON'S EYE
2 'WRITTEN BY JON DUECK
3 'COPYRIGHT (C) NOVEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 REM THE DEMON'S EYE
20 REM  AN ADVENTURE
30 REM       BY
40 REM   JON DUECK
50 REM **INITIALIZATION**
60 V=22:W=22:G=16
70 DIM D$(63),R$(63),O$(W),V$(V),L(G),C(G),F(W)
80 GOSUB 1820:RM=0 'READ IN DATA
90 CLS
100 PRINT "THE DEMON'S EYE"
110 PRINT"--------------"
120 REM **DESCRIPTION AND FEEDBACK**
130 PRINT
140 PRINT "LOCATION:";
150 PRINT D$(RM)
160 PRINT "EXITS:";
170 FOR I=1 TO LEN(R$(RM))
180 :     PRINT MID$(R$(RM),I,1);",";
190 NEXT I
200 PRINT
210 FOR I=1 TO G
220 :     IF L(I)=RM AND F(I)<>1 THEN PRINT "I SEE ";O$(I);" HERE"
230 NEXT I
240 PRINT M$:PRINT
250 M$="WHAT?"
260 REM **INPUT**
270 PRINT "WHAT NOW";
280 INPUT Q$
290 V$=""
300 W$=""
310 FOR I=1 TO LEN(Q$)
320 :      IF MID$(Q$,I,1)=" " AND V$="" THEN V$=LEFT$(Q$,I-1)
330 :      IF MID$(Q$,I+1,1)<>" " AND V$<>"" THEN W$=MID$(Q$,I+1,LEN(Q$)-1):I=LEN(Q$)
340 NEXT I
350 IF W$="" THEN V$=Q$
360 REM ** ANALYSIS **
370 VB=0
380 FOR I=1 TO V
390 :     IF V$=V$(I) THEN VB=I
400 NEXT I
410 OB=0
420 FOR I=1 TO W
430 :     IF W$=O$(I) THEN OB=I
440 NEXT I
450 IF W$>"" AND OB=0 THEN M$="NO CAN DO!"
460 IF VB=0 THEN VB=V+1
470 IF W$="" THEN M$="I NEED TWO WORDS!"
480 IF VB>V AND OB>0 THEN M$="SORRY--I WON'T "+Q$+"!"
490 IF VB>V AND OB=0 THEN M$="THAT'S NONSENSE!"
500 IF VB<V AND OB>0 AND OB<=G THEN IF C(OB)<>1 THEN M$="I DON'T HAVE "+O$(OB)
510 REM **OVERRIDES**
520 IF (RM>60 AND RM<64 AND F(7)<2) OR (((RM+1)/8=INT((RM+1)/8)) AND F(7)<2) OR (RM>0 AND RM<8 AND F(7)<2) THEN M$="TOO DARK!":GOTO 120
530 IF RM=2 AND C(4)<>1 THEN PRINT "THE PIRATE EYES ME AND SAYS...   'WHERE'S YOUR RING? NO RUBY RING?!? YOU SHALL  NOT PASS!' AND WITH THAT HE REACHES OUT A SURPRISINGLY SUBSTANTIAL HAND AND     SNAPS MY NECK.":GOTO 580
540 IF RM=0 AND C(5)=1 THEN PRINT "I HAVE ESCAPED THE PIRATE'S CAVE WITH THE FABLED DEMON'S EYE RUBY! I GO DOWN IN HISTORY AS ONE OF THE GREATEST TREASURE-HUNTERS EVER! CONGRATULATIONS, YOU MADE ME WIN!":GOTO 580
550 REM **VERBS**
560 ON VB GOSUB 630,660,740,740,740,740,740,740,740,960,960,1050,1140,1170,1210,1270,1140,1340,1370,1520,1680,1730
570 GOTO 120
580 REM END ROUTINE
590 GOSUB 1730
600 PRINT M$
610 PRINT"GAME OVER."
620 END
630 REM VERB 1--HELP
640 M$="HEY! YOU GOT ME INTO THIS, YOU GET ME OUT!"
650 RETURN
660 REM VERB 2--INVENTORY
670 PRINT "I AM CARRYING:"
680 FOR I=1 TO G
690 :    IF C(I)>0 THEN PRINT O$(I);",";
700 NEXT I
710 M$=""
720 PRINT
730 RETURN
740 REM VERBS 3-9--GO,N,S,W,E,U,D
750 D=0
760 IF OB=0 THEN D=VB-3
770 IF OB>16 THEN D=OB-16
780 IF RM=18 AND D=6 THEN D=2
790 IF RM=26 AND D=5 THEN D=1
800 R$(18)="SW":R$(26)="NS"
810 F(W)=0:RL=LEN(R$(RM))
820 FOR I=1 TO RL
830 :    U$=MID$(R$(RM),I,1)
840 :    IF (U$="N" AND D=1 AND F(W)=0) THEN RM=RM-8:F(W)=1
850 :    IF (U$="S" AND D=2 AND F(W)=0) THEN RM=RM+8:F(W)=1
860 :    IF (U$="W" AND D=3 AND F(W)=0) THEN RM=RM-1:F(W)=1
870 :    IF (U$="E" AND D=4 AND F(W)=0) THEN RM=RM+1:F(W)=1
880 NEXT I
890 M$="OK":R$(18)="WD":R$(26)="SU"
900 IF F(W)=0 THEN M$="CAN'T GO THAT DIRECTION!"
910 IF D<1 THEN M$="GO WHERE?"
920 IF RM=32 AND D=2 THEN M$="THE CAVERN WALL BEHIND YOU SOLIDIFIES WITH A MYSTERIOUS CHUCKLE!"
930 IF RM=61 AND D=4 THEN M$="THE BOULDER ROLLS BACK IN PLACE!":F(17)=0
940 IF RM=2 THEN M$="THE PIRATE EYES ME AND SAYS... 'YOU ARE A WISE SCOUNDREL, FOR WITH A RUBY RING  YOU CAN SAFELY PASS ME STING AND GET THE REAL RUBY.'"
950 RETURN
960 REM VERBS 10-11--GET,TAKE
970 IF OB=0 THEN RETURN
980 IF C(OB)=1 THEN M$="I HAVE IT ALREADY!":RETURN
990 IF OB>16 THEN RETURN
1000 IF OB>9 THEN M$="I CAN'T TAKE THAT!":RETURN
1010 IF L(OB)<>RM OR F(OB)=1 THEN M$="I DON'T SEE A "+O$(OB)+" HERE!":RETURN
1020 IF OB=2 AND RM=36 AND F(8)<2 AND L(8)<>36 THEN PRINT "A SMALL SQUARE OF ROCK WHERE THE BAG RESTED POPS UP AND THE CIELING CAVES IN    WITH A RESOUNDING CRASH. ONE OF THE STALACTITES IMPALES ME.":GOTO 580
1030 C(OB)=1:L(OB)=65:M$="I'VE GOT IT."
1040 RETURN
1050 REM VERB 12--EXAMINE
1060 M$="NOTHING SPECIAL FOR A "+W$
1070 IF RM=36 AND OB=2 THEN M$="I SEE A SMALL SQUARE TRACING IN THE STONE AROUND THE COINS."
1080 IF RM=60 AND OB=11 THEN M$="IT LOOKS LIKE I MIGHT BE ABLE TO MOVE IT."
1090 IF RM=50 AND OB=12 THEN M$="I DISCOVER A BEAUTIFUL PEARL NECKLACE HIDDEN WITHIN THE RUBBLE!":F(1)=0
1100 IF RM=37 AND OB=13 THEN M$="ONE BEARS THE INSCRIPTION 'LIKE A ROLLING STONE'.  FUNNY BOB WAS AROUND IN THE  1600'S, EH?"
1110 IF RM=34 AND OB=14 THEN M$="ON ONE OF THE BONY FINGERS ON ITS TIGHLY CLENCHED HANDS I SEE THE BLOODY GLINT  OF A RUBY RING!":F(4)=0
1120 IF RM=1 AND OB=16 THEN M$="I GASP AS I NOTICE ONE OF THE EYES ON THE DEMON WATCHING ME--IT'S A RUBY THE    SIZE OF MY FIST! THIS IS THE DEMON'S EYE!":F(5)=0
1130 RETURN
1140 REM VERB 13 & 17--OPEN & UNLOCK
1150 IF RM=30 AND F(16)=0 AND C(9)=1 THEN M$="THE KEY TURNS RELUCTANTLY IN THE RUSTY LOCK AND THE DOOR SWINGS OPEN.":F(16)=2:R$(30)="SW"
1160 RETURN
1170 REM VERB 14--LIGHT
1180 IF C(6)=0 THEN M$="NOTHING TO LIGHT IT WITH."
1190 IF OB=7 AND C(7)=1 AND C(6)=1 THEN M$="THE ANCIENT BRASS LANTERN LIGHTS SPUTTERINGLY.":F(7)=2
1200 RETURN
1210 REM VERB 15--PUT
1220 IF OB<>8 THEN GOTO 1270
1230 IF C(8)=0 THEN M$="I DON'T HAVE IT.":RETURN
1240 M$="IT FALLS OVER AND I PICK IT UP AGAIN."
1250 IF RM=36 THEN M$="IT FITS UNDER ONE OF THE STALACTITES AND FORMS A PILLAR SUPPORTING THE CIELING.":F(8)=2:C(8)=0:L(8)=36
1260 RETURN
1270 REM VERB 16--DROP
1280 IF C(OB)=0 THEN M$="I DON'T HAVE IT.":RETURN
1290 M$="DONE."
1300 IF OB=7 THEN M$="AS I DROP IT, THE LIGHT SNUFFS OUT.":F(7)=0
1310 C(OB)=0
1320 L(OB)=RM
1330 RETURN
1340 REM VERB 18--ROLL
1350 IF RM=60 AND OB=11 AND F(11)=0 THEN M$="MY MUSCLES STRAIN TO THEIR LIMIT, BUT WITH A FINAL GROAN I MANAGE TO ROLL THE   BOULDER AWAY TO REVEAL A ROUND BLACK HOLE.":R$(60)="WE":F(11)=2
1360 RETURN
1370 REM VERB 19--SAVE
1380 OPEN "O",#1,"DEMON.SAV"
1390 PRINT #1,RM
1400 FOR I=1 TO G
1410 :     PRINT #1,L(I),C(I)
1420 NEXT I
1430 FOR I=1 TO W
1440 :     PRINT #1,F(I)
1450 NEXT I
1460 FOR I=0 TO 63
1470 :     PRINT #1,R$(I)
1480 NEXT I
1490 CLOSE
1500 M$="SAVED."
1510 RETURN
1520 REM VERB 20--LOAD
1530 OPEN "I",#1,"DEMON.SAV"
1540 INPUT #1,RM
1550 FOR I=1 TO G
1560 :    INPUT #1,L(I),C(I)
1570 NEXT I
1580 FOR I=1 TO W
1590 :    INPUT #1,F(I)
1600 NEXT I
1610 FOR I=0 TO 63
1620 :    INPUT #1,R$(I)
1630 NEXT I
1640 CLOSE
1650 M$="HERE I AM AGAIN!"
1660 RETURN
1670 RETURN
1680 REM VERB 21--QUIT
1690 INPUT "ARE YOU SURE YOU WANT TO QUIT";Q$
1700 IF Q$="Y" THEN GOTO 580
1710 M$="OK"
1720 RETURN
1730 REM VERB 22--SCORE
1740 SC=0
1750 IF C(1)=1 THEN SC=SC+5
1760 IF C(2)=1 THEN SC=SC+10
1770 IF C(3)=1 THEN SC=SC+2
1780 IF C(4)=1 THEN SC=SC+5
1790 IF C(5)=1 THEN SC=SC+18
1800 M$="YOUR SCORE IS"+STR$(SC)+" OUT OF A POSSIBLE 40."
1810 RETURN
1820 REM **READ DATA SUBROUTINE**
1830 FOR I=0 TO 63
1840 :   READ D$(I)
1850 NEXT I
1860 FOR I=0 TO 63
1870 :   READ R$(I)
1880 NEXT I
1890 FOR I=1 TO W
1900 :   READ O$(I)
1910 NEXT I
1920 FOR I=1 TO V
1930 :   READ V$(I)
1940 NEXT I
1950 FOR I=1 TO G
1960 :   READ L(I)
1970 NEXT I
1980 FOR I=1 TO W
1990 :   READ F(I)
2000 NEXT I
2010 RETURN
2020 REM DESCRIPTIONS DATA
2030 DATA BEACH NEXT TO CLIFFS,ROOM WITH DEMON STATUES,FROZEN ROOM,FREEZING NARROW BLACK CORRIDOR
2040 DATA FREEZING NARROW BLACK CORRIDOR,CHILLY NARROW BLACK CORRIDOR,CHILLY NARROW BLACK CORRIDOR,CHILLY NARROW BLACK CORRIDOR
2050 DATA BEACH,DARK ALCOVE,NARROW PASSAGEWAY,DAMP PASSAGE
2060 DATA ROOM WITH WATER-COVERED FLOOR,DAMP PASSAGE,GLOOMY CORNER,COOL NARROW BLACK CORRIDOR
2070 DATA BEACH COVERED WITH SHELLS AND SEAWEED,HALL SMELLING OF DEATH,DUSTY WORN STAIRS,DAMP PASSAGE
2080 DATA PIT FILLED WITH WATER,ROOM WITH CAVE DRAWINGS,COLD ROOM,COOL NARROW BLACK CORRIDOR
2090 DATA MOUTH OF CAVE,DIM PATH,MAUSOLEUM ENTRANCE,DAMP PASSAGE
2100 DATA DAMP ROOM,ARMORY COVERED WITH WEAPONS,ROOM WITH CRUMBLY DRY MOSS,NARROW BLACK CORRIDOR
2110 DATA CAVERN ENTRANCE,POOL OF LIGHT,MAUSOLEUM,DAMP PASSAGE
2120 DATA LOW ROOM WITH STALACTITES,ROOM WITH DARK STATUES,MOSSY WALLS,NARROW BLACK CORRIDOR
2130 DATA GLOOMY HALL,THIN PATH,THIN PATH,ROUNDED CORRIDOR
2140 DATA SLOPING CORRIDOR,SLOPING CORRIDOR,HIGH CORNER ABOVE WALL,NARROW BLACK CORRIDOR
2150 DATA DEPRESSION IN GROUND,THIN PATH,CORNER STREWN WITH RUBBLE,ROUNDED CORRIDOR
2160 DATA COBWEBBY ROOM,FALLEN PEBBLES,CRUMBLING WALL,NARROW BLACK CORRIDOR
2170 DATA HALL WITH DRIPPING STALACTITES,VAULTED HALL,VAULTED HALL,VAULTED HALL
2180 DATA VAULTED HALL,NARROW BLACK CORRIDOR,NARROW BLACK CORRIDOR,NARROW BLACK CORRIDOR
2190 REM ROUTES DATA
2200 DATA S,WE,WE,WE,WE,WE,WE,WS
2210 DATA NS,SE,WE,SWE,SWE,WE,SW,NS
2220 DATA NS,NSE,WD,NS,,E,NW,NS
2230 DATA NS,NS,SU,NSE,NSW,E,S,NS
2240 DATA S,NS,N,NS,NE,WE,NW,NS
2250 DATA NS,NSE,WE,NSWE,WE,WE,SW,NS
2260 DATA NS,NE,W,NS,E,WE,NW,NS
2270 DATA NE,WE,WE,NWE,W,E,WE,NW
2280 REM OBJECTS DATA
2290 DATA NECKLACE,COINS,GOBLET,RING,RUBY,FLINT,LANTERN,LOG,KEY
2300 DATA DOOR,BOULDER,RUBBLE,STATUES,SKELETON,GHOST PIRATE,STATUE,NORTH,SOUTH
2310 DATA WEST,EAST,UP,DOWN
2320 REM VERBS DATA
2330 DATA HELP,INVENTORY,GO,N,S,W,E,U,D,GET,TAKE,EXAMINE,OPEN,LIGHT,PUT,DROP,UNLOCK,ROLL,SAVE,LOAD,QUIT,SCORE
2340 REM LOCATIONS OF OBJECTS DATA
2350 DATA 50,36,52,34,1,48,29,21,34,30,60,50,37,34,2,1
2360 REM FLAGS DATA
2370 DATA 1,0,0,1,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
