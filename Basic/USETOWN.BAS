0 GOTO63999
1 GOTO3
2 PCLEAR4:GOTO1
3 POKE248,50:POKE249,98:POKE250,28:POKE251,175:POKE252,126:POKE253,173:POKE254,165:POKE410,126:POKE411,0:POKE412,248:POKE113,87:POKE114,2:POKE115,93
200 DEFUSR9=PEEK(27)*256+PEEK(28)-26
202 CLEAR450:DIMA(3),C(3),A$(3),U$(3),D$(3),P$(3),C$(3),FP$(5,1),FP(5),SP$(3,1),SP(3)
204 CR$=CHR$(13):S$="V15T45O5B":FM$="$$######,###"
206 A$(0)="FOREST":A$(1)="FARMLAND":A$(2)="WETLAND":A$(3)="AN URBAN AREA"
208 C$(0)="$150,000":C$(1)="$200,000":C$(2)="$60,000":C$(3)="$300,000":C(0)=150000:C(1)=200000:C(2)=60000:C(3)=300000
210 U$(0)="GRAZING OR PARK AREA.":U$(1)="GRAZING OR GROWING CROPS.":U$(2)="FARMING OR A NATURAL AREA       PARK.":U$(3)="FACTORIES AND STORES."
212 P$(0)="HOUSING DEVELOPMENT":P$(1)="SHOPPING CENTER":P$(2)="PARK":P$(3)="WASTE FACILITY"
220 D=RND(-TIMER)
298 GOTO1000
300 D$=INKEY$:PRINT@482,"PRESS ANY KEY TO CONTINUE...";:D=USR9(1504)
304 IF INKEY$=""THEN304ELSEPLAYS$:CLS:RETURN
308 D$=INKEY$:PRINT@481,"PRESS  Y  FOR YES,  N  FOR NO";:D=USR9(1504)
310 POKE282,255:D$=INKEY$:IFD$=CHR$(95)THENCLS:RUNELSEIFD$<>"Y"ANDD$<>"N"THEN310ELSEPLAYS$:RETURN
314 D$=INKEY$:PRINT@480,"PRESS THE NUMBER OF YOUR CHOICE";:D=USR9(1504)
316 D$=INKEY$:IFD$=CHR$(95)THENCLS:RUNELSEIFD$=""THEN316
318 D=VAL(D$):IFD<1ORD>MX THEN316ELSEPLAYS$:RETURN
320 FORD=1TO600:NEXT:RETURN
340 FORD=1TOLEN(D$):PRINTMID$(D$,D,1);:IFMID$(D$,D,1)>" "THENPLAY"V15T255O2D"
342 FORDE=1TO3:NEXTDE,D:RETURN
1000 CLS:PRINTSTRING$(32,42);:PRINT@72,"WELCOME TO THE":PRINT@137,"USETOWN ANNEX":PRINT@204,"PROJECT":PRINT@256,STRING$(32,42);:PRINT@268,"V. 2.01";:PRINT@289,"COPR. (C) 1985  BY PAUL FRENCH":GOSUB3000
1002 PRINT@387,"WOULD YOU LIKE DIRECTIONS?":GOSUB308:IFD$="Y"GOSUB4000
1005 SCREEN1,0:GOSUB304
1006 PC=0:FF=0:MX=0:FORD=0TO3:D$(D)="":NEXTD:FORD=0TO3:IFA$(D)>""THEND$(MX)=A$(D):MX=MX+1
1008 NEXTD
1010 CLS:PRINT@35,"WHICH AREA WOULD YOU LIKE     TO DEVELOP?":PRINT:FORD=1TOMX:PRINTTAB(6);STR$(D);") ";D$(D-1):NEXTD
1012 GOSUB314:FORDE=0TO3:IFA$(DE)=D$(D-1)THENA=DE:NEXT:ELSENEXT
1016 D$=" TO:  CITY PLANNER"+CR$+" FROM:  CITY ASSESSOR"+CR$+" SUBJECT:  COST OF LAND"+CR$+CR$+"   THE COST OF THE ":IFA<3THEND$=D$+A$(A)ELSED$=D$+RIGHT$(A$(A),10)
1018 GOSUB2030:PRINT:D$=" IS "+C$(A)+".":GOSUB340:GOSUB300
1019 PC=PC+C(A)
1020 CLS:PRINT:PRINT"   YOU SHOULD LOOK AT THE        ZONING CODE (WHICH TELLS HOW    THE LAND MAY BE USED) BEFORE    DECIDING HOW TO DEVELOP ";:IFA<3THENPRINT"THE "ELSEPRINT" "
1022 PRINT" "A$(A)".":GOSUB300
1023 CLS:PRINT"********* ZONING CODE **********":PRINT"   IN USETOWN, ";A$(A):PRINT" MAY BE USED EXCLUSIVELY FOR":PRINT" "U$(A)
1024 PRINT:PRINT"   ALL OTHER USES ARE STRICTLY   AGAINST THE LAW.  A ZONING      VARIANCE (PERMISSION TO USE     THE LAND FOR OTHER PURPOSES)    MUST BE OBTAINED TO USE THE     LAND FOR ANY PURPOSE BESIDES"
1025 PRINT" "U$(A):GOSUB300
1026 MX=0:FORD=0TO3:D$(D)="":NEXTD:FORD=0TO3:IFP$(D)>""THEND$(MX)=P$(D):MX=MX+1
1028 NEXTD
1030 CLS:PRINT@34,"WHAT ARE YOU GOING TO DEVELOP  IN THE ";:IFA=3THENPRINTRIGHT$(A$(A),10);:ELSEPRINTA$(A);
1032 PRINT"?":PRINT:FORD=1TOMX:PRINTTAB(6);STR$(D);") ";D$(D-1):NEXTD
1034 GOSUB314:FORDE=0TO3:IFP$(DE)=D$(D-1)THENP=DE:NEXT:ELSENEXT
1038 IFA=2ANDP=2GOSUB2010
1050 IFA=2ANDP=2ANDO1=2ORA=0ANDP=2ORA=3ANDP=1THEN1100
1052 CLS:PRINT:PRINT"   GETTING A ZONING VARIANCE     WON'T BE EASY.  CHANCES OF      GETTING THE VARIANCE APPROVED   MIGHT BE BETTER IF YOU HIRE A   LAWYER.":PRINT:PRINT"   DO YOU WISH TO HIRE ONE?":GOSUB308
1054 L$=D$:IFL$="N"THEN1070
1056 CLS:PRINT@35,"LAWYERS NEED TIME TO PREPARE  THEIR ARGUMENTS.  YOUR HEARING  HAS BEEN DELAYED TWO WEEKS.":PRINT:GOSUB2014:GOSUB300
1070 CLS:PRINT@36,"PROPOSAL - TO PERMIT A":PRINT"  ZONING VARIANCE SO THE":PRINT"  ";:IFA=3THENPRINTRIGHT$(A$(A),10);ELSEPRINTA$(A);
1072 PRINT" MAY BE DEVELOPED":PRINT"  AS A ";:IFA=2ANDP=2THENPRINT"SPORTS RECREATION AREA."ELSEPRINTP$(P)"."
1074 FORDE=1TO4:GOSUB320:NEXT
1076 DATAWARREN PEASE,MARY LEE,ROBERT BARON,SOL LIGHT,FOREST H. GRUPE
1078 PRINT@194,"BOARD MEMBERS":PRINT@218,"VOTE":PRINT@226,STRING$(28,45):FORDE=258TO386STEP32:READD$:PRINT@DE,D$:NEXT:GOSUB320:RESTORE
1080 V=0:FORDE=282TO410STEP32:GOSUB320:D=RND(5):PRINT@DE,;:IFL$="Y"ANDD>2ORD>3THENV=V+1:PRINT"YES"ELSEPRINT" NO"
1082 NEXT:GOSUB320:PRINT:PRINT"  RESULTS:  VARIANCE ";:IFV<3THENPRINT"FAILS."ELSEPRINT"PASSES."
1083 IFV<3THENFF=1:FP=FP+1
1084 GOSUB300
1086 IFL$="N"THEN1092ELSECLS:PRINT@33,"DEAR CITY PLANNER,":PRINT:PRINT"   OUR BILL FOR LEGAL SERVICES   IN THE USETOWN ANNEX ZONING     HEARING COMES TO";:DE=RND(15)*1000+10000:PRINTUSING"$$##,###.";DE
1088 PRINT:PRINT"   IT HAS BEEN A PLEASURE TO     SERVE YOU.":PC=PC+DE
1090 PRINT:PRINTTAB(13);"WILSON & WILSON":PRINTTAB(16);"(LAWYERS)":GOSUB300
1092 IFFF<>0THEN1700
1100 IFA<>2THEN1110
1102 CLS:PRINT@35,"IT WOULD COST $200,000 TO     FILL AND LEVEL THE WETLAND TO   ENSURE PROPER BUILDING          CONDITIONS.  DO YOU WISH TO     SPEND THE MONEY?":GOSUB308
1104 IFD$="Y"THENPC=PC+200000:OT=OT OR1
1106 CLS:PRINT@35,"FLOOD CONTROL CONSTRUCTION    WOULD COST $300,000.  DO YOU    WISH TO SPEND THE MONEY FOR     FLOOD CONTROL?":GOSUB308
1108 IFD$="Y"THENPC=PC+300000:OT=OT OR2
1110 IFP<>0THEN1130
1112 CLS:PRINT@35,"WHICH TYPE OF HOUSING         WOULD YOU PREFER TO BUILD?":PRINT@131,"1) TWELVE SINGLE FAMILY            HOMES FOR $780,000":PRINT@227,"2) THREE MULTI-FAMILY UNITS        (12 HOMES) FOR $540,000":MX=2:GOSUB314
1114 IFD=2THENPC=PC+540000:OT=OT OR4ELSEPC=PC+780000
1130 IFP<>1THEN1160
1132 CLS:PRINT@35,"YOU CAN BUILD":PRINT:PRINT"   1) A LARGE SHOPPING CENTER":PRINTTAB(6)"FOR $6,000,000 WHICH WILL       PROVIDE MORE JOBS AND":PRINTTAB(6)"INCOME, OR"
1134 PRINT:PRINT"   2) A SMALLER ONE FOR":PRINTTAB(6)"$3,500,000.":MX=2:GOSUB314
1136 IFD=1THENPC=PC+6000000:OT=OT OR8ELSEPC=PC+3500000
1160 IFP<>2THEN1190
1161 IFA<>2THENGOSUB2010
1162 CLS:PRINT@35,"PARK FACILITIES WITH CONVEN-  TIONAL HEATING, COOLING, AND    LIGHTING WILL COST $90,000.":PRINT:PRINT"   SOLAR FACILITIES MIGHT HELP   KEEP UTILITY BILLS DOWN IN THE  FUTURE.  DO YOU WANT TO SPEND"
1164 PRINT" $40,000 FOR SUPPLEMENTAL SOLAR  FACILITIES?":GOSUB308:PC=PC+90000
1166 IFD$="Y"THENPC=PC+40000:OT=OT OR16
1190 IFP<>3THEN1220
1192 CLS:PRINT@35,"YOU CAN BUILD":PRINT:PRINT"   1) A LANDFILL FOR":PRINTTAB(6)"$2,500,000, OR":PRINT:PRINT"   2) A WASTE RECYCLING CENTER        FOR $4,500,000.":MX=2:GOSUB314
1193 IFA<>2ORD<>1THEN1195ELSECLS:PRINT@35,"EVEN WITH THE VARIANCE YOU    WILL NOT BE ALLOWED TO BUILD A  LANDFILL IN THE WETLAND.  DO    YOU WANT TO":PRINT:PRINT"   1) CANCEL THIS DEVELOPMENT,        OR":PRINT:PRINT"   2) BUILD A RECYCLING PLANT?"
1194 MX=2:GOSUB314:OT=OT OR2048:IFD=1THENFF=1:FP=FP+1:GOTO1700
1195 IFD=1THENPC=PC+2500000:OT=OT OR32ELSEPC=PC+4500000
1198 IFOT AND32THEN1220ELSEGOSUB2020:IFD$="Y"THENPC=PC+80000:OT=OT OR512
1220 IFA<>0THEN1350
1222 CLS:PRINT@35,"USING SPECIAL CONSTRUCTION    METHODS WOULD ALLOW YOU TO      SAVE MANY OF THE TREES AT A     COST OF $80,000.  DO YOU WANT   TO SAVE THE TREES?":GOSUB308
1224 IFD$="Y"THENPC=PC+80000:OT=OT OR64
1350 IFP=3THEN1362
1352 CLS:PRINT@35,"PROVISIONS FOR HANDICAPPED    ACCESS WOULD ADD ";:PRINTUSING"$$##,###";(.02*PC):PRINT" TO THE COST.  WILL YOU PROVIDE  HANDICAPPED ACCESS?":GOSUB308
1353 IFD$="Y"THENOT=OT OR128:PC=PC+(.02*PC):GOTO1362ELSEIFRND(10)>8THEN1362
1354 CLS:PRINT@35,"THE STATE COMMISSION ON THE   HANDICAPPED HAS REQUESTED YOU   TO APPEAR WITH YOUR LAWYER SO   THAT THEY CAN EXPLAIN THE       FINER POINTS OF THE STATE       HANDICAPPED ACCESS LAW TO YOU."
1355 GOSUB300:CLS:PRINT@35,"THE FINER POINTS INDICATE     THAT YOU DON'T HAVE A CHOICE.   YOU WILL PROVIDE HANDICAPPED    ACCESS AT A COST OF ";:PRINTUSING"$$##,###.";(.02*PC):PC=PC+(.02*PC):GOSUB300
1356 CLS:PRINT@33,"DEAR CITY PLANNER,":PRINT:PRINT"   OUR BILL FOR LEGAL SERVICES   IN THE HANDICAPPED ACCESS       MATTER COMES TO ";:DE=RND(5)*1000+10000:PRINTUSING"$$#,###.";DE:PRINT:PRINT"   IT HAS BEEN A PLEASURE TO     SERVE YOU.":PC=PC+DE
1357 PRINT:PRINTTAB(13)"WILSON & WILSON":PRINTTAB(16)"(LAWYERS)":GOSUB300
1362 DE=RND(5)*1000+4000:CLS:PRINT@35,"WILL YOU PAY ";:PRINTUSING"$$,###";DE;:PRINT" TO":PRINT" CONSERVE AS MUCH SOIL AS        POSSIBLE DURING CONSTRUCTION?":GOSUB308:IFD$="Y"THENPC=PC+DE:OT=OT OR256
1372 D$=" TO:  CITY PLANNER"+CR$+" FROM:  ACE CONSTRUCTION CO."+CR$+" SUBJECT:  PROJECT CONSTRUCTION"+CR$+CR$+"    CONSTRUCTION HAS BEGUN"+CR$+" IN THE ":GOSUB2050
1383 IFA=0THENDE=4000
1384 IFA=1THENDE=1250
1385 IFA=2AND(OT AND1)THEN1402ELSEIFA=2THENDE=20000
1386 IFA=3AND(OT AND32)THENDE=97000ELSEIFA=3THENDE=48000
1389 CLS:PRINT@35,"CLEARING THE AREA FOR         CONSTRUCTION COSTS ";:PRINTUSING"$$#,###.";DE:PC=PC+DE:GOSUB300
1402 IFA=0ORA=2THENCLS:PRINT@35,"CONSTRUCTION IS DELAYED       WHILE AN ENVIRONMENTAL IMPACT   STUDY IS CONDUCTED.":PRINT:GOSUB2014:GOSUB300ELSEGOTO1422
1404 IFRND(10)>5THEN1422ELSECLS:PRINT@35,"AN ENDANGERED SPECIES WAS     FOUND AND CONSTRUCTION WILL     NOT CONTINUE UNTIL              CONSTRUCTION METHODS ARE        APPROVED WHICH WILL PROTECT     THE ENDANGERED SPECIES.":PRINT:GOSUB2014
1406 GOSUB300:DE=PC*.05:CLS:PRINT@35,"CONSTRUCTION MAY CONTINUE,    BUT PROTECTING THE ENDANGERED   SPECIES INCREASES YOUR COSTS    BY ";:PRINTUSING"$$##,###.";DE:PC=PC+DE:GOSUB300
1422 DU=RND(10)*10:D$="USETOWN WEATHER REPORT"+CR$+CR$:IFDU<60THEND$=D$+" MOSTLY SUNNY.":GOSUB2040:GOTO1432ELSED$=D$+" CLOUDY WITH A"+STR$((DU-55)*2)+"% CHANCE OF"+CR$+" RAIN.":GOSUB2040:D=RND(10)*10
1423 IFD>(DU-55)*2THEND$="USETOWN WEATHER UPDATE"+CR$+CR$+" WE HAVE CLOUDY SKIES - NO"+CR$+" RAIN.":GOSUB2040:GOTO1432
1424 D$="USETOWN WEATHER UPDATE"+CR$+CR$+" WE CURRENTLY HAVE HEAVY RAINS"+CR$+" IN USETOWN...":GOSUB2040:CLS:PRINT@35,"CONSTRUCTION IS HALTED BY     THE RAIN."+CR$:GOSUB2014:GOSUB300
1426 IFOT AND256THEN1428ELSED$=" TO:  CITY PLANNER"+CR$+" FROM:  STATE E.P.A."+CR$+" SUBJECT:  SOIL EROSION"+CR$+CR$+"   YOU ARE FINED $40,000 FOR     FAILING TO PREVENT SOIL         EROSION.":PC=PC+40000:GOSUB2030:GOSUB300
1428 IFA<>2THEN1452ELSEIFOT AND2THEN1432ELSED$="USETOWN WEATHER UPDATE"+CR$+CR$+" RAINS CAUSE FLOODING IN"+CR$+" WETLAND.  CONSTRUCTION PROJECT"+CR$+" DESTROYED.":GOSUB2040:FF=1:FP=FP+1:GOTO1700
1432 IFA<>2THEN1452ELSEIFOT AND1THEN1452ELSEPC=PC*2:PRINT@35,"PROBLEMS ENCOUNTERED WHILE    TRYING TO BUILD ON MARSHY LAND  DOUBLE THE COST OF THIS         PROJECT TO ";:PRINTUSING"$$#####,###.";PC:GOSUB300
1452 CLS:PRINT@35,"CONSTRUCTION IS IN ITS FINAL  STAGES.  WILL YOU LANDSCAPE AT  A COST OF ";:PRINTUSING"$$###,##.";PC*.04:GOSUB308:IFD$="Y"THENPC=PC+PC*.04:OT=OT OR1024
1462 D$=" TO:  CITY PLANNER"+CR$+" FROM:  ACE CONSTRUCTION CO."+CR$+" SUBJECT:  PROJECT CONSTRUCTION"+CR$+CR$+"    CONSTRUCTION IS COMPLETE"+CR$+" IN THE ":GOSUB2050
1472 IFP<3OR(OT AND512)OR(OT AND32)THEN1482ELSECLS:PRINT@35,"THE STATE E.P.A. WILL NOT     ALLOW THE RECYCLING PLANT TO    OPEN UNTIL STACK EMISSIONS      ARE CONTROLLED."+CR$:GOSUB2014:GOSUB300:GOSUB2020:IFD$="N"THEN1472ELSEPC=PC+80000
1482 IFA=2ANDP<>2THENCLS:PRINT@35,"SEEPAGE AND SETTLING HAS      RESULTED IN A PENALTY TO YOU    OF ";:PRINTUSING"$$####,###.";PC*.1:PC=PC+PC*.1:GOSUB300
1492 IFOT AND256THENDE=153:D$="SOIL CONSERVATION":DU$="WORKING TO SAVE SOIL":GOSUB2070
1494 IFOT AND512THENDE=169:D$="CLEAN AIR":DU$="KEEPING OUR AIR CLEAN":GOSUB2070
1496 IFOT AND1024THENDE=185:D$="CITY BEAUTIFUL":DU$="BEAUTIFUL PLANTINGS":GOSUB2070
1498 IFOT AND16THENDE=249:D$="ENERGY EFFICIENCY":DU$="HELPING CONSERVE RESOURCES":GOSUB2070
1700 IFP=1ANDA=2THENBR=BR-2
1703 IFP=1ANDA=0THENBR=BR-1
1706 IFOT AND2048THENBR=BR-2
1709 IFP=3ANDA=3AND(OT AND32)THENBR=BR-2
1710 IFFF>0THENBR=BR-1:GOTO1760
1712 IFP=2ANDA=2ANDO1=2THENBR=BR+5
1715 IFP=3ANDA=1THENBR=BR+5
1718 IFP=0ANDA=0THENBR=BR+5
1721 IFP=1ANDA=3THENBR=BR+5
1724 IFOT AND1THENBR=BR+1
1727 IFOT AND2THENBR=BR+1
1730 IFOT AND4THENBR=BR+1
1733 IFOT AND8THENBR=BR+1
1736 IFOT AND16THENBR=BR+1
1739 IFOT AND32THEN1742ELSEIFP=3THENBR=BR+1
1742 IFOT AND64THENBR=BR+1
1745 IFOT AND128THENBR=BR+1
1748 IFOT AND256THENBR=BR+1
1751 IFOT AND512THENBR=BR+1
1754 IFOT AND1024THENBR=BR+1
1760 IFFF=1THENSCREEN1,0:GOSUB304ELSEGOSUB3100
1770 OT=0:O1=0
1800 IFA=3THEND$=MID$(A$(A),4,5)ELSED$=A$(A)
1801 IFP=0ORP=1THENDU$=LEFT$(P$(P),8)ELSEIFP=3THENDU$=LEFT$(P$(P),6)ELSEDU$=P$(P)
1802 IFFF=1THENFP$(FP-1,0)=D$:FP$(FP-1,1)=DU$:FP(FP-1)=PC:GOTO1806ELSESP=SP+1:SP$(SP-1,0)=D$:SP$(SP-1,1)=DU$:SP(SP-1)=PC:A$(A)="":P$(P)=""
1806 IFFP=0THEN1856
1808 CLS:PRINT@5,"USETOWN PROJECT REPORT":PRINTSTRING$(32,61);:PRINT"INCOMPLETE PROJECTS:":PRINT"region    project     cost"
1810 IC=0:FORD=0TOFP-1:PRINTFP$(D,0);TAB(10)FP$(D,1);TAB(20);:PRINTUSINGFM$;FP(D);:IC=IC+FP(D):NEXT
1812 PRINT@320,STRING$(32,61);:PRINT@352,"TOTAL COST OF":PRINT@384,"INCOMPLETE PROJECTS:";:PRINTUSINGFM$;IC
1814 PRINT@448,"PRESS ANY KEY TO VIEW":D=USR9(1472):PRINT@480,"THE NEXT PAGE OF THIS REPORT...";:D=USR9(1504):GOSUB304
1856 CLS:PRINT@5,"USETOWN PROJECT REPORT":PRINTSTRING$(32,61);:PRINT"COMPLETED PROJECTS:":PRINT"region    project     cost"
1857 IFSP=0THEN1862
1858 RT=0:FORD=0TOSP-1:PRINTSP$(D,0);TAB(10)SP$(D,1);TAB(20);:PRINTUSINGFM$;SP(D);:RT=RT+SP(D):NEXT
1862 PRINT@256,STRING$(20,61):PRINT@288,"INCOMPLETE PROJECTS:";:PRINTUSINGFM$;IC
1864 PRINT@340,STRING$(12,45);:PRINT@361,"TOTAL COST:";:PRINTUSINGFM$;IC+RT
1866 PRINT@396,"BALANCE:";:PRINTUSINGFM$;14000000-(IC+RT)
1867 IFFP=0GOSUB300:GOTO1872
1868 PRINT@448,"PRESS  P  TO VIEW PREVIOUS PAGE,";:D=USR9(1472):PRINT@480,"  ANY OTHER KEY TO CONTINUE...";:D=USR9(1504)
1870 POKE282,255:D$=INKEY$:IFD$=""THEN1870ELSEIFD$=CHR$(95)THENCLS:RUNELSEIFD$="P"THENPLAYS$:GOTO1806ELSEPLAYS$
1872 IF14000000-(IC+RT)<0THEN1920ELSEIFFP=6THEN1930ELSEIFSP<4THEN1006
1910 CLS:PRINT@37,"YOU HAVE DEVELOPED ALL        FOUR AREAS IN THE USETOWN       ANNEX.  WE RATE YOUR JOB        PERFORMANCE:":PRINT:FORDE=1TO3000:NEXT
1911 PRINTTAB(9)"EXCELLENT":PRINTTAB(14)"GOOD":PRINTTAB(11)"AVERAGE":PRINTTAB(14)"FAIR":PRINTTAB(14)"POOR"
1915 GOSUB320:SOUND89,1:PRINT@340,"*";:IFBR>9THENGOSUB320:PRINT@340," ";:SOUND125,1:PRINT@308,"*";:IFBR>18THENGOSUB320:PRINT@308," ";:SOUND147,1:PRINT@276,"*";
1916 IFBR>27THENGOSUB320:PRINT@276," ";:SOUND176,1:PRINT@244,"*";:IFBR>35THENGOSUB320:PRINT@244," ";:SOUND189,1:SOUND193,1:PRINT@212,"*";
1917 IFBR<19THENFORDE=1TO2000:NEXT:PRINT@387,"YOUR SERVICES ARE NO LONGER     REQUIRED.  YOU'RE FIRED."
1918 GOSUB300:GOTO1950
1920 CLS:PRINT@35,"YOU OVERSPENT YOUR BUDGET.    IF YOU HAVE THE GOOD GRACE TO   SEEK OTHER EMPLOYMENT, WE       WON'T HINDER YOUR EFFORTS.      WILL YOU LOOK FOR ANOTHER JOB?":GOSUB308:IFD$="Y"THEN1950ELSE1940
1930 CLS:PRINT@35,"TOO MANY PROJECTS HAVE        FAILED.":PRINT:PRINT"   EVEN THOUGH IT IS NOT ALL     YOUR FAULT, SOMEONE MUST TAKE   RESPONSIBILITY.  DO YOU WISH    TO LOOK FOR A DIFFERENT JOB?":GOSUB308:IFD$="Y"THEN1950
1940 PRINT@357,"SORRY - YOU'RE FIRED!":GOSUB300
1950 CLS:PRINT@259,"DO YOU WANT TO PLAY AGAIN?":GOSUB308:IFD$="Y"THENRUN202ELSE1950
1999 END
2010 CLS:PRINT@66,"WHICH DO YOU PLAN TO BUILD?":PRINT@131,"1. A SPORTS RECREATION AREA":PRINT@163,"2. NATURAL AREA PARK":MX=2:GOSUB314:O1=D:RETURN
2014 DE=RND(30)*1000+10000:PRINT"   DELAYS COST MONEY.  THIS      DELAY COSTS YOU";:PRINTUSING"$$##,###.";DE:PC=PC+DE:RETURN
2020 CLS:PRINT@35,"DO YOU WISH TO CONTROL THE    EMISSIONS FROM THE INCINERATOR  AT A COST OF $80,000?":GOSUB308:RETURN
2030 CLS:PRINT@64,STRING$(32,35):PRINT@352,STRING$(32,35):PRINT@96," ":GOSUB340:RETURN
2040 CLS:PRINT@96,STRING$(32,42):PRINT@320,STRING$(32,42):PRINT@165,"";:GOSUB340:GOSUB300:RETURN
2050 IFA<3THEND$=D$+A$(A)+"."ELSED$=D$+RIGHT$(A$(A),10)+"."
2052 CLS:PRINT@64,STRING$(32,35):PRINT@352,STRING$(32,35):PRINT@96," ":GOSUB340:GOSUB300:RETURN
2070 CLS:PRINTSTRING$(32,DE):FORD=32TO416STEP32:PRINT@D,CHR$(DE):PRINT@D+31,CHR$(DE);:NEXT:PRINT@448,STRING$(32,DE);:PRINT@112-LEN(D$)/2,D$;:PRINT@173,"AWARD";:PRINT@202,STRING$(11,45);:PRINT@292,"TO THE CITY PLANNER FOR";
2071 PRINT@368-LEN(DU$)/2,DU$;:GOSUB300:RETURN
2100 END
3000 DIMG1(4),L$(27),G2(3),G4(3),G3(6)
3005 RQ$="NU5R3BU2U2R1D2BD2R3U4R1D4R2NR4U4R5D4R3NU5E3U6G3L18E3R17U6H1U5L2D5G1D5R1U5R1NU4ND5R1D5"
3007 LQ$="R1E4F1E2NF3U1E2R2E1R2NF2H1U1E1R1F3R1F1R1E1R2F3F2F1D1NF3BD1BL1H1L2G1L1G1BE2BU1U1H1NE1L2G4NL2BE2U1H1L1H1L1G2NF1H1L1G2BH6R1BE3R1BE3BR6R1BF2BR3R1BF4R1"
3010 MQ$="NU7R=QQ;NR7U4R2ND3R2ND3R2ND3R2D4R=QQ;NU7E7U8L=QQ;L8L=QQ;G7R1R=QR;D2NR21U7R2NR19BD2ND3F2E2D3BR2U1NR2U1E1R1F1D2BR2NU3R2BR2NU3R2BR2ND1NU4R2R=QR;E7"
3015 PQ$="NU3R6BU2U2R1D2BD2R2U4R1D4R3NU4E5U3E1L1H1U6L1D5H1U1H1U1G5L1NE5G1L3G5R3E1R1E1R1E1U1E2D1F1D1F3E4"
3018 WI$="BU2U2BU4U2R1D2BD4D2BD2R2":NQ$="NU11R2XWI$;XWI$;U4BU4U2R1D2BD4D4R2XWI$;XWI$;NU11E3U10E1L1H1L2H1L2H1L3G1L2G1L2G1L1G3R1E1R2E1R2E1R3F1R2F1R2F1R1E2"
3020 PMODE3,1:PCLS4:DRAW"BM40,20;C2U2L3U1E3E1D1F4L3":PAINT(40,16),2,2:GET(35,11)-(46,21),G2,G
3021 PUT(3,19)-(14,29),G2,PSET:PUT(27,3)-(38,13),G2,PSET:PUT(47,25)-(58,35),G2,PSET:PUT(17,27)-(28,37),G2,PSET:PUT(51,15)-(62,25),G2,PSET:PUT(55,4)-(66,14),G2,PSET:PUT(74,2)-(85,12),G2,PSET:PUT(87,16)-(98,26),G2,PSET:PUT(41,42)-(52,52),G2,PSET
3022 PUT(23,68)-(34,78),G2,PSET:PUT(33,55)-(44,65),G2,PSET:PUT(67,65)-(78,75),G2,PSET:PUT(83,53)-(94,63),G2,PSET:PUT(100,41)-(111,51),G2,PSET:PUT(3,50)-(14,60),G2,PSET
3025 DRAW"BM11,15;U4L2H3U1E3R1E1F1D1F3G3L1":PAINT(11,9),2,2:GET(7,3)-(16,15),G4,G
3026 PUT(19,12)-(28,24),G4,PSET:PUT(15,41)-(24,53),G4,PSET:PUT(35,29)-(44,41),G4,PSET:PUT(45,2)-(54,14),G4,PSET:PUT(71,12)-(80,23),G4,PSET:PUT(93,4)-(102,15),G4,PSET:PUT(111,16)-(120,27),G4,PSET
3027 PUT(19,55)-(28,67),G4,PSET:PUT(7,67)-(16,78),G4,PSET:PUT(49,61)-(58,72),G4,PSET:PUT(65,39)-(74,50),G4,PSET:PUT(87,29)-(96,40),G4,PSET:PUT(97,66)-(106,77),G4,PSET:PUT(89,124)-(98,135),G4,AND
3030 CIRCLE(165,36),14,3,.4:PAINT(163,35),3,3:CIRCLE(213,44),16,3,.4:PAINT(221,45),3,3:DRAW"C3BM180,36;R4F5R4F2":PMODE4,1
3035 COLOR 0,1:LINE(0,0)-(255,180),PSET,B:LINE(127,0)-(127,180),PSET:LINE(0,90)-(255,90),PSET
3040 L$(1)="U3NR4U2E2F2D5":L$(2)="U7R3F1D1G1NL2F1D2G1L3":L$(3)="BR4BU1G1L2H1U5E1R2F1":L$(4)="U7R3F1D5G1L3":L$(5)="NR4U4NR2U3R4":L$(6)="U4NR3U3R4":L$(7)="BR4BU6H1L2G1D5F1R3U3L1":L$(8)="U7D3R4NU3D4":L$(9)="BR1R2L1U7NL1R1":L$(11)="U7D4NE4R1F3"
3041 L$(12)="NU7R4":L$(13)="U7F2E2D7":L$(14)="U7F4ND3U4":L$(15)="U7R4D7L4":L$(16)="U7R3F1D2G1L2":L$(18)="U7R3F1D2G1L2F3":L$(19)="BU1F1R2E1U2H1L2H1U1E1R2F1":L$(20)="BR2U7L2R4":L$(21)="BU1NU6F1R2E1U6":L$(22)="BU7D5F2E2U5"
3042 L$(23)="NU7E2F2U7":L$(25)="BR2U3H2U2D2F2E2U2":L$(26)="BR1R1":L$(0)="":L$(27)="BR1BU2E3"
3045 LINE(0,180)-(255,192),PSET,BF:COLOR1,0:MS$="PRESS ANY KEY TO CONTINUE...":MX=32:MY$="189":GOSUB3990:COLOR0,1
3050 MS$="FARMLAND":MX=5:MY$="178":GOSUB3990:MS$="URBAN":MX=132:MY$="178":GOSUB3990:MS$="WETLAND":MX=132:MY$="88":GOSUB3990:MS$="FOREST":MX=5:MY$="88":GOSUB3990
3055 DRAW"BM99,126":DRAW"NU4R2BU2U2R1D2BD2R2U4R1D4R2NU4E2U3NG1E1H5G2NG5F4":GET(97,126)-(111,114),G1,G
3056 DRAW"BM78,121":DRAW"NU4R4NR4U4R2BU4U1R1D1BD4R2D4R4NU4E3U3NG2E1H1U2NL1NH3U7H1L2G1D3L1H1NG2L1G4L1G3D1G1D1G1E1U1E1U1E3R1E1R1F1R1F3D1F1D1F1"
3060 DRAW"BM175,150":DRAW"U18E1R1E1F1R1F1R2F1D17L2NL6BU2U2BU4U2BU4U2L1D2BD4D2BD4D2BD1BL3U3BU4U2BU4U2L1D2BD4D2BD4D3BR8E1U17NG1L4":GET(175,130)-(186,150),G3,G:PUT(184,129)-(195,149),G3,PSET:PUT(193,132)-(204,152),G3,PSET
3099 RETURN
3100 CLS:PRINT@261,"ONE MOMENT PLEASE . . .":IFA<>0THEN3140ELSEIFOT AND64THEN3115ELSELINE(1,1)-(126,80),PRESET,BF:GOTO3200
3115 IFP=3AND(OT AND32)GOTO3120ELSEIFP=3ORO1=2THENLINE(76,27)-(114,53),PRESET,BF:GOTO3200
3120 LINE(12,27)-(114,53),PRESET,BF  
3125 IFP=1ORP=3THENLINE(1,50)-(126,80),PRESET,BF
3140 IFA<>1THEN3160ELSELINE(77,103)-(110,135),PRESET,BF
3160 IFA<>3THEN3200ELSELINE(175,129)-(204,152),PRESET,BF
3200 IFA=0THENX=1:Y=1ELSEIFA=1THENX=1:Y=91ELSEIFA=3THENX=128:Y=91
3205 DRAW"BM=X;,=Y;":IFP<>0THEN3300ELSEIFOT AND4THEN3250ELSEIFA=2THEN3225ELSEX=X+10:Y=Y+12:FORD=1TO2:FORD1=0TO5:PUT(X+D1*18,10+Y+15*D)-(X+D1*18+14,10+Y+15*D-12),G1,AND:NEXTD1:NEXTD
3215 IFOT AND1024THENX=X-7:Y=Y+23:FORD=1TO7:PUT(X,Y)-(X+9,Y+12),G4,AND:X=X+18:NEXTD:GOTO3300ELSEGOTO3300
3225 PUT(133,40)-(147,28),G1,AND:PUT(234,48)-(248,36),G1,AND:PUT(144,53)-(158,41),G1,AND:PUT(163,55)-(177,43),G1,AND:PUT(182,58)-(196,46),G1,AND:PUT(201,63)-(215,51),G1,AND:PUT(220,61)-(234,49),G1,AND:PUT(150,29)-(164,17),G1,AND
3226 PUT(169,28)-(183,16),G1,AND:PUT(188,34)-(202,22),G1,AND:PUT(207,33)-(221,21),G1,AND:PUT(226,36)-(240,24),G1,AND
3230 IFOT AND1024THENX=128:Y=7:GOTO3555ELSEGOTO3300
3250 IFA=2THEN3255ELSEDRAW"BM+15,+43;XNQ$;BM+15,+21;XNQ$;BM+19,+9;XNQ$;"
3252 IFOT AND1024THENFORD=0TO3:PUT(X+D*36+3,Y+37)-(X+D*36+12,Y+49),G4,PSET:NEXTD:GOTO3300ELSEGOTO3300
3255 DRAW"BM192,33;XNQ$;BM147,60;XNQ$;BM213,68;XNQ$;"
3260 IFOT AND1024THENX=128:Y=9:GOTO3555
3300 IFP<>1THEN3400
3305 IFOT AND8THENQQ=38:QR=30:DRAW"BM+18,+45;"ELSEQQ=18:QR=10:DRAW"BM+38,+45;"
3310 IFA=2THENDRAW"BM+128,+23;XMQ$;":IFOT AND1024THENX=128:Y=9:GOTO3555ELSE3400
3315 DRAW"XMQ$;"
3320 IFOT AND1024THEN3555
3400 IFP<2ORP>3THEN3700ELSEIFOT AND32THEN3550ELSEIFA=0ORA=1THENDRAW"BM+83,+48"ELSEIFA=2ANDP=2THENDRAW"BM+167,+60;"ELSEIFA=2ANDP=3THENDRAW"BM+215,+30"ELSEIFA=3THENDRAW"BM+46,+70"
3402 IFOT AND32THEN3550
3425 IFP=2THENDRAWPQ$ELSEIFP=3THENDRAWRQ$
3430 IFOT AND1024THENX=PEEK(200):Y=PEEK(202)-2:PUT(X+5,Y)-(X+16,Y+10),G2,PSET:GOTO3700ELSEGOTO3700
3550 DRAW"BM+50,+50;XLQ$;"
3555 IFOT AND1024THENFORD=1TO6:PUT(X+16*D,Y+60)-(X+16*D+11,Y+70),G2,PSET:NEXTD
3700 IFA=0THENMX=47:MY$="88"ELSEIFA=1THENMX=61:MY$="178"ELSEIFA=2THENMX=181:MY$="88":ELSEIFA=3THENMX=167:MY$="178"
3705 IFP=0THENMS$="/HOUSING"ELSEIFP=1THENMS$="/SHOPPING"ELSEIFP=2THENMS$="/PARK"ELSEIFP=3THENMS$="/WASTE"
3710 GOSUB 3990
3900 SCREEN1,0
3901 GOSUB304:X=0:Y=0:RETURN
3990 FORDU=1TOLEN(MS$):CH=ASC(MID$(MS$,DU,1))-64:IFCH=-18THENCH=26ELSEIFCH=-17THENCH=27ELSEIFCH<0ORCH>27THENCH=0
3991 DRAW"BM"+STR$(MX)+","+MY$+";XL$(CH);":MX=MX+7:NEXT DU:RETURN
4000 CLS:PRINT@35,"YOU ARE THE CITY PLANNER      FOR THE CITY OF USETOWN, IOWA.  YOUR CITY HAS RECENTLY ANNEXED, OR ADDED, A NEW PARCEL OF LAND  WHICH INCLUDES FOREST, FARM-    LAND, WETLAND, AND AN URBAN     RENEWAL AREA.":PRINT
4002 PRINT"   YOU HAVE RECEIVED A GOVERN-   MENT GRANT OF $14 MILLION       ($14,000,000) TO DEVELOP THIS   LAND.":GOSUB300
4004 PRINT@35,"YOUR JOB IS TO DEVELOP        HOUSING, A PARK, A SHOPPING     CENTER, AND A SOLID WASTE       DISPOSAL FACILITY.":PRINT
4006 PRINT"   YOU HAVE ONLY $14 MILLION     TO WORK WITH.  THE COST FOR     EACH TYPE OF DEVELOPMENT WILL   DIFFER DEPENDING ON WHERE YOU   WANT TO PUT IT.":GOSUB300
4008 PRINT@35,"FOR EXAMPLE, BUILDING HOUSES  IN THE FOREST MIGHT NOT COST    THE SAME AS BUILDING HOUSES     ON THE FARMLAND.":PRINT:PRINT"   ONCE YOU BEGIN TO DEVELOP     AN AREA THERE IS NO TURNING     BACK.":PRINT
4010 PRINT"   TRY TO STAY WITHIN YOUR       BUDGET.  PLANNERS WHO GO OVER   THEIR BUDGETS OFTEN LOSE THEIR  JOBS.    GOOD LUCK!":GOSUB300:RETURN
63999 PMODE0:PCLEAR1:GOTO2
