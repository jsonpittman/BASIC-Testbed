0 'DESPACE
1 'WRITTEN BY JOHN MUSUMECI
2 'COPYRIGHT (C) DECEMBER 1991
3 'BY FALSOFT, INC.
4 'RAINBOW MAGAZINE
5 '*****************************
10 CLEAR9000:M=200:DIMA$(M),C$(M):CLS
20 PRINT@70,"NAME OF PROGRAM TO BE":PRINT@100,"MODIFIED";:SOUND200,1
30 INPUTD$:D$=D$+"/BAS"
35 PRINTD$
40 CLS:PRINT@73,"L I S T I N G"
50 OPEN"I",#1,D$:A=0
60 IFEOF(1)=-1 THEN100
70 LINEINPUT#1,A$(A):PRINTA$(A)
80 FOR X= 1 TO 250:NEXT X:A=A+1
90 GOTO60
100 CLOSE#1:A=A-1
125 CLS:PRINT"MODIFIED PROGRAM LISTING":PRINT:PRINT
130 FOR I=0 TO A:C$(I)="":S=0
140 L=LEN(A$(I))
150 FOR B=1 TO L
160 B$=MID$(A$(I),B,1)
170 IF B$=CHR$(32) AND S=0 THEN S=1:GOTO190
180 IF B$=CHR$(32) THEN NEXT B
190 C$(I)=C$(I)+B$
195 IF B$=CHR$(34) THEN 510
197 IF B$=CHR$(39) THEN 550
200 NEXT B
205 PRINTC$(I)
210 NEXT I
300 OPEN "O",#1,D$
310 FOR I=0 TO A
320 PRINT#1,C$(I)
330 NEXT I
340 CLOSE #1
500 CLS:PRINT"COMPLETED":SOUND200,1:END
510 B=B+1:FOR BB=B TO L:B$=MID$(A$(I),BB,1):C$(I)=C$(I)+B$
520 IF BB=L THEN B=BB:GOTO205
530 IF B$<>CHR$(34) THEN NEXT BB
540 IF B$=CHR$(34) THEN B=BB:GOTO 200
550 B=B+1:FOR BB=B TO L:B$=MID$(A$(I),BB,1):C$(I)=C$(I)+B$:NEXT BB
560 GOTO 205
