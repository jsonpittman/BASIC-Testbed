0 ' COPYRIGHT 1989  FALSOFT,INC
100 REM FUNSTATS
110 REM SOFTBALL STATS PROGRAM
120 REM DELBERT BAKER COPYRIGHT 1987
130 REM 32K ECB COCO 1
140 REM RSDOS 1.0 OR JDOS 1.11
150 REM
160 CLEAR 5000
170 DIM N$(20), A$(20)
180 DIM C$(20), P$(20)
190 DIM ST$(20), GS$(20)
200 DIM SU$(13), SA$(13)
210 DIM N2$(20), PL$(20)
220 DIM S$(20,13), T$(20,13)
230 DIM S(20,13), T(20,13)
240 DIM U(13)
270 CLS:LINE INPUT"SEASON (YY): ";Y$
280 Y$=RIGHT$(Y$,2):F1$="ROSTER"+Y$
290 GOSUB 6000
300 CLS:PRINT:PRINT@47,"MENU":PRINT
310 PRINT"1.  ENTER A GAME":PRINT
320 PRINT"2.  DISPLAY A GAME OR TOTALS":PRINT
330 PRINT"3.  PRINT A REPORT":PRINT
340 PRINT"4.  REVIEW ROSTER":PRINT
350 PRINT"5.  QUIT":PRINT
360 PRINT@452,"WHICH ONE";:INPUT B
370 IF B<1 OR B>5 THEN 360
380 IF B=5 THEN 410
390 ON B GOSUB 1000,2000,3000,4000
400 GOTO 300
410 CLOSE:END
999 REM NEW GAME DATA ENTRY MODULE
1000 CLS:PRINT:LINE INPUT"GAME NUMBER? ";GN$
1010 PRINT:LINE INPUT"OPPOSING TEAM? ";OT$
1020 PRINT:LINE INPUT"ARE WE THE HOME TEAM?(Y/N) ";HT$
1030 PRINT:LINE INPUT"WHAT DID WE SCORE? ";R1$
1040 PRINT:LINE INPUT"WHAT DID THEY SCORE? ";R2$
1050 PRINT:LINE INPUT"GAME DATE (MM/DD/YY): ";D$
1060 PRINT:F$="GM"+GN$+RIGHT$(D$,2)
1070 OPEN"O",#1,F$:OPEN"D",#2,F1$,128
1075 FIELD#2,25 AS NF$,30 AS AF$,20 AS CF$,8 AS PF$,45 AS SF$
1080 IF HT$="N" THEN 1090 ELSE T1$="BUFFALOES":HS$=R1$:T2$=OT$:VS$=R2$:GOTO1100
1090 T1$=OT$:HS$=R2$:T2$="BUFFALOES":VS$=R1$
1100 CLS:PRINT"GAME NO. ";GN$:PRINT"HOME ";T1$;"   ";HS$
1110 PRINT"VISITORS ";T2$;"   ";VS$:PRINT"PLAYED ";D$
1115 WRITE #1, GN$,OT$,HT$,R1$,R2$,D$
1120 FOR K=1 TO R
1130 CLS:PRINT N2$(K):LINE INPUT"PLAYED IN THIS GAME(Y/N)? ";PL$
1135 IF LEFT$(PL$,1)="N" THEN 1480 ELSE 1140
1140 INPUT"AT BATS";U(1):INPUT"RUNS";U(2)
1150 INPUT"HITS";U(3):INPUT"WALKS";U(4)
1160 INPUT"DOUBLES";U(5):INPUT"TRIPLES";U(6)
1170 INPUT"HOMERS";U(7):INPUT"SACS";U(8)
1180 INPUT"RBI'S";U(9):INPUT"TIMES ENDED INNING";U(10)
1190 IF (U(1)-U(4)-U(8))=0 THEN U(11)=0 ELSE U(11)=U(3)/(U(1)-U(4)-U(8))
1200 IF (U(1)-U(8))=0 THEN U(12)=0 ELSE U(12)=(U(3)+U(4))/(U(1)-U(8))
1210 IF U(3)=0 THEN U(13)=1 ELSEU(13)=(U(3)+U(5)+2*U(6)+3*U(7))/U(3)
1220 FOR Y=1 TO 10:SA$(Y)=STR$(U(Y)):NEXT Y
1230 FOR Y=1 TO 10:L=LEN(SA$(Y)):SA$(Y)="00"+RIGHT$(SA$(Y),L-1):NEXT Y
1240 FOR Y=1 TO 10:SA$(Y)=RIGHT$(SA$(Y),3):NEXT Y
1250 FOR Y=11 TO 13:SA$(Y)=STR$(1000*U(Y)+.5):NEXT Y
1260 IF U(11)=1 THEN SA$(11)="1.000" ELSE IF U(11)=0 THEN SA$(11)="0.000" ELSE SA$(11)="0."+MID$(SA$(11),2,3)
1270 IF U(12)=1 THEN SA$(12)="1.000" ELSE IF U(12)=0 THEN SA$(12)="0.000" ELSE SA$(12)="0."+MID$(SA$(12),2,3)
1280 SA$(13)=MID$(SA$(13),2,1)+"."+MID$(SA$(13),3,3)
1290 GS$(K)="":FOR Y=1 TO 13:GS$(K)=GS$(K)+SA$(Y):S(K,Y)=U(Y):NEXT Y
1300 WRITE #1,N2$(K),PL$,GS$(K)
1305 REM NOW PARSE THE ROSTER FILE STATS AND UPDATE
1320 FOR Y=1 TO 10:T(K,Y)=T(K,Y)+U(Y):NEXT Y
1330 IF (T(K,1)-T(K,4)-T(K,8))=0 THEN T(K,11)=0 ELSE T(K,11)=T(K,3)/(T(K,1)-T(K,4)-T(K,8))
1340 IF (T(K,1)-T(K,8))=0 THEN T(K,12)=0 ELSE T(K,12)=(T(K,3)+T(K,4))/(T(K,1)-T(K,8))
1350 IF T(K,3)=0 THEN T(K,13)=1 ELSE T(K,13)=(T(K,3)+T(K,5)+2*T(K,6)+3*T(K,7))/T(K,3):T(K,13)=INT(1000*T(K,13)+.5)/1000
1360 FOR Y=1 TO 10:SU$(Y)=STR$(T(K,Y)):NEXT Y
1370 FOR Y=1 TO 10:L=LEN(SU$(Y)):SU$(Y)="00"+RIGHT$(SU$(Y),L-1):NEXT Y
1380 FOR Y=1 TO 10:SU$(Y)=RIGHT$(SU$(Y),3):NEXT Y
1390 FOR Y=11 TO 13:SU$(Y)=STR$(1000*T(K,Y)+.5):NEXT Y
1400 IF T(K,11)=1 THEN SU$(11)="1.000" ELSE IF T(K,11)=0 THEN SU$(11)="0.000" ELSE SU$(11)="0."+MID$(SU$(11),2,3)
1410 IF T(K,12)=1 THEN SU$(12)="1.000" ELSE IF T(K,12)=0 THEN SU$(12)="0.000" ELSE SU$(12)="0."+MID$(SU$(12),2,3)
1420 SU$(13)=MID$(SU$(13),2,1)+"."+MID$(SU$(13),3,3)
1430 ST$(K)="":FOR Y=1 TO 13:ST$(K)=ST$(K)+SU$(Y):NEXT Y
1440 LSET NF$=N$(K):LSET AF$=A$(K):LSET CF$=C$(K):LSET PF$=P$(K):LSET SF$=ST$(K)
1450 PUT #2,K
1460 NEXT K
1470 G=R:CLOSE:RETURN
1480 GS$(K)=STRING$(45,48)
1490 FOR X=1 TO 13:S(K,X)=0:NEXT X
1500 WRITE #1,N2$(K),PL$,GS$(K)
1510 GOTO 1460
1999 REM GAME OR TOTALS DISPLAY
2000 CLS:PRINT"DISPLAY A <G>AME OR <T>OTALS TO DATE OR GOTO THE <M>AIN MENU.";
2010 B$=INKEY$:IF B$="G" THEN 2020 ELSE IF B$="T" THEN 2190 ELSE IF B$="M" THEN RETURN ELSE 2010
2020 CLS:LINE INPUT"GAME NO. ";GA$
2030 IF GA$=GN$ THEN 2040 ELSE GN$=GA$: GOSUB 5010
2040 CLS:PRINT@0,"GAME NO.: ";GN$:PRINT@16,"PLAYED: ";D$
2050 PRINT@32,"HOME: ";T1$;"-";HS$
2060 PRINT@64,"VISITOR: ";T2$;"-";VS$
2070 PRINT@96,STRING$(32,"-"):X=G
2080 FOR K=1 TO X
2090 PRINT@128,N2$(K)
2100 PRINT@192,"AT BATS: ";S(K,1):PRINT@208,"RUNS: ";S(K,2)
2110 PRINT@224,"HITS: ";S(K,3):PRINT@240,"WALKS: ";S(K,4)
2120 PRINT@256,"DOUBLES: ";S(K,5):PRINT@272,"TRIPLES: ";S(K,6)
2130 PRINT@288,"HOMERS: ";S(K,7):PRINT@304,"SACS: ";S(K,8)
2140 PRINT@320,"RBI'S: ";S(K,9):PRINT@336,"END INN: ";S(K,10)
2150 PRINT@352,"BAT AV: ";:PRINTUSING"#.###";S(K,11):PRINT@368,"ON BS AV: ";:PRINTUSING"#.###";S(K,12)
2160 PRINT@384,"SLUG AV: ";:PRINTUSING"#.###";S(K,13)
2170 PRINT@448,"PRESS ANY KEY TO CONTINUE";:B$=INKEY$:IF B$="" THEN 2170
2180 NEXT K:GOTO 2000
2190 CLS:LINE INPUT"TODAY'S DATE: ";TD$
2200 CLS:PRINT@32,"TOTAL STATS AS OF ";TD$
2210 PRINT@96,STRING$(32,"-"):X=R
2220 FOR K=1 TO X
2230 PRINT@128,N2$(K)
2240 PRINT@192,"AT BATS: ";T(K,1):PRINT@208,"RUNS: ";T(K,2)
2250 PRINT@224,"HITS: ";T(K,3):PRINT@240,"WALKS: ";T(K,4)
2260 PRINT@256,"DOUBLES: ";T(K,5):PRINT@272,"TRIPLES: ";T(K,6)
2270 PRINT@288,"HOMERS: ";T(K,7):PRINT@304,"SACS: ";T(K,8)
2280 PRINT@320,"RBI'S: ";T(K,9):PRINT@336,"END INN: ";T(K,10)
2290 PRINT@352,"BAT AV: ";:PRINTUSING"#.###";T(K,11):PRINT@368,"ON BS AV: ";:PRINTUSING"#.###";T(K,12)
2300 PRINT@384,"SLUG AV: ";:PRINTUSING"#.###";T(K,13)
2310 PRINT@448,"PRESS ANY KEY TO CONTINUE";:B$=INKEY$:IF B$="" THEN 2310
2320 NEXT K:GOTO 2000
2999 REM REPORT PRINTING ROUTINE
3000 CLS:PRINT:PRINT"<G>AME REPORT"
3010 PRINT:PRINT"<S>TATS TO DATE REPORT"
3020 PRINT:PRINT"<R>OSTER LISTING"
3030 PRINT:PRINT"<M>AIN MENU":PRINT:PRINT
3040 B$=INKEY$:IF B$="" THEN 3040
3050 IF B$="G" THEN 3060 ELSE IF B$="S" THEN 3160 ELSE IF B$="R" THEN 3220 ELSE IF B$="M" THEN RETURN ELSE 3040
3060 CLS:LINE INPUT"GAME NO. ";GA$
3070 IF GA$=GN$ THEN 3090 ELSE GN$=GA$
3080 GOSUB 5010
3085 REM GAME REPORT
3090 INPUT"PRINTER ON? PAPER POSITIONED?   PRESS <ENTER> WHEN READY";B$
3100 PRINT#-2,"GAME NO. ";GN$;"  ";"HOME ";T1$;"--"HS$;"  ";"VISITORS ";T2$;"--";VS$;"  ";"PLAYED ";D$:PRINT#-2
3110 X=G
3120 PRINT#-2,CHR$(10);CHR$(15);TAB(4)"NAME";TAB(20)"  AB   R   H  BB  2B  3B  HR SAC RBI  EI   BA    OBA   SLA";CHR$(14);CHR$(10)
3130 FOR K=1 TO G
3140 PRINT#-2,N2$(K);TAB(20)S(K,1);TAB(24)S(K,2);TAB(28)S(K,3);TAB(32)S(K,4);TAB(36)S(K,5);TAB(40)S(K,6);TAB(44)S(K,7);TAB(48)S(K,8);TAB(52)S(K,9);TAB(56)S(K,10);TAB(60)S(K,11);TAB(67)S(K,12);TAB(73)S(K,13)
3150 NEXT K:GOTO 3000
3160 CLS:LINE INPUT"TODAY'S DATE: ";TD$
3165 INPUT"PRINTER ON? PAPER POSITIONED?   PRESS <ENTER> WHEN READY.";B$
3170 PRINT#-2,CHR$(10);TAB(24);CHR$(15);"STATISTICS REPORT AS OF ";TD$;CHR$(14);CHR$(10)
3180 PRINT#-2,CHR$(10);CHR$(15);TAB(4)"NAME";TAB(20)"  AB  R   H   BB  2B  3B  HR SAC RBI  EI   BA    OBA   SLA";CHR$(14);CHR$(10)
3190 FOR K=1 TO R
3200 PRINT#-2,N2$(K);TAB(19)T(K,1);TAB(23)T(K,2);TAB(27)T(K,3);TAB(31)T(K,4);TAB(35)T(K,5);TAB(39)T(K,6);TAB(43)T(K,7);TAB(47)T(K,8);TAB(51)T(K,9);TAB(55)T(K,10);TAB(59)T(K,11);TAB(64)T(K,12);TAB(72)T(K,13)
3210 NEXT K:GOTO 3000
3215 REM MAKE A HARDCOPY OF THE ROSTER
3220 CLS:INPUT"PRINTER ON? PAPER POSITIONED?   PRESS <ENTER> WHEN READY.";B$
3230 PRINT#-2,CHR$(31);TAB(17)CHR$(15);"ROSTER";CHR$(14);CHR$(30);CHR$(10)
3240 FOR K=1 TO R
3250 FOR X=1 TO 10:IF RIGHT$(N$(K),1)<>" " THEN 3260 ELSE N$(K)=LEFT$(N$(K),LEN(N$(K))-1):NEXT X
3260 FOR X=1 TO 10:IF RIGHT$(A$(K),1)<>" " THEN 3270 ELSE A$(K)=LEFT$(A$(K),LEN(A$(K))-1):NEXTX
3270 PRINT#-2,N$(K);TAB(22)A$(K);TAB(46)C$(K);TAB(66)P$(K)
3280 NEXT K:GOTO 3000
3999 REM ROSTER REVIEW
4000 CLS:PRINT TAB(13)"ROSTER LISTING":Y=1
4010 PRINT"PRESS ANY KEY TO CONTINUE";
4020 B$=INKEY$:IF B$="" THEN 4020
4030 CLS:FOR K=Y TO Y+2
4040 PRINTN$(K):PRINTA$(K):PRINTC$(K);TAB(23)P$(K):PRINT
4050 IF K=R THEN 4080 ELSE 4060
4060 NEXT K
4070 Y=Y+3:GOTO 4010
4080 PRINT@481,"THAT'S ALL--PRESS ANY KEY";
4090 B$=INKEY$:IF B$="" THEN 4090 ELSE RETURN
4999 REM INPUT A GAME FILE
5000 CLS:LINE INPUT"GAME NO.: ";GN$
5010 F$="GM"+GN$+Y$
5020 OPEN "I",#1,F$
5030 INPUT #1,GN$,OT$,HT$,R1$,R2$,D$
5040 IF HT$="N" THEN 5050 ELSE T1$="BUFFALOES":HS$=R1$:T2$=OT$:VS$=R2$:GOTO 5060
5050 T1$=OT$:HS$=R2$:T2$="BUFFALOES":VS$=R1$
5060 G=0
5070 IF EOF(1)=-1 THEN 5100
5080 G=G+1:INPUT #1,N2$(G),PL$(G),GS$(G)
5090 GOTO 5070
5095 REM NOW PARSE THE STAT STRING
5100 FOR K=1 TO G
5110 FOR Y=1 TO 10:S$(K,Y)=MID$(GS$(K),3*Y-2,3):NEXT Y
5120 Z=31:FOR Y=11 TO 13:S$(K,Y)=MID$(GS$(K),Z,5):Z=Z+5:NEXT Y
5130 FOR Y=1 TO 13:S(K,Y)=VAL(S$(K,Y)):NEXT Y
5140 NEXT K
5150 CLOSE #1:RETURN
5999 REM INPUT THE ROSTER FILE AND FIND N2$
6000 CLS:PRINT"READING THE ROSTER FILE."
6010 OPEN "D",#2,F1$,128:R=LOF(2)
6020 FIELD #2,25 AS NF$,30 AS AF$,20 AS CF$,8 AS PF$,45 AS SF$
6030 FOR K=1 TO R
6040 GET #2,K
6050 N$(K)=NF$:A$(K)=AF$:C$(K)=CF$:P$(K)=PF$:ST$(K)=SF$
6060 NEXT K
6065 REM PARSE THE STAT STRING
6070 PRINT"PARSING THE STAT STRING":FOR K=1 TO R
6080 FOR Y=1 TO 10:T$(K,Y)=MID$(ST$(K),3*Y-2,3):NEXT Y
6090 Z=31:FOR Y=11 TO 13:T$(K,Y)=MID$(ST$(K),Z,5):Z=Z+5:NEXT Y
6100 FOR Y=1 TO 13:T(K,Y)=VAL(T$(K,Y)):NEXT Y
6110 NEXT K
6115 REM FIND N2$
6120 PRINT"FINDING THE 2ND NAME STRINGS":Q$=","
6130 FOR K=1 TO R:M=INSTR(1,N$(K),Q$)+1
6140 N2$(K)=LEFT$(N$(K),M)
6150 NEXT K
6160 CLOSE #2:RETURN
