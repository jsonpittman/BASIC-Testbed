1 REM: BIORHYTHM
11 REM: STEVE TENNEY 1987
21 CLEAR 200:CLS
31 L=O:T=11:P=3.14159265
41 PRINT#-2,"";"********BIORYTHM BY STEVE TENNEY********":PRINT#-2,;
51 PRINT#-2,"";"NAME"
61 INPUT"NAME         ";N$
71 PRINT#-2,;N$
81 PRINT#-2,"";"ENTER BIRTH DATE"
91 PRINT"ENTER BIRTH DATE"
101 PRINT#-2,"";"MONTH            DAY            YEAR
111 GOSUB 331:GOSUB 451:JB=JD
121 PRINT#-2,"";"ENTER START DATE FOR CHART"
131 PRINT "ENTER START DATE
141 PRINT#-2,"";"MONTH            DAY            YEAR
151 GOSUB 331:GOSUB 451:JC=JD
161 IF JC>=JB THEN 201
171 PRINT#-2,"";"CHART DATE CAN'T BE EARLIER"
181 PRINT#-2,"";"THAN BIRTH DATE."
191 SOUND 8,8:PRINT#-2,"";:GOTO 81
201 FOR K=1 TO 1000:NEXT
211 GOSUB 531
221 N=JC-JB
231 V=23:GOSUB 601
241 V=28:GOSUB 601
251 V=33:GOSUB 601
261 GOSUB 781
271 PRINT#-2,""; C$;TAB(8);L$
281 JC=JC+1:L=L+1:IF L<15 THEN 221
291 PRINT"'E' = END, SPACE = GO ON";
301 R$=INKEY$:IF R$="" THEN 301
311 IF R$="E" THEN 931
321 L=O:GOTO 211
331 PRINT#-2,"";
341 INPUT "MONTH(1 TO 12)";M
351 PRINT#-2,"";
361 M=INT(M):IF M<1 OR M>12 THEN 341
371 INPUT"DAY (1 TO 31)";D
381 D=INT(D):IF D<1 OR D>31 THEN 371
391 INPUT"YEAR";Y
401 PRINT#-2,;M,D,Y,W$
411 Y=INT(Y):IF Y<0 THEN 391
421 IF Y>99 THEN 441
431 Y=Y+1900:PRINT Y;"ASSUMED."
441 RETURN
451 W=0:IF M<3 THEN W=-1
461 JD=INT(1461*(Y+4800+W)/4)
471 B=INT(367*(M-2-W*12)/12)
481 IF B<0 THEN B=B+1
491 JD=JD+B
501 B=INT(INT(3*(Y+4900+W)/100)/4)
511 JD=JD+D-32075-B
521 RETURN
531 CLS
541 PRINT#-2,;"(I)INTELLECTUAL (E)EMOTIONAL (P)PHYSICAL"
551 PRINT#-2," "; TAB(10);'BIORYTHM'
561 PRINT#-2," ";"-DOWN----*----UP-";TAB(12);
571 PRINT#-2," ";TAB(20);
581 FOR K=1 TO T+T+1:PRINT CHR$(250);
591 NEXT:PRINT#-2," ":RETURN
601 W=INT(N/V):R=N-(W*V)
611 IF V<>23 THEN 661
621 L$=CHR$(32):FOR K=1 TO 4
631 L$=L$+L$:NEXT
641 L$=LEFT$(L$,T)+CHR$(30 )+LEFT$(L$,T)
651 IF V=23 THEN C$="P"
661 IF V=28 THEN C$="E"
671 IF V=33 THEN C$="I"
681 W=R/V:W=W*2*P
691 W=T*SIN(W):W=W+T+1.5
701 W=INT(W):A$=MID$(L$,W,1)
711 IF A$="P" OR A$="E" OR A$="*" THEN C$="*"
721 IF W=1 THEN 761
731 IF W=T+T+1 THEN 771
741 L$=LEFT$(L$,W-1)+C$+RIGHT$(L$,T+T+1-W)
751 RETURN
761 L$=C$+RIGHT$(L$,T+T):RETURN
771 L$=LEFT$(L$,T+T)+C$:RETURN
781 W=JC+68569:R=INT(4*W/146097)
791 W=W-INT((146097*R+3)/4)
801 Y=INT(4000*(W+1)/1461001)
811 W=W-INT(1461*Y/4)+31
821 M=INT(80*W/2447)
831 D=W-INT(2447*M/80)
841 W=INT(M/11):M=M+2-12*W
851 Y=100*(R-49)+Y+W
861 A$=STR$(M):W=LEN(A$)-1
871 C$=MID$(A$,2,W)+"/"
881 A$=STR$(D):W=LEN(A$)-1
891 C$=C$+MID$(A$,2,W)+"/"
901 A$=STR$(Y):W=LEN(A$)-1
911 C$=C$+MID$(A$,W,2)
921 RETURN
931 PRINT#-2,;"************BIORYTHM BY STEVE TENNEY********"
941 PRINT#-2,;" 725 N. FIG APT.56 ESCONDIDO CALIFORNIA 92025"
951 PRINT#-2,;"THE IDEA OF THE BIORYTHM IS TO TELL YOU YOUR"
961 PRINT#-2,;"HIGH AND LOW CYCLES OF THE MONTH, SO READ THE"
971 PRINT#-2,;"CHART AND DETERMINE YOUR GOOD DAYS AND YOUR"
981 PRINT#-2,;"BAD DAYS OF THE MONTH, FOR MORE INFORMATION"
991 PRINT#-2,;"ON THE BIORYTHM CHECK WITH YOUR LOCAL LIBRARY"
1001 PRINT#-2,;"****************HOPE YOU ENJOY****************"
