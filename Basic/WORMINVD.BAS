0 ' COPYRIGHT 1989 FALSOFT, INC
10 '****************************
15 '* WORM INVADERS FROM SPACE *
20 '* Written by: Eric A. Wolf *
25 '* TANDY Color Computer 128 *
30 '****************************
35 '
40 PCLEAR1:POKE65497,0:WIDTH40:CLS3:ATTR2,4:CLEAR512:DIM MX(35),MY(35),MS(35),C(4),P(4),SC(10),SC$(10):POKE &HF015,18:POKE &HF016,18
45 MONITOR=0 ' 0=RGB & 1=CMP/TV
50 IF MONITOR THEN 55 ELSE 60
55 CMP:PALETTE0,0:PALETTE1,16:PALETTE2,32:PALETTE3,63:PALETTE4,32:PALETTE5,7:PALETTE6,36:PALETTE7,18:PALETTE8,21:PALETTE9,30:PALETTE12,18:PALETTE13,36:PALETTE14,16:PALETTE15,0:C(0)=36:C(1)=5:C(2)=7:C(3)=7:C(4)=36:GOTO65
60 RGB:PALETTE0,0:PALETTE1,7:PALETTE2,56:PALETTE3,63:PALETTE4,56:PALETTE5,36:PALETTE6,54:PALETTE7,18:PALETTE8,34:PALETTE9,10:PALETTE12,18:PALETTE13,54:PALETTE14,7:PALETTE15,0:C(0)=54:C(1)=4:C(2)=32:C(3)=36:C(4)=54
65 CLS3:FORY=1TO10:SC$(Y)=STRING$(16,127):SC(Y)=0:NEXTY
70 CLS:ATTR3,4:PRINTTAB(4);"<< Worm Invaders From Space >>":ATTR6,4:PRINTTAB(8);"Written by Eric A. Wolf":ATTR2,4:PRINTTAB(3);"For a TANDY 128k Color Computer 3"
75 LOCATE6,6:ATTR5,4:PRINT"Worm Invaders' Hall of Fame":ATTR4,4:FORY=1TO10:S$=STR$(Y):LOCATE 7-LEN(S$),Y+8:PRINTS$;") ";SC$(Y);:S$=STR$(SC(Y)):S$=RIGHT$(S$,LEN(S$)-1):S$=STRING$(6-LEN(S$),"0")+S$:PRINT"  ";S$:NEXTY
80 LOCATE0,22:ATTR3,4:INPUT"SKILL LEVEL (1-EASY 2-MEDIUM 3-HARD)";SK:IF SK<1 OR SK>3 THEN 80
85 IF SK=1 THEN S1=.8:S2=1:R1=2.5:R2=.7 ELSE IF SK=2 THEN S1=.9:S2=1:R1=2.6:R2=.8 ELSE IF SK=3 THEN S1=1.1:S2=1:R1=2.7:R2=.9
90 C=&HF09D:FORY=8 TO 15:POKE C+Y,254:NEXTY:POKE C+8,124:POKE C+15,124:FORY=24 TO 30:POKE Y+C,24:NEXTY:POKE C+31,0:FORY=16 TO 23:POKE C+Y,62:NEXTY:POKE C+16,0:POKE C+17,28:POKE C+21,28:POKE C+22,0:POKE C+23,0
95 HCOLOR1,0:HSCREEN2:C1$="S2;R20D20R10U10R10U30R20U20R16D36R10D8R10D8R30U20R4U10R4U12R12D12R8U12R12D12R4D20R16U40R20D30R10U24R12D20R10D10R20D12F4R12U40R10U10R20D12R4D12R16":HDRAW"BM0,188;C1;"+C1$:HDRAW C1$:HPAINT(0,191),1,1:HPAINT(160,191),1,1
100 C2$="R8U20E4U20E2R12F2D4F2D16F4R8U4R16D16R8U12R20U38R20D22F4R16F4D16R16U8R8R16U8R12D8R12U16R16D24R8E2U8EU16R20D40R12U20E4U20E2R16F2D16F4R20F8R16":HDRAW"BM0,188;C2;"+C2$:HDRAW C2$:HPAINT(0,191),2,2
105 'HLINE(160,156)-(168,192),PRESET,BF:HDRAW"S4;BM161,192;C9;BR6U12H3G3D12E2R2F2":HPAINT(164,184)
110 HCOLOR3:HPRINT(0,0),"SCORE: 000000":PLAY"T255L255;V31;O5;"
115 SC=0:LV=1:SV=100:MI=1:WM=7+SK:ST=0
120 REM ** SETUP FOR LEVEL
125 WL=WM:WP=40-WL+ST*40:A$="CURRENT SURVIVAL RATE "
130 IF INT(SV)<=0 THEN 310
135 S$=RIGHT$(STR$(INT(SV)),LEN(STR$(INT(SV)))-1):A$=A$+S$+"%":HCOLOR5:HPRINT(20-(LEN(A$)/2),10),A$
140 PLAY"T255L255;V31;O2;":IF LV<>1 THEN FORY=1 TO SV:SC=SC+1:GOSUB285:PLAY"CC":NEXTY
145 HLINE(160,156)-(168,192),PRESET,BF:HDRAW"S4;BM161,192;C9;BR6;M-2,-10;E2H3G3F2M-2,+10;E2R2F2":HPAINT(164,186),9,9
150 HCOLOR2:HPRINT(11,5),"DEFENSE SHIELDS UP"
155 ST=ST+1:IF ST>4+SK THEN ST=0
160 HCOLOR3:FORX=0TO146 STEP 6:FORY=113 TO 145 STEP 8:HLINE(X,Y)-(X+4,Y+6),PSET,BF:HLINE(315-X,Y)-(315-X+4,Y+6),PSET,BF:NEXTY,X
165 A$=STRING$(38,32):HPRINT(1,10),A$:HPRINT(1,5),A$
170 REM ** MAIN GAME LOOP **
175 GOSUB280 'Update Worm
180 GOSUB290 'Check spacebar
185 GOSUB295 'Update user's missile
190 WP=WP+1:IFWP+WL=760 THEN225
195 IF WL THEN 175 ELSE IF WP+WL>559 THEN 225 ELSE HCOLOR2:HPRINT(11,6),"DEFENSE TOO STRONG":HPRINT(11,7),"FOR METEOR ATTACK":HCOLOR3:HPRINT(13,9),"BONUS 2000 PTS":PLAY"O3":FORY=1 TO 40:SC=SC+50:GOSUB285:PLAY"CC":NEXTY:LV=LV+1
200 PLAY"O5":TIMER=0
205 IF TIMER<300 THEN 205
210 WM=WM+S2:IF WM>20 THEN WM=20
215 A$=STRING$(30,32):HPRINT(5,6),A$:HPRINT(5,7),A$:HPRINT(5,9),A$:GOTO125
220 REM ** METEOR SHOWER **
225 FORZ=WP TO WP+WL:HPRINT(Z-(INT(Z/40)*40),INT(Z/40))," ":NEXTZ:HCOLOR6:HPRINT(10,6),"-- METEOR ATTACK --":PLAY"V31L255T255":FORZ=1TO15:PLAY"12;11;9;7;5;2;V-;V-":NEXTZ:MT=RND(15)+(5*SK)
230 FORY=1TO MT:MX(Y)=RND(40)-1:MY(Y)=-Y*2:MS(Y)=RND(2):NEXTY:T=MT:A$=STRING$(40,32):HPRINT(0,6),A$
235 HCOLOR8:FORZ=1TO MT:FL=0:EXEC43345:MY(Z)=MY(Z)+1:IFMY(Z)<2 ORMY(Z)>99 THEN265 ELSE IFMY(Z)>23 THENT=T-1:HPRINT(MX(Z),MY(Z))," ":MY(Z)=99:GOTO265
240 HPRINT(MX(Z),MY(Z)-1)," ":A=HPOINT(MX(Z)*8+4,MY(Z)*8+5):IF((A=1 ORA=2) AND MY(Z)>22 AND MS(Z)=2) OR ((A=1 ORA=2 ORA=3) AND MS(Z)=1) THENFL=1
245 HPRINT(MX(Z),MY(Z)),CHR$(35-MS(Z)):IFMS(Z)=2 ANDA=3 THENMS(Z)=1
250 'IF A=3 THEN 260
255 IFA=3 THEN260 ELSE IFFL=0 THEN265 ELSE IFMS(Z)=2 THENSV=SV-R1 ELSE IFMS(Z)=1 THENSV=SV-R2
260 IF FL THEN GOSUB275:HPRINT(MX(Z),MY(Z))," ":MY(Z)=99:HCOLOR8:T=T-1
265 NEXTZ:IFT<>0 THEN235
270 LV=LV+1:GOTO 210
275 IF A=3 THEN RETURN ELSE FORQ=0TO4:P(Q)=PEEK(&HFFB0+Q):PALETTE Q,C(Q):NEXTQ:PLAY"O1;V31;AAFF":FORQ=0TO4:POKE &HFFB0+Q,P(Q):NEXTQ:RETURN
280 HCOLOR7:Z=WP:HPRINT(Z-(INT(Z/40)*40),INT(Z/40))," ":Z=WP+WL:HPRINT(Z-(INT(Z/40)*40),INT(Z/40)),"!":RETURN
285 HCOLOR3:SC$=RIGHT$(STR$(SC),LEN(STR$(SC))-1):HPRINT(13-LEN(SC$),0),SC$:RETURN
290 IF PEEK(345)<>247 THEN RETURN ELSE IF MI=0 THEN RETURN ELSE PLAY"O1;EEEE;V27;DDD;V23;CC;V20;BB;V16;A;V31;":MY=20:MI=0:RETURN
295 IF MI THEN RETURN ELSE HCOLOR6:HPRINT(20,MY),"#":HPRINT(20,MY+1)," ":MY=MY-1:IF MY<1 THENMI=1:HPRINT(20,MY+1)," ":RETURN ELSE IF HPOINT(160,MY*8+4)<>7 THEN RETURN ELSE WL=WL-1:WP=WP-1:HPRINT(20,MY+1)," "
300 HCOLOR5:HPRINT(20,MY),"!":PLAY"T255L255;O4;CCAA":HPRINT(20,MY)," ":MI=1:SC=SC+(SK*5):GOSUB285:RETURN
305 REM ** "END".. NO SURVIVORS!
310 HCOLOR5:HPRINT(15,8),"GAME  OVER":HCOLOR6:PLAY"V31;O3;T255L255;":FORY=1TO15:PLAY";12;11;10;9;8;7;6;5;4;3;2;1;V-;V-":NEXTY:HPRINT(8,10),"SURVIVING POPULATION = 0":TIMER=0
315 IF TIMER<360 THEN 315
320 HSCREEN0:CLS:PL=0:FORP=1TO10:IF SC>SC(P) THEN PL=P ELSE NEXTP
325 IF PL=0 THEN 70 ELSE CLS:ATTR2,4:LOCATE4,7:PRINT"You've acheived a Great Score!!":LOCATE0,10
330 ATTR3,4:PRINTTAB(5);"Now enter your name for all":PRINTTAB(5)"to see on the Worm Invaders'":PRINTTAB(5)"Hall of Fame Scoreboard"
335 ATTR3,4:LOCATE0,14:PRINTSTRING$(40,32):LOCATE11,14:PRINTSTRING$(16,127):LOCATE10,16:PRINT"(16 Letter Maximum)"
340 ATTR2,4:LOCATE11,14:LINEINPUT N$:IF LEN(N$)>16 THEN SOUND1,10:GOTO335 ELSE N$=N$+STRING$(16-LEN(N$)," ")
345 IF P=10 THEN 350 ELSE FORX=10 TO P+1 STEP-1:SC(X)=SC(X-1):SC$(X)=SC$(X-1):NEXTX
350 SC$(P)=N$:SC(P)=SC:GOTO70
