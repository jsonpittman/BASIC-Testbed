0 ' COPYRIGHT 1989  FALSOFT,INC
1 'SUPER QUIZ BY:
2 'DOUGLAS W. GILES
3 '530 COUNTRY KNOLL
4 '3093 PEMBINA HIGHWAY
5 'WINNIPEG, MANITOBA
6 'CANADA    R3T 4R6
8 GOTO 10
9 CLS0:VERIFYON:PRINT@228,"saving"CHR$(128)" QUIZ/RDY:0";:SAVE"QUIZ/RDY:0":PRINT@292,"saving"CHR$(128)" QUIZ/BKP:0";:SAVE"QUIZ/BKP:0":CLS:PRINT@224," PROGRAM AND BACKUP ARE SAVED ":PRINT:PRINT:PRINT:END
10 'DIMENSION ARRAYS & STARTUP      GRAPHICS
12 POKE65495,0:GOTO900
13 POKE65494,0
14 SCREEN0,0:CLS3:PMODE0:PCLEAR1:CLEAR18000
16 DIM W$(99),D$(99),R(99)
18 D$=CHR$(125):E$=CHR$(128):F$=STRING$(10,128):G$=STRING$(5,128):AA$=STRING$(32,45)
100 '**** startup menu ****
102 CLS(3):SOUND 175,1
106 PRINT@74,"select"E$"one";
108 PRINT@162,E$"a"D$E$"load"E$"questions"F$;
110 PRINT@194,E$"b"D$E$"begin"E$"questionnaire"G$;
112 PRINT@226,E$"c"D$E$"quit"E$"programme"F$;
114 PRINT@258,E$"d"D$E$"save"E$"questions"E$"to"E$"disk"E$E$;
116 PRINT@290,E$"e"D$E$"formulate"E$"questions"G$;
118 PRINT@322,E$"f"D$E$"print"E$"view"E$"or"E$"amend"G$;
120 PRINT@354,E$"g"D$E$"file"E$"directory"F$;
122 IFZZ$=""THENZZ$="NO NAME"
124 IF ZZ$="NO NAME"ANDYY$<>""THENZZ$=YY$
126 PRINT@454,"this"E$"is"E$"file";E$E$ZZ$;
128 U$=INKEY$:IF U$=""THEN 128
130 U=ASC(U$)
132 IF U<65 OR U>71 THEN 128
134 SOUND 190,1:ON U-64 GOTO 200,500,140,300,400,600,1000
140 IF N>0 THEN GOSUB 150 ELSE 142
142 CLEAR200:PCLEAR4:PMODE2,1:CLS:POKE65494,0:CLOSE:END
150 '**BUFFER DUMP FAILSAFE**
152 IF N>0 THEN 154 ELSE RETURN
154 CLS:PRINT@197,"YOU ARE ABOUT TO ERASE":PRINT@225,"QUESTIONS CURRENTLY IN BUFFER":PRINT@258,"DO YOU STILL WISH TO PROCEED":PRINT@300,"yES/nO"
156 SOUND 150,2
158 CLS0:PRINT@197,"you"E$"are"E$"about"E$"to"E$"erase";:PRINT@225,"questions"E$"currently"E$"in"E$"buffer";:PRINT@258,"do"E$"you"E$"still"E$"wish"E$"to"E$"proceed";:PRINT@300,"Yes"CHR$(124)"No";
160 SOUND 200,2
162 A$=INKEY$:IF A$="Y" THEN RETURN
164 IF A$="N" THEN 100 ELSE 154
200 '**menu for load questions**
202 IF N>0 THEN GOSUB 150 ELSE 216
216 'DRIVE1
218 CLS:PRINT@225,"TO LOAD QUESTIONS ENTER FILE#";
220 PRINT@294,"OR [@] FOR MAIN MENU"
222 FORX=1TO4:SOUND100,2:SOUND150,2:NEXT
224 PRINT@362,"=> ";:LINEINPUTZZ$
225 IF ZZ$=""THEN216
226 IF ZZ$=CHR$(64)THENZZ$="":GOTO229
228 GOSUB800
229 'DRIVE0
230 GOTO100
300 '**menu to save questions**
301 'DRIVE 1
302 CLS:PRINT@71,"TO SAVE QUESTIONS"
304 PRINT@136,"ENTER FILE NAME"
306 PRINT@198,"OR [@] FOR MAIN MENU"
307 FORX=1TO4:SOUND100,2:SOUND150,2:NEXT
308 PRINT@300,"";:LINEINPUTYY$
310 IF YY$=CHR$(64)THENYY$=ZZ$:GOTO315
312 IFYY$=""THENYY$=ZZ$
314 GOSUB700
315 'DRIVE0
316 GOTO 100
400 '*** input quest/answers ***
402 IF N>0THEN450
404 CLS:PRINT@4,"PRESS [@] FOR MAIN MENU"
406 N=N+1
408 PRINT"ENTER QUESTION"N":":LINEINPUTW$(N):SOUND225,1
410 IF W$(N)=CHR$(64)THEN N=N-1:GOTO100
412 PRINT:PRINT"ENTER ANSWER:":LINEINPUTD$(N):SOUND225,1
414 CLS
416 IF N<99 THEN 404
418 CLS:PRINT@225,"QUESTION/ANSWER BUFFER IS FULL"
420 PRINT@289,"SAVE THESE QUESTIONS AND BEGIN"
422 PRINT@364,"NEW QUIZ"
424 FOR QX=1TO1500:NEXTQX:GOTO100
450 'add to existing quiz
452 CLS3
454 PRINT@74,"select"E$;"one";
456 PRINT@162,"a"D$E$"add"E$"questions"E$"to"E$"quiz"E$E$E$;
458 PRINT@194,"b"D$E$"start"E$"new"E$"quiz"F$;
460 PRINT@226,"c"D$E$"return"E$"to"E$"main"E$"menu"G$;
462 U$=INKEY$:IFU$=""THEN462
464 U=ASC(U$)
466 IF U<65 OR U>67 THEN 462
468 SOUND200,1:ON U-64 GOTO 414,480,100
480 GOSUB150:GOTO14
500 ' ***** questionnaire *****
502 CLS:POKE65495,0
504 IFN=0THEN100
506 NN=N
508 FORL=1TON
510 I=RND(N)
512 IFR(I)=1 THEN 510
514 CLS
515 PRINT"QUESTION:"
516 PRINT@28,NN
518 PRINT@32,AA$;:PRINTW$(I):PRINTAA$;:IF W$(I)=" " THEN A$="Y":GOTO540
520 REM A$=INKEY$:IF A$=""THEN 520
522 REM SOUND 200,1
524 PRINT:PRINT"YOUR RESPONSE:":LINEINPUTA$:SOUND 225,1
526 PRINT"THE CORRECT ANSWER IS:"
528 PRINTD$(I)
530 REM PRINTAA$
532 PRINT@416,"IS YOUR ANSWER RIGHT (yES OR nO)";
534 PRINT@452,"ENTER [@] FOR MAIN MENU"        
536 A$=INKEY$:IFA$=""THEN536
538 IF A$=CHR$(64)THEN GOSUB 668:POKE65494,0:GOTO100
540 IF A$="Y" OR A$=CHR$(13) THEN R(I)=1:SOUND225,1:NN=NN-1:GOTO546
542 IF A$="N" OR A$=";" THEN L=L-1:SOUND150,1:GOTO546
544 GOTO 536
546 NEXT L
548 GOSUB668
550 CLS:POKE65494,0:PRINT@228,"END OF QUESTIONNAIRE"
552 PRINT@323,"MAKE ANOTHER SELECTION"
554 FORX=1TO1000:NEXTX:GOTO100
600 '*menu for view/print/amend*
601 IF N<1 THEN 716
603 CLS0
604 PRINT@107,"select"E$"one";
605 PRINT@262,"Print"E$"View"E$"or"E$"Amend";
606 U$=INKEY$:IFU$=""THEN606
607 IF U$="V" OR U$="A"THEN640
608 IF U$="P"THEN620
609 GOTO 606
620 'print q/a to printer
621 CLS0:SOUND150,2:PRINT@292,"press"E$"any"E$"key"E$"to"E$"continue";
622 PRINT@228,"set"E$"printer"E$"to"E$"baud"E$"1200";
623 EXEC 44539
624 CLS0:PRINT@231,"printing"E$"hard"E$"copy";
625 POKE 65495,0
626 FORX=1TO5:PRINT#-2:NEXTX
627 PRINT#-2,CHR$(31)"QUESTIONS AND ANSWERS FOR FILE "ZZ$
628 FORX=1TO5:PRINT#-2:NEXTX
629 FOR I=1TON
630 PRINT#-2,CHR$(30)TAB(35)"QUESTION #"I":"
631 PRINT#-2,CHR$(10)"Q: ";W$(I)
632 PRINT#-2,CHR$(10)CHR$(10)"A: ";D$(I)
633 PRINT#-2,CHR$(10)CHR$(10)
634 NEXTI
635 FORX=1TO5:PRINT#-2:NEXTX
636 POKE 65494,0:GOTO100
640 'view/amend file
641 FORI=1TON
642 IF W$(I)=W$(0)THEN100
643 CLS:PRINT@0,G$"this"E$"is"E$"file"E$CHR$(123)ZZ$D$;STRING$(39,128);
644 PRINT@64,"QUESTION #"I":"
645 PRINT:PRINT"Q:   "W$(I)
646 PRINT:PRINT"A:   "D$(I)
647 PRINTAA$;" TYPE <ENTER> FOR NEXT QUESTION":PRINT" <C> TO CHANGE OR <@> TO ESCAPE"
648 SOUND 200,2
649 K$=INKEY$:IFK$=""THEN649
650 IF K$="@"THEN100
651 IF K$="C" THEN GOSUB 660:GOTO643
652 IF K$=CHR$(13) THEN 654
653 GOTO 649
654 CLS:NEXT
655 GOTO100
660 'amend file entry
661 CLS:SOUND200,2:SOUND150,2:PRINT"OLD QUESTION:":PRINTW$(I)
662 PRINT:PRINT"TYPE NEW QUESTION AND <ENTER> OR   PRESS <ENTER> IF NO CHANGE":PRINT:PRINT"=>";:LINEINPUTNW$
663 CLS:PRINT"OLD ANSWER:":PRINTD$(I)
664 SOUND200,2:SOUND150,2:PRINT:PRINT" TYPE NEW ANSWER AND <ENTER> OR   PRESS <ENTER> IF NO CHANGE":PRINT:PRINT"=>";:LINEINPUTND$
665 IF NW$<>""THEN W$(I)=NW$:NW$=""
666 IF ND$<>""THEN D$(I)=ND$:ND$=""
667 RETURN
668 FORL=1TON:R(L)=0:NEXT:RETURN
700 '**** save quiz ****
702 CLS3:SOUND200,2:SOUND150,2
704 PRINT@228,"FILE <"YY$"> NOW SAVING";      
706 IF N<1THEN716
708 OPEN"O",#1,YY$+"/DAT"
710 WRITE #1,N
712 FORL=1TON:WRITE#1,W$(L),D$(L):NEXT
714 IFN>0THEN724
716 CLS:PRINT@224,"THERE ARE NO QUESTIONS IN BUFFER"
718 PRINT@293,"MAKE ANOTHER SELECTION"
719 YY$="NO NAME"
720 FORX=1TO15:SOUND150,2:SOUND200,2:NEXTX
722 CLOSE#1:GOTO100
724 CLOSE#1
726 CLS:RETURN
800 '**** load quiz ****
802 CLS3:SOUND200,2:SOUND150,2
804 PRINT@228,"FILE <"ZZ$"> NOW LOADING";
806 OPEN"I",#1,ZZ$+"/DAT"
808 INPUT #1,N
810 FORL=1TON:INPUT#1,W$(L),D$(L):NEXT
812 CLOSE#1
814 CLS:RETURN
900 '**graphics data & display**
902 CLEAR 200
904 SOUND 100,2
906 PCLEAR4:PMODE 4,1:PCLS:SCREEN1,1
908 FOR I= 2 TO 110 STEP 2
910 CIRCLE(129,96),I
912 NEXT I
914 SOUND 100,2
916 FOR X=2 TO 120 STEP 1.1
918 CIRCLE(128,96),X,,.2
920 NEXT X
922 'GRAPHIC LETTERING
924 AA$="SUPER QUIZ"
926 DRAW"S8;C0;BM60,99"
932 FOR XX=1 TO LEN(AA$)
934 RESTORE:LL=0
936 READ LL$,CC$
938 IF LL$=MID$(AA$,XX,1)THEN DRAW CC$:GOTO942
940 LL=LL+1:IF LL<48 THEN 936
942 SOUND 200,1:FORX=1TO10:NEXTX:SOUND 200,1:NEXTXX
944 DATA " ", "BM+7, 0"
946 DATA"E","NR4;U3;NR2;U3;R4;BM+3,+6"
948 DATA"I","BM+1,0;R1;NR1;U6;NL1;R1;BM+4,+6"
950 DATA"P","U6;R3;F1;D1;G1;L3;BM+7,3"
952 DATA "Q","BM+1,0;H1;U4;E1;R2;F1;D3;G1;NH1;NF1;G1;L1;BM+6,0"
954 DATA"R","U6;R3;F1;D1;G1;L2;NL1;F3;BM+3,0"
956 DATA"S","BM+0,-1;F1;R2;E1;U1;H1;L2;H1;U1;E1;R2;F1;BM+3,+5"
958 DATA"U","BM+0,-1;NU5;F1;R2;E1;U5;BM+3,6"
960 DATA"Z","NR4;U1;E4;U1;L4;BM+7,6"
970 FORX=1TO2500:NEXT:GOTO13
1000 '**** file directory ****
1002 QQ=0:CLS0:PRINT@9,"file"E$"directory";G$E$E$E$E$
1004 PRINT@32,STRING$(32,128);
1006 FORGG=3TO11
1007 DSKI$ 0,17,GG,A$,B$
1008 'DSKI$ 1,17,GG,A$,B$
1010 C$=A$+LEFT$(B$,127)
1012 NAM$(0)=LEFT$(C$,8)
1014 EXT$(0)=MID$(C$,9,3)
1016 FOR HH=1TO7
1018 NAM$(HH)=MID$(C$,HH*32+1,8)
1020 EXT$(HH)=MID$(C$,9+HH*32,3)
1022 NEXT HH
1024 FOR HH=0TO7
1026 IF EXT$(HH)="DAT"AND LEFT$(NAM$(HH),1)<>CHR$(0)THEN PRINTNAM$(HH),;:QQ=QQ+1:IFQQ>19THENQQ=0:GOSUB1032
1028 NEXT HH
1030 NEXT GG:GOSUB1032:GOTO100
1032 PRINT@451,"press"E$"any"E$"key"E$"to"E$"continue";
1034 EXEC 44539:CLS0:RETURN
2000 'SUMMARY OF REM STATEMENTS
2002 '*************************
2004 '* BEGIN WITH REM YES/NO *
2005 '* FULL OR SHORT DISPLAY *
2006 '*-------*------*--------*
2008 '* LINE# * FULL *  SHORT *
2010 '*-------*------*--------*
2012 '*  515  *  NO  *   YES  *
2014 '*  520  *  YES *   NO   *
2016 '*  522  *  YES *   NO   *
2018 '*  524  *  NO  *   YES  *
2020 '*  526  *  NO  *   YES  *
2022 '*  530  *  YES *   NO   *
2023 '*************************
2024 '* ONE OR TWO DISK DRIVES*
2025 '*-------*------*--------*
2026 '* LINE# *1 DSK * 2 DSKS *
2028 '*-------*------*--------*
2030 '*  216  *  YES *   NO   *
2032 '*  229  *  YES *   NO   *
2034 '*  301  *  YES *   NO   *
2036 '*  315  *  YES *   NO   *
2038 '* 1007  *  NO  *   YES  *
2040 '* 1008  *  YES *   NO   *
2042 '*************************
