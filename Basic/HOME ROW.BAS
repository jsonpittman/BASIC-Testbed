0 'HOME ROW BOMBER 1.0; (C) 1986 BRUCE BELL 
5 POKE359,57
10 L$="JFKDLS;AHGURIEOWPQYTMV,C.X/ZNB74839201:-@!#$%&'()=?><+":POKE282,1
20 CLS0:PRINT@8,"home"CHR$(128)"row"CHR$(128)"bomber";:FORK=0TO9:S(K)=0:NEXT:M=0:H=0
30 FORY=28TO31:FORX=0TO63:SET(X,Y,3):NEXTX,Y:FORK=1TO8:C=(RND(5)+2)*16:S$=CHR$(129+C)+CHR$(135+C)+CHR$(135+C)+CHR$(131+C):PRINT@412+K*4,S$;:NEXT:PRINT@480,"score";:PRINT@500,"level";:GOSUB130
40 PRINT@32," PRESS (1-9) FOR STARTING LEVEL":I$=INKEY$:IFI$="Q"THEN170ELSEL=VAL(I$):IFL<1THEN40ELSEFORK=32TO63:PRINT@K,CHR$(128);:NEXT:SC=0:GOSUB130
50 FORK=0TO9
60 IFS(K)>0THEN90ELSEIFRND(DL)>1THEN120
70 L$(K)=MID$(L$,RND(LL),1):S(K)=1
80 P(K)=32+RND(128)-1:IFPEEK(P(K)+1024)<>128ORPEEK(P(K)+1056)<>128THEN80
90 IFS(K)=0THEN110ELSEPRINT@P(K),CHR$(128);:P(K)=P(K)+32:PK=PEEK(P(K)+1024):PRINT@P(K),L$(K);
100 IFP(K)>415THENS(K)=0:PRINT@P(K),"*";:SOUND100,1:PRINT@P(K)AND4092,CHR$(128)CHR$(128)CHR$(128)CHR$(128);:M=M+1:IFM=20THEN20ELSEIFPK<>128THENH=H+1:IFH=8THEN20
110 Q$=INKEY$:IFQ$=""THEN120ELSEIFQ$="["THEN20ELSEFORQ=0TO9:IFS(Q)=0THENNEXTQ ELSEIFQ$<>L$(Q)THENNEXTELSES(Q)=0:POKEP(Q)+1024,42:SOUND200,1:PRINT@P(Q),CHR$(128);:SC=SC+2:PRINT@486,SC;:S=SC/100:IFS=INT(S)THENL=L+1:GOSUB130
120 NEXTK:GOTO50
130 LL=L*9:IFLL>54THENLL=54
140 DL=91-L*10:IFDL<1THENDL=1:POKE65495,0ELSEPOKE65494,0
150 SCREEN0,(L+1)AND1
160 PRINT@486,SC*L"  ";:PRINT@506,L;:RETURN
170 POKE359,126:POKE282,255:CLS4:PRINT"BYE BYE!":END
