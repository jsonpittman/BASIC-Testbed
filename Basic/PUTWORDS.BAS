0 ' COPYRIGHT 1989  FALSOFT,INC
1 ' *** ACCESS temp/dat VIA pass/wrd
10 FILES 3,630:PCLEAR1:CLEAR15000
15 OPEN"D",#1,"PASS/WRD",14
20 FIELD#1,12 AS A$,2 AS B$
25 GET#1,1
30 F=INSTR(A$,"/")
35 PRINT A$;"#";"  F = ";F;
62 F$=STRING$(12," ")
64 MID$(F$,1,F-1)=MID$(A$,1,F-1):MID$(F$,9,4)=MID$(A$,F,4)
70 CLOSE#1
82 DIM W$(3,100)
99 CLS(5):PRINT@32*7+6,"GETTING ";F$;
100 OPEN"D",#1,F$,51
110 FIELD#1,20 AS A$,20 AS B$,11 AS C$
120 N=LOF(1)
130 FOR I=1 TO N 
140 GET#1,I
150 W$(1,I)=A$:W$(2,I)=B$:W$(3,I)=C$
160 NEXT I
200 CLOSE#1
800 ' ***SORT temp/dat ALPHABETICALLY
801 POKE65495,0:GOSUB2300:PRINT@32*7+6,"sorting ";F$;:PRINT@32*8+9,"alphabetically";
802 FOR I=1 TO N-1:K=0
803 FOR J=1 TO N-1
804 IF W$(1,J)<W$(1,J+1)THEN805 ELSE K=1:W1$=W$(1,J):W$(1,J)=W$(1,J+1):W$(1,J+1)=W1$:W2$=W$(2,J):W$(2,J)=W$(2,J+1):W$(2,J+1)=W2$:W3$=W$(3,J):W$(3,J)=W$(3,J+1):W$(3,J+1)=W3$
805 NEXT J
807 IF K=0THEN810ELSE NEXT I
810 GOSUB2300:CLS(4):PRINT@32*4+11,"accessing";:PRINT@32*6+12,"WORDLIST";
819 POKE65494,0 'SLOW DOWN TO get
820 OPEN"D",#1,"WORDLIST/DAT",51
822 FIELD#1,51 AS B$
855 E=LOF(1):NL=1:NN=1:GET#1:BB$=B$:GOSUB9000
860 K=0:FOR I=1 TO N
861 W$=STRING$(51,CHR$(0)):MID$(W$,1,20)=W$(1,I):MID$(W$,21,20)=W$(2,I):MID$(W$,41,11)=W$(3,I):PRINT@32*8+6,USING"## = ";I;:PRINTW$(1,I);:IF K=1THEN900
862 PRINT@32*12+5,USING"#### WORDS IN wordlist";E+I;:PRINT@32*14+9,USING"### WORDS TO GO";N-I;
863 NL=NN:IF NN=0THEN K=1:GOTO861ELSE GET#1,NN:BB$=B$:GOSUB9000
864 PRINT@32*13,USING"####";NL;:PRINT@32*14,USING"####";NN;:PRINT@32*15,LEFT$(BB$,20);
865 IF LEFT$(BB$,20)<LEFT$(W$,20)THEN863 'NEXT WORD
867 IF LEFT$(BB$,20)=LEFT$(W$,20)THEN GOSUB9100:NEXT I:GOTO923 ' *** (OUT) - NOT "<" OR "=" --> RESTACK POINTERS
870 MID$(W$,48,2)=MID$(BB$,48,2):MM=NL:GOSUB9200:MID$(W$,50,1)=M1$:MID$(W$,51,1)=M2$' *** CHANGE 'W' POINTERS TO (E+I)TH & PREVIOUS WORDS
871 LSET B$=W$:PUT#1,E+I
872 MM=E+I:GOSUB9200:MID$(BB$,48,1)=M1$:MID$(BB$,49,1)=M2$:LSET B$=BB$:PUT#1,NL ' *** PUT 'LAST' POINTER TO 'E+I'TH WORD
874 BB$=W$:GOSUB9005:GET#1,NN:BB$=B$
875 MID$(BB$,50,1)=M1$:MID$(BB$,51,1)=M2$
877 LSET B$=BB$:PUT#1,NN
880 ' *** PUT POINTER TO NL
888 LSET B$=W$:PUT#1,E+I
889 NN=E+I:NEXT I:GOTO923
900 MM=E+I:GOSUB9200
910 MID$(BB$,50,1)=M1$:MID$(BB$,51,1)=M2$
912 LSET B$=BB$:PUT#1,NL
914 MM=NL:GOSUB9200:MID$(W$,48,1)=M1$:MID$(W$,49,1)=M2$:MID$(W$,50,1)=CHR$(0):MID$(W$,51,1)=CHR$(1)
916 MM=E+I:GET#1,MM:BB$=B$:NL=MM
918 NEXT I
923 CLOSE#1:GOSUB2300:PRINT@32*8+6,"going to MAINMENU";:RUN"MAINMENU"
1280 YY$=INKEY$
1290 YY$=INKEY$:IF YY$=""THEN1290
1300 Y$=YY$:RETURN
2300 CLS(5):SOUND200,1:RETURN
5000 FOR X9=1 TO 500:NEXT X9:RETURN ' *** SHORT DELAY S/R
9000 ' *** GET POINTER TO NEXT WORD AND CONVERT CODE ASC -> ##
9001 X1=50:X2=51:GOTO9010
9005 X1=48:X2=49
9010 NL=NN
9020 N1$=MID$(BB$,X1,1):N2$=MID$(BB$,X2,1)
9030 N1=ASC(N1$):N2=ASC(N2$):NN=256*N1+N2
9039 RETURN
9100 ' *** COMBINE (IF DUPLICATE WORD) # TIMES GIVEN, ANSWERED CORRECTLY AND REPEAT REQUESTS
9105 FOR II=1 TO 3
9110 A1=ASC(MID$(BB$,40+II,1)):A2=ASC(MID$(W$(3,I),II,1)):MID$(BB$,40+II,1)=CHR$(A1+A2)
9115 NEXT II
9120 LSET B$=BB$:PUT#1,NL
9125 RETURN
9200 M1=FIX(MM/256):M2=MM-256*M1:M1$=CHR$(M1):M2$=CHR$(M2):RETURN   ' *** CONVERT ## TO CHR$
