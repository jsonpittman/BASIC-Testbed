0 'BOOKLET' by Keiran Kenny,       Sydney, 1988.
10 CLS
20 GOTO800
30 PRINT@104,CHR$(34)+"THE BOOKLET"+CHR$(34)
40 PRINT@174,"BY"
50 PRINT@233,"KEIRAN KENNY"
60 PRINT@328,"PRESS ANY KEY."
70 K$=INKEY$:IFK$=""THEN70ELSECLS
80 POKE150,18 '2400 BAUD
90 CLEAR5508
100 DIMA$(162)
110 PRINT@225,"";:INPUT"LINES PER PAGE (MAX. 27):";LP
120 IFLP>27THENCLS:GOTO110
130 LF=INT(ABS((LP-27)/2))
140 CLS:PN=1
150 P=32:PP=0
160 FORT=1TOLP*6
170 LN=LN+1:PRINT@PP,"TYPE PAGE"PN;CHR$(8)", LINE"LN
180 PRINT@P+34,"]"
190 PRINT@P,"";:LINEINPUTA$(T)
200 IFA$(T)=CHR$(95)THENFORT=T TOLP*6:A$(T)="":NEXT:GOTO270
210 IFLEN(A$(T))>34THENPRINT"TOO LONG! TYPE LINE"LN"AGAIN.":PRINT@P,STRING$(LEN(A$(T)),32):PRINT@P+34,"]":GOTO190
220 PRINT
230 P=P+96:PP=PP+96
240 IFLN=LP THENPN=PN+1:LN=0
250 IFPP>=480THENCLS:P=32:PP=0
260 NEXT
270 CLS:PRINT"TYPE 3 LINES FOR COVER."
280 P=64
290 FORX=1TO3
300 PRINT@P,"LINE"X
310 LINEINPUTC$(X)
320 IFLEN(C$(X))>34THENCLS:PRINT"LINE"X"TOO LONG!":PRINT@P,"";:GOTO310
330 P=P+96
340 NEXT
350 CLS:PRINT@224,"PRINTER READY? HOW MANY COPIES  DO YOU WANT TO PRINT";:INPUTNC
360 CLS:PRINT@224,"PRINTING PAGES 1 & 6 AND 3 & 4"
370 FORCC=1TONC
380 FORNR=1TO2
390 IFNR=1THENQ=1:PX=1:PZ=6
400 IFNR=2THENQ=LP*2+1:PX=3:PZ=4
410 PRINT#-2:PRINT#-2,TAB(3)"PAGE"PX;TAB(72)"PAGE"PZ
420 PRINT#-2
430 IFLP<27THENFORX=1TOLF:PRINT#-2:NEXT
440 FORT=Q TOQ+LP-1
450 IFNR=1THENM=T+LP*5
460 IFNR=2THENM=T+LP
470 PRINT#-2,TAB(3)A$(T)TAB(46)A$(M)
480 NEXT
490 FORX=1TO30-(LF+LP):PRINT#-2:NEXT
500 NEXTNR
510 NEXTCC
520 CLS:PRINT@128,"REVERSE PAPER. PRINTER READY?   PRESS <ENTER>."
530 K$=INKEY$:IFK$<>CHR$(13)THEN530
540 CLS:PRINT@224,"PRINTING COVER AND PAGES 2 & 5."
550 FORCC=1TONC
560 FORX=1TO10:PRINT#-2:NEXT
570 FORX=1TO3
580 PRINT#-2,TAB(63-LEN(C$(X))/2)C$(X)
590 FORZ=1TO5:PRINT#-2:NEXT
600 NEXT
610 FORX=1TO6:PRINT#-2:NEXT
620 Q=LP*4+1:PX=5:PZ=2
630 PRINT#-2,TAB(3)"PAGE"PX;TAB(72)"PAGE"PZ:PRINT#-2
640 IFLP<27THENFORX=1TOLF:PRINT#-2:NEXT
650 FORT=Q TO Q+LP-1
660 M=T-LP*3
670 PRINT#-2,TAB(3)A$(T)TAB(46)A$(M)
680 NEXTT
690 FORX=1TO30-(LF+LP):PRINT#-2:NEXT
700 NEXTCC:TC=TC+NC
710 CLS:PRINT@64,"COPIES PRINTED ="TC:PRINT:PRINT"DO YOU WANT MORE? Y/N"
720 PRINT:PRINT"(TO CORRECT LINES PRESS <BREAK>)"
730 K$=INKEY$:IFK$=""THEN730
740 IFK$="Y"ORK$="y"THENNR=0:GOTO770
750 IFK$="N"ORK$="n"THENCLS:END
760 GOTO730
770 PRINT@192:PRINT@192,"HOW MANY MORE";:INPUTNC
780 PRINT:PRINT"PRINTER READY? PRESS <ENTER>."
790 K$=INKEY$:IFK$<>CHR$(13)THEN790ELSE360
800 PMODE0,1:PCLEAR1:GOTO30
