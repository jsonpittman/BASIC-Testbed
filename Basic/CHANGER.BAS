10 '****************************
20 '*      LOW-RES PICTURE     *
30 '*   PALETTE COLOR CHANGER  *
40 '* VERSION: 1.0     8610.31 *
50 '****************************
60 '*  (C) 1986 BY ERIC WHITE  *
70 '****************************
100 CLEAR2000,31231:PMODE3:PCLEAR8:DIM CP(15),P$(72),A$(1),SP(24):P=3:SC=0:OP=7
150 G$="/,5>?%* ?=:/-*OLEJOANMBOLKENLHOL/ 5:?%* ?5:/-"+CHR$(34)+"O@ENOEKGJO@OEJMJOL,,4<<$,(<<8,$(LLDHLDHDHL@LDLLHLL"
160 FORX=1TO96:MID$(G$,X,1)=CHR$(ASC(MID$(G$,X,1))+112):NEXT
200 FORZ=0TO15:READ CP(Z),A$:PALETTE Z,CP(Z):NEXTZ
250 ON BRK GOTO 1100
270 'CMP DEFAULT PALETTE COLORS
300 DATA 0   , PMODE COLOR 1
301 DATA 12  , PMODE COLOR 2
302 DATA 7   , PMODE COLOR 3
303 DATA 63  , PMODE COLOR 4
304 DATA 0   , LO-RES COLOR 1
305 DATA 12  , LO-RES COLOR 2
306 DATA 7   , LO-RES COLOR 3
307 DATA 63  , LO-RES COLOR 4
308 DATA 0   ,BG COLOR 128-255
309 DATA 18  ,
310 DATA 16  ,
311 DATA 63  ,
312 DATA 16  ,FG COLOR   0-127
313 DATA 32  ,BG COLOR   0-127
314 DATA 0   ,
315 DATA 38  ,
400 SL$(0)="change":SL$(1)="select"
500 P(0)=161:P(1)=169:P(2)=177:P(3)=185:P(4)=353:P(5)=361:P(6)=369:P(7)=377
600 GB$=CHR$(128)+CHR$(128):N$="UNTITLED"
700 G1$=CHR$(143):G1$=G1$+G1$+G1$+G1$+G1$+G1$
800 G2$=CHR$(159):G2$=G2$+G2$+G2$+G2$+G2$+G2$
900 G3$=CHR$(175):G3$=G3$+G3$+G3$+G3$+G3$+G3$
1000 G4$=CHR$(191):G4$=G4$+G4$+G4$+G4$+G4$+G4$
1100 CLS0:A$=INKEY$:PRINTG$;
1200 C$="__BNOXQHFGS_pxwu_AX_DQHB_VGHSD__":FORX=1TO16:POKE1119+X,ASC(MID$(C$,X,1))-63:POKE1152-X,ASC(MID$(C$,33-X,1))-63:NEXT
1300 PRINT@193,G1$GB$G2$GB$G3$GB$G4$;
1400 PRINT@225,G1$GB$G2$GB$G3$GB$G4$;
1500 PRINT@257,G1$GB$G2$GB$G3$GB$G4$;
1600 PRINT@289,G1$GB$G2$GB$G3$GB$G4$;
1700 PRINT@385,"MODIFY"GB$"MODIFY"GB$" SAVE "GB$" LOAD "GB$" LOW  "GB$" HIGH "GB$"GRAPHS"GB$"GRAPHS"GB$"SCREEN"GB$"SCREEN"GB$"  TO  "GB$" FROM "GB$"&H0E00"GB$"&H2600"GB$" DISK "GB$" DISK ";
1800 U$=" "+CHR$(207)+"=##  "+CHR$(223)+"=##  "+CHR$(239)+"=##  "+CHR$(255)+"=##  ":SC$(0)="HI-SCRN":SC$(1)="LO-SCRN":B$=CHR$(143)
1900 '
2000 PD=0'PICTURE DISK DRIVE NO.
2100 FE$="PIC"'DISK FILE EXTENSION
2200 DE$="/"+FE$+":"+RIGHT$(STR$(PD),1):PP=OP:KB=0:LD=0:GOTO 3100
2300 MN=1:GOSUB 10000:MN=0:ON KB GOTO 2400,2500,2600,2700,2800
2400 PP=PP+4:GOTO 2900
2500 PP=PP-4:GOTO 2900
2600 PP=PP-1:GOTO 2900
2700 PP=PP+1:GOTO 2900
2800 LD=1:OP=PP
2900 IF LD THEN ON PP+1 GOTO 4040,4040,4040,4040,9950,9950,3200,3300
3000 IF PP<0 THEN PP=PP+8 ELSE IF PP>7 THEN PP=PP-8
3100 PRINT@P(OP),GB$GB$GB$;:OP=PP:PRINT@P(OP),SL$(INT(OP/4));:GOTO 2300
3200 Y$="SAVE PICTURE TO DISK":GOTO3400
3300 Y$="LOAD PICTURE FROM DISK":GOTO3400
3400 CLS:GOSUB9700:PRINTSTRING$(32,140)TAB((32-LEN(Y$))/2)Y$:PRINTSTRING$(32,131):PRINT
3500 ON PP-5 GOTO 8600,4100
3600 A$=INKEY$:IF A$="" THEN 3600
3700 SOUND200,1:RETURN
3800 PRINT@128,"":PRINTTAB(6)"ENTER NAME OF PICTURE":PRINT:PRINT:PRINT@233,"";:INPUTY$:RETURN
3900 CLS
4000 A$=INKEY$:GOTO1100
4040 PMODE P,SC*4+1:SCREEN1,1:NC=CP(PP)
4050 PALETTE PP+4,NC:PLAY"P10"
4060 GOSUB10000:IF KB=5 THEN CP(PP)=NC:CP(PP+4)=NC:FORZ=0TO7:PALETTE Z,CP(Z):NEXT:GOTO1100
4070 IF KB=1 THEN NC=NC+1:IF NC>63 THEN NC=0:SOUND1,1
4080 IF KB=2 THEN NC=NC-1:IF NC<0 THEN NC=63:SOUND1,1
4090 GOTO 4050
4100 VERIFY ON
4200 IF DT THEN4600
4300 FORX=1TO12:SP(X)=(X-1)*32+1:SP(X+12)=SP(X)+16:NEXT:Z$="LOAD>":EX$="exitdisk"
4400 NN=0:FORX=1TO72:P$(X)="":NEXT
4500 DSKI$ PD,17,18,A$(0),A$(1):DN$=MID$(A$(1),113,8):DT$=MID$(A$(1),121,8):FR=FREE(PD)
4600 GOSUB9500
4700 IF DT THEN5000
4800 IF VAL(DT$)=0 THEN 8200
4900 PRINT@66,"WHEN DIR. IS PRINTED,USE THE  ARROW KEYS TO SCROLL THROUGH THE  LIST OF PROGRAMS ON THE DISK.":GOSUB7400:S=1
5000 S=1:GOSUB7200
5100 FORT=0TO11:Y$=INKEY$:PRINTTAB(7);LEFT$(P$(S+T),8);TAB(23);LEFT$(P$(S+T+12),8):NEXTT:PP=1:LD=0
5200 PRINT@SP(PP),Z$;:P1=PP
5300 GOSUB10000:ON KB GOTO 5400,5600,5800,6000,6200
5400 PP=PP+1:IF PP>NN THEN PP=NN
5500 GOTO6300
5600 PP=PP-1:IF PP<1 THENPP=1
5700 GOTO 6300
5800 PP=PP-12:IF PP<1 THENPP=1
5900 GOTO 6300
6000 PP=PP+12:IF PP>NN THENPP=NN
6100 GOTO 6300
6200 LD=1:P1=0
6300 IF PP=P1 THEN5200
6400 PRINT@SP(P1),"     ";:IF LD=0 THEN5200ELSEGOTO8000
6500 IF PP<1THENPP=1
6600 NV=INT((S-1)/24):IFPP>(NN-24*NV)THENPP=(NN-24*NV)
6700 IFPP>24THENPP=24
6800 'IFIN=9THEN7100ELSE IFIN<>8THEN6400 ELSEIFS-24<1THEN6400
6900 'IFIN<>8THEN6400
7000 'PP=1:S=S-24:GOTO5000
7100 GOTO 6400'IF S+24>49 OR S+24>NN THEN6400ELSEPP=1:S=S+24:GOTO5000
7200 PRINT@480,USING"## PICTURE ";NN-1;:IF NN>2 THENPRINTCHR$(8)"S";
7300 PRINT@0,"";:RETURN
7400 PRINT@232,"READING DIRECTORY"
7500 NN=0:FORZ=3TO11
7600 DSKI$ PD,17,Z,A$(0),A$(1):FORQ=0TO1:FORW=0TO3:P$=MID$(A$(Q),W*32+1,32):P1$=LEFT$(P$,1):P1=ASC(P1$):IFP1=0THEN7700ELSEIF P1=255THENGOTO7800ELSE IF MID$(P$,9,3)=FE$ THEN NN=NN+1:P$(NN)=P$
7700 NEXTW,Q,Z
7800 POKE&HFF40,0:NN=NN+1:P$(NN)=EX$
7900 RETURN
8000 IF P$(PP)=EX$ THENDT=0:GOTO3900ELSEDT=1:LD=0:PRINT@492,"LOADING:"LEFT$(P$(PP),8);:PCLS1:SCREEN1,1:LOADM LEFT$(P$(PP),8)+DE$:N$=LEFT$(P$(PP),8)
8010 IF MID$(P$(PP),17,16)=STRING$(16,0) THEN 8100 ELSE FORX=0 TO 15:CP(X)=ASC(MID$(P$(PP),17+X,1)):NEXT:FORX=0TO7:PALETTE X,CP(X):NEXT
8100 PLAY"P50":GOTO 1100
8200 CLS:PRINT@66,"HEY!  THIS DISK HAS NO NAME!"
8300 PRINT@96,"  WHAT DO YOU WANT TO NAME IT?":PRINT@269,".......":PRINT@266,"";:INPUTDN$:IF DN$=""OR LEN(DN$)>8GOTO8300
8400 PRINT@321,"ENTER TODAY'S DATE FOLLOWED BY":PRINT@355,"YOUR IDENTIFICATION LETTER":PRINT@395,"YYMM.DDI":PRINT@392,"";:INPUTDT$:IF DT$="" OR LEN(DT$)<>8THEN8400
8500 DSKI$ PD,17,18,A$(0),A$(1):MID$(A$(1),113,16)=STRING$(16,32):MID$(A$(1),113,8)=DN$:MID$(A$(1),121,8)=DT$:DSKO$ PD,17,18,A$(0),A$(1):DT=0:GOTO1100
8600 PRINT@128,"";:A$=INKEY$
8700 IF N$="" THEN INPUT"NAME OF PICTURE TO BE SAVED";N$ ELSE Y$=N$:GOTO8800
8800 PRINT:PRINT:IF Y$="" THENPRINTTAB(5)"ABORT ILLEGAL FILE NAME!":SOUND5,10:GOTO1100
8900 IF SC THEN GOSUB 10700
9000 Y$=LEFT$(Y$,8):SAVEM Y$+DE$,&HE00,&H25FF,&H0006:IF SC THEN GOSUB 10700
9100 CP$="":FORZ=0TO15:CP$=CP$+CHR$(CP(Z)):NEXTZ
9200 FORZ=3 TO 11:DSKI$ PD,17,Z,A$(0),A$(1):FORQ=0 TO 1:FORW=0 TO3:IF Y$+FE$=MID$(A$(Q),W*32+1,11) THEN MID$(A$(Q),W*32+1+16,16)=CP$:DSKO$ PD,17,Z,A$(0),A$(1):Z=11:Q=1:W=3
9300 'PRINT MID$(A$(Q),W*32+1,32):EXEC44539
9400 NEXT W,Q,Z:GOTO1100
9500 CLS:PRINT@384,STRING$(32,143)DN$" "RIGHT$(DE$,2)TAB(11)B$DT$B$;:PRINTUSING"## GRANULE";FR;:IF FR>1 THEN PRINT"S"
9600 PRINT@395,B$;:PRINT@404,B$;:PRINT@391,CHR$(143);:GOTO9800
9700 PRINT@384,STRING$(32,143):PRINT@416,"PALETTE:";:PRINTUSINGU$;CP(0),CP(1),CP(2),CP(3)
9800 PRINT@448,STRING$(32,143)"SCN:";SC$(SC);B$"PICNAME:"N$;LEFT$(DE$,3);:POKE1535,ASC(RIGHT$(FE$,1)):PRINT@459,B$;
9900 PRINT@0,"";:RETURN
9950 SC=PP-4
9960 PMODE P,SC*4+1:SCREEN1,1
9970 EXEC44539:GOTO1100
10000 KB=0' READ KEYBOARD
10100 IF PEEK(342)=247 THEN POKE 342,255:KB=1
10200 IF PEEK(341)=247 THEN POKE 341,255:KB=2
10300 IF PEEK(343)=247 THEN POKE 343,255:KB=3
10400 IF PEEK(344)=247 THEN POKE 344,255:KB=4
10500 IF PEEK(345)=247 OR PEEK(338)=191 THEN POKE 345,255:KB=5
10550 IF PEEK(343)=191 THEN SCREEN1,1 ELSE IF MN=1 THEN SCREEN0,0
10600 IF KB=0 THEN 10100 ELSE PLAY"P255":RETURN
10700 FORX=1TO4:PCOPY X TO 19:PCOPY X+4 TO X:PCOPY 19 TO X+4:NEXT:RETURN
