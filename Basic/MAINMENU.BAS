0 ' COPYRIGHT 1989  FALSOFT,INC
1 PMODE0:CLEAR5000:CLS5
2 Z9=1   ' Z9 IS A FLAG
3 Z8=0   ' Z8 IS A FLAG
4 ' Z8=0 --> INITIALIZE DATE /      Z8=1 --> STORING DATE    /      Z8=2 --> STORING A STRING WITH THE NAME OF THE TEST TO BE TAKEN  -  OR  -  NAMES OF FILES TO BE PRINTED
11 DIM D(4):D(1)=FREE(0) 'IF USING > 1 DRIVE THEN ADD D(2)=FREE(1), ETC.
12 ZB$="<d>ATE  <h>ELP"
14 GOTO6370
100 GOSUB9000:PRINT@32*3+9,"E D U S P E L L";:PRINT@32*6+7,"select option:";:PRINT@32*8+3,"<1> - put words into dic'y";:PRINT@32*9+3,"<2> - build test from files";:PRINT@32*10+3,"<3> - build direct test";:PRINT@32*11+3,"<4> - take a test";
102 PRINT@32*15+10,ZB$;
105 PRINT@5,USING"system date: ##/##/##";YR,MO,DA;
115 K=0:GOSUB 8000:Z=VAL(Y$)
116 IF Y$="D"THEN3000 'CHANGE SYSTEM DATE
117 IF Y$="H"THEN HS=1:HE=5:GOSUB18000:GOTO100 'CALL HELP SCREENS
120 ON Z GOTO 130,130,130,6000:GOTO100
130 GOSUB9000:PRINT@32*8+6,"shifting to 'SPELLER'";:GOSUB1500:RUN"SPELLER"
1500 OPEN"D",#1,"HELP/SCN",17
1510 FIELD #1,12AS A$,2AS B$
1520 Z$=STR$(Z)
1530 LSET A$="WORDLIST/DAT":LSET B$=RIGHT$(Z$,2)
1540 PUT #1,1:CLOSE#1:RETURN
3000 'CHANGE DATE /R
3010 GOSUB9000:Z8=2:PRINT@32*11+15,"##/##/##";:PRINT@32*12+15,"yr mo da";:PRINT@32*11,"";:LINEINPUT"SYSTEM DATE:   ";C$
3019 '*** CHECK FORMAT
3020 S1=INSTR(C$,"/"):S2=INSTR(4,C$,"/"):IF S1=3AND S2=6THEN3100
3030 PRINT@32=14+6,"wrong format ....";:SOUND120,3:GOSUB7000:GOTO3010
3100 '*** IF FORMAT OK THEN ...
3110 YR=VAL(MID$(C$,1,2)):MO=VAL(MID$(C$,4,2)):DA=VAL(MID$(C$,7,2))
3119 '*** STORE DATE ON DISK
3120 Z8=2:GOTO6370
5000 CLS4:PRINT@32*7+8,"TRANSFERRING TO";:PRINT@32*9+12,"editlist";:RUN"EDITLIST"
6000 'S/R TO LIST FILES - ADOPTED FROM rainbow
6005 CLS4:PRINT@32*7+11,"ONE MOMENT";:PRINT@32*9+13,"PLEASE";
6010 DIM T$(11,7),N$(68),TP(68)
6020 N=1:DR=0:DN$="0"
6026 F1$="TST"
6030 FORX=3TO11:DSKI$DR,17,X,A$,B$:C$=A$+LEFT$(B$,127):FORI=0TO7
6040 T$(X,I)=MID$(C$,I*32+1,32):C1=ASC(T$(X,I)):IF C1=255THEN6070ELSE IF C1=0THEN6060
6041 IF LEFT$(T$(X,I),8)="DOS BOOT"THEN6060 ' CAN'T PRINT /BIN FILES
6042 IF MID$(T$(X,I),9,3)<>F1$ THEN6060ELSE N$(N)=LEFT$(T$(X,I),12):TP(N)=ASC(MID$(T$(X,I),12,1))
6050 N=N+1:IF N=69THEN6070
6060 NEXTI,X
6070 N=N-1:FORX=1TON:N$(X)=LEFT$(N$(X),8)+"."+MID$(N$(X),9,3):NEXT
6080 CLS6:PRINT@32*15+14,"<f>";:PRINT@0,"";
6090 IFN<=30THENPP=1:N1=N
6100 IFN>30THENPP=2:N1=30
6110 IFN>60THENPP=3:N1=30
6120 FORX=1TON1:PRINT" "N$(X),:NEXTX
6130 IFPP=2ORPP=3THENPRINT@480,"PRESS <M> FOR MORE...";
6140 L$=">":R$="<":P=0:Y=1
6150 PRINT@P,L$;:PRINT@P+13,R$;
6160 FORX=338TO345:POKEX,255:NEXTX
6170 IFPEEK(338)=191THEN6360
6180 IFPEEK(339)=191THENCLEAR:GOTO6010
6190 IFPEEK(341)=247THEN6320
6200 IFPEEK(342)=247THEN6330
6210 IFPEEK(343)=247THEN6340
6220 IFPEEK(344)=247THEN6350
6230 IFPEEK(344)=254THEN6260
6240 IFPEEK(343)=253THEN6270
6250 GOTO6170
6260 PRINT@32*13+2,USING"## FREE GRANULES ON DRIVE #";FREE(DR),DR;:GOSUB7000:GOTO6160
6270 IFPP=2ANDY=1THENCLS:FORX=31TON:PRINT" "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=2:P=0:N1=N-30:GOTO6150
6280 IFPP=2ANDY=2THENY=1:P=0:N1=30:GOTO6080
6290 IFPP=3ANDY=1THENCLS:FORX=31TO61:PRINT" "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=2:P=0:N1=N-59:GOTO6150
6300 IFPP=3ANDY=2THENCLS:FORX=62TON:PRINT" "N$(X),:NEXT:PRINT@480,"PRESS <M> FOR MORE...";:Y=3:P=0:N1=N-61:GOTO6150
6310 IFPP=3ANDY=3THENY=1:P=0:N1=30:GOTO6080
6320 IFP<17THEN6150ELSEP=P-32:PRINT@P+32," ";:PRINT@P+45," ";:GOTO6150
6330 IF P/16=>(N1-2)THEN6160ELSEP=P+32:PRINT@P-32," ";:PRINT@P-19," ";:GOTO6150
6340 P=P-16:IFP<0THENP=0:GOTO6150ELSEPRINT@P+16," ";:PRINT@P+29," ";:GOTO6150
6350 IF P/16=>(N1-1)THEN6160ELSEP=P+16:PRINT@P-16," ";:PRINT@P-3," ";:GOTO6150
6360 F=P/16+(Y-1)*30+1:F$=N$(F)+":"+DN$:TP=TP(F)
6365 Z8=1
6370 OPEN"D",#1,"HELP/SCN",17 'STORE NAME OF TEST FILE TO RUN
6380 FIELD #1,12AS A$,2AS B$,3AS C$
6381 IF Z8=0 AND Z9=1THEN6388ELSE IF Z8=2THEN6395ELSE F1$=MID$(F$,10,3) ' GET DATE IF INITIAL ELSE PUT NEW DATE WILL PUT NAME OF TEST TO TAKE
6383 BB$=CHR$(0)+CHR$(0):GOTO 6390
6388 GET#1,1:B=VAL(B$):GOSUB10000:IF B=9THEN CLOSE#1:Z=6:GOTO6000ELSE Z9=0:LSET B$="11":PUT#1,1:CLOSE#1:GOTO100 '*** NOTE: B-0 ON STARTUP
6390 LSET A$=F$:LSET B$=BB$:GOTO6400
6395 LSET C$=CHR$(YR)+CHR$(MO)+CHR$(DA) ' DATE OF TEST 
6400 PUT #1,1
6410 CLOSE#1
6411 IF Z8=2THEN Z8=0:GOTO100
6420 GOSUB9000:PRINT@32*8+4,"transferring to TAKETEST"
6430 RUN"TAKETEST/BAS"
7000 FOR X9=1TO600:NEXT X9:RETURN  ' PROGRAMMED DELAY LOOP
8000 YY$=INKEY$ ' ROUTINE TO MINIMIZE INKEY$ ERRORS
8010 YY$=INKEY$:IF YY$=""THEN8010ELSE Y$=YY$:YY$=INKEY$:RETURN
9000 CLS:SOUND200,1:RETURN
10000 'GET DATE S/R
10010 YR=ASC(MID$(C$,1,1)):MO=ASC(MID$(C$,2,1)):DA=ASC(MID$(C$,3,1)):RETURN
18000 XH=0:OPEN"D",#2,"HELP/SCN",130
18010 FIELD#2,130 AS H$
18020 CLS8:PRINT@43,"H  E  L  P";:IF XH=1THEN RETURN
18030 FOR H=6TO13:PRINT@32*H+8,STRING$(16," ");:NEXT H
18040 FOR H=HS TO HE:GET#2,H+1
18050 H1$=MID$(H$,1,2):PRINT@32*3+14,USING"<%%>";H1$;
18060 FOR H1=1TO8:PRINT@32*(4+H1)+8,MID$(H$,16*(H1-1)+3,16);:NEXT H1
18070 PRINT@32*15+7,"<W>ait   <R>eturn>";
18080 Y$=INKEY$:FOR X9=1TO300:Y$=INKEY$:IF Y$=""THEN NEXT X9
18090 IF Y$="W"THENPRINT@32*15+2,STRING$(28," ");:PRINT@32*15+3,"press any key to continue";:GOSUB8000:GOTO18110
18100 IF Y$="R"THEN CLOSE#2:Z=0:RETURN
18110 XH=1:GOSUB18020:XH=0:NEXT H:GOTO18020
