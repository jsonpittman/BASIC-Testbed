0 'COPYRIGHT 1989  FALSOFT,INC
1 'AHP.BAS
2 'BY WILLIS STANLEY
3 '23 NOV 1988
4 'USE OF THE ANALYTIC HIERARCHY PROCESS IN
5 'MULTI-FACTOR DECISION MAKING (3-5 FACTORS)
10 WIDTH80
20 DIM R$(5),J$(5),P(5),D$(5),N(25),G(5),AV(5),V(5,5),T(5),S(25)
40 CLS
45 PRINT"THE ALTERNATIVES ARE THE DIFFERENT CHOICES YOU COULD MAKE"
46 PRINT"IN THIS PROBLEM."
47 PRINT
50 INPUT"NUMBER OF ALTERNATIVES BEING CONSIDERED";C
60 IFC<3 THEN GOTO 50: IF C>5 THEN GOTO50
64 PRINT:PRINT
65 PRINT"THE FACTORS ARE THE THINGS YOU THINK ARE IMPORTANT IN JUDGING WHICH"
66 PRINT"ALTERNATIVE YOU PREFER."
67 PRINT
70 INPUT"NUMBER OF FACTORS TO BEING CONSIDERED";W
80 IFW<3 THEN GOTO70:IF W>5THEN GOTO70
90 CLS:FORX=1TOC
100 INPUT"ENTER NAME FOR ALTERNATIVE ";R$(X):NEXTX
110 CLS:FORX=1TOW
120 INPUT"ENTER NAME FOR FACTOR ";J$(X):NEXTX
130 FORX=1TOC:D$(X)=R$(X):NEXTX
140 IF H=13 THEN GOTO 530
150 IF F<>1 THEN B=W ELSE B=1
160 IFF=1THENA=W ELSE A=C
170 IFF<>1THEN GOTO 200
180 FOR X=1TOW
190 D$(X)=J$(X):NEXTX
200 CLS
210 FORK=1TOB
220 CF=0
230 Z=0
240 FOR Y=1 TO A
250 FOR X= 1 TO A
260 Z=Z+1
270 IF X<=Y THEN GOTO450
280 IFF=1THENU$="THE FACTORS' RELATIVE IMPORTANCE" ELSE U$=J$(K)
290 CLS:PRINT"IN TERMS OF "U$
300 PRINT:PRINT"HOW DOES "D$(Y)" COMPARE TO "D$(X) " USING THE SCALE BELOW?"
310 PRINT:PRINT
320 PRINT"1--EQUAL":PRINT"2--VERY SLIGHT PREFERENCE",:PRINT"  .5--VERY SLIGHT DISLIKE"
330 PRINT"3--SLIGHT PREFERENCE",:PRINT"       .33--SLIGHT DISLIKE"
340 PRINT"4--MODERATE PREFERENCE",:PRINT"     .25--MODERATE DISLIKE"
350 PRINT"5--DEFINITE PREFERENCE",:PRINT"     .20--DEFINITE DISLIKE"
360 PRINT"6--STRONG PREFERENCE",:PRINT"       .166--STRONG DISLIKE"
370 PRINT"7--VERY STRONG PREFERENCE",:PRINT"  .14--VERY STRONG DISLIKE"
380 PRINT"8--EXTREME PREFERENCE",:PRINT"      .125--EXTREME DISLIKE"
390 PRINT"9--UNQUESTIONED PREFERENCE",:PRINT" .11--UNQUESTIONED DISLIKE"
400 PRINT:PRINT:PRINT"ENTER THE NUMERICAL VALUE THAT REPRESENTS YOUR PERCEPTION OF"
401 PRINT D$(Y)" WHEN COMPARED WITH "D$(X)
410 INPUT"??";N(Z)
420 IF N(Z)>9 THEN GOTO 410
430 IFN(Z)<.1 THEN GOTO 410
440 CLS
450 NEXTX:NEXTY
460 IFA=3 THEN GOSUB1000
470 IFA=4 THEN GOSUB2000
480 IFA=5 THEN GOSUB3000
490 IF CF=1 THEN GOTO 230
500 NEXT K
510 IFF=1THENH=13
520 F=1:GOTO140
530 FORX=1TOC:FORY=1TOW
540 O=V(Y,X)*AV(Y)
550 P(X)=P(X)+O
560 NEXTY:NEXTX
570 CLS:PRINT"TOTAL WEIGHTED EVALUATION:":PRINT:PRINT
580 FORX=1TOC
590 PRINTR$(X),:PRINTP(X)
600 NEXTX
610 PRINT:PRINT:PRINT"HIGHEST VALUE IS RECOMENDED CHOICE"
620 PRINT"OK, YOU'RE BACK IN BASIC.":END
1000 'PROCESS FOR 3 ALTERNATIVES
1010 N(1)=1:N(4)=1/N(2):N(5)=1:N(7)=1/N(3):N(8)=1/N(6):N(9)=1
1020 FORX=1TOA:G(X)=0:NEXTX
1030 FOR X=1 TO A: FOR Y= X TO Z STEP A
1040 G(X)=G(X)+N(Y):NEXTY:NEXTX
1050 S(1)=N(1)/G(1):S(4)=N(4)/G(1):S(7)=N(7)/G(1)
1060 S(2)=N(2)/G(2):S(5)=N(5)/G(2):S(8)=N(8)/G(2)
1070 S(3)=N(3)/G(3):S(6)=N(6)/G(3):S(9)=N(9)/G(3)
1080 AV(1)=(S(1)+S(2)+S(3))/3
1090 AV(2)=(S(4)+S(5)+S(6))/3
1100 AV(3)=(S(7)+S(8)+S(9))/3
1110 IFF<>1THEN V(K,1)=AV(1)
1120 IFF<>1THEN V(K,2)=AV(2)
1130 IFF<>1THEN V(K,3)=AV(3)
1140 T(1)=((AV(1)*N(1))+(AV(2)*N(2))+(AV(3)*N(3)))/AV(1)
1150 T(2)=((AV(1)*N(4))+(AV(2)*N(5))+(AV(3)*N(6)))/AV(2)
1160 T(3)=((AV(1)*N(7))+(AV(2)*N(8))+(AV(3)*N(9)))/AV(3)
1170 L=(T(1)+T(2)+T(3))/3
1180 I=(L-3)/(3-1)
1190 RI=I/.58
1200 GOSUB4000
1210 RETURN
2000 'PROCESS FOR 4 ALTERNATIVES
2010 N(1)=1:N(5)=1/N(2):N(6)=1:N(9)=1/N(3):N(10)=1/N(7):N(11)=1
2020 N(13)=1/N(4):N(14)=1/N(8):N(15)=1/N(12):N(16)=1
2030 FORX=1 TO A:G(X)=0:NEXT X
2040 FOR X=1 TO A:FOR Y=X TO Z STEP A
2050 G(X)=G(X)+N(Y):NEXT Y:NEXT X
2060 S(1)=N(1)/G(1):S(2)=N(2)/G(2):S(3)=N(3)/G(3):S(4)=N(4)/G(4)
2070 S(5)=N(5)/G(1):S(6)=N(6)/G(2):S(7)=N(7)/G(3):S(8)=N(8)/G(4)
2080 S(9)=N(9)/G(1):S(10)=N(10)/G(2):S(11)=N(11)/G(3):S(12)=N(12)/G(4)
2090 S(13)=N(13)/G(1):S(14)=N(14)/G(2):S(15)=N(15)/G(3):S(16)=N(16)/G(4)
2100 AV(1)=(S(1)+S(2)+S(3)+S(4))/4
2110 AV(2)=(S(5)+S(6)+S(7)+S(8))/4
2120 AV(3)=(S(9)+S(10)+S(11)+S(12))/4
2130 AV(4)=(S(13)+S(14)+S(15)+S(16))/4
2140 IF F<>1 THEN V(K,1)=AV(1)
2150 IF F<>1 THEN V(K,2)=AV(2)
2160 IF F<>1 THEN V(K,3)=AV(3)
2170 IF F<>1 THEN V(K,4)=AV(4)
2180 T(1)=((AV(1)*N(1))+(AV(2)*N(2))+(AV(3)*N(3))+(AV(4)*N(4)))/AV(1)
2190 T(2)=((AV(1)*N(5))+(AV(2)*N(6))+(AV(3)*N(7))+(AV(4)*N(8)))/AV(2)
2200 T(3)=((AV(1)*N(9))+(AV(2)*N(10))+(AV(3)*N(11))+(AV(4)*N(12)))/AV(3)
2210 T(4)=((AV(1)*N(13))+(AV(2)*N(14))+(AV(3)*N(15))+(AV(4)*N(16)))/AV(4)
2220 L=(T(1)+T(2)+T(3)+T(4))/4
2230 I=(L-4)/(4-1)
2240 RI=I/.9
2250 GOSUB 4000
2260 RETURN
3000 'PROCESS FOR 5 ALTERNATIVES
3010 N(1)=1:N(6)=1/N(2):N(7)=1:N(11)=1/N(3):N(12)=1/N(8):N(13)=1
3020 N(16)=1/N(4):N(17)=1/N(9):N(18)=1/N(14):N(19)=1
3030 N(21)=1/N(5):N(22)=1/N(10):N(23)=1/N(15):N(24)=1/N(20):N(25)=1
3040 FORX=1 TO A:G(X)=0:NEXTX
3050 FORX=1 TO A:FORY=X TO Z STEP A
3060 G(X)=G(X)+N(Y):NEXTY:NEXTX
3070 S(1)=N(1)/G(1):S(2)=N(2)/G(2):S(3)=N(3)/G(3):S(4)=N(4)/G(4):S(5)=N(5)/G(5)
3080 S(6)=N(6)/G(1):S(7)=N(7)/G(2):S(8)=N(8)/G(3):S(9)=N(9)/G(4):S(10)=N(10)/G(5)
3090 S(11)=N(11)/G(1):S(12)=N(12)/G(2):S(13)=N(13)/G(3):S(14)=N(14)/G(4):S(15)=N(15)/G(5)
3100 S(16)=N(16)/G(1):S(17)=N(17)/G(2):S(18)=N(18)/G(3):S(19)=N(19)/G(4):S(20)=N(20)/G(5)
3110 S(21)=N(21)/G(1):S(22)=N(22)/G(2):S(23)=N(23)/G(3):S(24)=N(24)/G(4):S(25)=N(25)/G(5)
3120 AV(1)=(S(1)+S(2)+S(3)+S(4)+S(5))/5
3130 AV(2)=(S(6)+S(7)+S(8)+S(9)+S(10))/5
3140 AV(3)=(S(11)+S(12)+S(13)+S(14)+S(15))/5
3150 AV(4)=(S(16)+S(17)+S(18)+S(19)+S(20))/5
3160 AV(5)=(S(21)+S(22)+S(23)+S(24)+S(25))/5
3170 IFF<>1 THEN V(K,1)=AV(1)
3180 IFF<>1 THEN V(K,2)=AV(2)
3190 IF F<>1 THEN V(K,3)=AV(3)
3200 IFF<>1 THEN V(K,4)=AV(4)
3210 IFF<>1 THEN V(K,5)=AV(5)
3220 T(1)=((AV(1)*N(1))+(AV(2)*N(2))+(AV(3)*N(3))+(AV(4)*N(4))+(AV(5)*N(5)))/AV(1)
3230 T(2)=((AV(1)*N(6))+(AV(2)*N(7))+(AV(3)*N(8))+(AV(4)*N(9))+(AV(5)*N(10)))/AV(2)
3240 T(3)=((AV(1)*N(11))+(AV(2)*N(12))+(AV(3)*N(13))+(AV(4)*N(14))+(AV(5)*N(15)))/AV(3)
3250 T(4)=((AV(1)*N(16))+(AV(2)*N(17))+(AV(3)*N(18))+(AV(4)*N(19))+(AV(5)*N(20)))/AV(4)
3260 T(5)=((AV(1)*N(21))+(AV(2)*N(22))+(AV(3)*N(23))+(AV(4)*N(24))+(AV(5)*N(25)))/AV(5)
3265 L=(T(1)+T(2)+T(3)+T(4)+T(5))/5
3270 I=(L-5)/(5-1)
3280 RI=I/1.12
3290 GOSUB4000
3300 RETURN
4000 IFRI<.1 THEN CF=0:IFRI<.1 THEN GOTO 4090
4010 SOUND45,3:SOUND2,8:PRINT"YOUR ANSWERS ARE NOT CONSISTENT!!!":CF=1
4020 PRINT"HERE IS ANOTHER CHANCE TO EXPRESS YOUR PREFERENCE."
4030 PRINT"PERHAPS YOU SHOULD END THE PROGRAM AND RETHINK YOUR VIEWS ABOUT"
4040 PRINTU$:PRINT"CHOOSE <Y> TO CONTINUE <N> TO ABORT PROGRAM"
4050 P$=INKEY$:IFP$=""GOTO4050
4060 IFP$="N"THENGOTO620
4070 IFP$="Y"THENGOTO4090
4080 PRINT"??? TRY AGAIN ???":GOTO4050
4090 RETURN
