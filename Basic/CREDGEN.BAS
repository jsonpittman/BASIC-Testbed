0 'COPYRIGHT 1990 FALSOFT, INC.
10 'credgen.bas
20 'A CREDITS GENERATOR FOR THE
30 'COCO IN TV ENVIRONMENT.
40 '
50 CLS:PRINT"SETTING UP, PLEASE WAIT..."
60 CLEAR 11000:DIM CP$(21,15):FL$="CREDITS.GEN:0":DN=1:FORX=1 TO 20:FOR Y=1 TO 14:CP$(X,Y)=STRING$(32,32):NEXT Y:NEXT X:VERIFY ON
70 FOR X=1 TO 20:CP$(X,1)="PAGE #"+RIGHT$(STR$(X),LEN(STR$(X))-1):Q=32-LEN(CP$(X,1)):CP$(X,1)=CP$(X,1)+STRING$(Q,32):NEXT X
80 CLS
90 PRINT@12,"CREDGEN"
100 PRINT@44,"=======":PRINT
110 PRINT"    (1) EDIT SOME CREDITS"
120 PRINT"    (2) DISPLAY SOME CREDITS"
130 PRINT"    (3) QUIT PROGRAM"
140 PRINT"    (4) CHANGE FILESPECS"
150 PRINT"    (5) SAVE MEMORY TO FILE"
160 PRINT:PRINT"    FILESPEC:";FL$
170 MU$=INKEY$:IFMU$=""THEN170
180 ON VAL(MU$) GOTO 320,620,990,210,1010
190 SOUND 200,1:GOTO 80
200 'set filespec
210 CLS:PRINT@0,"FILENAME:########";:PRINT@9,"";:LINEINPUT FL$
220 IFFL$=""THENFL$="CREDITS.GEN:0":DN=1:GOTO 80
230 IFLEN(FL$)>8THENFL$=LEFT$(FL$,8)
240 FL$=FL$+".GEN"
250 PRINT@0,"FILENAME:";FL$
260 PRINT"dISK OR tAPE ?"
270 DT$=INKEY$:IFDT$=""THEN270
280 IFDT$="D"THENDN=1ELSEDN=-1
290 IFDN=-1THENFL$=LEFT$(FL$,8):PRINT@0,"FILENAME:";FL$;"    "
300 IFDN=1THEN LINEINPUT"DRIVE:";DD$:IF INSTR("0123",DD$)=0 THEN PRINT@64,"";:GOTO300:ELSE FL$=FL$+":"+DD$:PRINT@0,"FILENAME:";FL$
310 GOTO 80
320 'edit some credits
330 CLS
340 PRINT@0,"(1) LOAD FROM FILE"
350 PRINT@32,"(2) LOAD FROM MEMORY"
360 MU$=INKEY$:IFMU$=""THEN360
370 IFINSTR("12",MU$)=0THEN360
380 IFMU$="2"THEN 440
390 OPEN "I",#DN,FL$
400 FOR X=1 TO 20
410 FOR Y=0 TO 14
420 LINEINPUT#DN,CP$(X,Y)
430 NEXT X
440 CLS
450 PRINT@0,"WHICH PAGE ? (1 - 20)"
460 LINEINPUT ">";PN$:IFPN$=""THEN 80
470 IF(VAL(PN$)>20 OR VAL(PN$)<1) THEN 450
480 PN=VAL(PN$)
490 CLS:PRINT@33,CHR$(8);:FORX=1 TO 14:PRINTLEFT$(CP$(PN,X),31):NEXT X:FOR TD=1 TO 1000:NEXTTD:CLS:LINEINPUT"EDIT IT?";ES$:IFES$="N"THEN 440
500 LINEINPUT"WHICH LINE ? (1 - 14) ";WL$:WL=VAL(WL$)
510 IF(WL<1 OR WL>14) THEN 500
520 CLS
530 PRINT@0,"PAGE:";PN;"  LINE:";WL;
540 PRINT@32,STRING$(32,175);:PRINTCP$(PN,WL);:PRINTSTRING$(32,175):PRINT
550 PRINT "SURE YOU WANT TO CHANGE IT?"
560 K$=INKEY$:IFK$="" THEN 560
570 IFK$="N"THEN 440
580 PRINT@64,"";:LINEINPUT L$
590 IFLEN(L$)>32THEN L$=LEFT$(L$,32)
600 IFLEN(L$)<32THEN L$=L$+STRING$(32-LEN(L$),32)
610 CP$(PN,WL)=L$:GOTO 440
620 'display some credits
630 CLS
640 PRINT" (1) LOAD FROM FILE"
650 PRINT" (2) LOAD FROM MEMORY"
660 K$=INKEY$:IFK$=""THEN660
670 IFK$="2" THEN 770
680 OPEN"I",#DN,FL$
690 FOR X=1 TO 20
700 FOR Y=1 TO 14
710 LINEINPUT #DN,CP$(X,Y)
720 NEXT Y
730 NEXT X
740 CLOSE #DN
750 PRINT"CREDITS LOADED...PRESS ANY KEY"
760 K$=INKEY$:IFK$=""THEN 760
770 CLS:PRINT@5,"Q-QUIT ANY OTHER TO PAUSE";
780 FOR X=5 TO 2 STEP -1
790 PRINT@0,X;:FORTD=1TO370:NEXTTD
800 NEXT X
810 CLS:FOR TD=1 TO 740:NEXT TD
820 CLS
830 FOR X=1 TO 20
840 PRINT@33,CHR$(8);
850 FOR Y=1 TO 14
860 PRINTCP$(X,Y);
870 NEXT Y
880 FOR TD=1 TO 450
890 K$=INKEY$:IFK$<>"" THEN 960
900 NEXT TD
910 NEXT X
920 POKE 1534,110
930 K$=INKEY$:IF K$="" THEN 930
940 GOTO 80
950 'pause credit screen
960 IF K$="Q" THEN 80
970 K$=INKEY$:IFK$="" THEN 970
980 GOTO 900
990 'terminate process
1000 VERIFY OFF:END
1010 'save memory to file
1020 PRINT@480,"WORKING...";
1030 OPEN "O",#DN,FL$
1040 FOR X=1 TO 20
1050 FOR Y=1 TO 14
1060 PRINT #DN,CP$(X,Y)
1070 NEXT Y
1080 NEXT X
1090 PRINT@480,"FILE SAVED...";
1100 FOR TD=1 TO 1000:NEXT TD
1110 CLOSE #DN:GOTO 80
