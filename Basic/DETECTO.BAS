0 ' COPYRIGHT 1990 FALSOFT, INC
1 '      *** DETECTO ***
2 '   *** BY ROB BECKER ***
3 ' *** COPYRIGHT (C) 1990 ***
10 CLS(C):PRINT@40,"*** DETECTO ***";:PRINT@69,"*** BY ROB BECKER ***";:PRINT@99,"*** COPYRIGHT (C) 1990 ***";:FORX=1TO800:NEXT:IFDI=1 THENGOSUB60:DI=0:GOTO1250
20 X=RND(-TIMER):FORZ=1TO10:X=RND(63):Y=RND(20)+11:SET(X,Y,5):PLAY"V31O1T20C":NEXT
30 FORX=1TO300:NEXT:PRINT@266,"WHO DUN IT?";
40 FORX=1TO2000:NEXT:DI=1:C=1:GOTO10
50 RM=RND(10):TL=51:SC=51
60 SU$(1)="LONNIE FALK":SU$(2)="CRAY AUGSBURG":SU$(3)="TONY OLIVE":SU$(4)="GREG LAW":SU$(5)="KELLY GOFF":SU$(6)="HEIDI NELSON"
70 WP$(1)="COCO":WP$(2)="CASSETTE":WP$(3)="DISK DRIVE":WP$(4)="MULTI-PAK":WP$(5)="PRINTER":WP$(6)="MODEM"
80 RM$(1)="COMPUTER ROOM":RM$(2)="LOBBY":RM$(3)="OFFICE":RM$(4)="HALLWAY":RM$(5)="CONF.ROOM":RM$(6)="KITCHEN":RM$(7)="STAIRWAY":RM$(8)="BASEMENT":RM$(9)="ELEVATOR":RM$(10)="PARKING LOT"
90 IFDI=1THEN RETURN
100 FORX=1TO6
110 RP(X)=RND(9):FORY=1TO6:IFY=X THENNEXTY ELSEIFRP(X)=RP(Y) THEN110ELSENEXTY
120 NEXTX
130 FORX=1TO6
140 RW(X)=RND(9):FORY=1TO6:IFY=X THENNEXTY ELSEIFRW(X)=RW(Y) THEN140 ELSENEXTY
150 NEXTX
160 KS=RND(6):KS$(KS)=SU$(KS):KW=RND(6):KW$(KW)=WP$(KW):KR=RND(6):KR$(KR)=RM$(KR):RW(KW)=0
170 TL=TL-1:IFTL=0 THEN1130 ELSESC=TL+SW:CLS:SOUND200,1:PRINT@37,"ROOM : ";RM$(RM)
180 PRINT@62,"l";:PRINT@94,"I";:PRINT@126,"S";:PRINT@158,"T";
190 PRINT@101,"TURNS LEFT :";TL;
200 PRINT@165,"YOU SEE : ";:IFSU$(RM)="" THEN PRINT"NOBODY" ELSEPRINTSU$(RM)
210 GOSUB810:PRINT@262,"*";
220 IFD(1)=1 THENPRINT@230,"N"; ELSEPRINT@230,"-";
230 IFD(2)=1 THENPRINT@263,"E"; ELSEPRINT@263,"-";
240 IFD(3)=1 THENPRINT@294,"S"; ELSEPRINT@294,"-";
250 IFD(4)=1 THENPRINT@261,"W"; ELSEPRINT@261,"-";
260 IFRP(RM)>0 AND QQ(RM)=0 OR QQ(RM)=2 THENPRINT@268,"qUESTION";
270 PRINT@332,"sEARCH";:PRINT@396,"mOVE";
280 A$=INKEY$:IFA$="M" THEN 290 ELSEIFA$="S"THEN330ELSEIFA$="Q"THEN390ELSEIFA$="L"THEN560ELSE280
290 SOUND100,1:IFQQ(RM)=2 THEN QQ(RM)=0
300 PRINT@396,"direction?"
310 D$=INKEY$:IFD$="N" OR D$="S" OR D$="E" OR D$="W" THEN320ELSE310
320 GOSUB920:SOUND1,10:GOTO300
330 PRINT@332,"search";:FORY=1TO350:NEXT:FORX=1TO5:PRINT".";:SOUND100,1:FORY=1TO350:NEXTY:NEXTX
340 IFRND(5)=1 AND TL>10 THEN730
350 IFRW(RM)=0 THEN 360 ELSEIFRND(5)=1 THEN360 ELSEIFWF(RM)=1THEN360 ELSE370
360 PRINT@332,"NOTHING FOUND.":SOUND1,10:GOSUB1340:GOTO170
370 FORX=1TO2:PRINT@332,"SEARCH";:FORY=1TO200:NEXTY:PRINT@332,"search";:SOUND200,1:FORY=1TO200:NEXTY:NEXTX
380 FORX=1TO350:NEXT:PRINT@332,WP$(RM);".":WF(RM)=1:SW=SW+5:GOSUB1340:GOTO170
390 IFRP(RM)=0 OR QQ(RM)=1 OR QQ(RM)=2 THEN 280ELSEIFRND(4)=1 THENPRINT@268,"SUSPECT REFUSES.":SOUND1,5:FORX=1TO200:NEXT:SOUND1,5:GOSUB1340:IFQQ(RM)=0 THEN QQ(RM)=2:GOTO170 ELSE 170
400 QQ(RM)=1:PRINT@268,"SUSPECT QUESTIONED.":SOUND200,1:PRINT@300,"ASK : ";SU$(RM):PRINT@332,"1) WHO DID IT?":PRINT@364,"2) WHAT WEAPON?":PRINT@396,"3) WHAT ROOM?":PRINT@428,"QUESTION";:INPUTQ:IFQ<1 OR Q>3 THEN400
410 PRINT@428,"***************";:PRINT@460,"*";:PRINT@474,"*";:PRINT@492,"***************";:PRINT@461,"";:FORY=1TO250:NEXTY:FORX=1TO13:PRINT".";:SOUND200,1:FORY=1TO250:NEXTY:NEXTX
420 ON Q GOTO 430,480,520
430 IFKS=RM THEN 440 ELSE460
440 BL=RND(6):IFBL=KS OR BL=RM THEN440
450 SS(BL)=SS(BL)+1:PRINT@461,SU$(BL):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
460 IFRND(3)=1THEN440
470 SS(KS)=SS(KS)+1:PRINT@461,SU$(KS):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
480 IFRND(2)=1 THEN490 ELSE510
490 BL=RND(6):IFBL=KW THEN 490
500 WS(BL)=WS(BL)+1:PRINT@461,WP$(BL):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
510 WS(KW)=WS(KW)+1:PRINT@461,WP$(KW):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
520 IFRND(3)=1 THEN530 ELSE550
530 BL=RND(9):IFBL=KR THEN530
540 RS(BL)=RS(BL)+1:PRINT@461,RM$(BL):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
550 RS(KR)=RS(KR)+1:PRINT@461,RM$(KR):PRINT@474,"*";:SOUND150,1:GOSUB1340:GOTO170
560 TL=TL+1:CLS:PRINT@0,"suspects":FORX=1TO6:PRINT@0+(32*X),SU$(X);" ";:IFSS(X)=0 THEN NEXTX ELSEFORY=1TO SS(X):PRINT"*";:NEXTY:NEXTX
570 PRINT@225,"weapons":FORX=1TO6:PRINT@224+(32*X),"";:IFWF(X)=1 THENPRINT">"; ELSEPRINT" ";
580 PRINTWP$(X);" ";:IFWS(X)=0 THEN NEXTX ELSEFORY=1TO WS(X):PRINT"*";:NEXTY:NEXTX
590 PRINT@17,"rooms":FORX=1TO9:PRINT@17+(32*X),RM$(X);:IFRS(X)=0 THENNEXTX ELSEIFRS(5)>0 THENPRINTRS(5);"M"; ELSEPRINTRS(X);:NEXTX
600 PRINT@369,"SOLVE(Y/N)";:INPUTS$:IFS$="Y" THEN610 ELSE170
610 SC=TL+SW:CLS:PRINT@1,"suspects":FORX=1TO6:PRINT@0+(32*X),X;") ";SU$(X)" ";:IFSS(X)=0 THEN NEXTX ELSEFORY=1TO SS(X):PRINT"*";:NEXTY:NEXTX
620 PRINT@257,"SUSPECT";:INPUTYS
630 CLS:PRINT@1,"weapons":FORX=1TO6:PRINT@0+(32*X),X;") ";:IFWF(X)>0 THENPRINT">"; ELSEPRINT" ";
640 PRINTWP$(X);" ";:IFWS(X)=0 THENNEXTX  ELSEFORY=1TO WS(X):PRINT"*";:NEXTY:NEXTX
650 PRINT@257,"WEAPON";:INPUTYW
660 CLS:PRINT@1,"rooms":FORX=1TO9:PRINT@0+(32*X),X;") ";RM$(X);" ";:IFRS(X)=0 THENNEXTX ELSEFORY=1TO RS(X):PRINT"*";:NEXTY:NEXTX
670 PRINT@353,"ROOM";:INPUTYR
680 FORX=1TO10:C=RND(8):CLS(C):SOUNDC*3,1:FORY=1TO50:NEXTY:NEXTX:CLS0:FORX=1TO2000:NEXT
690 CLS:IFYS<>KS THEN 1140ELSEPRINT@172,"SUSPECT":A=32-LEN(SU$(KS)):A=A/2:PRINT@256+A,SU$(KS):SOUND50,20:FORX=1TO500:NEXT:CLS0:FORX=1TO1500:NEXT
700 CLS:IFYW<>KW THEN 1140ELSEPRINT@173,"WEAPON":A=32-LEN(WP$(KW)):A=A/2:PRINT@256+A,WP$(KW):SOUND100,20:FORX=1TO500:NEXT:CLS0:FORX=1TO1500:NEXT
710 CLS:IFYR<>KR THEN 1140ELSEPRINT@174,"ROOM":A=32-LEN(RM$(KR)):A=A/2:PRINT@256+A,RM$(KR):SOUND150,20:FORX=1TO500:NEXT:CLS0:FORX=1TO1500:NEXT
720 SC=SC+60:WI=1:GOTO1140
730 X=RND(3)
740 ON X GOTO 750,760,770
750 TL=TL+1:PRINT@332,"FREE TIP":SOUND255,5:GOTO780
760 PRINT@332,"TIP - 5 TURNS";:SOUND250,5:INPUTT$:IFT$="N" THEN TL=TL+1:GOTO170 ELSEIFT$="Y" THEN TL=TL-4:GOTO780 ELSE760
770 PRINT@332,"TIP - 10 TURNS";:SOUND240,5:INPUTT$:IFT$="N" THEN TL=TL+1:GOTO170 ELSEIFT$="Y" THEN TL=TL-9:GOTO780 ELSE770
780 PRINT@364,"1) SUSPECT":PRINT@396,"2) WEAPON":PRINT@428,"3) ROOM":PRINT@460,"WHICH ONE";:INPUTQ:IFQ<1 OR Q>3 THEN780 ELSEPRINT@460," "
790 GOTO410
800 IFINKEY$=""THEN800ELSERUN
810 FORX=1TO4:D(X)=0:NEXT:ON RM GOTO 820,830,840,850,860,870,880,890,900,910
820 D(2)=1:D(4)=1:D(1)=1:RETURN
830 D(4)=1:D(2)=1:RETURN
840 D(4)=1:D(2)=1:RETURN
850 D(4)=1:D(2)=1:RETURN
860 D(4)=1:D(2)=1:RETURN
870 D(2)=1:D(3)=1:RETURN
880 D(4)=1:D(3)=1:RETURN
890 D(1)=1:D(2)=1:RETURN
900 D(1)=1:D(4)=1:RETURN
910 D(4)=1:D(2)=1:D(3)=1:RETURN
920 ON RM GOTO 930,950,970,990,1010,1030,1050,1070,1090,1110
930 IFD$="N" THEN RM=10 ELSEIFD$="E" THENRM=3 ELSEIFD$="W" THENRM=2 ELSERETURN
940 GOTO170
950 IFD$="W" THEN RM=8 ELSEIFD$="E" THEN RM=1 ELSERETURN
960 GOTO170
970 IFD$="W" THEN RM=1 ELSEIFD$="E" THEN RM=9 ELSERETURN
980 GOTO170
990 IFD$="W" THEN RM=6 ELSEIFD$="E" THEN RM=10ELSERETURN
1000 GOTO170
1010 IFD$="W" THEN RM=10 ELSEIFD$="E" THEN RM=7ELSERETURN
1020 GOTO170
1030 IFD$="E" THEN RM=4 ELSEIFD$="S" THEN RM=8 ELSERETURN
1040 GOTO170
1050 IFD$="W" THEN RM=5 ELSEIFD$="S" THEN RM=9 ELSERETURN
1060 GOTO170
1070 IFD$="N" THEN RM=6 ELSEIFD$="E" THEN RM=2 ELSERETURN
1080 GOTO170
1090 IFD$="N" THEN RM=7 ELSEIFD$="W" THEN RM=3 ELSERETURN
1100 GOTO170
1110 IFD$="W" THEN RM=4 ELSEIFD$="S" THEN RM=1 ELSEIFD$="E" THEN RM=5 ELSERETURN
1120 GOTO170
1130 PRINT@101,"turns left : 0";:SOUND200,30:GOTO610
1140 CLS:IFWI=1 THENPRINT@43,"YOU WON!!!":SOUND200,20:GOTO1160
1150 PRINT@43,"YOU LOST.":SOUND1,20
1160 IFYS=KS THEN SC=SC+40
1170 IFYW=KW THEN SC=SC+30
1180 IFYR=KR THEN SC=SC+50
1190 SC=SC*10
1200 PRINT@106,"SCORE :";SC
1210 PRINT@165,"KILLER : ";SU$(KS):PRINT@197,"WEAPON : ";WP$(KW):PRINT@229,"ROOM   : ";RM$(KR)
1220 PRINT@293,"YOUR SUSPECT":PRINT@357,"SUSPECT : ";SU$(YS):PRINT@389,"WEAPON  : ";WP$(YW):PRINT@421,"ROOM    : ";RM$(YR)
1230 IFWI=1 THENSCREEN0,1:FORX=1TO50000:NEXT
1240 GOTO1240
1250 FORX=227TO252:PRINT@X,CHR$(128);:NEXT:FORX=291TO316:PRINT@X,CHR$(128);:NEXT
1260 FORX=1TO6:US$(X)=SU$(X):PW$(X)=WP$(X):NEXT:FORX=1TO9:MR$(X)=RM$(X):NEXT
1270 EX$(1)=US$(RND(6)):EX$(2)=PW$(RND(6)):EX$(3)=MR$(RND(9))
1280 EX$(4)=US$(RND(6)):EX$(5)=PW$(RND(6)):EX$(6)=MR$(RND(9))
1290 IFEX$(1)=EX$(4) OR EX$(2)=EX$(5) OR EX$(3)=EX$(6) THEN1280
1300 EX$(3)=EX$(3)+"?":EX$(6)=EX$(6)+"?"
1310 P$(1)="WAS IT ":P$(2)="WITH THE ":P$(3)="IN THE ":P$(4)="OR ":P$(5)="WITH THE ":P$(6)="IN THE ":P$(7)="IT'S YOUR JOB TO FIND OUT":P$(8)="WHO THE MURDERER IS.":P$(9)="GOOD LUCK!!!"
1320 FORX=1TO9:M=32-LEN(P$(X)+EX$(X)):M=M/2:PRINT@256+M,P$(X);EX$(X):FORY=1TO1500:NEXTY:PRINT@256," ":NEXTX
1330 GOTO50
1340 A$=INKEY$:IFA$=""THEN1340
1350 RETURN
