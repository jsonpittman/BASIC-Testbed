0 'COPYRIGHT 1990 FALSOFT, INC.
1 'HOT GOLD' by Keiran Kenny,      Sydney, 1989.
10 CLEAR200:DL=200
20 DIMP(68)
30 FORT=1TO68:READL:P(T)=L:NEXT
40 DATA 0,1,2,3,4,32,36,64,96,128,160,192,193,194,195,196,164,132,131,7,8,9,10,11,39,71,103,135,167,199,200,201,202,203,171,139,107,75,43
50 DATA 14,46,78,110,142,174,206,207,208,209,210,21,53,85,117,149,181,213,214,215,216,185,153,121,89,57,24,23,22
60 CLS0
70 PRINT@0,"**HOT OR COLD!  LOOK FOR GOLD!**";
80 FORD=1TO2000:NEXT
90 H=5+RND(58):V=5+RND(26)
100 X=RND(64)-1:Y=2+RND(29):IFX=H ORY=V THEN100ELSESET(X,Y,2)
110 IFX<H THENHL=H-X ELSEHL=X-H
120 IFY<V THENVL=V-Y ELSEVL=Y-V
130 K$=INKEY$:IFK$<>CHR$(8)ANDK$<>CHR$(9)ANDK$<>CHR$(10)ANDK$<>CHR$(94)ORK$=""THEN130ELSERESET(X,Y)
140 PRINT@0,CHR$(31)
150 IFPEEK(341)=247THENY=Y-1:IFY+1<=V THENPRINT@14,"COLD!"ELSEIFY+1>V THENPRINT@14,"HOT!"
160 IFPEEK(342)=247THENY=Y+1:IFY-1>=V THENPRINT@14,"COLD!"ELSEIFY-1<V THENPRINT@14,"HOT!"
170 IFPEEK(344)=247THENX=X+1:IFX-1>=H THENPRINT@14,"COLD!"ELSEIFX-1<H THENPRINT@14,"HOT!"
180 IFPEEK(343)=247THENX=X-1:IFX+1<=H THENPRINT@14,"COLD!"ELSEIFX+1>H THENPRINT@14,"HOT!"
190 IFX=H ANDY=V THENRESET(X,Y):GOTO260
200 IFY<3THENY=3
210 IFY>31THENY=31
220 IFX<0THENX=0
230 IFX>63THENX=63
240 SET(X,Y,2):FORD=1TODL:NEXT:RESET(X,Y)
250 GOTO140
260 HV=131
270 FORZ=1TO68:SOUND60+Z,1:PRINT@HV+P(Z),CHR$(159);:NEXT
280 PRINT@6,"YOU STRUCK GOLD!!!"
290 FORD=1TO3000:NEXT
300 PRINT@1,"LUCKY YOU! LOOK FOR MORE? Y/N"
310 K$=INKEY$:IFK$=""THEN310
320 IFK$="Y"THENCLS0:GOTO70
330 IFK$="N"THENCLS:END
340 GOTO310
