0 CLEAR 400:DIM A$(4,5)
1 GOSUB 18 'DISPLAY TITLE
2 POKE 65495,0
3 FOR X=1 TO 5:GOSUB 21:NEXT X:POKE 65494,0 'DECODE DATA
4 ' PLAY SONG & DISPLAY WORDS
5 FOR X=1 TO 5
6 PRINT TAB(4);A$(1,X):PLAY "L8BL16BA#L8BL2G":PRINT TAB(4);A$(2,X):PLAY "L8GO+DL16DC#L8DL2O-A"
7 PRINT TAB(4);A$(3,X):PLAY "L8AO+DDDL2DL8D":PRINT TAB(4);A$(4,X):PLAY "CO-BAL2GL8G"
8 PRINT:PRINT
9 NEXT X
10 '  ENCODED DATA
11 DATA "UIF!QSFU.UJ.FTU!HJSM","UIBU!FWFS!J!TBX","XBT!TJQQJOH!DJ.EFS","UISP(!B!TUSBX"
12 DATA "J!UPME!UIBU!HJSM","J!EJEO(U!TFF!IPX","TIF!TJQQFE!DJEFS","UISPVHI!UIBU!TUSBX"
13 DATA "BOE!DIFFL!CZ!DIFFL","BOE!KBX!CZ!KBX","XF!TJQQFE!UIBU!DJEFS","UISPVHI!UIBU!TUSBX"
14 DATA "BOE!BMM!BU!PODF","UIBU!TUSBX!EJE!TMJQ<","J!TJQQFE!TPNF!DJEFS","GSPN!IFS!MJQ"
15 DATA "BOE!OPX!J(WF!HPU!NF",B!NPUIFS!JO!MBX","GSPN!TJQQJOH!DJEFS","UISPVHI!B!TUSBX"
16 GOSUB 25:END
17 ' TITLE SCREEN SUBROUTINE
18 CLS:PRINT@2,"SIPPING CIDER THROUGH A STRAW":PRINT @32+9,"BY DARREN DAY":PRINT STRING$(32,131)
19 RETURN
20 ' DECODE DATA SUBROUTINE
21  FOR Y=1 TO 4:READ L$
22 FOR P=1 TO LEN(L$)
23 B$=MID$(L$,P,1):C=ASC(B$)
24 C=C-1:A$(Y,X)=A$(Y,X)+CHR$(C):NEXT P:NEXT Y:RETURN
25 FOR Y=0 TO 31
26 FOR X=0 TO 63
27 SET (X,Y,0):NEXT X:NEXT Y
28 RETURN
