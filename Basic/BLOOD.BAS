0 GOTO600
1 REM ************************
2 REM *  KNOWING YOUR BODY   *
3 REM * HOW YOUR BLOOD WORKS *
4 REM * A GRAPHIC SIMULATION *
5 REM *   BY FRED B.SCERBO   *
6 REM *    60 HARDING AVE.   *
7 REM * NORTH ADAMS,MA 01247 *
8 REM *  COPYRIGHT (C) 1987  *
9 REM ************************
10 CLEAR1000:CLS0:PRINTSTRING$(32,172);
15 FORI=1TO288:READA:PRINTCHR$(A+128);:NEXT
20 PRINTSTRING$(32,163);
25 DATA,93,81,94,,93,91,84,94,85,92,92,90,93,80,85,,85,88,92,94,88,84,95,82,93,88,94,92,92,93,,
30 DATA85,93,82,,85,84,91,90,85,,,90,,90,90,90,90,,,90,,,90,93,87,,90,,83,83,,
35 DATA87,,93,82,87,,87,91,85,83,83,90,,85,,85,,,83,91,82,81,90,81,95,82,91,83,83,86,80,80
40 DATA18,,18,19,19,18,18,,17,17,19,19,19,,115,115,115,115,,113,115,115,115,113,115,115,114,113,,113,,
45 DATA26,,26,26,,26,26,,21,21,,,21,,117,,,117,,117,,,117,117,,,117,117,,117,,
50 DATA26,,26,26,,26,26,,21,21,16,,21,,117,,,117,,117,,,117,117,,,117,117,112,117,,
55 DATA20,22,16,26,,26,26,,21,21,28,29,28,,117,124,124,124,125,117,,,117,117,,,117,,121,120,,
60 DATA,26,,26,,26,26,,21,21,,20,26,,117,,,,117,117,,,117,117,,,117,,117,,,
65 DATA,26,,27,19,26,27,19,23,21,,,29,32,119,115,115,115,119,117,115,115,119,117,115,115,118,,117,,
70 PRINT@389," HOW YOUR BLOOD WORKS ";:PRINT@421," (T)ALKING OR (N)OT ? ";
75 PRINT@453,"   BY FRED B.SCERBO   ";
80 PRINT@485,"  COPYRIGHT (C) 1987  ";
85 X$=INKEY$:IFX$="T"THEN105
90 IFX$="N"THEN100
95 GOTO85
100 NT=1
105 CLS0
110 XX=&HFF00:YY=&HFF7E
115 POKEXX+1,52:POKEXX+3,63
120 POKEXX+35,60
125 PMODE4,1:PCLS1:PMODE4,5:PCLS1
130 DIMR(23),L$(26),Y(40):C$(1)="C1":C$(2)="C2":C$(3)="C3":C$(4)="C4"
135 FORI=1TO26:READL$(I):NEXT
140 GOTO270
145 AA$=JK$
150 A$=STR$(A):B$=STR$(B)
155 DRAW"BM"+A$+","+B$+C$(CL)
160 IF LEN(JK$)<=21THEN180
165 FOR T=21TO0STEP-1:IF MID$(JK$,T,1)=" "THEN175
170 NEXT T:GOTO180
175 L$=LEFT$(JK$,T):W$=L$:GOSUB185:JK$=" "+RIGHT$(JK$,(LEN(JK$))-T):GOTO150
180 W$=JK$:B=B+14:GOSUB185:RETURN
185 SL=LEN(W$):FORI=1TOSL:BB$=MID$(W$,I,1):C=ASC(BB$)-64:IF C=-32THEN DRAW"BR12":GOTO205
190 IF C=-18THENDRAW"BR2RBR9":GOTO205
195 IFC=-20THENDRAW"BR2R2D2G2E4BR7":GOTO205
200 DRAWL$(C)
205 NEXTI:B=B+14:RETURN
210 IFNT=1THEN250
215 FORII=1TOLEN(AA$)
220 IF PEEK(YY)AND 128=0 THEN220
225 POKEYY,ASC(MID$(AA$,II,1))
230 NEXTII
235 IFPEEK(YY)AND128=0THEN235
240 POKEYY,13
245 FORHH=1TO600:NEXTHH:RETURN
250 FORHH=1TO3000
255 X$=INKEY$:IFX$=CHR$(13)THEN265
260 NEXTHH
265 RETURN
270 PMODE2,1:PCLS1:SCREEN1,1:PMODE1:SCREEN1,1:PCLS0:POKE65314,248
275 PCLS3:A=0:B=56:CL=4:JK$=" PRESS RESET AND RUN IF SCREEN IS BLUE.":GOSUB145:B=B+20:JK$=" PRESS ENTER WHEN THE SCREEN IS RED.":GOSUB145
280 X$=INKEY$:IFX$<>CHR$(13)THEN280
285 PCLS0:R=3:BL=2
290 PMODE2,6:PMODE1,6:COLOR2,3:LINE(0,0)-(256,48),PRESET,BF:A=0:B=14:CL=4:JK$=" HUMAN BLOOD PLAYS A VERY BIG ROLE IN YOUR DAILY LIFE.":GOSUB145:PCOPY6TO1:GOSUB210
295 COLOR2,3:LINE(0,54)-(256,92),PSET,B:B=70:A=0:CL=1:JK$=" IT IS MUCH MORE THAN JUST A LIQUID.":GOSUB145:PCOPY6TO1:GOSUB210
300 COLOR2,3:LINE(0,98)-(256,180),PSET,BF:B=114:A=0:CL=1:JK$=" IT IS ACTUALLY MADE UP OF MILLIONS AND MILLIONS OF INDIVIDUAL LIVE CELLS.":GOSUB145:PCOPY7TO2:GOSUB210
305 PMODE2,3:PMODE1,3::PCLS4
310 FORI=2TO252STEP4:PSET(I,0,R):PSET(I+2,2,R):NEXTI:GET(0,0)-(256,2),Y,G:FORI=4TO96STEP4:PUT(0,I)-(256,I+2),Y,PSET:NEXTI
315 PCOPY3TO5
320 PCOPY3TO6:PMODE2,6:PMODE1,6:PCLS4:LINE(0,108)-(256,180),PRESET,B:A=0:B=126:CL=2:JK$=" THE LIQUID PART OF THE BLOOD IS A YELLOW COLORED FLUID CALLED PLASMA.":GOSUB145:PCOPY3TO1:PCOPY7TO2
325 GOSUB210
330 PCOPY5TO6:LINE(8,10)-(246,70),PSET,BF:A=0:B=24:CL=4:JK$=" PLASMA CONSISTS OF WATER, PROTEINS, SUGAR, SALTS AND OTHER SUBSTANCES.":GOSUB145:PCOPY6TO1
335 GOSUB210
340 GOSUB345:PCOPY8TO3:GOTO365
345 PCOPY5TO3:PMODE2,3:PMODE1,3:FORI=1TO10+TH
350 A=(RND(70)*3)+15:B=RND(66)+10:CIRCLE(A,B),26,BL,.6:PAINT(A,B),BL,BL:CIRCLE(A,B),26,R,.6:PAINT(A,B),R,R:CIRCLE(A,B),12,1,.6,.8,.4
355 CIRCLE(A,B),26,1,.6
360 NEXT:PCOPY3TO6:PCOPY3TO4:RETURN
365 PCOPY8TO7:PMODE2,6:PMODE1,6:LINE(0,108)-(256,164),PRESET,B:A=0:B=126:CL=3:JK$=" FLOATING IN THE PLASMA ARE MILLIONS OF RED BLOOD CELLS.":GOSUB145:PCOPY6TO1:PCOPY7TO2:PCOPY8TO7
370 GOSUB210:GOSUB345
375 PCOPY8TO7
380 COLOR3,2:PMODE2,6:PMODE1,6:LINE(0,108)-(256,164),PRESET,BF:A=0:B=126:CL=4:JK$=" THE RED BLOOD CELLS ARE ALSO KNOWN AS RED CORPUSCLES.":GOSUB145:PCOPY7TO2:PCOPY6TO1:GOSUB345:PCOPY8TO7
385 AA$=LEFT$(AA$,43)+"KOHR PUSSULS":GOSUB210
390 PMODE2,6:PMODE1,6:LINE(0,108)-(256,194),PRESET,B:A=0:B=126:CL=2:JK$=" THE COLOR OF RED CORPUSCLES COMES FROM THE HEMOGLOBIN IN EACH CELL WHICH CARRIES OXYGEN.":GOSUB145:PCOPY6TO1:PCOPY7TO2:GOSUB345:PCOPY8TO7
395 AA$="THE COLOR OF RED KOHR PUSSELS COMES FROM THE HE MO GLO BIN IN EACH CELL WHICH CARRIES OXYGEN":GOSUB210
400 PMODE2,6:PMODE1,6:LINE(0,108)-(256,184),PRESET,BF:A=0:B=126:CL=3:JK$=" RED BLOOD CELLS ONLY LAST FOR ABOUT ONE HUNDRED AND TWENTY DAYS.":GOSUB145:PCOPY6TO1:PCOPY7TO2:GOSUB345:PCOPY8TO7:GOSUB210
405 GOSUB410:GOTO425
410 PMODE2,6:PMODE1,6:X=2:GOSUB415:PAINT(60+WM,18),2,2:X=1:GOSUB415:PAINT(60+WM,18),4,1:RETURN
415 PMODE2,6:PMODE1,6:CIRCLE(60+WM,32),22,X,1.4,.56,1:DRAWC$(X)+"BM"+STR$(80+WM)+",32F4M+1,+5F2M+8,+3":CIRCLE(96+WM,60),20,X,.9,.75,.4:DRAW"BM-15,+26L2H4L2M-8,+2G4DG4L4H4L2H4U4H4L2H2L8H2U2H4U4H2U8E4R2U2R6U2R4U2E4"
420 CIRCLE(64+WM,40),10,1:PAINT(64+WM,40),1,1:RETURN
425 LINE(0,108)-(256,164),PRESET,B:A=0:B=126:CL=3:JK$=" WHITE BLOOD CELLS ARE ALSO FOUND IN OUR BLOOD.":GOSUB145:PCOPY6TO1:PCOPY7TO2
430 GOSUB210
435 PCOPY8TO7:GOSUB345:COLOR3,2:PMODE2,6:PMODE1,6:WM=96:GOSUB410:LINE(0,108)-(256,190),PRESET,BF:A=0:B=126:CL=4:JK$=" THE WHITE BLOOD CELLS PLAY A BIG PART IN HELPING FIGHT DISEASE AND INFECTIONS.":GOSUB145:PCOPY6TO1:PCOPY7TO2
440 GOSUB210
445 PCOPY8TO7:A=0:B=126:CL=2:LINE(0,108)-(256,190),PRESET,B:JK$=" THE WHITE BLOOD CELLS ATTACK AND DESTROY HARMFUL SUBSTANCES IN YOUR BLOOD.":GOSUB145:WM=0:GOSUB410:PCOPY4TO1:PCOPY7TO2
450 GOSUB210
455 PMODE2,6:PMODE1,6:PCOPY8TO7:FORWM=-22TO160STEP22:PCOPY4TO6:FORV=2TO14STEP4:CIRCLE(220,50),V,BL:NEXTV:GOSUB410:PCOPY6TO1:NEXTWM
460 PCOPY8TO7:A=0:B=126:CL=3:LINE(0,108)-(256,190),PRESET,B:JK$=" THE WHITE BLOOD CELLS DESTROY THE HARMFUL SUBSTANCE AND REMOVES IT FROM THE BLOOD.":GOSUB145:PCOPY4TO1:PCOPY7TO2
465 PMODE2,6:PMODE1,6:PCOPY8TO7:PCOPY8TO6:A=0:B=14:CL=1:COLOR2,3:LINE(0,0)-(256,62),PSET,BF:JK$=" THE BLOOD HAS A SPECIAL MECHANISM THAT HELPS IT TO REPAIR WOUNDS.":GOSUB145:PCOPY7TO2:PCOPY6TO1
470 AA$=LEFT$(AA$,58)+"WOONDS":GOSUB210
475 PMODE2,6:PMODE1,6:A=0:LINE(0,68)-(256,180),PRESET,BF:CL=4:JK$=" THIS MECHANISM USES TINY PLATELETS IN THE BLOOD WHICH HELP IT TO CLOT OR CLUMP WHEN YOU RECEIVE A CUT OR AN OPEN WOUND.":GOSUB145:PCOPY6TO1:PCOPY7TO2
480 AA$=LEFT$(AA$,113)+"WOOND":GOSUB210
485 PCOPY8TO6:PCOPY4TO7:PMODE2,6:PMODE1,6:A=0:B=16:CL=1:JK$=" THESE PLATELETS ARE TINY DISKS THAT STICK TO EACH OTHER WHEN DAMAGE OCCURS.":GOSUB145
490 GOSUB210
495 CL=3:JK$=" EX. PLATELET":GOSUB145:DRAW"BR10C1E8R2G8":GOSUB500:GOTO505
500 PMODE2,6:PMODE1,6:FORI=120TO180STEP10:FORY=1TO3:A=RND(180)+20:DRAW"BM"+STR$(A)+","+STR$(I)+"C1E8R2G8BF10F8REH2":NEXTY,I:RETURN
505 PCOPY6TO1:PCOPY7TO2
510 PMODE2,6:PMODE1,6:TH=20:GOSUB345:PCOPY6TO7:PCOPY8TO6:GOSUB500
515 A=0:B=16:CL=2:LINE(0,0)-(256,80),PSET,B:JK$=" WHEN BLOOD CLOTS OR CLUMPS INTO A SMALL PLUG A THROMBUS IS FORMED IN THE BLOOD STREAM.":GOSUB145
520 PCOPY7TO2:PCOPY6TO1:GOSUB210
525 PCOPY8TO6:PCOPY8TO7:PMODE2,6:PMODE1,6:A=0:B=16:CL=1:JK$=" IF A THROMBUS GOES TO THE HEART IT CAN CAUSE A HEART ATTACK.":GOSUB145
530 PCOPY7TO2:PCOPY6TO1
535 GOSUB210
540 A=0:CL=2:JK$=" IF A THROMBUS GOES TO THE BRAIN IT CAN CAUSE A STROKE AND BRAIN DAMAGE.":GOSUB145
545 PCOPY7TO2:PCOPY6TO1:GOSUB210
550 A=0:CL=3:JK$=" BOTH CAN BE FATAL.":GOSUB145
555 PCOPY7TO2:AA$="BOTH AN BE FAYTUL":GOSUB210:FORHH=1TO500:NEXTHH
560 PCOPY8TO6:PCOPY8TO7
565 PMODE2,6:PMODE1,6:COLOR2,3:LINE(0,0)-(256,82),PRESET,BF:A=0:B=14:CL=4:JK$=" THE BLOOD WILL WORK TO SERVE YOUR BODY AS LONG AS YOU WATCH AFTER YOUR HEALTH.":GOSUB145:PCOPY6TO1:PCOPY7TO2
570 AA$=LEFT$(AA$,73)+"HELLTH":GOSUB210
575 B=B+20:CL=1:JK$=" PRESS ENTER TO RETURN TO START.":GOSUB145:PCOPY6TO1:PCOPY7TO2
580 DATA U6E2R2F2D2NL4D4BR6,U8R4F2G2NL4F2G2NL4BR8,U8R4BD8NL4BR6,U8R4F2D4G2NL4BR8,U8NR4D4NR4D4R4BR6,U8NR4D4NR4D4BR10,U8R6BD4NL2D4NL4BR6,U4NU4R6U4D8BR6
585 DATA R2U8L2R4L2D8R2BR6,NU4R4U8L4R6BD8BR6,U8D4R2NE4F4BR6,NU8R4BR6,U8F4E4D8BR6,U8F6NU6D2BR6,U8R6D8NL6BR6,U8R6D4L6D4BR12,U8R6D8NL6NH4NF2BR6
590 DATA U8R6D4L4F4BR6,R6U4L6U4R6BD8BR6,BR4U8L4R8BD8BR6,NU8R6NU8BR6,BU8D4F4E4U4BD8BR6,NU8R4NU6R4NU8BR6,E8G4H4F8BR6,BU8D2F4ND2E4U2BD8BR6,NR8E8NL8BD8BR6
595 IFINKEY$=CHR$(13)THEN RUN ELSE595
600 PCLEAR8:RUN1
