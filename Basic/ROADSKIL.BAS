10 REM**************************
15 REM* ROAD SKILLS INSTRUCTOR *
20 REM*    BY FRED B.SCERBO    *
25 REM*     60 HARDING AVE     *
30 REM*  NORTH ADAMS,MA 01247  *
35 REM*   COPYRIGHT (C) 1987   *
40 REM**************************
45 CLEAR2000:R$=CHR$(128)
50 DIM AO(50),A$(50),B$(50),C$(50),NP(50),C(8)
55 CLS0:FORI=1TO32:PRINTCHR$(172);:NEXT
60 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
65 DATA126,124,122,126,124,122,126,124,122,125,124,125,,,30,28,26,29,,,30,20,30,20,30,16,20,30,,21,28,29
70 DATA122,,122,122,,122,122,96,122,117,,117,,,26,,24,21,16,22,16,,26,,26,,16,26,,21,,20
75 DATA123,115,122,122,,122,123,115,122,117,96,117,,,27,19,18,21,22,16,,,26,,26,,16,26,,21,19,19
80 DATA122,117,,122,,122,122,112,122,117,112,117,,,,,26,21,20,18,,,26,,26,,16,26,,,,21
85 DATA122,117,96,122,,122,122,,122,117,,117,,16,26,,26,21,,20,18,,26,,26,21,16,26,21,21,,21
90 DATA122,117,114,123,115,122,122,112,122,119,115,119,,,27,19,26,23,18,,27,17,27,17,27,23,17,27,23,21,19,23
95 FORI=1TO32:PRINTCHR$(163);:NEXT
100 PRINT@325," INSTRUCTIONAL QUIZ   ";
105 PRINT@357,"   BY FRED B.SCERBO   ";
110 PRINT@389,"  COPYRIGHT (C) 1987  ";
115 PRINT@453,"  (R)EVIEW OR (Q)UIZ  ";
120 SW=30:KZ=RND(-TIMER)
125 FORJ=1TO50
130 READA$(J),B$(J),C$(J):IF A$(J)="END"THEN140
135 NEXTJ
140 J=J-1
145 FORI=143TO255STEP16:KK=KK+1:C(KK)=I:NEXT
150 FORI=1TOJ
155 AO(I)=RND(J)
160 IF NP(AO(I))=1 THEN155
165 NP(AO(I))=1:NEXTI
170 X$=INKEY$:IFX$="R"THEN185
175 IFX$="Q"THEN355
180 GOTO170
185 CLS0:FORI=1024TO1535:POKEI,32:NEXT:FORP=1TOJ:WW=RND(8)
190 FORI=1024TO1055:POKEI,C(WW):POKEI+480,C(WW):NEXT
195 VL=96:JK$=A$(P)+" "+B$(P)+".":GOSUB375
200 GOSUB205:GOTO215
205 X$=INKEY$:IFX$<>CHR$(13)THEN205
210 RETURN
215 FORI=1088TO1120+VL:POKEI,32:NEXTI
220 NEXTP
225 CLS7:GOSUB230:GOTO235
230 FORI=1440TO1535:POKEI,32:NEXT:RETURN
235 FORY=0TO31:FORI=26TO37:RESET(I,Y):NEXTI,Y:PRINT@128,"";:FORI=1TO128:PRINTR$;:NEXTI
240 VL=384:JK$="NOW WE WILL LOOK AT SOME EXAMPLES OF THE RIGHT OF WAY  LAW.":GOSUB375
245 X$=INKEY$:IFX$<>CHR$(13)THEN245
250 GOSUB230:GOSUB255:GOSUB260:GOTO265
255 FORI=191TO180STEP-1:PRINT@I,"A";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:PRINT@I,"A";:RETURN
260 FORI=401TO305STEP-32:PRINT@I,"B";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:PRINT@I,"B";:RETURN
265 VL=384:JK$="AT THIS INTERSECTION, CAR A HAS THE RIGHT OF WAY OVER CAR B SINCE A IS ON B'S RIGHT.":GOSUB375
270 GOSUB205:GOSUB275:GOSUB280:GOTO285
275 FORI=179TO160STEP-1:PRINT@I,"A";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:RETURN
280 FORI=273TO17STEP-32:PRINT@I,"B";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:RETURN
285 GOSUB230:GOSUB290:GOSUB260:GOTO295
290 FORI=192TO203:PRINT@I,"A";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:PRINT@I,"A";:RETURN
295 VL=384:JK$="IN THIS CASE, CAR B HAS THE RIGHT OF WAY SINCE IT IS ENTERING ON CAR A'S RIGHT":GOSUB375
300 GOSUB205:GOSUB280:GOSUB305:GOSUB230:GOSUB310:GOSUB290:GOTO315
305 FORI=204TO223:PRINT@I,"A";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:RETURN
310 FORI=14TO78STEP32:PRINT@I,"B";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:PRINT@I,"B";:RETURN
315 GOSUB230:VL=384:JK$="THIS TIME, CAR A HAS THE RIGHT OF WAY SINCE IT IS ON CAR B'S RIGHT.":GOSUB375
320 GOSUB205:GOSUB305:GOSUB325:GOTO330
325 FORI=110TO396STEP32:PRINT@I,"B";:FORY=1TO20:NEXTY:PRINT@I,R$;:NEXTI:RETURN
330 GOSUB230:GOSUB310:GOSUB255:VL=384:JK$="THIS TIME, CAR B HAS THE RIGHT OF WAY SINCE IT IS ON CAR A'S RIGHT.":GOSUB375
335 GOSUB205:GOSUB325:GOSUB275
340 GOSUB230:FORY=0TO7:FORI=26TO37:SET(I,Y,7):NEXTI,Y:PRINT@179,CHR$(128);:PRINT@273,"A";:VL=384:JK$="AT A T-SHAPED INTERSECTION, CAR A MUST COME TO A COMPLETE STOP BEFORE TURNING.":GOSUB375
345 GOSUB205
350 RUN
355 CLS0:FORP=1TOJ
360 FORI=1056TO1503:POKEI,32:NEXT:WW=RND(8):FORI=1024TO1055:POKEI,C(WW):POKEI+480,C(WW):NEXT
365 VL=32
370 JK$=A$(AO(P))+" .......":GOSUB375:GOTO410
375 IFLEN(JK$)<=SW THEN 400
380 FOR T=SW TO0STEP-1:IFMID$(JK$,T,1)=" "THEN390 
385 NEXT T:GOTO400
390 L$=LEFT$(JK$,T):L$="  "+L$:GOSUB600:JK$=RIGHT$(JK$,(LEN(JK$))-T):GOTO375
395 GOTO395
400 L$="  "+JK$:GOSUB600
405 RETURN
410 VL=VL+64
415 D=RND(20):IFD=>11THEN425
420 F$=B$(AO(P)):H$=C$(AO(P)):J$="A":M$="B":GOTO430
425 F$=C$(AO(P)):H$=B$(AO(P)):J$="B":M$="A":GOTO430
430 JK$="A) "+F$:GOSUB375:VL=VL+32
435 JK$="B) "+H$:GOSUB375:VL=VL+32
440 G$=INKEY$:IF G$=""THEN440
445 IF G$=J$THEN465
450 IF G$=M$THEN465
455 IF G$="@"THEN525
460 GOTO440
465 IF G$=J$THEN480
470 GOTO490
475 IF C(F(G))<>AO(P) THEN490
480 L$="  YOU ARE CORRECT!":GOSUB600
485 CR=CR+1:GOTO515
490 L$="  NO! THAT IS INCORRECT!":GOSUB600
495 IR=IR+1
500 IFINKEY$<>CHR$(13)THEN500
505 FORI=1088TO1502:POKEI,32:NEXTI
510 VL=32:JK$="THIS IS WHAT THE CORRECT RESPONSE SHOULD BE.":GOSUB375:VL=VL+32:JK$=A$(AO(P))+" "+B$(AO(P))+".":GOSUB375
515 IFINKEY$<>CHR$(13)THEN515
520 NEXTP
525 CLS:PRINT:PRINT:PRINT
530 PRINT"    NUMBER CORRECT = "CR
535 PRINT
540 PRINT"    NUMBER WRONG   = "IR
545 J=CR+IR:IFJ=0THENJ=1
550 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/J);"%"
555 PRINT:PRINT"    ANOTHER TRY (Y/N/C)"
560 IF INT(CR*100/J)=>70THEN570
565 PRINT:PRINT"    YOU NEED TO STUDY MORE!!!":GOTO575
570 PRINT:PRINT"    NOT BAD AT ALL !!!"
575 W$=INKEY$:IF W$=""THEN575
580 IF W$="Y" THEN RUN
585 IF W$="C"THENCLS0:GOTO520
590 IF W$="N" THEN CLS:END
595 GOTO575
600 HL=0:VL=VL+32:DR=LEN(L$):FORK=1TO DR:ZV=ASC(MID$(L$,K,1)):IFZV<=64THENZV=ZV+64
605 POKE1024+VL+HL,ZV-64:HL=HL+1:NEXTK:RETURN
1000 DATA "IN MANY STATES, DURING THE ROAD TEST, THE OPERATOR MUST",USE HAND SIGNALS FOR TURNS,USE THE DIRECTIONAL LIGHTS
1010 DATA "AN INTERSECTING WAY IS WHERE",ANY ROADS JOIN AT AN ANGLE,ROADS BRANCH OFF
1020 DATA "THE RIGHT OF WAY LAW STATES THAT WHEN CARS ARE AT A FOUR WAY STOP AT THE SAME TIME, THE RIGHT OF WAY BELONGS TO",THE CAR ON THE RIGHT,THE CAR TURNING
1030 DATA "WHEN AN OPERATOR APPROACHES A PEDESTRIAN WHO IS IN THE STREET, THE DRIVER MUST",COME TO A STOP,SOUND HIS HORN
1040 DATA "WHEN AN OPERATOR APPROACHES AN INTERSECTION WITH A YIELD SIGN, THE DRIVER MUST",STOP UNTIL SAFE TO PROCEED,PROCEED SLOWLY
1050 DATA "WHEN AN OPERATOR APPROACHES A HORSE OR ANIMAL BEING DRIVEN, THE OPERATOR MUST",STOP IF THE ANIMAL IS FRIGHTENED,SOUND HIS HORN
1060 DATA "WHEN YOU ARE APPROACHED BY A FIRE VEHICLE, YOU MUST PULL TO THE",RIGHT UNTIL IT PASSES,LEFT UNTIL IT PASSES
1070 DATA "WHEN YOU ARE BEING PASSED BY ANOTHER VEHICLE, YOU  MUST STAY TO THE",RIGHT AND NOT BLOCK THE PASSER,CENTER AND LET THEM GO RIGHT
1080 DATA "WHEN APPROACHING A BLIND PERSON, AN OPERATOR MUST",STOP FOR THE PERSON CROSSING,SOUND THE HORN AND SLOW DOWN
1090 DATA "WHEN PASSING A VEHICLE GOING IN THE SAME DIRECTION, YOU MUST STAY TO",THE LEFT AND PASS WITH SAFE CLEARANCE,WHICHEVER SIDE HAS THE BEST CLEARANCE
1100 DATA "THE AMOUNT OF CLEAR AND UNOBSTRUCTED VIEW YOU MUST HAVE BEFORE OVERTAKING A VEHICLE BEING PASSED IS",FOUR HUNDRED FEET,TWO HUNDRED FEET
1110 DATA "WHEN APPROACHING OR PASSING A SCHOOL BUS WHICH HAS STOPPED TO ALLOW PASSENGERS ON OR OFF, YOU MUST",STOP UNTIL THE BLINKING LIGHTS ARE OFF,SOUND YOUR HORN AND SLOWLY PASS
1120 DATA "WHEN MAKING A LEFT HAND TURN ON A ONE WAY STREET, THE OPERATOR MUST SIGNAL, POSITION THE CAR IN THE LANE NEAREST THE",LEFT HAND CURB AND TURN,RIGHT HAND LANE AND TURN
1130 DATA "WHEN APPROACHING A CURVE OR CORNER WITH AN OBSTRUCTED VIEW, YOU MUST","SLOW DOWN AND KEEP TO THE RIGHT",SLOW DOWN AND SOUND THE HORN
1140 DATA "THE FUNDAMENTAL SPEED LAW IS THE REASONABLE SPEED FOR","TRAFFIC, ROADWAY USE AND SAFETY","THE CONDITION AND AGE OF THE CAR"
1150 DATA "THE PRINCIPLE CAUSE OF AUTOMOBILE ACCIDENTS, BOTH FATAL AND OTHERWISE IS",SPEED TOO FAST FOR CONDITIONS,DRIVING TOO FAR BELOW THE POSTED RATE
1160 DATA "AN OPERATOR IS REQURED TO DRIVE AT SPEEDS BELOW POSTED SPEEDS WHEN ROAD CONDITIONS ARE",NOT IDEAL DUE TO WEATHER OR TRAFFIC,MADE WORSE BY OLDER DRIVERS
1170 DATA "OUTSIDE A THICKLY SETTLED DISTRICT, AN UNREASONABLE SPEED IS ANYTHING ABOVE",40 MPH,30 MPH
1180 DATA "IN A THICKLY SETTLED DISTRICT, AN UNREASONABLE SPEED IS ANY SPEED ABOVE",30 MPH,40 MPH
1190 DATA "THE OPERATOR OF A VEHICLE EMRGING FROM A DRIVEWAY, OR A GARAGE MUST",STOP AND YIELD TO TRAFFIC,HONK AND PROCEED WITH CAUTION
1200 DATA "BEFORE MOVING AWAY FROM THE CURB OR PARKING POSITION, THE OPERATOR MUST YIELD TO TRAFFIC AND",SIGNAL HIS INTENT TO ENTER TRAFFIC,SIGNAL IF IT IS AT NIGHT
1210 DATA "WHEN A RED LIGHT SHOWS ALONE ON A TRAFFIC SIGNAL,THE OPERATOR MUST",STOP,SLOW DOWN AND BE PREPARED TO STOP
1220 DATA "A FLASHING RED LIGHT MEANS THE OPERATOR MUST",STOP AND PROCEED WHEN SAFE TO DO SO,PROCEED WHILE SOUNDING HORN
1230 DATA "WHEN A YELLOW LIGHT SHOWS ALONE ON A TRAFFIC SIGNAL, AN OPERATOR",MUST BE PREPARED TO STOP,SHOULD PROCEED BEFORE IT CHANGES TO RED
1240 DATA  "A FLASHING YELLOW LIGHT MEANS",SLOW DOWN AND PROCEED WITH CAUTION,JUST KEEP GOING
1250 DATA "A FLASHING GREEN LIGHT MEANS",GO IF IT IS SAFE,IT IS SAFE TO GO
1260 DATA "A RED AND YELLOW LIGHT TOGETHER MEANS","VEHICLES STOP, PEDESTRIANS CROSS","WATCH OUT FOR PEDESTRIANS
1270 DATA "YOU MAY PROCEED WHEN A RED LIGHT IS ILLUMINATED WHEN YOU ALSO SEE",GREEN ARROWS,FLASHING GREEN
1280 DATA "A RED-YELLOW-RED BAND ON THE POST OF A TRAFFIC CONTROL SIGNAL MEANS","PEDESTRIAN BUTTON, PREPARE TO STOP",NOTHING TO THE DRIVER OF A MOTOR VEHICLE
1290 DATA "WHEN YOU ARE DRIVING A CAR WHILE YOU HAVE A LEARNER'S PERMIT, THE LICENSED DRIVER MUST SIT",IN THE FRONT SEAT,DIRECTLY BEHIND YOU
5000 DATA END,,
