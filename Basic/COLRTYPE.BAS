0 ' COPYRIGHT 1990 FALSOFT, INC
10 CLEAR 1000 'COLRTYPE V:1
20 CLS:PRINT@ 104,"COLOR TYPEWRITER"
30 PRINT@ 166,"ROBERT ALLEN TURNER":PRINT@ 231,"(C) 25 APRIL 1989"
40 PRINT:PRINT:PRINT "LOAD & PRINT OLD FILE (Y/N)?"
50 A$=INKEY$:IF A$="Y" THEN 350 ELSE IF A$<>"N" THEN 50
60 PRINT@ 256,STRING$(32,"-");:PRINT "SET MARGINS (0-132):"
70 PRINT @320,STRING$(30,32);:PRINT @320,;
80 LINEINPUT "LEFT-->";L$:L=VAL(L$):IF L<0 OR L>132 THEN SOUND 150,1:PRINT "ILLEGAL! 0-132":GOTO 80
90 LINEINPUT "RIGHT->";R$:R=VAL(R$):IF R<0 OR R>132 THEN SOUND 150,1:PRINT "ILLEGAL! 0-132":GOTO 90
100 LINE INPUT "SPACING (1-3) 1=NORMAL, 2=DOUBLE3=TRIPLE--->";S$:S=VAL(S$):IF S<1 OR S>3 THEN SOUND 150,1:PRINT "ILLEGAL: 1-3":GOTO 100
110 PRINT STRING$(32,"-"):PRINT "SAVE <T>APE <D>ISK <N>O SAVE"
120 A$=INKEY$:IF A$=""THEN 120 ELSE IF A$="D" THEN X=1 ELSE IF A$="C" THEN X=-1 ELSE IF A$="N" THEN X=0 ELSE 120
130 SOUND 150,1:IF X<>0 THEN LINEINPUT "FILENAME (8 CHARACTERS)-------->";F$:F=LEN(F$):IF (F<1)+(F>8) THEN PRINT "ILLEGAL!":GOTO 130
140 IF X=1 THEN LINEINPUT "ENTER EXTENSION (3 CHARACTERS)->";X$:IF LEN(X$)>3 THEN SOUND 150,1:PRINT "3 CHARACTERS MAX.!":GOTO 140 ELSE F$=F$+"."+X$
150 CLS:IF X=-1 THEN PRINT "PREPARE CASSETTE PLAYER IN      RECORD MODE" ELSE IF X=1 THEN PRINT "INSERT TARGET DISK IN DRIVE:0"
160 SOUND 150,1:INPUT "PRESS [ENTER] WHEN READY:";A$
170 IF X<>0 THEN OPEN "O",X,F$
180 SOUND 150,1:CLS:A=R-L
190 PRINTCHR$(128);
200 B=0:C=1:T$=STRING$(132,32)
210 IF C<1 THEN C=1
220 C$=INKEY$:IF C$=""THEN 220 ELSE IF C$=CHR$(12) GOTO 300
230 IFC=1THENPRINTCHR$(8);" ";
240 IF C$=CHR$(8) THEN PRINT C$;C$;" ";C$;CHR$(128);:C=C-1:IF C<1 THEN C=1:SOUND 150,1:ELSEMID$(T$,C,1)=CHR$(32):GOTO 210
250 IF C=A THEN SOUND 150,1:SOUND 200,1:SOUND 150,1:B=1
260 IF B=1 THEN IF C$=CHR$(32) THEN C$=CHR$(13)
270 PRINTCHR$(8);:MID$(T$,C,1)=C$:C=C+1:PRINT C$;CHR$(128);:IF C$=CHR$(8) THEN MID$(T$,C-1,1)=" "
280 IF C$=CHR$(13) AND X><0 THEN PRINT #X,MID$(T$,1,C-1);
290 IF C$=CHR$(13) GOSUB 320:GOTO 200 ELSE 210
300 IF X><0 THEN CLOSE #X:PRINT:PRINT "DATA SAVED. YOU MAY REMOVE MEDIA"
310 PRINT #-2,CHR$(13);:END
320 PRINT #-2,TAB(L);MID$(T$,1,C-1);
330 IF S>1 THEN PRINT #-2,"":IF S>2 THEN PRINT #-2,""
340 RETURN
350 LINEINPUT "ENTER LEFT MARGIN (0-132)";L$:L=VAL(L$):IF L<0 OR L>132 THEN PRINT "ILLEGAL!":SOUND 150,1:GOTO 350
360 PRINT "SPACING (1,2,3)"
370 A$=INKEY$:IF A$=""THEN 370 ELSE A=VAL(A$):IF A<1 OR A>3 THEN 370
380 CLS:PRINT "<T>APE <D>ISK OR <E>XIT"
390 A$=INKEY$:IF A$="" THEN 390 ELSE IF A$="D" THEN X=1 ELSE IF A$="T" THEN X=-1 ELSE IF A$="E" THEN CLS:PRINT "BYE." ELSE SOUND 150,1:GOTO 390
400 LINEINPUT "FILENAME (8 CHARACTERS)-------->";F$:IF LEN(F$)>8 OR LEN(F$)<1 THEN SOUND 150,1:PRINT "ILLEGAL!":GOTO 400
410 IF X=1 THEN LINEINPUT "ENTER EXTENSION (3 CHARACTERS)->";X$:IF LEN(X$)>3 THEN PRINT "ILLEGAL! MAX. 3 CHARACTERS!":GOTO 410 ELSE F$=F$+"."+X$
420 IF X=1 THEN A$="DISK" ELSE A$="TAPE"
430 PRINT "INSERT "A$" CONTAINING FILE---->"F$
440 INPUT "PRESS [ENTER]";A$
450 OPEN "I",X,F$
460 LINEINPUT #X,C$
470 PRINT #-2,TAB(L);C$:IF A>1 THEN PRINT #-2,"":IF A>2 THEN PRINT #-2,""
480 IF EOF(X)=-1 THEN 500 ELSE IF INKEY$=CHR$(12) THEN 500
490 GOTO 460
500 CLOSE #1:PRINT "**DONE**":END
