10 '   --------------------
30 ' CUSTOM ICON SET CREATOR
40 '   --------------------
50 ' WRITTEN BY SHANE MESSER
60 '    COPYRIGHT (C) 1989
70 '     BY FALSOFT INC.     
80 '   ALL  RIGHTS RESERVED
90 '   --------------------
100 '        DESIGNED       
110 '  FOR THE 128K COCO 3
120 '  --------------------
130 POKE 65497,0:ON BRK GOTO 130:ON ERR GOTO 620
140 GOSUB 300:X=0:Y=0
150 GOTO 470
160 FOR T=338 TO 345:POKE T,255:NEXT T:A$=INKEY$:IF A$="" THEN A$=" "
170 IF A$="^" THEN Y=Y-1
180 O=ASC(A$):IF O=9 THEN X=X+1 ELSE IF O=10 THEN Y=Y+1
190 IF O=8 THEN X=X-1
200 IF O=13 THEN RETURN
210 IF O=83 THEN GOSUB 560
220 IF O=69 THEN GOSUB 540
230 IF X<MX THEN X=MX
240 IF Y<MY THEN Y=MY
250 IF X>XX THEN X=XX
260 IF Y>YY THEN Y=YY
270 POKE 65497,0
280 HGET(X*16-5,Y*15)-(X*16+8,Y*15+14),1:HCOLOR 15:HLINE(X*16-5,Y*15)-(X*16+8,Y*15+14),PSET,B:HPUT(X*16-5,Y*15)-(X*16+8,Y*15+14),1
290 GOTO 160
300 ON BRK GOTO 300:DIM PS(16):HSCREEN 2:HCLS 0:HBUFF 1,4000:HBUFF 2,2000:POKE &HE6C6,0
310 ON BRK GOTO 50:CMP:WIDTH 40:CLS 3:ATTR 2,4:CLS:FOR H=1 TO 3
320 ON BRK GOTO 320:CLS:PRINT"What is the filename of icon set"+STR$(H)+"      ->";:LINEINPUT F$
330 IF F$="" AND H>1 THEN 390 ELSE IF F$="" THEN CLS4:ATTR 2,4:CLS:PRINT"         No icons selected!":END
340 IF H=1 THEN A$=F$
350 POKE 65496,0:POKE &HE0D4,&H34:POKE &HFFA2,&H34:POKE 65496,0:LOADM F$:POKE &HFFA2,&H3A:POKE &HE0D4,&H34
360 HSCREEN 2:HPUT(10,(H-1)*45)-(220,H*45),1
370 WIDTH 40:CLS 3:ATTR 2,4:CLS
380 NEXT H
390 HSCREEN 2
400 IF INSTR(A$,".")>0 THEN A$=LEFT$(A$,INSTR(A$,".")-1)
410 OPEN"I",#1,A$:FOR K=1 TO 16:INPUT #1,PS(K):PALETTE K-1,PS(K):NEXT:CLOSE
420 POKE 64597,0:HSCREEN 2
430 HCOLOR 15:HLINE(222,0)-(319,191),PSET,B
440 FOR K=1 TO 20:READ A$:A=LEN(A$):A=12-A:A=INT(A/2):A$=STRING$(A," ")+A$:HPRINT(28,K),A$:NEXT
450 RETURN
460 DATA Icon Desg.,Customizer,------------,Written by,Shane,Messer,------------,Copyright,(c) 1989,By Falsoft,------------,,Options,-------,(E)xit ,(S)ave ,,,,,,,,,,,,,,,
470 ON BRK GOTO 470:MX=1:MY=0:XX=13:YY=8:GOSUB 160
480 FX=X:FY=Y
490 ON BRK GOTO 490:MX=1:MY=9:XX=13:YY=11
500 IF PEEK(338)=191 THEN 500
510 GOSUB 160
520 HGET(FX*16-5,FY*15)-(FX*16+8,FY*15+14),1:HPUT(X*16-5,Y*15)-(X*16+8,Y*15+14),1
530 GOTO 470
540 WIDTH 40:CLS 3:ATTR 2,4:CLS:LINEINPUT"                 Exit                              Are you sure ->";Y$
550 IF LEFT$(Y$,1)="Y" OR LEFT$(Y$,1)="y" THEN WIDTH 32:CLS:CMP:POKE 65496,0:END ELSE HSCREEN 2:RETURN
560 WIDTH 40:CLS 3:ATTR 2,4:CLS:LINEINPUT"                  Save                             Filename:";F$
570 IF F$="" THEN HSCREEN 2:RETURN
580 POKE 65496,0:HSCREEN 2:POKE &HE0D4,&H34:HGET(10,135)-(220,180),1:POKE &HFFA2,&H34:SAVEM F$,&H4000,&H5FFF,&HA027:POKE &HFFA2,&H3A
590 IF INSTR(F$,".")>0 THEN F$=LEFT$(F$,INSTR(F$,".")-1)
600 OPEN"O",#1,F$:FOR K=1 TO 16:WRITE #1,PS(K):NEXTK:CLOSE
610 HSCREEN 2:RETURN
620 POKE 65496,0:V=ERNO:ER$="Unknown Error!"
630 IF V=20 THEN ER$="I/O Error!" ELSE IF V=10 THEN ER$="Device number error!" ELSE IF V=31 OR V=26 THEN ER$="Unlocatale disk name!" ELSE IF V=23 THEN ER$="Input past end of file!" ELSE IF V=1 THEN ER$="Syntax Error in line"+STR$(ERLIN)
640 A$=ER$:WIDTH 40:CMP:CLS 4:ATTR 2,4:CLS:FOR K=1 TO 10:PRINT:NEXT K:K=LEN(A$):K=40-K:K=INT(K/2):LOCATE K,11:PRINTA$:END
