1 REM ************************
2 REM * RECOGNIZING COMPLETE *
3 REM * SENTENCES & FRAGMENTS*
4 REM *   BY FRED B.SCERBO   *
5 REM *    60 HARDING AVE.   *
6 REM * NORTH ADAMS,MA 01247 *
7 REM *  COPYRIGHT (C) 1987  *
8 REM ************************
9 CLEAR2000
10 CLS0:FORI=1TO64:PRINTCHR$(172);:NEXT
15 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
20 DATA94,92,92,88,53,60,60,53,60,56,59,48,58,60,61,60,56,62,60,53,50,53,53,60,60,53,60,56,,94,92,92
25 DATA90,,,,52,60,61,53,56,,58,57,58,,53,,48,62,48,53,52,55,53,,,53,56,,,90,,
30 DATA94,92,92,,52,60,60,52,60,56,56,,56,,52,,48,60,60,52,48,52,52,60,60,52,60,56,,92,92,93
35 DATA90,,94,92,93,85,92,92,93,85,92,92,93,85,92,94,93,85,92,92,85,82,,90,92,92,94,92,88,,,85
40 DATA90,80,91,83,87,85,83,83,87,85,,81,83,85,,90,85,85,92,92,85,84,82,90,,,90,,,,,85
45 DATA90,,90,84,83,85,,,85,85,83,83,87,85,80,80,85,85,83,83,85,,84,90,,,90,,83,83,83,87
50 FORI=1TO64:PRINTCHR$(163);:NEXT
55 PRINT@357," RECOGNIZING COMPLETE  ";:PRINT@389," SENTENCES & FRAGMENTS ";:PRINT@421," (T)ALKING OR (N)OT ?  ";
60 PRINT@453,"   BY FRED B.SCERBO    ";
65 PRINT@485,"  COPYRIGHT (C) 1987   ";
70 X$=INKEY$:IFX$="T"THEN90
75 IFX$="N"THEN85
80 GOTO70
85 NT=1:GOTO150
90 CLS0
95 XX=&HFF00:YY=&HFF7E
100 POKEXX+1,52:POKEXX+3,63
105 POKEXX+35,60:GOTO150
110 IFNT=1THENRETURN
115 FORII=1TOLEN(AA$)
120 IF PEEK(YY)AND 128=0 THEN120
125 POKEYY,ASC(MID$(AA$,II,1))
130 NEXTII
135 IFPEEK(YY)AND128=0THEN135
140 POKEYY,13
145 RETURN
150 DIM AO(51),A$(51),B$(51),C$(51),N(51)
155 CLS0:PRINT@264," PLEASE STAND BY ";
160 SW=28:KZ=RND(-TIMER)
165 FORJ=1TO51
170 READ A$(J),B$(J),C$(J):IF A$(J)="END" THEN180
175 NEXTJ
180 J=J-1
185 FORI=1 TO J
190 AO(I)=RND(J)
195 IFN(AO(I))=1THEN190
200 N(AO(I))=1:NEXTI
205 FOR Y=1TO1000:NEXTY
210 CLS
215 FOR P=1TOJ
220 IF P>J THEN RUN
225 CLS:PRINT"   EXAMPLE NUMBER";P"."
230 PRINT
235 AA$="   IS THE FOLLOWING A COMPLETE     SENTENCE OR A SENTENCE          FRAGMENT ?":PRINTAA$:GOSUB110:PRINT
240 FORHH=1TO2000:NEXT
245 JK$=A$(AO(P)):IF RIGHT$(JK$,1)="?"THEN255
250 JK$=JK$+"."
255 GOSUB260:GOTO285
260 IF LEN(JK$)<=SW THEN 280
265 FOR T= SW TO 0STEP-1:IF MID$(JK$,T,1)=" "THEN275 
270 NEXT T:GOTO280
275 L$=LEFT$(JK$,T):PRINT"   ";L$:AA$=L$:GOSUB110:JK$=RIGHT$(JK$,(LEN(JK$))-T):GOTO260
280 PRINT"   ";JK$:AA$=JK$:GOSUB110:RETURN
285 PRINT
290 PRINT"      C)OMPLETE SENTENCE":PRINT"              OR"
295 PRINT"      S)ENTENCE FRAGMENT"
300 AA$="PRESS C FOR COMPLETE SENTENCE OR S FOR SENTENCE FRAGMENT":GOSUB110
305 G$=INKEY$:IF G$=""THEN305
310 IF G$="C"THEN330
315 IF G$="S"THEN330
320 IF G$="@"THEN425
325 GOTO305
330 IF G$=B$(AO(P))THEN 345
335 GOTO355
340 IF C(F(G))<>AO(P) THEN355
345 PRINT:AA$="   YOU ARE CORRECT!":PRINTAA$:GOSUB110
350 CR=CR+1:GOTO375
355 PRINT:AA$="   WRONG! THIS STATEMENT IS A":PRINTAA$:GOSUB110
360 IF B$(AO(P))="S" THEN AA$="       SENTENCE FRAGMENT.":PRINTAA$:GOSUB110
365 IF B$(AO(P))="C" THEN AA$="      COMPLETE SENTENCE.":PRINTAA$:GOSUB110
370 IR=IR+1
375 IFB$(AO(P))="C"THEN405
380 X$=INKEY$:IFX$<>CHR$(13)THEN380
385 CLS:PRINT:JK$="IN ORDER TO BE A COMPLETE SENTENCE, THIS STATEMENT COULD BE READ AS FOLLOWS:":GOSUB260:PRINT:FORH=1TO2500:NEXTH
390 JK$=A$(AO(P))+" "+C$(AO(P)):GOSUB260:PRINT:FORH=1TO2000:NEXTH
395 JK$="THIS EXPRESSES A COMPLETE THOUGHT.":GOSUB260:FORH=1TO2000:NEXTH
400 PRINT:AA$="  <<PRESS ENTER TO CONTINUE.>>":PRINTAA$:GOSUB110
405 X$=INKEY$:IFX$=CHR$(13)THEN420
410 IFX$="@"THEN P=P+1:GOTO425
415 GOTO405
420 NEXTP
425 CLS:PRINT:PRINT:PRINT
430 PQ=CR+IR:IF PQ=0THEN PQ=1
435 PRINT"    NUMBER CORRECT = "CR
440 PRINT
445 PRINT"    NUMBER WRONG   = "IR
450 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/PQ);"%"
455 PRINT:PRINT"    ANOTHER TRY (Y/N/C)";
460 W$=INKEY$:IF W$=""THEN460
465 IF W$="C" THEN 220
470 IF W$="Y" THEN RUN
475 IF W$="N" THEN CLS:END
480 GOTO460
990 REM ENTER DATA AT LINE 1000
1000 DATA THEY THREW A PARTY ON SUNDAY,C,OK
1010 DATA AS THEY WERE LEAVING THE PARTY,S,", IT STARTED TO RAIN."
1020 DATA THE CHILDREN RACED TO THE CAR,C,OK
1030 DATA SITTING ON A PILLOW,S,WAS A BIG BLACK SLEEPING CAT.
1040 DATA THE ROCKET SHOT ACROSS THE SKY,C,OK
1050 DATA HER BROTHER WHO LIVED IN KANSAS,S,WAS PLANNING TO VISIT HER IN THE SPRING.
1060 DATA HARVEY CRIED,C,OK
1070 DATA THE SUN ALMOST HIDDEN BY THE CLOUDS,S,TRIED TO SHOW ITS FACE ALL DAY LONG.
1080 DATA "AS THEY LEFT, SHE SLIPPED ON THE ICE",C,OK
1090 DATA THE FIRE ENGINE SCREAMING DOWN THE STREET,S,WOKE UP THE SLEEPING BABY.
1100 DATA THE OLD CAR SPUTTERED TO A HALT,C,OK
1110 DATA AFTER THE LONG PARTY,S,WE ALL DECIDED TO GO HOME
1120 DATA "AFTER THE FOOTBALL GAME, THEY WENT OUT TO DINNER",C,OK
1130 DATA WITH ALL HIS SUITCASES PACKED,S,", HE LEFT TOWN IN A HURRY."
1140 DATA HE WANTED TO JOIN OUR CAMERA CLUB,C,OK
1150 DATA WHERE WERE YOU STANDING?,C,OK
1160 DATA PLEASE CARRY THIS UMBRELLA FOR ME,C,OK
1170 DATA THE GIRLS WERE WEARING BLUE JEANS,C,OK
1180 DATA "WITHOUT LOOKING, JIMMY STARTED FORWARD",C,OK
1190 DATA THE UNHAPPY OLD MAN SAT ALONE UNTIL DARK,C,OK
1200 DATA I SHALL STAY HERE?,C,OK
1210 DATA WHAT IS THE DIFFERENCE BETWEEN PAINT AND SHELLAC?,C,OK
1220 DATA THREE BOYS STUCK THEIR HEADS OUT OF THE WATER,C,OK
1230 DATA THE LITTLE DOG STOOD OBEDIENTLY BY HIS MASTER,C,OK
1240 DATA MAY I DRIVE THE CAR?,C,OK
1250 DATA THE MEETING WAS HELD IN LOS ANGELES,C,OK
1260 DATA THE BRIGHT SUNLIGHT SHINING THROUGH THE WINDOW,S,BLINDED ME AS I GOT UP FROM BED.
1270 DATA THE WAY THE GIANT LIFTED THE WHOLE TREE,S,AMAZED THE LITTLE CHILDREN.
1280 DATA TO PUT OUT THE FIRE,S,YOU SHOULD SMOTHER IT.
1290 DATA AS THE PRESIDENT BOARDED THE PLANE,S,THE CAMERA FOLLOWED HIS EVERY MOVE.
1300 DATA JUST IN CASE YOU DIDN'T KNOW,S,", TODAY IS MY BIRTHDAY."
1310 DATA COMING THROUGH THE TUNNEL AT 80 MPH,S,WAS THE BRIGHT NEW TRAIN
1320 DATA ALL THE IMPORTANT PEOPLE COMING TO DINNER,S,WILL EXPECT TO BE TREATED PROPERLY.
1330 DATA HAVING TOO MUCH MONEY,S,IS THE LEAST OF MY PROBLEMS!
1340 DATA THE ROOM ALMOST FILLED WITH SMOKE,C,OK
1350 DATA WHEN THE OLD MAN DIED,S,HE WAS ALL ALONE.
1360 DATA AS I WAS ON MY WAY TO THE STATE CAPITOL,S,I HEARD THE NEWS.
1370 DATA SOMETHING IN THE WAY SHE MOVES,S,ATTRACTS ME LIKE NO OTHER LOVER
1380 DATA YOU GIVE LOVE A BAD NAME,C,OK
1390 DATA EVERYTIME YOU GO AWAY,S,", YOU TAKE AWAY A PART OF ME."
1400 DATA 'WANTED DEAD OR ALIVE',S,IS THE BEST SONG I KNOW!
1410 DATA EVERBODY HAVE FUN TONIGHT,C,OK
1420 DATA I WISH THEY ALL COULD BE CALIFORNIA GIRLS,C,OK
1430 DATA 'LIVING ON A PRAYER',S,IS ANOTHER SONG BY BON JOVI!
1440 DATA LET ME BE THE ONE,C,OK
1450 DATA SINCE YOU'VE BEEN GONE,S,", THERE'S SOMETHING I WANT TO SAY."
1460 DATA DON'T DREAM IT'S OVER,C,OK
1470 DATA HERE I GO AGAIN,C,OK
1480 DATA I SHOULD HAVE KNOWN BETTER,C,OK
1490 DATA HAVE IT YOUR WAY,C,OK
5000 DATA END,END,END
1 REM ************************
2 REM *  RECOGNIZING SIMPLE  *
3 REM * SUBJECTS & PREDICATES*
4 REM *   BY FRED B.SCERBO   *
5 REM *    60 HARDING AVE.   *
6 REM * NORTH ADAMS,MA 01247 *
7 REM *  COPYRIGHT (C) 1987  *
8 REM ************************
9 CLEAR3000
10 CLS0:FORI=1TO64:PRINTCHR$(156);:NEXT
15 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
20 DATA126,124,124,120,53,60,60,53,60,56,59,48,58,60,61,60,56,62,60,53,50,53,53,60,60,53,60,56,112,126,124,124
25 DATA122,,,,52,60,61,53,56,,58,57,58,,53,,48,62,48,53,52,55,53,,,53,56,,112,122,,
30 DATA123,115,115,114,52,60,60,52,60,56,56,,56,,52,,48,60,60,52,48,52,52,60,60,52,60,56,112,123,115,115
35 DATA80,,80,122,124,125,124,120,126,124,122,122,80,117,117,124,124,124,116,124,126,124,117,80,80,122,126,124,122,122,112,112
40 DATA80,80,80,122,80,117,80,80,126,126,120,122,80,117,117,80,80,80,80,80,122,80,117,80,,122,126,126,120,122,,80
45 DATA123,115,115,122,80,117,112,,122,116,114,123,115,119,117,115,115,115,112,80,122,,117,115,115,122,122,116,114,123,115,115
50 FORI=1TO64:PRINTCHR$(147);:NEXT
55 PRINT@357,"  RECOGNIZING SIMPLE   ";:PRINT@389," SUBJECTS & PREDICATES ";:PRINT@421," (T)ALKING OR (N)OT ?  ";
60 PRINT@453,"   BY FRED B.SCERBO    ";
65 PRINT@485,"  COPYRIGHT (C) 1987   ";
70 X$=INKEY$:XX=RND(-TIMER):IFX$="T"THEN90
75 IFX$="N"THEN85
80 GOTO70
85 NT=1:GOTO150
90 CLS0
95 XX=&HFF00:YY=&HFF7E
100 POKEXX+1,52:POKEXX+3,63
105 POKEXX+35,60:GOTO150
110 IFNT=1THENRETURN
115 FORII=1TOLEN(AA$)
120 IF PEEK(YY)AND 128=0 THEN120
125 POKEYY,ASC(MID$(AA$,II,1))
130 NEXTII
135 IFPEEK(YY)AND128=0THEN135
140 POKEYY,13
145 FORI=1TO1000:NEXT:RETURN
150 SW=31
155 CLS0
160 DIMAO(50),A$(50),SS$(50),SP$(50),NP(50)
165 CLS0:GOTO205
170 AA$=JK$:GOSUB110
175 IF LEN(JK$)<=SW THEN195
180 FOR T=SW TO 0STEP-1:IF MID$(JK$,T,1)=" "THEN190
185 NEXT T:GOTO195
190 L$=LEFT$(JK$,T):W$=L$:GOSUB200:JK$="   "+RIGHT$(JK$,(LEN(JK$))-T):GOTO175
195 W$=JK$:PRINTW$:RETURN
200 PRINTW$:RETURN
205 FORJ=1TO50:READ A$(J),SS$(J),SP$(J):IF A$(J)="END" THEN215
210 NEXTJ
215 REM START QUIZ
220 J=J-1:FORI=1 TO J
225 AO(I)=RND(J)
230 IF NP(AO(I))=1 THEN 225
235 NP(AO(I))=1:NEXTI
240 FOR Y=1TO1000:NEXTY
245 GOTO395
250 CLS
255 PRINT@35,"HERE IS EXAMPLE NUMBER";P
260 RETURN
265 AA$="   WHAT IS THE SIMPLE SUBJECT      OF THIS SENTENCE ?"::GOSUB110:PRINT@96,AA$
270 PRINT@192,"";:JK$="   "+A$(AO(P)):GOSUB170
275 PRINT:PRINT"   => ";:LINEINPUTA$
280 IFA$="@"THENGOSUB480
285 IF FX=1 THEN RETURN
290 IF A$<>SS$(AO(P))THEN305
295 PRINT:JK$="   YOU ARE CORRECT! THE SIMPLE SUBJECT IS: "+SS$(AO(P)):GOSUB170
300 CR=CR+1:GOTO315
305 PRINT:JK$="   WRONG! THE CORRECT SIMPLE SUBJECT IS: "+SS$(AO(P)):GOSUB170
310 IR=IR+1
315 GOSUB475
320 X$=INKEY$:IFX$<>CHR$(13)THEN320
325 RETURN
330 AA$="   WHAT IS THE SIMPLE              PREDICATE OF THIS SENTENCE ?":GOSUB110:PRINT@96,AA$
335 PRINT@192,"";:JK$="   "+A$(AO(P)):GOSUB170
340 PRINT:PRINT"   => ";:LINEINPUTA$
345 IFA$="@"THENGOSUB480
350 IF FX=1 THEN RETURN
355 IF A$<>SP$(AO(P))THEN370
360 PRINT:JK$="   YOU ARE CORRECT! THE SIMPLE PREDICATE IS: "+SP$(AO(P)):GOSUB170
365 CR=CR+1:GOTO380
370 PRINT:JK$="   SORRY! THE CORRECT SIMPLE PREDICATE IS: "+SP$(AO(P)):GOSUB170
375 IR=IR+1
380 GOSUB475
385 X$=INKEY$:IFX$<>CHR$(13)THEN385
390 RETURN
395 FORP=1TOJ
400 WW=RND(2):IFWW=1THEN410
405 FX=0:GOSUB250:GOSUB265:GOSUB250:GOSUB330:GOTO415
410 FX=0:GOSUB250:GOSUB330:GOSUB250:GOSUB265
415 CLS:JK$="   HERE IS HOW WE WOULD DIAGRAM THE SIMPLE SUBJECT AND THE SIMPLE PREDICATE.":PRINT@32,"";:GOSUB170
420 PRINT:JJ=LEN(SS$(AO(P))):PRINTSTRING$(JJ+6,32);CHR$(133)
425 PRINT"     "SS$(AO(P));" "CHR$(133)" ";SP$(AO(P))
430 PRINT"   "STRING$(26,131)
435 PRINT@230+JJ,CHR$(129);
440 PRINT@262+JJ,CHR$(133)
445 PRINT@320,"  SIMPLE       SIMPLE"
450 PRINT"  SUBJECT      PREDICATE"
455 GOSUB475
460 X$=INKEY$:IFX$<>CHR$(13)THEN460
465 NEXT P
470 GOSUB480:RUN
475 PRINT@483," PRESS <ENTER> TO CONTINUE";:RETURN
480 CLS:PRINT:PRINT:PRINT
485 PQ=CR+IR:IF PQ=0THEN PQ=1
490 PRINT"    NUMBER CORRECT = "CR
495 PRINT
500 PRINT"    NUMBER WRONG   = "IR
505 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/PQ);"%"
510 PRINT:PRINT"    ANOTHER TRY (Y/N/C)";
515 W$=INKEY$:IF W$=""THEN515
520 IF W$="C" THEN FX=1:RETURN
525 IF W$="Y" THEN RUN
530 IF W$="N" THEN CLS:END
535 GOTO515
990 REM ENTER DATA AT LINE 1000
1000 DATA AROUND HER HEAD SHE WORE A YELLOW RIBBON.,SHE,WORE
1010 DATA IT LOOKS LIKE IT IS GOING TO RAIN.,IT,LOOKS
1020 DATA I WILL BE LEAVING WHEN THE SUN COMES OUT.,I,WILL BE LEAVING
1030 DATA GET ME A PLATE OF THAT PASTA WITH MEATBALLS.,YOU,GET
1040 FIVE TIRED MEN PLODDED ACROSS THE MUDDY FIELD.,MEN,PLODDED
1050 DATA THE SIGHT OF THE TOWN IN THE DISTANCE GAVE THEM HOPE.,SIGHT,GAVE
1060 DATA HE WAS WEARING A PINK SHIRT.,HE,WAS WEARING
1070 DATA WE SAW HER DANCING ALL NIGHT LONG.,WE,SAW
1080 DATA MUCH FRUIT GROWS RAPIDLY IN THE SUNNY VALLEY DOWN SOUTH.,FRUIT,GROWS
1090 DATA THOSE EXPENSIVE JEWELS IN THE WINDOW ARE NOT FOR YOU.,JEWELS,ARE
5000 DATA END,END,END
1 REM ************************
2 REM *  RECOGNIZING DIRECT  *
3 REM * AND INDIRECT OBJECTS *
4 REM *   BY FRED B.SCERBO   *
5 REM *    60 HARDING AVE.   *
6 REM * NORTH ADAMS,MA 01247 *
7 REM *  COPYRIGHT (C) 1988  *
8 REM ************************
9 CLEAR3000
10 CLS0:FORI=1TO64:PRINTCHR$(172);:NEXT
15 FORI=1TO192:READA:PRINTCHR$(A+128);:NEXT
20 DATA126,124,124,120,53,60,60,53,60,56,59,48,58,60,61,60,56,62,60,53,50,53,53,60,60,53,60,56,112,126,124,124
25 DATA122,,,,52,60,61,53,56,,58,57,58,,53,,48,62,48,53,52,55,53,,,53,56,,112,122,,
30 DATA123,115,115,114,52,60,60,52,60,56,56,,56,,52,,48,60,60,52,48,52,52,60,60,52,60,56,112,123,115,115
35 DATA80,,80,122,124,125,124,120,126,124,122,122,80,117,117,124,124,124,116,124,126,124,117,80,80,122,126,124,122,122,112,112
40 DATA80,80,80,122,80,117,80,80,126,126,120,122,80,117,117,80,80,80,80,80,122,80,117,80,,122,126,126,120,122,,80
45 DATA123,115,115,122,80,117,112,,122,116,114,123,115,119,117,115,115,115,112,80,122,,117,115,115,122,122,116,114,123,115,115
50 FORI=1TO64:PRINTCHR$(163);:NEXT
55 PRINT@357,"  RECOGNIZING DIRECT   ";:PRINT@389," AND INDIRECT OBJECTS  ";:PRINT@421," (T)ALKING OR (N)OT ?  ";
60 PRINT@453,"   BY FRED B.SCERBO    ";
65 PRINT@485,"  COPYRIGHT (C) 1988   ";
70 X$=INKEY$:XX=RND(-TIMER):IFX$="T"THEN90
75 IFX$="N"THEN85
80 GOTO70
85 NT=1:GOTO150
90 CLS0
95 XX=&HFF00:YY=&HFF7E
100 POKEXX+1,52:POKEXX+3,63
105 POKEXX+35,60:GOTO150
110 IFNT=1THENRETURN
115 FORII=1TOLEN(AA$)
120 IF PEEK(YY)AND 128=0 THEN120
125 POKEYY,ASC(MID$(AA$,II,1))
130 NEXTII
135 IFPEEK(YY)AND128=0THEN135
140 POKEYY,13
145 FORI=1TO1000:NEXT:RETURN
150 SW=31
155 CLS:PRINT@228,"(I)NSTRUCTION OR (Q)UIZ";
160 X$=INKEY$:IFX$="Q"THEN250ELSEIFX$="I"THEN165ELSE160
165 CLS0
170 CLS:PRINT:JK$="   IN THIS PROGRAM WE WILL QUIZ YOUR KNOWLEDGE OF DIRECT OBJECTS AND INDIRECT OBJECTS.":GOSUB260
175 PRINT:JK$="   BEFORE WE DO THAT WE MUST DEFINE SOME TERMS.":GOSUB260
180 PRINT:JK$="   FIRST, A DIRECT OBJECT IS A WORD IN A SENTENCE WHICH RECEIVES THE ACTION EXPRESSED BY THE VERB.":GOSUB260
185 GOSUB530
190 CLS:PRINT:JK$="   IN THE SENTENCE, 'I GAVE MY GIRLFRIEND A DIAMOND RING', THE WORD 'RING' IS THE DIRECT OBJECT.":GOSUB260
195 PRINT:JK$="   'RING' RECEIVES THE ACTION EXPRESSED BY THE VERB 'GAVE'.":GOSUB260
200 PRINT:JK$="   THE DIRECT OBJECT ANSWERS THE QUESTION 'GAVE WHAT?'. I 'GAVE' THE 'RING' (DIRECT OBJECT).":GOSUB260
205 GOSUB530
210 CLS:PRINT:JK$="   THE INDIRECT OBJECT NAMES THE THING OR PERSON TO WHOM THE ACTION IS DIRECTED.":GOSUB260
215 PRINT:JK$="   IN THE SENTENCE, 'I GAVE MY GIRLFRIEND A DIAMOND RING', THE WORD 'GIRLFRIEND' IS THE INDIRECT OBJECT.":GOSUB260
220 PRINT:JK$="   'GIRLFRIEND' TELLS TO WHOM THE ACTION OF THE VERB 'GAVE' IS EXPRESSED.":GOSUB260
225 GOSUB530
230 CLS:PRINT:JK$="   IN THE FOLLOWING QUIZ YOU WILL BE ASKED TO IDENTIFY BOTH THE DIRECT OBJECT AND THE INDIRECT OBJECT IN A SAMPLE SENTENCE.":GOSUB260
235 JK$="   SIMPLY TYPE IN THE CORRECT ANSWER. THE COMPUTER WILL TELL YOU IF YOU ARE CORRECT OR INCORRECT.":GOSUB260
240 JK$="   PRESSING THE <@> KEY WILL SHOW YOUR SCORE. YOU MAY PRESS <C> TO CONTINUE THE QUIZ. GOOD LUCK.":GOSUB260
245 GOSUB530
250 CLS0:DIMAO(50),A$(50),D$(50),I$(50),V$(50),NP(50)
255 CLS0:GOTO295
260 AA$=JK$:GOSUB110
265 IF LEN(JK$)<=SW THEN285
270 FOR T=SW TO 0STEP-1:IF MID$(JK$,T,1)=" "THEN280
275 NEXT T:GOTO285
280 L$=LEFT$(JK$,T):W$=L$:GOSUB290:JK$="   "+RIGHT$(JK$,(LEN(JK$))-T):GOTO265
285 W$=JK$:PRINTW$:RETURN
290 PRINTW$:RETURN
295 FORJ=1TO50:READ A$(J),D$(J),I$(J),V$(J):IF A$(J)="END" THEN305
300 NEXTJ
305 REM START QUIZ
310 J=J-1:FORI=1 TO J
315 AO(I)=RND(J)
320 IF NP(AO(I))=1 THEN 315
325 NP(AO(I))=1:NEXTI
330 FOR Y=1TO1000:NEXTY
335 GOTO475
340 FX=0:CLS
345 PRINT@35,"HERE IS EXAMPLE NUMBER";P
350 RETURN
355 AA$="   WHAT IS THE DIRECT OBJECT       OF THIS SENTENCE ?"::GOSUB110:PRINT@96,AA$
360 PRINT@192,"";:JK$="   "+A$(AO(P)):GOSUB260
365 PRINT:PRINT"   => ";:LINEINPUTA$
370 IFA$="@"THENGOSUB545
375 IF FX=1 THENGOSUB340:GOTO355
380 IF A$<>D$(AO(P))THEN395
385 PRINT:JK$="   YOU ARE CORRECT! THE DIRECT OBJECT IS: "+D$(AO(P)):GOSUB260
390 CR=CR+1:GOTO405
395 PRINT:JK$="   WRONG! THE CORRECT DIRECT OBJECT IS: "+D$(AO(P)):GOSUB260
400 IR=IR+1
405 GOSUB530
410 RETURN
415 AA$="   WHAT IS THE INDIRECT            OBJECT OF THIS SENTENCE ?":GOSUB110:PRINT@96,AA$
420 PRINT@192,"";:JK$="   "+A$(AO(P)):GOSUB260
425 PRINT:PRINT"   => ";:LINEINPUTA$
430 IFA$="@"THENGOSUB545
435 IF FX=1 THENGOSUB340:GOTO415
440 IF A$<>I$(AO(P))THEN455
445 PRINT:JK$="   YOU ARE CORRECT! THE INDIRECT OBJECT IS: "+I$(AO(P)):GOSUB260
450 CR=CR+1:GOTO465
455 PRINT:JK$="   SORRY! THE CORRECT INDIRECT OBJECT IS: "+I$(AO(P)):GOSUB260
460 IR=IR+1
465 GOSUB530
470 RETURN
475 FORP=1TOJ
480 WW=RND(2):IFWW=1THEN490
485 FX=0:GOSUB340:GOSUB355:GOSUB340:GOSUB415:GOTO495
490 FX=0:GOSUB340:GOSUB415:GOSUB340:GOSUB355
495 CLS
500 PRINT@32,"";:JK$="   IN THE SENTENCE '"+A$(AO(P))+"',":GOSUB260
505 PRINT:JK$="   THE DIRECT OBJECT, '"+D$(AO(P))+"', IS THE THING WHICH IS BEING ACTED UPON BY THE VERB, '"+V$(AO(P))+"'":GOSUB260
510 JK$="   WHILE THE INDIRECT OBJECT, '"+I$(AO(P))+"' NAMES TO WHOM OR WHAT THE ACTION OF THE VERB IS DIRECTED.":GOSUB260
515 GOSUB530
520 NEXT P
525 GOSUB545:RUN
530 PRINT@483," PRESS <ENTER> TO CONTINUE";
535 X$=INKEY$:IFX$<>CHR$(13)THEN535
540 RETURN
545 CLS:PRINT:PRINT:PRINT
550 PQ=CR+IR:IF PQ=0THEN PQ=1
555 PRINT"    NUMBER CORRECT = "CR
560 PRINT
565 PRINT"    NUMBER WRONG   = "IR
570 PRINT:PRINT"    STUDENT SCORE  = ";INT(CR*100/PQ);"%"
575 PRINT:PRINT"    ANOTHER TRY (Y/N/C)";
580 W$=INKEY$:IF W$=""THEN580
585 IF W$="C"THEN FX=1:RETURN
590 IF W$="Y" THEN RUN
595 IF W$="N" THEN CLS:END
600 GOTO580
990 REM ENTER DATA AT LINE 1000 IN THIS ORDER: SENTENCE, DIRECT OBJECT, INDIRECT OBJECT,VERB
1000 DATA I MAILED MY BROTHER A LARGE PACKAGE.,PACKAGE,BROTHER,MAILED
1010 DATA MY UNCLE GAVE ME A NEW FOOTBALL HELMET.,HELMET,ME,GAVE
1020 DATA WILL YOU TELL ME A BEDTIME STORY?,STORY,ME,TELL
1030 DATA DON'T ASK ME ANY MORE QUESTIONS,QUESTIONS,ME,ASK
1040 DATA I JUST BOUGHT MYSELF A BRAND NEW AUTOMOBILE.,AUTOMOBILE,MYSELF,BOUGHT
1050 DATA FEED YOUR CAT THIS BRAND OF CAT FOOD.,BRAND,CAT,FEED
1060 DATA HE SENT ME A BEAUTIFUL BIRTHDAY CARD FOR MY BIRTHDAY.,CARD,ME,SENT
1070 DATA COULD YOU LEND ME TEN DOLLARS UNTIL TUESDAY?,DOLLARS,ME,LEND
1080 DATA I DO NOT OWE YOU ANYTHING.,ANYTHING,YOU,OWE
1090 DATA THE BROKER OFFERED US A GOOD PRICE FOR THE PROPERTY.,PRICE,US,OFFERED
1100 DATA WE SENT HARRY A NEW TIE.,TIE,HARRY,SENT
1110 DATA THOMAS OFFERED BILL A NEW JOB AT THE OFFICE.,JOB,BILL,OFFERED
1120 DATA BRING ME THE LATEST COPY OF THE RAINBOW.,COPY,ME,BRING
1130 DATA THE TEAM GAVE THE COACH A TROPHY FOR HIS HARD WORK.,TROPHY,COACH,GAVE
1140 DATA I SOLD HIM THE FIRST HOUSE HE LOOKED AT.,HOUSE,HIM,SOLD
5000 DATA END,END,END,END
