10 REM"CROSS REFERENCES GAME"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1988"
30 DIM A$(10),B$(10),C$(20)
40 FOR S=1 TO 10:READ A$(S):NEXT S
50 FOR T=1 TO 10:READ B$(T):NEXT T
60 RESTORE
70 FOR T= 1 TO 20:READ C$(T):NEXT T
80 CLS:Z=Z+1:IF Z>10 THEN 240
90 PRINT@0,"#";Z;"MATCH ONE SUBJECT AND TOPIC";
100 FOR T=1 TO 20:A=RND(20):PRINT@32+B,C$(A):B=B+16:NEXT T:B=0
110 PRINT"--------------------------------";
120 PRINT"CHOOSE A SUBJECT ";:LINEINPUT AA$
130 FOR T=1 TO 10:IF AA$=A$(T) THEN 150 ELSE NEXT T
140 SOUND 10,3:PRINT"THAT'S NOT ANY OF OUR SUBJECTS.":GOTO 210
150 SOUND 200,2:PRINT"GOOD-NOW MATCH THE LARGER TOPIC.";:SC=SC+5
160 LINEINPUT BB$
170 PRINT@464,"";
180 IF BB$=B$(T) THEN PLAY"L100CDEGGG":PRINT"CORRECT"ELSE 200
190 SC=SC+5:GOTO 210
200 PRINT"*";B$(T):PLAY"L2F"
210 PRINT@484,"PRESS ENTER TO GO ON";
220 EN$=INKEY$
230 IF EN$=CHR$(13) THEN 80 ELSE 220
240 CLS:PRINT@97,"YOUR SCORE WAS";SC;"THIS TIME"
250 PRINT@192,"PRESS A TO GO AGAIN OR E TO END";
260 EN$=INKEY$
270 IF EN$="A" THEN RUN ELSE IF EN$="E" THEN END ELSE 260
280 DATA FILM,ALGAE,HAWAII,BRONTOSAURUS,WHALES,CLOUDS,MARS,HALLOWEEN,AUTOMOBILES,COLLIES
290 DATA MOTION PICTURES,PLANTS,ISLANDS,PREHISTORIC,MAMMALS,WEATHER,PLANETS,HOLIDAYS,TRANSPORTATION,DOGS
