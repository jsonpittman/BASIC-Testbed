1 'KEYF
2 'BY DAWN A. SMITH
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW
10 POKE 65497,0: REM HI-SPEED COCO III
20 WIDTH 32:TP=3:GOTO 280:       REM COCO III
30 POKE 65495,0:TP=2:GOTO 280:       REM HI-SPEED COCO 2
40 TP=1:GOTO 280
50 REM ====== KEY-FRAME ANIMATION ========
60 REM  BY DAWN A. SMITH
70 REM  FILE NAME = KEYF/BAS
80 REM ===================================
90 REM ===== DRAW SHAPES =====
100 FOR C=0 TO DV
110 REM ====UNDRAW LAST SHAPE ====
120 IF C=0 GOTO 180
130 FOR A=1 TO N-1
140 DX = CX(A)*C:DY=CY(A)*C
150 FX=CX(A+1)*C: FY=CY(A+1)*C
160 LINE (SX(A)-CX(A)+DX,SY(A)-CY(A)+DY) -(SX(A+1)-CX(A+1)+FX,SY(A+1)-CY(A+1)+FY),PRESET
170 NEXT A
180 REM ==== DRAW NEW SHAPE ====
190 FOR A=1 TO N-1
200 DX = CX(A)*C:DY=CY(A)*C
210 FX=CX(A+1)*C: FY=CY(A+1)*C
220 LINE (SX(A)+DX,SY(A)+DY) - (SX(A+1)+FX,SY(A+1)+FY),PSET
230 NEXT A
240 FOR Z=1 TO 30:NEXT Z
250 NEXT C
260 RETURN
270 REM
280 REM ===== STORE POINT =====
290 DIM SX(30), SY(30):   REM ==== START SHAPE X,Y
300 DIM EX(30), EY(30):   REM ==== END SHAPE X,Y
310 DIM CX(30), CY(30):   REM ==== CHANGE IN X,Y
320 REM
330 REM ===== BEGIN PROGRAM =====
340 INPUT "NUMBER OF DIVISIONS "; DV: DV=DV+1
350 IF DV<1 GOTO 450
360 REM ===== LOOP THROUGH THE 5 SHAPES =====
370 FOR CT=1 TO 5
380 READ N:               REM   NUMBER OF POINT IN SHAPES
390 GOSUB 490:            REM   LOAD SHAPES
400 PMODE 4,1:SCREEN1,1:PCLS
410 GOSUB 90              REM   DRAW SHAPES
420 NEXT CT
430 RESTORE
440 GOTO 330              REM   RE-RUN PROGRAM
450 REM ==== END OF PROGRAM ====
460 IF TP=3 THEN POKE 65496,0
470 END
480 REM
490 REM ===== LOAD START SHAPE  ======
500 FOR A = 1 TO N
510 READ SX(A)
520 READ SY(A)
530 NEXT A
540 REM ===== LOAD END SHAPE ======
550 FOR A = 1 TO N
560 READ EX(A)
570 READ EY(A)
580 REM ===== COMPUTE SIZE OF DIVISIONS =====
590 CX(A) = ((EX(A)-SX(A))/DV)
600 CY(A) = ((EY(A)-SY(A))/DV)
610 NEXT A
620 RETURN
630 REM
640 REM ===== SHAPE DATA =====
650 REM
660 REM ---- NUMBER OF POINTS
670 DATA 2
680 REM ---- STARTING SHAPE X,Y, ...
690 DATA 20,20,  20,20
700 REM ---- ENDING SHAPE X,Y . . . 
710 DATA 120,20, 120,20
720 DATA 4
730 DATA 20,30, 110,50, 100,20, 20,30
740 DATA 20,60, 100,30, 20,100, 20,60
750 DATA 6
760 DATA 20,40, 60,40, 60,80, 40,80, 20,80, 20,40
770 DATA 40,40, 40,40, 60,60, 40,80, 20,60, 40,40
780 DATA 8
790 DATA 20,20, 20,30, 20,40, 30,40, 38,38, 38,22, 30,20, 20,20
800 DATA 20,36, 27,40, 38,38, 38,32, 30,30, 24,26, 28,20, 38,22
810 DATA 3
820 DATA 20,20, 20,40, 22,40
830 DATA 20,20, 40,20, 42,20
840 DATA 9
850 DATA 40,20, 44,22, 45,25, 44,27, 40,30, 37,28, 35,25, 37,22, 40,20
860 DATA 40,55, 45,56, 47,58, 45,59, 40,60, 35,59, 32,58, 35,56, 40,55
