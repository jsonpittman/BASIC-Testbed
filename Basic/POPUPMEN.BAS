1 'POP-UP MENUS                    BY BILL BERNICO                 COPYRIGHT (C) JANUARY 1992      BY FALSOFT, INC.                RAINBOW MAGAZINE
2 HSCREEN2:HCLS2:HCOLOR8,4:RGB:POKE65497,0:ON BRK GOTO 35
3 HBUFF 1,1660:HBUFF 2,1660:HBUFF 3,1660:'CREATE THREE STORAGE BUFFERS-ONE FOR REGULAR BACKGROUND AND TWO FOR MENUS
4 HCOLOR 0:'MENU TITLE #1 WILL BE GREEN 
5 HPRINT(8,1),"MENU #1":'NEXT 5 LINES ARE MENU #1 TEXT
6 HCOLOR1:'MENU TEXT WILL BE YELLOW
7 HPRINT(8,2),"S=Save
8 HPRINT(8,3),"L=Load
9 HPRINT(8,4),"P=Print
10 HPRINT(8,5),"2=MENU 2
11 HGET(60,4)-(130,49),1:HCLS3:'STORE PICTURE OF MENU #1 IN BUFFER 1 & CLEAR SCREEN
12 HCOLOR 4:'MENU TITLE #2 WILL BE WHITE
13 HPRINT(8,1),"MENU #2":'NEXT FIVE LINES ARE MENU #2 TEXT
14 HCOLOR 5:'MENU TEXT WILL BE LIGHT BLUE
15 HPRINT(8,2),"1=Sort
16 HPRINT(8,3),"2=List
17 HPRINT(8,4),"3=Append
18 HPRINT(8,5),"4=MENU 1
19 HGET(60,4)-(130,49),2:HCLS4:'STORE PICTURE OF MENU #2 IN BUFFER 2 AND CLEAR THE SCREEN
20 HCOLOR8:'RETURN TO BLACK TEXT ON WHITE BACKGROUND
21 FOR X=0 TO 23:'HPRINT ON LINES 0 THRU 23
22 HPRINT(0,X),STRING$(40,RND(90)+32):'FILL THE SCREEN WITH RANDOM TEXT
23 NEXT X:'COMPLETE LOOP
24 GOSUB 31:'CHOOSE RANDOM LOCATIONS & STORE SCREEN BACKGROUND (WHATEVER THAT MAY BE AT THE TIME) AND STORE IN BUFFER 3
25 HPUT(H,V)-(H+70,V+45),1:EXEC 44539:'DISPLAY THE PRE-STORED MENU #1 & PAUSE
26 GOSUB 32:'REPLACE ORIGINAL BACKGROUND WHEN MENU DISAPPEARS
27 GOSUB 31
28 HPUT(H,V)-(H+70,V+45),2:EXEC 44539:'CHOOSE DIFFERENT LOCATIONS & DISPLAY MENU #2 AND PAUSE
29 GOSUB 32
30 GOTO 24:'GO BACK AND PUT MENU #1 AT ANOTHER LOCATION
31 GOSUB 33:HGET(H,V)-(H+70,V+45),3:EXEC44539:RETURN
32 HPUT(H,V)-(H+70,V+45),3:RETURN:'DISPLAY ORIGINAL BACKGROUND & GO BACK FOR ANOTHER RANDOM MENU LOCATION
33 H=RND(249):V=RND(145):'RANDOM HORIZONTAL & VERTICAL LOCATIONS FOR MENU TO APPEAR
34 RETURN:'GO BACK
35 POKE65496,0:CLS:END
