0 'CHECK2/BAS  CHECK, BUDGET MANAGEMENT PROGRAM VERISION 2.00.00  09/01/86
1 'COPYRIGHT 1989, FALSOFT INC.
5 GOTO 900
10 GOTO100
25 OPEN"D",#1,"CHECK/ACC",11:FIELD#1,11AS CA$:L=LOF(1):RETURN
30 OPEN"D",#1,"CHECK/DAT",157:FIELD#1,9AS CN$,9AS DN$,8AS DT$,50AS PT$,50AS FR$,11AS AC$,9AS AM$,1AS ST$,10AS BL$:L=LOF(1):RETURN
40 IFST$="N"THEN S2$="NOT VERIFIED"ELSEIFST$="Y"THEN S2$="VERIFIED"
45 CLS:PRINT@40,"RECORD NUMBER "R:PRINT@96,"CHECK NO. . ."CN$:PRINT"DEPOSIT NO. ."DN$:PRINT"DATE. . . . ."DT$:PRINT"PAID TO . . ."PT$:PRINT"FOR . . . . ."FR$:PRINT"ACCOUNT . . ."AC$:PRINT"AMOUNT. . . .":PRINT"STATUS. . . ."S2$
50 PRINT@365,USINGC$;VAL(AM$):RETURN
70 G3$=RIGHT$(G1$,2)+LEFT$(G1$,2)+MID$(G1$,4,2):G4$=RIGHT$(G2$,2)+LEFT$(G2$,2)+MID$(G2$,4,2):RETURN
75 IFVAL(CN$)>0THEN V=V-VAL(AM$)ELSE V=V+VAL(AM$):RETURN
80 TD$=RIGHT$(DT$,2)+LEFT$(DT$,2)+MID$(DT$,4,2):RETURN
100 CLS:PRINT@32,"CHECK, BUDGET MANAGEMENT PROGRAM":PRINT@97,"STATEMENT VERIFACATION PRESS A":PRINT@129,"SEARCH IN RECORDS -  - PRESS B":PRINT@161,"PRINTOUT OF RECORDS  - PRESS C":PRINT@193,"TOTALS OF ACCOUNTS-  - PRESS D"
105 PRINT@225,"FOR OTHER ROUTINES-  - PRESS E"
110 A$=INKEY$:IFA$="A"THEN115ELSEIFA$="B"THEN235ELSEIFA$="C"THEN300ELSEIFA$="D"THEN420ELSEIFA$="E"THEN RUN"CHECK1"ELSE110
112 'BANK STATEMENT VERIFACTION
115 V=0:CLS:PRINT@2,"BANK STATEMENT VERIFACATION":PRINT@66,"HAVE ALL DEBITS AND CREDITS,":PRINT@101,"TO DATE, BEEN ENTERED?"
120 PRINT@173,"DEBITS":PRINT@192," CHECK, CASH WITHDRAWL, SERVICE  CHARGE,TELEPHONE TRANSFER, OR         AUTOMATIC PAYMENTS"
125 PRINT@300,"CREDITS":PRINT@320,"DEPOSIT, INTEREST, CASH ADVANCE,DIRECT DEPOSIT, PAYROLL SAVINGS, OR TRANSFERS INTO YOUR ACCOUNT"
130 PRINT@449,"PRESS Y TO VERIFY   N FOR MENU"
135 A$=INKEY$:IFA$="Y"THEN140ELSEIFA$="N"THEN100ELSE135
140 GOSUB30:IFL<1THEN225ELSEFORR=1TO L:GET#1,R:IFST$="Y"THEN NEXTR ELSER1=R
145 IFR-1<>L THEN155ELSE CLS:PLAY"DCD":PRINT@65,"ALL RECORDS HAVE BEEN VERIFIED":PRINT@226,"FOR VARIFIED BALANCE PRESS V":PRINT@290,"FOR MAIN MENU    PRESS ENTER"
150 A$=INKEY$:IFA$="V"THEN200ELSEIFA$=CHR$(13)THEN225ELSE150
155 B0$="":CLS:PRINT@37,"ENTER CHECK/DEPOSIT NO.":PRINT@196,"FOR VERIFIED BANK BALANCE":PRINT@236,"ENTER V":PRINT@453,"FOR MAIN MENU  ENTER M":PRINT@64:INPUTB0$:IFB0$=""THEN155
160 B1$="         ":B2$=B0$+B1$:IFB0$="V"THEN200ELSEIFB0$="M"THEN225
165 FORR=R1 TO L:GET#1,R:IFCN$=LEFT$(B2$,9)ORDN$=LEFT$(B2$,9)THEN180ELSENEXTR
170 CLS:PLAY"DCD":PRINT@40,"CHECK/DEPOSIT NO.":PRINT@76,B0$:PRINT@107,"NOT FOUND":PRINT@164,"TO CONTINUE VERIFICATION":PRINT@202,"PRESS ENTER":PRINT@453,"FOR MAIN MENU  ENTER M"
175 A$=INKEY$:IFA$=CHR$(13)THEN155ELSEIFA$="M"THEN225ELSE175
180 GOSUB40:PRINT@458,"VERIFY Y/N?"
185 A$=INKEY$:IFA$="Y"THEN190ELSEIFA$="N"THEN155ELSE185
190 C1$=CN$:D1$=DN$:D2$=DT$:P1$=PT$:F1$=FR$:A1$=AC$:A2$=AM$:S1$="Y":B1$=BL$
195 LSET CN$=C1$:LSET DN$=D1$:LSET DT$=D2$:LSET PT$=P1$:LSET FR$=F1$:LSET AC$=A1$:LSET AM$=A2$:LSET ST$=S1$:LSET BL$=B1$:PUT#1,R:GOTO155
200 CLS:PRINT@194,"CALCULATING VERIFIED BALANCE"
205 FORR=1TO L:GET#1,R:IFST$="Y" AND VAL(CN$)>0 THEN V=V-VAL(AM$)ELSEIFST$="Y" AND VAL(DN$)>0 THEN V=V+VAL(AM$)
210 NEXTR
215 CLS:PRINT@200,"VERIFIED BALANCE":PRINT@235,USINGCC$;V:PRINT@451,"FOR MAIN MENU  PRESS ENTER"
220 A$=INKEY$:IFA$=CHR$(13) THEN225ELSE220
225 CLOSE:GOTO100
230 'SEARCH ROUTINE
235 GOSUB30:FIELD#1,157AS B$:IFL<1THEN295
240 CLS:PRINT@39,"SEARCH IN RECORDS":PRINT@101,"SEARCH IN       PRESS":PRINT@164,"CHECK NUMBER  .  . A":PRINT@196,"DEPOSIT NUMBER   . B":PRINT@228,"DATE .  .  .  .  . C":PRINT@260,"PAID TO .  .  .  . D"
245 PRINT@292,"FOR  .  .  .  .  . E":PRINT@324,"ACCOUNT .  .  .  . F":PRINT@356,"AMOUNT  .  .  .  . G":PRINT@388,"STATUS  .  .  .  . H":PRINT@420,"FOR MAIN MENU .  . I"
250 A$=INKEY$:IFA$="A"THEN E=1ELSEIFA$="B"THEN E=10ELSEIFA$="C"THEN E=19ELSEIFA$="D"THEN E=27ELSEIFA$="E"THEN E=77ELSEIFA$="F"THEN E=127ELSEIFA$="G"THEN E=138ELSEIFA$="H"THEN E=147ELSEIFA$="I"THEN295ELSE250
255 PRINT:INPUT"SEARCHING FOR: ";B1$:B=LEN(B1$)
260 FORR=1TOLOF(1):GET#1,R:IFMID$(B$,E,B)=B1$THEN275
265 IFR=L THEN285
270 NEXTR
275 GOSUB40:PRINT@453,"CONTINUE SEARCH Y/N?"
280 A$=INKEY$:IFA$="Y"THEN265ELSEIFA$="N"THEN285ELSE280
285 CLS:PRINT@40,"SEARCH FINISHED":PRINT@101,"ANOTHER SEARCH PRESS A":PRINT@133,"FOR MAIN MENU   PRESS B"
290 A$=INKEY$:IFA$="A"THEN240ELSEIFA$="B"THEN295ELSE290
295 CLOSE:GOTO100
300 'PRINT
305 CLS:PRINT@38,"PRINTOUT OF RECORDS":PRINT@99,"PRINT ENTIRE FILE  PRESS A":PRINT@131,"PRINT PART OF FILE PRESS B":PRINT@163,"FOR MAIN MENU -  - PRESS C"
310 A$=INKEY$:IFA$="A"THEN315ELSEIFA$="B"THEN340ELSEIFA$="C"THEN100ELSE310
315 CLS:PRINT@38,"PRINTING ENTIRE FILE":PRINT@100,"DISABLE PRINTER AUTOMATIC":PRINT@138,"LINE ADVANCE":PRINT@195,"PRESS SPACE BAR WHEN READY"
320 A$=INKEY$:IFA$=CHR$(32)THEN325ELSE320
325 GOSUB30:R=1:V=0:C=1:C8=0:IFL=0THEN CLOSE:GOTO100
330 GET#1,R:GOSUB75:GOSUB375
335 R=R+1:IFR<=L THEN330ELSEIFC=2THEN GOSUB400ELSECLOSE:GOTO100
338 CLOSE:GOTO100
340 'PRINT PART OF FILE
345 CLS:PRINT@35,"PRINTING PART OF THE FILE":PRINT@451,"FOR MAIN MENU PRESS ENTER":PRINT@99,"DISABLE PRINTER AUTOMATIC":PRINT@137,"LINE ADVANCE":PRINT
350 INPUT" DATE PRINTOUT IS TO START AT   MM/DD/YY";G1$:IFG1$=""THEN345ELSEIFG1$="M"THEN100ELSEPRINT:INPUT" DATE PRINTOUT IS TO FINISH AT  MM/DD/YY";G2$:IFG2$=""THEN345ELSEIFG2$="M"THEN100
355 GOSUB70:GOSUB30:C=1:R=1:C8=0:V=0:IFL=0THEN CLOSE:GOTO100
360 GET#1,R:GOSUB80:IFTD$>=G3$ AND TD$<=G4$THEN GOSUB375
365 R=R+1:IFR>L AND C=2THEN GOSUB400ELSEIFR>L AND C=1THEN370ELSE360
370 CLOSE:GOTO100
375 IFC=1THEN380ELSE390
380 IFVAL(CN$)>0THEN E1$=CN$ELSEE1$=DN$
385 E0=R:E2$=DT$:E3$=PT$:E4$=FR$:E5$=AC$:E6=VAL(AM$):E7$=ST$:E8$=BL$:C=2:RETURN
390 IFVAL(CN$)>0THEN F1$=CN$ELSEF1$=DN$
395 F0=R:F2$=DT$:F3$=PT$:F4$=FR$:F5$=AC$:F6=VAL(AM$):F7$=ST$:F8$=BL$
400 PRINT#-2,TAB(4)"RECORD "E0 TAB(42)"RECORD "F0;CHR$(10):PRINT#-2,TAB(4)"CKDP "E1$ TAB(42)"CKDP "F1$;CHR$(10):PRINT#-2,TAB(4)"DATE "E2$ TAB(42)"DATE "F2$;CHR$(10)
402 PRINT#-2,TAB(4)"TO   "LEFT$(E3$,19) TAB(42)"TO   "LEFT$(F3$,19);CHR$(10):PRINT#-2,TAB(4) RIGHT$(E3$,31) TAB(42) RIGHT$(F3$,31);CHR$(10)
403 PRINT#-2,TAB(4)"FOR  "LEFT$(E4$,19) TAB(42)"FOR  "LEFT$(F4$,19);CHR$(10):PRINT#-2,TAB(4) RIGHT$(E4$,31) TAB(42) RIGHT$(F4$,31);CHR$(10)
405 PRINT#-2,TAB(4)"ACC  "E5$ TAB(42)"ACC  "F5$;CHR$(10):PRINT#-2,TAB(4):PRINT#-2,USING"AMT  "+C$;E6:PRINT#-2,TAB(42):PRINT#-2,USING"AMT  "+C$;F6:PRINT#-2,CHR$(10)
410 PRINT#-2,TAB(4)"STAT "E7$ TAB(42)"STAT "F7$;CHR$(10):PRINT#-2,TAB(4):PRINT#-2,USING"BAL  "+CC$;VAL(E8$):PRINT#-2,TAB(42):PRINT#-2,USING"BAL  "+CC$;VAL(F8$):PRINT#-2,CHR$(10);CHR$(10);CHR$(10)
412 C8=C8+2:IFC8=10THEN PRINT#-2,CHR$(10):C8=0
415 C=1:F0=0:F1$="":F2$="":F3$="":F4$="":F5$="":F6=0:F7$="":F8$="":RETURN
420 'ACCOUNT TOTALING
425 CLS:PRINT@8,"TOTAL OF ACCOUNTS":PRINT@65,"TOTAL OF EACH ACCOUNT":PRINT@97,"FOR ENTIRE FILE-  -  - PRESS A":PRINT@161,"TOTAL OF EACH ACCOUNT":PRINT@193,"FOR A CERTAIN PERIOD - PRESS B
430 PRINT@257,"TOTAL OF ONE ACCOUNT":PRINT@289,"FOR ENTIRE FILE-  -  - PRESS C":PRINT@353,"TOTAL OF ONE ACCOUNT":PRINT@385,"FOR A CERTAIN PERIOD - PRESS D":PRINT@449,"FOR MAIN MENU  -  -  - PRESS E"
435 A$=INKEY$:IFA$="A"THEN440ELSEIFA$="B"THEN520ELSEIFA$="C"THEN655ELSEIFA$="D"THEN695ELSEIFA$="E"THEN100ELSE435
440 'TOTAL OF ACCOUNTS FOR ENTIRE FILE
445 C1=0:CLS:PRINT@37,"TOTAL OF EACH ACCOUNT":PRINT@72,"FOR ENTIRE FILE":PRINT@132,"PRINT ON SCREEN  PRESS A":PRINT@164,"PRINT ON PRINTER PRESS B":PRINT@196,"FOR MAIN MENU    PRESS C"
450 A$=INKEY$:IFA$="A"THEN460ELSEIFA$="B"THEN455ELSEIFA$="C"THEN100ELSE450
455 C1=1
460 CLS:PRINT@193,"PROCESSING TOTALS OF ACCOUNTS":PRINT@232,"FOR ENTIRE FILE":GOSUB25:D1=1:C=L:G1$="":G2$=""
465 FORR=1TO L:GET#1,R:D1$(D1)=CA$:D1=D1+1:NEXTR:CLOSE:GOSUB30
470 FORR=1TO L:GET#1,R:FORD1=1TO C:IFD1$(D1)=AC$THEN475ELSENEXTD1
475 IFVAL(CN$)>0THEN V(D1)=V(D1)-VAL(AM$)ELSE V(D1)=V(D1)+VAL(AM$)
480 NEXTR:CLOSE:IFC1=1THEN610
485 D1=1:C2=99:C3=115
490 CLS:PRINT@39,"TOTAL OF ACCOUNTS"
495 PRINT@C2,D1$(D1):PRINT@C3,USINGC$;V(D1):D1=D1+1:C2=C2+32:C3=C3+32:IFC2>355OR D1>C THEN500ELSE495
500 C2=99:C3=115:PRINT@421,"TO CONTINUE   PRESS SB":PRINT@453,"FOR MAIN MENU PRESS  M"
505 A$=INKEY$:IFA$=CHR$(32)THEN510ELSEIFA$="M"THEN515ELSE505
510 IFD1>C THEN515ELSE490
515 FORD1=1TO C:V(D1)=0:NEXTD1:CLOSE:GOTO100
520 'TOTAL OF EACH ACCOUNT FOR A CERTAN PERIOD
525 CLS:PRINT@37,"TOTAL OF EACH ACCOUNT":PRINT@70,"FOR A CERTAIN PERIOD":PRINT@453,"FOR MAIN MENU  ENTER M"
530 PRINT@96:INPUT"STARTING DATE OF PERIOD MM/DD/YY";G1$:IFG1$=""THEN525ELSEIFG1$="M"THEN100ELSEPRINT:INPUT" ENDING DATE OF PERIOD MM/DD/YY ";G2$:IFG2$=""THEN525ELSEIFG2$="M"THEN100ELSEGOSUB70
535 CLS:PRINT@193,"PROCESSING TOTALS OF ACCOUNTS":PRINT@228,"FOR "G1$" TO "G2$:GOSUB25:D1=1:C=L
540 FOR R=1TOL:GET#1,R:D1$(D1)=CA$:D1=D1+1:NEXTR:CLOSE:GOSUB30
545 IFL=0THEN100
550 FORR=1TO L:GET#1,R:GOSUB80:FORD1=1TO C:IFG3$<=TD$ AND TD$<=G4$ AND D1$(D1)=AC$THEN555ELSENEXTD1
555 IFVAL(CN$)>0THEN V(D1)=V(D1)-VAL(AM$)ELSEV(D1)=V(D1)+VAL(AM$)
560 NEXTR
565 CLOSE:CLS:PRINT@37,"TOTALS OF EACH ACCOUNT":PRINT@68,"FOR "G1$" TO "G2$:PRINT@132,"LIST ON SCREEN   PRESS A":PRINT@164,"PRINT ON PRINTER PRESS B"
570 A$=INKEY$:IFA$="A"THEN575ELSEIFA$="B"THEN610ELSE570
575 D1=1:C2=99:C3=115
580 CLS:PRINT@34,"TOTALS OF ACCOUNTS FOR PERIOD"
585 PRINT@C2,D1$(D1):PRINT@C3,USINGC$;V(D1):D1=D1+1:C2=C2+32:C3=C3+32:IFC2>355OR D1>C THEN590ELSE585
590 C2=99:C3=115:PRINT@421,"TO CONTINUE  PRESS SB":PRINT@453,"FOR MAIN MENU PRESS M"
595 A$=INKEY$:IFA$=CHR$(32)THEN600ELSEIFA$="M"THEN605ELSE595
600 IFD1>C THEN605ELSE580
605 FORD1=1TO C:V(D1)=0:NEXTD1:CLOSE:GOTO100
610 CLS:PRINT@37,"READY TO PRINT TOTALS":PRINT@99,"DISABLE PRINTER AUTOMATIC":PRINT@137,"LINE ADVANCE":PRINT@197,"WHEN PRINTER IS READY":PRINT@233,"PRESS  ENTER"
615 A$=INKEY$:IFA$=CHR$(13)THEN620ELSE615
620 IFG1$=""THEN625ELSEPRINT#-2,TAB(18)"TOTAL OF EACH ACCOUNT FOR "G1$" TO "G2$CHR$(10)CHR$(10):GOTO630
625 PRINT#-2,TAB(23)"TOTAL OF EACH ACCOUNT FOR ENTIRE FILE"CHR$(10)CHR$(10)
630 D1=1:D2=2:IFC=1THEN645
635 PRINT#-2,TAB(13)D1$(D1)TAB(28):PRINT#-2,USINGC$;V(D1):PRINT#-2,TAB(45)D1$(D2)TAB(60):PRINT#-2,USINGC$;V(D2):PRINT#-2,CHR$(10)
640 D1=D1+2:D2=D2+2:IFC/2=INT(C/2) AND D1<C THEN635ELSEIFC/2=INT(C/2) AND D1=C+1 THEN650ELSEIFD1<(C-1)THEN635ELSE645
645 PRINT#-2,TAB(13)D1$(D1)TAB(28):PRINT#-2,USINGC$;V(D1)
650 FORD1=1TO C:V(D1)=0:NEXTD1:GOTO100
655 'TOTAL OF ONE ACCOUNT FOR ENTIRE FILE
660 CLS:PRINT@38,"TOTAL OF ONE ACCOUNT":PRINT@72,"FOR ENTIRE FILE":PRINT@453,"FOR MAIN MENU  ENTER M":PRINT@96:INPUT"NAME OF ACCOUNT";D1$:IFD1$=""THEN660ELSEIFD1$="M"THEN100
665 CLS:PRINT@193,"FIGURING TOTAL OF "D1$:PRINT@232,"FOR ENTIRE FILE"
670 D$="           ":D2$=LEFT$(D1$+D$,11):GOSUB30:V=0:R=1:IFL=0THEN CLOSE:GOTO100
675 GET#1,R:IFD2$=AC$ THEN680ELSER=R+1:IFR>L THEN685ELSE675
680 GOSUB75:R=R+1:IFR>L THEN685ELSE675
685 CLS:PRINT@101,"TOTAL OF "D2$:PRINT@171,USINGC$;V:CLOSE:PRINT@453,"FOR MAIN MENU  PRESS M"
690 A$=INKEY$:IFA$="M"THEN100ELSE690
695 'TOTAL OF ONE ACCOUNT FOR A CERTAIN PERIOD
700 CLS:PRINT@38,"TOTAL OF ONE ACCOUNT":PRINT@70,"FOR A CERTAIN PERIOD":PRINT@453,"FOR MAIN MENU  ENTER M"
705 PRINT@128,"NAME OF ACCOUNT":INPUTD1$:IFD1$=""THEN700ELSEIFD1$="M"THEN100
710 PRINT@224,"STARTING TIME  MM/DD/YY        ":INPUTG1$:IFG1$=""THEN700ELSEIFG1$="M"THEN100ELSEPRINT@320,"ENDING TIME  MM/DD/YY          ":INPUTG2$:IFG2$=""THEN700ELSEIFG2$="M"THEN100
715 CLS:PRINT@193,"FIGURING TOTAL OF "D1$:PRINT@228,"FOR "G1$" TO "G2$
720 GOSUB70:D$="           ":D2$=LEFT$(D1$+D$,11):GOSUB30:V=0:R=1
725 GET#1,R:GOSUB80:IFG3$<=TD$ AND G4$>=TD$ AND D2$=AC$ THEN730ELSER=R+1:IFR>L THEN735ELSE725
730 GOSUB75:R=R+1:IFR>L THEN735ELSE725
735 CLS:PRINT@102,"TOTAL OF "D2$:PRINT@132,"FOR "G1$" TO "G2$:PRINT@203,USINGC$;V:CLOSE:PRINT@453,"FOR MAIN MENU  PRESS M"
740 A$=INKEY$:IFA$="M"THEN100ELSE740
900 CLEAR 500:DIMD1$(50):DIMV(50):C$="$######.##":CC$="$#######.##":GOTO 10
