1 'disk jacket designer
2 'copyright (c) 1986
3 'GAY CRAWFORD
4 '1001 FREDRICKSON
5 'OLATHE, KS  66061
6 '
10 DIM A(7)
20 FORL=1 TO 7:READ A(L):NEXTL
30 POKE150,18' BAUD RATE 2400
40 PRINT#-2,CHR$(27);"@";CHR$(27);"1";
50 PRINT#-2,CHR$(27);"1";
60 PRINT#-2,CHR$(27);CHR$(77);CHR$(5);
70 CLS:PRINT"DO YOU WANT TO PRINT A PICTURE  ON THE SLEEVE?"
80 I$=INKEY$:IFI$=""THEN80
90 IFI$="N"THEN140
100 IFI$<>"Y"THEN80
110 CLS:LINEINPUT"ENTER NAME OF PICTURE TO PRINT   ";P$
120 PMODE4,1:PCLS:SCREEN1,1
130 LOADMP$
140 'begin
150 GOSUB1040
160 PRINT#-2,CHR$(0);CHR$(1);
170 PRINT#-2,CHR$(2);CHR$(4);CHR$(4);
180 FORL=1TO13:PRINT#-2,CHR$(8);:NEXTL
190 FORL=1TO13:PRINT#-2,CHR$(16);:NEXTL
200 FORL=1TO13:PRINT#-2,CHR$(32);:NEXTL
210 PRINT#-2,CHR$(113);
220 FORL=1TO336:PRINT#-2,CHR$(64);:NEXTL
230 PRINT#-2,CHR$(113);
240 FORL=1TO13:PRINT#-2,CHR$(32);:NEXTL
250 FORL=1TO13:PRINT#-2,CHR$(16);:NEXTL
260 FORL=1TO13:PRINT#-2,CHR$(8);:NEXTL
270 PRINT#-2,CHR$(4);CHR$(4);CHR$(2);
280 PRINT#-2,CHR$(1);CHR$(0)
290 'print front
300 IF I$<>"Y" THEN F=39:GOSUB1060:GOTO700
310 'read screen
320 F=3:GOSUB1060
330 FORY=0 TO 188 STEP7
340 GOSUB1040
350 PRINT#-2,CHR$(127);
360 FORL=1TO43:PRINT#-2,CHR$(0);:NEXTL
370 PRINT#-2,CHR$(113);
380 FORL=1TO40:PRINT#-2,CHR$(0);:NEXTL
390 FORX=0 TO 255
400 N=0
410 FORD=0 TO 7
420 IFPPOINT(X,Y+D)=0 THEN N=N+A(D)
430 NEXTD
440 PRINT#-2,CHR$(N);
450 NEXTX
460 FORL=1TO40:PRINT#-2,CHR$(0);:NEXTL
470 PRINT#-2,CHR$(113);
480 FORL=1TO43:PRINT#-2,CHR$(0);:NEXTL
490 PRINT#-2,CHR$(127)
500 NEXTY
510 GOSUB1040
520 PRINT#-2,CHR$(127);
530 FORL=1TO43:PRINT#-2,CHR$(0);:NEXTL
540 PRINT#-2,CHR$(113);
550 FORL=1TO40:PRINT#-2,CHR$(0);:NEXTL
560 Y=189
570 FORX=0 TO 255
580 N=0
590 FORD=0 TO 2
600 IFPPOINT(X,Y+D)=0 THEN N=N+A(D)
610 NEXTD
620 PRINT#-2,CHR$(N);
630 NEXTX
640 FORL=1TO40:PRINT#-2,CHR$(0);:NEXTL
650 PRINT#-2,CHR$(113);
660 FORL=1TO43:PRINT#-2,CHR$(0);:NEXTL
670 PRINT#-2,CHR$(127)
680 F=5:GOSUB1060
690 'bottom fold
700 GOSUB1040
710 PRINT#-2,CHR$(64);CHR$(32);CHR$(16);CHR$(16);
720 FORL=1TO19:PRINT#-2,CHR$(8);:NEXT
730 FORL=1TO18:PRINT#-2,CHR$(4);:NEXT
740 PRINT#-2,CHR$(2);CHR$(2);CHR$(2);
750 PRINT#-2,CHR$(113);
760 FORL=1TO168:PRINT#-2,CHR$(1);CHR$(0);:NEXT
770 PRINT#-2,CHR$(113);
780 PRINT#-2,CHR$(2);CHR$(2);CHR$(2);
790 FORL=1TO18:PRINT#-2,CHR$(4);:NEXT
800 FORL=1TO19:PRINT#-2,CHR$(8);:NEXT
810 PRINT#-2,CHR$(16);CHR$(16);CHR$(32);CHR$(64)
820 'print back
830 GOSUB1040
840 FORL=1TO45:PRINT#-2,CHR$(0);:NEXT
850 PRINT#-2,CHR$(112);CHR$(15);
860 FORL=1TO332:PRINT#-2,CHR$(0);:NEXT
870 PRINT#-2,CHR$(15);CHR$(112);
880 FORL=1TO44:PRINT#-2,CHR$(0);:NEXT
890 PRINT#-2,CHR$(0)
900 'back sides
910 FORC=1TO47:GOSUB1050
920 FORL=1TO47:PRINT#-2,CHR$(0);:NEXTL
930 PRINT#-2,CHR$(127);
940 FORL=1TO330:PRINT#-2,CHR$(0);:NEXTL
950 PRINT#-2,CHR$(127)
960 NEXTC
970 GOSUB1050
980 FORL=1TO47:PRINT#-2,CHR$(0);:NEXT
990 PRINT#-2,CHR$(64);CHR$(32);CHR$(16);CHR$(8);CHR$(4);CHR$(2);
1000 FORL=1TO320:PRINT#-2,CHR$(1);:NEXT
1010 PRINT#-2,CHR$(2);CHR$(4);CHR$(8);CHR$(16);CHR$(32);CHR$(64)
1020 PRINT#-2,CHR$(12)
1030 STOP
1040 PRINT#-2,CHR$(27)"K"CHR$(170)CHR$(1);:RETURN
1050 PRINT#-2,CHR$(27)"K"CHR$(123)CHR$(1);:RETURN
1060 'front subroutine
1070 FORC=1TOF
1080 GOSUB1040
1090 PRINT#-2,CHR$(127);
1100 FORL=1TO43:PRINT#-2,CHR$(0);:NEXT
1110 PRINT#-2,CHR$(113);
1120 FORL=1TO336:PRINT#-2,CHR$(0);:NEXT
1130 PRINT#-2,CHR$(113);
1140 FORL=1TO43:PRINT#-2,CHR$(0);:NEXT
1150 PRINT#-2,CHR$(127):NEXTC
1160 RETURN
1170 DATA 64,32,16,8,4,2,1
