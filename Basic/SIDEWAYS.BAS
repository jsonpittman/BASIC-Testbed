1 'SIDEWAYS
2 'BY JEFF HAMELUCK
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 '*****************************
7 'FOR THE COCO 3 AND DMP-120
8 '*****************************
80 POKE 65497,0 ' SET PRINTER BAUD RATE TO 1/2 OF NORMAL
90 POKE 150,88  ' THIS GIVES 1200 NOT 600 BAUD
100 WIDTH 32 ' MUST BE 32 DUE TO CLEAR HI-RES TEXT SCREEN BUG
110 PCLEAR 1
120 CLEAR 21000
130 DIM B$(79)
140 ON BRK GOTO 880
150 PALETTE 1,0
160 HSCREEN 1
170 HPRINT (0,0),"SIDEWAYS  by: Jeff Hameluck"
180 HPRINT (0,2),"Is this reversed? (Y/N)"
190 A$=INKEY$
200 IF A$="" THEN 190
210 IF A$="Y" OR A$="y" THEN 270
220 IF A$<>"N" AND A$<>"n" THEN 190
230 IF PEEK(&HE00)=142 AND PEEK(&HE01)=240 THEN 290
240 POKE 65496,0
250 LOADM"BITTEST"
260 POKE 65497,0
270 DEFUSR0=&HE00
280 IF A$="Y" OR A$="y" THEN 300
290 A=USR0(0)
300 ON BRK GOTO 870
310 HSCREEN 0
320 FOR X=8 TO 1 STEP -1
330 PRINT TAB(X+1) "SIDEWAYS"
340 NEXT X
350 A$="sideways"
360 FOR Y=0 TO 7
370 PRINT @ 32*Y+9,MID$(A$,Y+1,1);
380 NEXT Y
390 PRINT @ 256
400 LINEINPUT "FILENAME: ";F$
410 ON ERR GOTO 460 ' I'M ASSUMING THE ONLY ERROR THAT WILL POP UP HERE
420 POKE 65496,0    ' WILL BE THE NE ERROR OR FILE NOT FOUND.
430 OPEN "I",#1,F$
440 POKE 65497,0
450 GOTO 480
460 PRINT " Filename doesn't exist!"
470 GOTO 400
480 POKE &HFE0E,0 ' RESETS ON ERROR POINTER SO THAT ANY FURTHER ERRORS
490 POKE &HFE0F,0 ' WILL ABORT EXECUTION OF THE PROGRAM.
500 LINEINPUT "LINE BETWEEN PAGES: ";A$
510 IF A$="" THEN LP=5:PRINT @ 339,LP
520 LP=VAL(A$)
530 IF LP<1 THEN LP=1
540 PRINT "cONDENSED OR sTANDARD: ";
550 A$=INKEY$
560 IF A$="" THEN 550
570 IF A$="C" OR A$="c" THEN PRINT #-2,CHR$(30);CHR$(27);CHR$(20);CHR$(18):W=79:PRINT "C":GOTO 590
580 IF A$<>"S" AND A$<>"s" THEN 550 ELSE PRINT #-2,CHR$(27);CHR$(19);CHR$(18):W=47:PRINT"S"
590 CLS 0
600 LL=0
610 FOR X=0 TO W
620 B$(X)=""
630 NEXT X
640 FOR X=0 TO W
650 POKE 65496,0
660 IF EOF(1) THEN CLOSE#1:D=1:POKE 65497,0:GOTO 710
670 LINEINPUT #1,B$(X)
680 POKE 65497,0
690 IF LEN(B$(X))>LL THEN LL=LEN(B$(X))
700 NEXT X
710 E=W
720 P=1
730 FOR X=E TO 0 STEP -1
740 IF LEN(B$(X))<P THEN PRINT #-2,STRING$(10,128);:GOTO 820
750 A=ASC(MID$(B$(X),P,1))
760 IF A<33 OR A>122 THEN PRINT #-2,STRING$(10,128);:GOTO 820
770 A=A-33
780 FOR T=7 TO 0 STEP -1
790 PRINT #-2,CHR$(PEEK(61605+A*8+T)+128);
800 NEXT T
810 PRINT #-2,STRING$(2,128);
820 NEXT X
830 PRINT #-2
840 IF P<>LL THEN P=P+1:GOTO 730
850 IF D<>1 THEN PRINT #-2,STRING$(LP,13):GOTO 560
860 PRINT #-2,CHR$(30);CHR$(27);CHR$(19)
870 A=USR0(0)
880 POKE 65496,0
890 RGB
900 CLEAR 100
910 UNLOAD
920 END
