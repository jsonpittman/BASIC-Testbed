5 REM COCO 3 USERS MUST SWITCH        TO COCO 2 MODE BEFORE KEY-      ING THIS PROGRAM IN. TYPE       POKE&HFFDE,0:POKE113,0:         EXEC&HA027
8 REM BEFORE KEYING THIS LISTING      INTO YOUR COCO, THE SUPER-      STAMPER ALGORITHM MUST BE       RESIDENT. YOU MAY DO THIS       BY FIRST RUNNING LISTING        #1 (SUPRSTMP.BAS), OR BY        EXECUTING THE ML CLONE          (STAMP.BIN)
20 REM FAILURE TO DO THIS WILL         RESULT IN SYNTAX ERRORS         AND NONSENSE LISTINGS!
30 CLEAR 1000, &H7E00
40 REM *************************
50 REM *   SUPERSTAMPER DEMO   *
60 REM *          BY           *
70 REM *     JEREMY SPILLER    *
80 REM *     COPYRIGHT 1988    *
90 REM *************************
100 CLS:PRINT"IN ORDER TO RUN ANY PROGRAM     WHICH USES 'SGET' OR 'SPUT',    YOU MUST FIRST INSTALL THE      SUPERSTAMPER ALGORITHM BY EITHERRUNNING SUPRSTMP.BAS OR BY      EXECUTING STAMP.BIN."
110 PRINT
120 LINE INPUT"PLEASE PREPARE YOUR DEVICE AND  INPUT (T)APE OR (D)ISK TO LOAD  AND EXECUTE STAMP.BIN, OR       (A)BORT IF YOU HAVE ALREADY     RUN SUPRSTMP.BAS.";A$
130 IF A$="T" THEN CLOADM"STAMP.BIN"ELSE IF A$="D"THEN LOADM"STAMP.BIN"ELSE GOTO 150
140 EXEC:GOTO 160
150 IF A$="A"THEN GOTO 160 ELSE GOTO 100
160 PRINT:PRINT STRING$(31,"-"):INPUT"CAN YOUR TV SUPORT ARTIFICIAL   COLORS (Y/N)";A$:IF A$="Y" THEN PMODE 4,1:SCREEN 1,1:PMODE 3,1 ELSE IF A$="N" THEN PMODE 3,1:SCREEN 1,0 ELSE 160
170 PCLS
180 S$="R6;U3;L6;U3;R6;BD6;BR4"
190 P$="U6;R6;D3;L6;BR6;BD3;BR4"
200 R$="U6;R6;D3;L6;M+6,+3;BR4
210 M$="U6;M+4,+6;M+4,-6;D6;BR4"
220 L$="U6;BD6;R6;BR4"
230 E$="U6;R6;BD3;L6;BD3;R6;BR4"
240 Y$="BR4;U3;M-3,-3;BR7;M-4,+3;BD3;BR7"
250 DRAW"BM74,12"+S$+";U6;BD6;R6;U6;BR4;BD6"+P$+E$+R$
260 DRAW S$+"BR4;U6;BL3;R8;BD6;BR3;U6;R6;D6;BU3;L6;BD3;BR11"+M$+P$+E$+R$
270 DRAW"BM124,22;L6;U6;R6;D6;BU3;L6;BD3;BR10"+Y$
280 DRAW"BM99,30;D3;R6;U6;BR3;BD6"+E$+R$+E$+M$+Y$
290 DRAW"BM99,44"+S$+P$+"U6;BR6;BD6"+L$+L$+E$+R$
300 WIDTH(74,13)-(74,5)-(200,5):REM GETS THE WORD "SUPERSTAMPER"
310 PALETTE(0,85)-(20,65)-(250,65):REM STAMPS WORD ELONGATED & SKEWED
320 PALETTE(0,90)-(20,110)-(250,110):REM STAMPS WORD UPSIDEDOWN &BACKWARD (INVERTED) AND ELONGATED & SKEWED
330 WIDTH(97,46)-(97,16)-(166,16)
340 PALETTE(50,190)-(50,110)-(220,110)
350 GOSUB 670
360 REM DRAWS FIGURE IN BOX
370 PCLS:LINE(88,125)-(168,65),PSET,B
380 DRAW"C2BM92,123;M+4,-6;M+4,+6;BM-2,-2;L2"
390 DRAW "BM92,67;D6;R8;U6;L8;BD3;R8"
400 DRAW"BM164,67;L8;D6;R8"
410 DRAW"BM128,82;D18;M-10,+20;BR20;M-10,-20"
420 CIRCLE(128,74),8
430 LINE(114,86)-(142,86),PSET
440 CIRCLE(112,86),4,2
450 PAINT (112,86),2,2
460 CIRCLE(144,86),4,3
470 PAINT(144,86),3,3
480 LINE(114,122)-(120,118),PSET,BF
490 COLOR3
500 LINE(136,122)-(142,118),PSET,BF
510 WIDTH(88,126)-(88,65)-(170,65):REM GETS STICK FIGURE
520 PCLS
530 PALETTE(0,62)-(0,0)-(82,0):REM     NO DISTORTION
540 PALETTE(168,62)-(168,0)-(84,0):REM STAMPS AS MIRROR IMMAGE
550 PALETTE(0,65)-(0,65+60)-(82,65+60):REM INVERTED (UPSIDEDOWN & BACKWARDS)
555 PALETTE(84,65)-(168,65)-(168,126):REM SIDEWAYS (90 DEGREE ROTATION)
560 PALETTE(0,191)-(0,129)-(255,129):REM ELONGATED RIGHT-LEFT
570 PALETTE(172,127)-(172,0)-(255,0):REM ELONGATED UP-DOWN
580 REM THE FOLLOWING DEMONSTRATES SKEWING (TILTING) & ROTATION
590 GOSUB 670:PCLS:PALETTE(0,127)-(0,0)-(255,0):REM ENLARGED ONLY
600 GOSUB 670:PCLS:PALETTE(0,127)-(50,0)-(255,0):REM ENLARGED &  SLIGHT SKEW
610 GOSUB 670:PCLS:PALETTE(0,127)-(128,0)-(255,0):REM ENLARGED & LARGER SKEW
620 GOSUB 670:PCLS:PALETTE(200,0)-(200,127)-(0,127):REM 180 DEGREE ROTATION (UPSIDEDOWN)
630 GOSUB 670:PCLS:PALETTE(128,50)-(190,110)-(128,160):REM 135 DEGREE ROTATION
640 GOSUB 670:PCLS:PALETTE(42,95)-(170,31)-(212,95):REM ELONGATED &45 DEGREE ROTATION
650 SOUND 150,5:SOUND 100,10
660 GOSUB 670:GOTO 680
670 FOR X=220 TO 255:SOUND X,1:NEXT:RETURN
680 CLS:PRINT"NOW YOU MAY TRY INSERTING YOUR  OWN POINTS A, B AND C. YOU DO   NOT NEED PARENTHESES.  TYPE THE X AND Y COORDINATES SEPARATED BYA COMMA.  REMEMBER THAT THE     TOTAL HEIGHT CANNOT EXCEED 128  PIXELS":PRINT
690 PRINT"EXAMPLE AX,AY ? 0,127":PRINT
700 INPUT"AX,AY";AX,AY
710 INPUT"BX,BY";BX,BY
720 INPUT"CX,CY";CX,CY
725 IF A$="N"THEN PMODE 3,1:SCREEN 1,0 ELSE IF A$="Y"THEN PMODE 4,1:SCREEN 1,1:PMODE 3,1
730 PCLS:PALETTE(AX,AY)-(BX,BY)-(CX,CY)
740 GOSUB 670:GOTO 680
