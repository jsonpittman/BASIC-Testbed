1 'THE ASSEMBLY LINE
2 'BY WILLIAM NEE
3 'COPYRIGHT (C) AUGUST 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR200,&H6000-1
20 IF PEEK(&H6015)<>16 THEN LOADM"ENLARGE":POKE&HFF40,0
30 PCLEAR8:L=16:FB=&HFF00
40 LO=&H62AE
50 CLS:INPUT"SIZE OF SQUARE [3-6] ";S:SS=S-1:IF S<3 OR S>6 THEN 50
60 POKE &H6000,S:POKE &H6001,SS:POKE &H6002,S*S:POKE &H6003,S*S*S
70 PRINT"THIS WILL BE -",," 1.RANDOM PATTERN"," 2.YOU PICK THE PATTERN"
80 A$=INKEY$:IF A$=""THEN 80
90 A=VAL(A$):ON A GOTO 100,120
100 FOR X=0 TO SS:FOR Y=0 TO SS
110 POKE LO+S*Y+X,RND(2)-1:NEXTY,X:GOTO260
120 PMODE4,5:COLOR0,5:PCLS:SCREEN1,1
130 FOR X=0 TO SS:FOR Y=0 TO SS
140 POKE LO+S*Y+X,1:LINE(X*L,Y*L)-(X*L+L,Y*L+L),PSET,B:NEXT Y,X
150 X=JOYSTK(0):Y=JOYSTK(1)
160 XX=INT(X/4):YY=INT(Y/4):IF XX>SS OR YY>SS THEN 150
170 LINE(XX*L,YY*L)-(XX*L+L,YY*L+L),PRESET,B:LINE(XX*L,YY*L)-(XX*L+L,YY*L+L),PSET,B:IF PEEK(FB)=254 OR PEEK(FB)=126 THEN  350
180 A$=INKEY$:IF A$=CHR$(13) THEN 190 ELSE 150
190 X0=XX:Y0=YY:CLS
200 PRINT"WHICH DIRECTION?"," 1.FROM THE CORNERS IN"," 2.FROM THE CENTER OUT"," 3.REGULAR PATTERN"
210 D$=INKEY$:IF D$=""THEN 210
220 D=VAL(D$):IF D<1 OR D>3 THEN  210 ELSE POKE &H6004,D
230 PMODE4,1:COLOR0,5:PCLS:SCREEN1,1
240 EXEC &H6015
250 A$=INKEY$:IF A$=""THEN 250 ELSE 390
260 PMODE4,5:COLOR0,5:PCLS:SCREEN1,1
270 FOR X=0 TO SS:FOR Y=0 TO SS
280 IF PEEK(LO+S*Y+X)=1 THEN LINE(X*L,Y*L)-(X*L+L,Y*L+L),PSET,B
290 IF PEEK(LO+S*Y+X)=0 THEN LINE(X*L,Y*L)-(X*L+L,Y*L+L),PSET,BF
300 NEXT Y,X
310 A$=INKEY$:IF A$="" THEN 310
320 IF A$="^" THEN 100
330 IF A$=CHR$(13) THEN 190
340 GOTO 310
350 V=PPOINT(XX*L+L/2,YY*L+L/2)
360 IF V=5 THEN LINE(XX*L,YY*L)-(XX*L+L,YY*L+L),PSET,BF:POKE LO+S*YY+XX,0
370 IF V=0 THEN LINE(XX*L,YY*L)-(XX*L+L,YY*L+L),PRESET,BF:POKE LO+S*YY+XX,1
380 GOTO180
390 CLS:PRINT
400 PRINT"1.REFORMAT THIS SQUARE","2.BACK TO MAIN MENU"
410 A$=INKEY$:IF A$="" THEN 410
420 A=VAL(A$):ON A GOTO 440,470
430 GOTO 410
440 PMODE4,5:COLOR0,5:SCREEN1,1
450 XX=X0:YY=Y0
460 GOTO 150
470 GOTO 50
