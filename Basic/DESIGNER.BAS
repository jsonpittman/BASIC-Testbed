10 '   --------------------
30 '      ICON  DESIGNER
40 '   --------------------
50 ' WRITTEN BY SHANE MESSER
60 '    COPYRIGHT (C) 1989
70 '     BY FALSOFT INC.     
80 '   ALL  RIGHTS RESERVED
90 '   --------------------
100 '        DESIGNED       
110 '  FOR THE 128K COCO 3
120 '  --------------------
130 POKE 65497,0
140 DIM PS(16)
150 '
160 ON ERR GOTO 1610
170 '
180 X=0:Y=0:X1=0:X2=0:X3=0:Y3=0:K=0:W=0:E=0:R=0:T=0:B=0:M=0:N=0:C=0:Z=0:A=0:A$=""
190 HSCREEN 2:POKE &HE6C6,18:POKE &HE6C7,18:HCLS 0:RG=1:GOSUB 440:POKE &HE0D4,&H34:GOSUB 530:GOTO 230
200 HPAINT(X,Y),C,15:X1=INT(X/10):Y1=INT(Y/10):HSET(164+X1,120+Y1,C):RETURN
210 X=JOYSTK(0):Y=JOYSTK(1):X=X*5+1:Y=Y*3+1:IF Y>180 THEN Y=180 ELSE IF X>310 THEN X=310
220 RETURN
230 GOSUB 410
240 GOSUB 210
250 HGET(X,Y)-(X+10,Y+13),2:HDRAW"BM=X;,=Y;C15"+AR$:HPUT(X,Y)-(X+10,Y+13),2:IF VV=1 THEN RETURN
260 A$=INKEY$:IF A$<>"" THEN PLAY"T25L25O3A"
270 IF A$="I" THEN GOSUB 610
280 IF A$="L" THEN GOSUB 680
290 IF A$="S" THEN GOSUB 760
300 IF A$="C" THEN GOSUB 830
310 IF A$="P" THEN GOSUB 900
320 IF A$="G" THEN GOSUB 970
330 IF A$="R" THEN GOSUB 1050
340 IF A$="A" THEN GOSUB 1510
350 POKE 65497,0
360 GOSUB 210
370 IF BUTTON(0)=1 AND Y>179 THEN C=HPOINT(X,Y):GOSUB 570
380 HSCREEN 2:IF BUTTON(0)=1 AND X>0 AND Y>20 AND X<150 AND Y<170 AND HPOINT(X,Y)<15 THEN GOSUB 200
390 GOTO 240
400 '--------------------------- 
410 GOSUB 580:HGET(0,64)-(9,96),4:GOSUB 590:HGET(10,20)-(310,65),1:POKE &HFF98,128:POKE &HFF99,62:FOR X=0 TO 300 STEP 20:HCOLOR Z:HLINE(X,180)-(X+20,191),PSET,BF:Z=Z+1:NEXT X
420 B$="R5D5L5U5R5":FOR X=0 TO 140 STEP 10:FOR Y=20 TO 160 STEP 10:HDRAW"S8BM =X;,=Y;C15"+B$:NEXT Y,X
430 HCOLOR 15:A$="    Icon designer - By Shane Messer":HPRINT(0,0),A$:HCOLOR 15:A$="    Copyright (c) 1989, By Falsoft Inc.":HPRINT(0,1),A$
440 PALETTE 0,0:PALETTE 1,32:PALETTE 2,36:PALETTE 3,52:PALETTE 4,54:PALETTE 5,18:PALETTE 6,25:PALETTE 7,45:PALETTE 8,16:PALETTE 9,9:PALETTE 10,12:PALETTE 11,23:PALETTE 12,43:PALETTE 13,56:PALETTE 14,35:PALETTE 15,63
450 PS(1)=0:PS(2)=32:PS(3)=36:PS(4)=52:PS(5)=54:PS(6)=18:PS(7)=25:PS(8)=45:PS(9)=16:PS(10)=9:PS(11)=12:PS(12)=23:PS(13)=43:PS(14)=56:PS(15)=35:PS(16)=63
460 IF RG=1 THEN RG=0:RETURN
470 FOR K=1 TO 16:PALETTE K-1,PS(K):NEXT K
480 FOR Y=6 TO 13:READ A$:HPRINT (20,Y),A$:NEXT Y
490 HPRINT(24,16),"- Actual size" 
500 HPRINT(24,19),"- Current color"
510 C=1:GOSUB 570
520 GOTO 540
530 HBUFF 1,6946:HBUFF 2,110:HBUFF 3,160:HBUFF 4,700:GOSUB 580:LPOKE &H6E000,255:LPOKE &H6E001,255:HBUFF 1,6946:HBUFF 2,110:HBUFF 3,160:HBUFF 4,700:GOSUB 590
540 AR$="S4D10M+1,-1U8M+1,+1D6RDRD2RD2RULU2LU2LU5M+1,+1D3RU2M+1,+1DR"
550 RETURN
560 DATA (I)nvert,(C)lear,(P)ut Icon into set,(A)lter palette slot,(G)et icon from set,(L)oad icon set,(S)ave icon set,(R)eplace colors
570 PLAY"T25L25O3A":HCOLOR C:HLINE(163,148)-(178,163),PSET,BF:RETURN
580 POKE &HE0D4,&H37:RETURN
590 POKE &HE0D4,&H34:RETURN
600 RETURN
610 'INVERT
620 QQ=C:A$="*** Invert current icon ***":GOSUB 1110:IF NO=1 THEN RETURN
630 FOR X2=0 TO 32 STEP 2:FOR Y2=6 TO 55 STEP 3:X=X2*5+1:Y=Y2*3+1
640 IF X>0 AND Y>20 AND X<150 AND Y<170 AND HPOINT(X,Y)<15 THEN GOSUB 660
650 NEXT Y2,X2:C=QQ:RETURN
660 Z=HPOINT(X,Y):C=14-Z:GOSUB 200:RETURN
670 RETURN
680 ' LOAD
690 A$="*** Load a new set from disk ***":RT=0:GOSUB 1110:IF NO=1 THEN RETURN
700 RT=1234:A$="*** Insert your disk ***":B$="Press any key":GOSUB 1110
710 GOSUB 1380
720 POKE 65496,0:IF NO=0 THEN POKE &HE0D4,&H34:POKE &HFFA2,&H34:LOADM F$:POKE &HFFA2,&H3A:POKE &HE0D4,&H34
730 IF NO=0 THEN SF$=LEFT$(F$,INSTR(F$,".")-1)
740 IF NO=0 THEN OPEN"I",#1,SF$:FOR K=1 TO 16:INPUT #1,PS(K):PALETTE K-1,PS(K):NEXT K:CLOSE:RETURN
750 RETURN
760 'SAVE
770 A$="*** Save current set to disk ***":GOSUB 1110:IF NO=1 THEN RETURN
780 RT=1234:A$="*** Insert your save disk ***":B$="Press any key":GOSUB 1110
790 GOSUB 1380
800 POKE 65496,0:IF NO=0 THEN POKE &HE0D4,&H34:POKE &HFFA2,&H34:SAVEM F$,&H4000,&H5FFF,&HA027:POKE &HFFA2,&H3A:POKE &HE0D4,&H34:SF$=LEFT$(F$,INSTR(F$,".")-1)
810 IF NO=0 THEN OPEN"O",#1,SF$:FOR K=1 TO 16:WRITE #1,PS(K):NEXT K:CLOSE:RETURN
820 RETURN
830 ' CLEAR
840 A$="*** Clear current icon ***":GOSUB 1110
850 IF NO=1 THEN RETURN
860 FOR X2=0 TO 32 STEP 2:FOR Y2=6 TO 55 STEP 3:X=X2*5+1:Y=Y2*3+1
870 IF X>0 AND Y>20 AND X<150 AND Y<170 AND HPOINT(X,Y)<15 THEN GOSUB 200
880 NEXT Y2,X2
890 RETURN
900 ' PUT
910 A$="*** Put icon into set ***":GOSUB 1120:IF NO=1 THEN RETURN
920 GOSUB 1240
930 HGET (164,122)-(178,136),3:HPUT(C1,C2)-(C1+14,C2+14),3
940 GOSUB 1330
950 RETURN
960 RETURN
970 ' GET
980 A$="*** Get icon and erase current one ***":GOSUB 1120:IF NO=1 THEN RETURN
990 GOSUB 1240
1000 HGET (C1,C2)-(C1+14,C2+14),3 
1010 HPUT(164,122)-(178,136),3
1020 GOSUB 1330
1030 QQ=C:FOR X2=0 TO 14:FOR Y2=0 TO 15:X=X2*10+1:Y=Y2*10+21:C=HPOINT(164+X2,122+Y2):HPAINT(X,Y),C,15:NEXT Y2,X2:HCOLOR QQ:C=QQ
1040 RETURN
1050 QQ=C:A$="*** Replace Source  color with dest. ***":GOSUB 1110:IF NO=1 THEN RETURN
1060 GOSUB 1490:IF NO=1 THEN RETURN:ELSE HSCREEN 2:FOR X2=0 TO 14:FOR Y2=0 TO 14:X=X2*10+1:Y=Y2*10+21:C=HPOINT(164+X2,122+Y2)
1070 IF C=V1 THEN HSET(164+X2,122+Y2,V2):HPAINT(X,Y),V2,15
1080 NEXT Y2,X2
1090 C=QQ
1100 RETURN
1110 ' * ARE YOU SURE *
1120 GOSUB 580
1130 A=LEN(A$):A=40-A:A=INT(A/2):A$=STRING$(A," ")+A$
1140 IF RT<>1234 THEN B$="Are you sure?"
1150 B=LEN(B$):B=40-B:B=INT(B/2):B$=STRING$(B," ")+B$
1160 RT=0
1170 HGET(0,64)-(320,96),1
1180 FOR X=0 TO 320 STEP 10:HPUT(X,64)-(X+9,96),4:NEXT X
1190 HCOLOR 15:HLINE(0,64)-(320,96),PSET,B:HPRINT(0,9),A$:HPRINT(0,10),B$
1200 A$=INKEY$:IF A$="" THEN 1200 
1210 IF A$="Y" OR A$="y" THEN YS=1:NO=0 ELSE YS=0:NO=1
1220 HPUT(0,64)-(320,96),1
1230 GOSUB 590:RETURN
1240 GOSUB 580
1250 HGET(10,20)-(220,65),1
1260 GOSUB 590
1270 HPUT(10,20)-(220,65),1
1280 '  MAIN ROUTINE TO SELECT
1290 X=JOYSTK(0):Y=JOYSTK(1):X=INT(X/5):Y=INT(Y/30)
1300 X1=X:Y1=Y:X=X*16+10:Y=Y*15+20:VV=1:X=X+5:Y=Y+5:GOSUB 250:X=X-5:Y=Y-5:VV=0
1310 IF BUTTON(0)=1 THEN C1=X+1:C2=Y:RETURN
1320 GOTO 1290
1330 ' END OF MAIN ROUTINE
1340 HGET(10,20)-(220,65),1
1350 GOSUB 580
1360 HPUT(10,20)-(220,65),1
1370 RETURN
1380 WIDTH 40:CLS 2:ATTR 0,1:CLS
1390 A$="*** ENTER FILENAME ***":GOSUB 1480:PRINT:PRINT:A$=STRING$(4,CHR$(8))+"->":GOSUB 1480:LINEINPUT F$:HSCREEN 2
1400 WIDTH 40:IF F$="DIR" THEN POKE 65496,0:DIR:PRINT"Free ->";FREE(0):EXEC44539:HSCREEN 2:F$=""
1410 NO=1:IF LEN(F$)>12 THEN RETURN
1420 IF F$="" THEN RETURN ELSE IF INSTR(F$,".")>9 THEN RETURN 
1430 FOR K=1 TO LEN(F$):IF MID$(F$,K,1)="/" THEN MID$(F$,K,1)="."
1440 NEXT K
1450 IF LEN(F$)>8 AND INSTR(F$,".")<1 THEN RETURN
1460 IF INSTR(F$,".")<1 THEN F$=F$+".BIN"
1470 NO=0:RETURN
1480 A=LEN(A$):A=40-A:A=INT(A/2):PRINTSTRING$(A," ");A$;:RETURN
1490 WIDTH 40:CLS 2:ATTR 0,1:CLS:A$="Enter source color ->":GOSUB 1480:LINEINPUT A$:V1=VAL(A$):A$="Enter destination color ->":GOSUB 1480:LINEINPUT A$:V2=VAL(A$)
1500 IF V1>-1 AND V1<15 AND V2>-1 AND V2<15 THEN NO=0:RETURN ELSE NO=1:HSCREEN 2:RETURN
1510 A$="*** Alter palette slots ***":GOSUB 1110:IF NO=1 THEN RETURN ELSE QQ=C
1520 X=JOYSTK(0):X=INT(X/3)
1530 IF X>15 THEN X=16
1540 HGET(X*20,180)-(X*20+20,191),4:HPUT(X*20,180)-(X*20+20,191),4,PRESET:HPUT(X*20,180)-(X*20+20,191),4,PSET
1550 IF BUTTON(0)=1 THEN 1570
1560 GOTO 1520
1570 IF BUTTON(0)=1 THEN 1570
1580 X=X+1
1590 PS(X)=JOYSTK(0):PALETTE X-1,JOYSTK(0):IF BUTTON(0)=1 THEN 1600 ELSE 1590
1600 IF BUTTON(0)=1 THEN 1600 ELSE RETURN
1610 HSCREEN2:POKE 65497,0:V=ERNO:ER$="Unknown Error!"
1620 IF V=20 THEN ER$="I/O Error!" ELSE IF V=10 THEN ER$="Device number error!" ELSE IF V=31 OR V=26 THEN ER$="Unlocatale disk name!" ELSE IF V=23 THEN ER$="Input past end of file!" ELSE IF V=1 THEN ER$="Syntax Error in line"+STR$(ERLIN)
1630 A$=ER$:B$="Press any key":RT=1234:GOSUB 1110:GOTO 300
