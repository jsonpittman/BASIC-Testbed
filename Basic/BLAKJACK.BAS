10 'BLAKJACK by Bill Bernico and                George Aftamonow
20 BA=100
30 CLS3:FORX=1056TO1472STEP32:POKEX,191:NEXT:PRINT@0,STRING$(32,191);:FORX=1087TO1535STEP32:POKEX,191:NEXT:PRINT@480,STRING$(31,191);:T=0:CA=1:PRINT@43,"CASH:";BA;:PRINT@75,"";:PLAY"O4T60B":INPUT"BET:";BE:PL=1:PRINT@85,STRING$(10,175);:POKE1119,191
40 IF BE>BA THEN 30
50 IFCA=1THENP=354ELSEIFCA=2THENP=359ELSEIFCA=3THENP=364ELSEIFCA=4THENP=369ELSEIFCA=5THENP=374ELSEIFCA=6THENP=379
60 GOTO90
70 IFCA=1THENP=129ELSEIFCA=2THENP=134ELSEIFCA=3THENP=139ELSEIFCA=4THENP=144ELSEIFCA=5THENP=149ELSEIFCA=6THENP=154
80 P=P+1
90 X=RND(13)+49:IFX=58THENX=10ELSEIFX=59THENX=74ELSEIFX=60THENX=75ELSEIFX=61THENX=81ELSEIFX=62THENX=65
100 E=RND(2):IFE=1THENE$=CHR$(128)ELSEIFE=2THENE$=CHR$(191)
110 PRINT@P+32," "+E$+" ";:IFX=10THENPRINT@P,"10 ";:PLAY"O5T60B":PRINT@P+64," 10";:FORU=1TO350:NEXT:R=10:GOTO150
120 PRINT@P,CHR$(X)"  ";:PRINT@P+64,"  ";CHR$(X);:PLAY"O5T60B":FORU=1TO350:NEXT
130 R=X-48:IFR=17THENIFR+T>27THENR=1ELSER=11
140 IFR>25THENR=10
150 CA=CA+1:T=T+R:IFCA=2AND PL=1THEN50
160 IFT>21 AND PL=1 THEN270
170 IFT>21 AND PL=2 THEN260
180 IFPL=1THEN210
190 IFCA<3THEN70
200 PL(2)=T:IFPL(2)<PL(1) ANDCA<6THEN70ELSE250
210 PRINT@258,"CARD TOTAL";T;:POKE1295,96:EXEC43345:PRINT@290,"ANOTHER CARD ?";:FORF=1314TO1327:POKEF,PEEK(F)-64:NEXT:ELSEPL(1)=T:GOTO240
220 I$=INKEY$:IFI$=""THEN220
230 IFI$="Y"THEN50ELSEIFI$="N"THENPL(1)=T ELSE220
240 PL=2:CA=1:T=0:GOTO70
250 IF(PL(2)=PL(1)OR PL(2)>PL(1))THEN270ELSE260
260 PRINT@277,"YOU WON";:PLAY"O3FGFGFG":BA=BA+BE:PRINT@309,"any key";:POKE1336,32:EXEC44539:GOTO30
270 PRINT@274,"COMPUTER WON";:BA=BA-BE:PRINT@306,"hit any key";:POKE1333,32:POKE1337,32:POKE1341,32:PLAY"O2ABABAB":EXEC44539:IFBA<1THEN280ELSE30
280 PRINT@258,"YOU'RE BROKE ";:PRINT@274,STRING$(12,175);:PRINT@306,STRING$(12,175);:PRINT@290,"ANOTHER GAME ?";:PLAY"O5BAGFEDCO4BAGFEDCO3BAGFEDCO2BAGFEDCO1BAGFEDC
290 I$=INKEY$:IFI$="Y"THENRUNELSEIFI$="N"THENCLS:ENDELSE290
1 'BLACK JACK
2 'WRITTEN BY RITCHIE CARROLL
3 'COPYRIGHT (C) MARCH 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
20 GOTO1025
48 DATA XLE$(14);BUNE3BD
49 DATA BR2R2LU6DNLD5BL3
50 DATA NR4U2ER2EUHL2GBD5
51 DATA BUFR2EUHNLEUHL2GBD5
52 DATA BR3U6G3R4BD3BL4
53 DATA BUFR2EU2HL3U2R4BD6BL4
54 DATA BRR2EUHL3D2U4ER2BL3BD6
55 DATA UE4UL4BD6
56 DATA XN$(3);BUUENRHUBD5
57 DATA BRR2EU4D2L3HUER2BL3BD6
65 DATA U2R4D2U4H2G2D4
66 DATA RU6LR3FDGNL2FDGL3
67 DATA BUU4ER2FBD4GL2BL
68 DATA R3EU4HL3RD6L
69 DATA NR4U3NR3U3NR4D6
70 DATA U3NR3U3NR4D6
71 DATA BRR3U3NLBU3L3GD4BD
72 DATA U6D3R4U3D6BL4
73 DATA BRR2LU6RL2BLBD6
74 DATA BU2DFR2EU5BL4BD6
75 DATA U6D3RNE3NF3LD3
76 DATA NR4NU6
77 DATA U6F2NDE2D6BL4
78 DATA U6F3RU3D6BL4
79 DATA BRR2EU4HL2GD4BD
80 DATA U6R3FDGL3D3
81 DATA BUU4ER2FD3G2LHUBR2F2BL4
82 DATA U6R3FDGL3RF3BL4
83 DATA BUFR2EUHL2HUER2FBD5BL4
84 DATA BR2U6R2L4BD6
85 DATA BUU5BR4D5GL2BL
86 DATA BR2UH2U3BR4D3G2DBL2
87 DATA NU6E2NUF2NU6BL4
88 DATA UE4UBL4DF4DBL4
89 DATA BR2U3H2UBR4DG2D3BL2
90 DATA NR4UE4UL4BD6
95 DATA BU3NR4BD3BR8
100 DATA BRBURULBU2RULBD5BR7
105 DATA YOU LOSE,T3L3O1AC
110 DATA BUST,L100T10O1ABCDEFGABCO2ABCDEFGABC
115 DATA TIE,T5L5O3AECEA
120 DATA YOU WIN,T8L8O3CEACEACEAAAAA
125 DATA FIVE CARD DRAW,T7L7O1AEAEAEO2AEAEAEO3AEAEAEL3A
130 DATA BLACK JACK,T7L7O1CGCGCGO2CGCGCGO3CGCGCGL3C
195 PROCEDURES:
200 A$=INKEY$:IFA$=""THEN200
205 RETURN
210 DRAW"BM=A;,=Z;"
215 RETURN
220 B=SGN(88-DH)+1
225 RETURN
230 IFPEEK(63030)=15THEN POKE65497,0:ON BRK GOTO1635 ELSE POKE65495,0
235 RETURN
240 IFPEEK(63030)=15THEN POKE65496,0 ELSE POKE65494,0
245 RETURN
250 DRAW CARDS:
255 COLOR1:DC=5+(X-1)*50
260 LINE(DC,34+DH)-(DC+44,88+DH),PSET,BF
265 RETURN
270 A=DC+2:Z=41+DH
275 GOSUB210:RETURN
280 A=DC+38:Z=87+DH
285 GOSUB210:RETURN
290 IFR=1THEN PUT(H,V)-(H+14,V+13),HR,PSET:RETURN
295 IFR=2THEN PUT(H,V)-(H+14,V+13),DM,PSET:RETURN
300 IFR=3THEN PUT(H,V)-(H+14,V+13),CB,PSET:RETURN
305 PUT(H,V)-(H+14,V+13),SD,PSET:RETURN
310 PUT(H,V)-(H+14,V+13),RC,PSET:RETURN
315 GOSUB220
320 DF(X,B)=1:E=NC
325 FORA=1TO4
330 IFCD(NC)<13*A+1 THEN340
335 NEXTA
340 N=CD(NC)-13*(A-1):R=A
345 A=238:Z=118:Y=0:PN=NC
350 COLOR0:GOSUB815
355 NC=NC-1:PN=NC
360 COLOR1:GOSUB815
365 GOSUB255:V=42+DH
370 IFN>9THEN A=2ELSE A=N
375 AW=A/3:A=INT(AW)
380 B=FIX(10*(AW-A))/10
385 IFY=1THEN C=A+1-ABS(SGN(B-.3)) ELSE C=A+SGN(SGN(B-.5)+1)
390 IFSGN(C-1)=1THEN H=DC:GOSUB290:H=DC+28:GOSUB290
395 IFSGN(C/2-INT(C/2))=1THEN H=DC+14:GOSUB290
400 V=V+13:Y=Y+1:IFY<3THEN385
405 COLOR0
410 IFN>9THEN ON N-9GOTO465,495,520,545ELSE IFN=1THEN A$=LE$(0)ELSE A$=N$(N)
415 GOSUB270:DRAWA$
420 GOSUB280:DRAWA$
425 RETURN
430 GOSUB255
435 LINE(DC+5,39+DH)-(DC+39,83+DH),PRESET,B
440 LINE(DC+6,40+DH)-(DC+38,82+DH),PRESET,B
445 H=DC+15
450 V=44+DH:GOSUB310
455 V=65+DH:GOSUB310
460 RETURN
465 GOSUB270
470 A$=N$(1)+S1$+LE$(14)
475 DRAW"BL2"+A$:GOSUB280
480 DRAWS2$+A$+"BL26BU23"
485 TS$="TEN":GOSUB705
490 RETURN
495 GOSUB270
500 DRAWLE$(9):GOSUB280
505 DRAWLE$(9)+"BL30BU23"
510 TS$="JACK":GOSUB705
515 RETURN
520 GOSUB270
525 DRAWLE$(16):GOSUB280
530 DRAWLE$(16)+"BL34BU23"
535 TS$="QUEEN":GOSUB705
540 RETURN
545 GOSUB270
550 DRAWLE$(10):GOSUB280
555 DRAWLE$(10)+"BL30BU23"
560 TS$="KING":GOSUB705
565 RETURN
570 TEXT INPUT:
575 D$=LEFT$(D$,LEN(D$)-1)
580 RETURN
585 C$=RIGHT$(B$,1)
590 B$=LEFT$(B$,LEN(B$)-1)
595 RETURN
600 LINE(DC+1,34)-(DC,40),PSET
605 RETURN
610 POKE282,1:B$=""
615 DC=PEEK(195)*256+PEEK(196)
620 GOSUB600:GOSUB200
625 COLOR0:GOSUB600:COLOR1
630 IFA$=CR$AND B$<>""THEN SB$=B$:RETURN
635 IFA$=BS$THEN675
640 IFLEN(B$)=28THEN615
645 IF"A">A$AND A$<>" "THEN615
650 IF"Z"<A$THEN615
655 IFA$=" "THEN665
660 DRAWLE$(ASC(A$)-65)
665 DRAWS1$
670 B$=B$+A$:GOTO615
675 IFB$=""THEN615
680 GOSUB585
685 IFC$=" "THEN DRAWS2$ELSE COLOR0:DRAWS2$+LE$(ASC(C$)-65):COLOR1
690 GOTO615
695 TEXT PRINT:
700 GOSUB210
705 FORC=1TOLEN(TS$)
710 C$=MID$(TS$,C,1)
715 IFC$=" "THEN DRAWS1$ELSE IFC$="-"THEN DRAWLE$(26)ELSE IFC$=":"THEN DRAWLE$(27)ELSE DRAWLE$(ASC(C$)-65)+S1$
720 NEXTC:RETURN
725 NUMERICAL INPUT:
730 DRAW"BM24,20"
735 SOUND10,1:B$=""
740 GOSUB200
745 IFA$="E"THEN1535
750 IFA$=BS$THEN790
755 IFB$<>""THEN IFA$="D"OR A$="H"THEN BT=VAL(B$):RETURN
760 IF"0">A$OR"9"<A$THEN740
765 D$=B$+A$
770 IFVAL(D$)>CT THEN GOSUB575:GOTO740
775 IFA$="0"AND B$=""THEN740
780 DRAWN$(VAL(A$))+S1$
785 B$=D$:GOTO740
790 IFVAL(B$)=0THEN740
795 GOSUB575:GOSUB585:COLOR0
800 DRAWS2$+N$(VAL(C$))
805 COLOR1:GOTO740
810 NUMERICAL PRINT:
815 GOSUB210
820 A$=STR$(PN):FORC=2TOLEN(A$)
825 DRAWN$(VAL(MID$(A$,C,1)))+S1$
830 NEXTC:RETURN
835 SUM CARDS:
840 GOSUB220:PN=S1(B)
845 D=S2(B):TS$=" OR "
850 S1(B)=0:S2(B)=0:AC=0:FL=0
855 FORY=E TO S
860 FORZ=1TO4
865 IFCD(Y)<13*Z+1 THEN875
870 NEXTZ
875 N=CD(Y)-13*(Z-1)
880 IFN=1THEN AC=AC+1 ELSE IFN>9THEN S1(B)=S1(B)+10 ELSE S1(B)=S1(B)+N
885 NEXTY:S2(B)=S1(B)+AC
890 IFAC=0THEN900
895 IFS1(B)>11-AC THEN S1(B)=S1(B)+AC ELSE S1(B)=S1(B)+10+AC
900 A=96:Z=98+DH
905 COLOR0:GOSUB815
910 GOSUB705:PN=D:GOSUB820
915 PN=S1(B):COLOR1:GOSUB815
920 IFS1(B)<>S2(B)AND S1(B)<>21THEN GOSUB705:PN=S2(B):GOSUB820:FL=1
925 RETURN
930 SHUFFLE DECK:
935 A=72:Z=108:PLAY"O1T255L255"
940 TS$="SHUFFLING DECK"
945 GOSUB700:FORX=1TO52
950 B=RND(52):C=RND(52)
955 IFB=C THEN950
960 PLAY"AE"
965 D=CD(B):CD(B)=CD(C)
970 CD(C)=D:NEXTX
975 COLOR0:GOSUB700
980 A=238:Z=118:PN=NC
985 GOSUB815:PN=52:NC=PN
990 COLOR1:GOSUB815
995 RETURN
1000 PRIMARY SCREEN:
1005 CLS:PRINT@235,"BLACK JACK"
1010 PRINT@483,"BY  JAMES RITCHIE CARROLL"
1015 RETURN
1020 INITIALIZE:
1025 PCLEAR4:CLEAR500
1030 GOSUB1005:SCREEN0,1
1035 IFPEEK(188)=14THEN DA=1:OPEN"D",#1,"HS",45
1040 GOSUB230:PLAY"V25"
1045 DIMHR(14,13),DM(14,13),CB(14,13),SD(14,13),RC(14,13),N$(10),LE$(28),RD$(6),P$(6),CD(52),DF(5,2),S1(2),S2(2)
1050 FORX=0TO9
1055 READN$(X)
1060 NEXT:FORX=0TO27
1065 READLE$(X)
1070 NEXT:FORX=1TO6
1075 READRD$(X),P$(X)
1080 NEXT:FORX=1TO52
1085 CD(X)=X
1090 NEXT:X=RND(-TIMER)
1095 S1$="BR8":S2$="BL8"
1100 BS$=CHR$(8):CR$=CHR$(13)
1105 PMODE4,1:PCLS1:COLOR0
1110 CIRCLE(4,4),3:CIRCLE(10,4),3:DRAW"BM3,7F4E4":PAINT(4,4),0:PAINT(10,4),0,0:PAINT(7,5),0,0:PSET(7,3,0)
1115 DRAW"BM7,24E5H5G5F5":PAINT(7,22),0,0
1120 CIRCLE(3,33),2:CIRCLE(11,33),2:CIRCLE(7,29),2:CIRCLE(7,32),2:DRAW"BM7,34DG2R4H2DU7D4L4R8"
1125 CIRCLE(4,45),2:CIRCLE(10,45),2:DRAW"BM2,45E5F5BM7,47DG2R4H2DBM4,45R6DL6":PAINT(7,43),0,0
1130 DRAW"BM1,59E3U3F3G3F3G3U3H3BR12H3U3G3F3G3F3U3E3":PAINT(7,59),0,0:COLOR1
1135 GET(0,0)-(14,13),HR,G
1140 GET(0,13)-(14,26),DM,G
1145 GET(0,26)-(14,39),CB,G
1150 GET(0,39)-(14,52),SD,G
1155 GET(0,52)-(14,65),RC,G
1160 PCLS:SCREEN1:A=88:Z=10
1165 TS$="BLACK JACK":GOSUB700
1170 Z=16:TS$="----------"
1175 GOSUB700:A=20:Z=10
1180 TS$="BET-":GOSUB700:A=196
1185 TS$="CREDIT-":GOSUB700
1190 DRAW"BM12,20"+LE$(27)
1195 A=6:Z=98:TS$="PLAYER HAS:"
1200 GOSUB700:Z=186
1205 TS$="DEALER HAS:":GOSUB700
1210 A=150:Z=118
1215 TS$="CARDS LEFT:":GOSUB700
1220 CT=1000:NX=CT:WN=0:NC=0
1225 FORX=1TO5:FORY=1TO2
1230 DF(X,Y)=1:NEXTY,X
1235 'BEGIN:
1240 COLOR0:A=24:Z=20
1245 PN=BT:GOSUB815:A=200
1250 PN=CT:GOSUB815:A=D:Z=108
1255 GOSUB700:A=96:Z=98
1260 PN=S1(2):GOSUB815
1265 TS$=" OR ":GOSUB705
1270 PN=S2(2):GOSUB820:Z=186
1275 PN=S1(1):GOSUB815:GOSUB705
1280 PN=S2(1):GOSUB820
1285 COLOR1:IFBJ=2THEN BT=BT*2
1290 CT=CT+BT*WN
1295 IFCT>NX THEN NX=CT
1300 IFCT>9999999THENA=200:Z=20:TS$="MAXIMUM":GOSUB700:A=208:Z=30:TS$="SCORE":GOSUB700:FORX=1TO4500:NEXTX:GOTO1535
1305 A=200:Z=20:PN=CT
1310 GOSUB815:DH=0
1315 IFWN<1AND CT=0THEN1535
1320 GOSUB220:FORX=1TO5
1325 IFDF(X,B)=1THEN GOSUB430
1330 DF(X,B)=0:NEXTX
1335 IFDH=0THEN DH=88:GOTO1320
1340 IFNC<9THEN GOSUB935
1345 BJ=0:GOSUB730:GOSUB1380
1350 TS$=RD$(WN)
1355 D=128-4*LEN(TS$)
1360 A=D:Z=108:GOSUB700
1365 PLAYP$(WN):WN=SGN(WN-3)
1370 GOTO1240
1375 PLAYER'S TURN:
1380 DH=0:S=NC
1385 X=1:GOSUB315
1390 X=2:GOSUB315
1395 GOSUB840
1400 IFS1(2)=21THEN BJ=1:GOTO1465
1405 GOSUB200
1410 IFA$="H"THEN1430
1415 IFA$="S"THEN1465
1420 IFA$="E"THEN1535
1425 GOTO1405
1430 X=X+1:GOSUB315
1435 GOSUB840
1440 IFS1(2)>21THEN WN=2:RETURN
1445 IFX=5THEN WN=5:BJ=2:RETURN
1450 IFS1(2)=21THEN1465
1455 GOTO1405
1460 DEALER'S TURN:
1465 DH=88:S=NC
1470 X=1:GOSUB315
1475 X=2:GOSUB315
1480 GOSUB840
1485 IFS1(1)=21THEN IFBJ=1THEN WN=3:RETURN ELSE WN=1:RETURN
1490 IFBJ=1THEN BJ=2:WN=6:RETURN
1495 IFS1(1)>S1(2)THEN WN=1:RETURN
1500 IFS1(1)>16THEN IFFL=1THEN1505 ELSE IFS1(1)<S1(2)THEN WN=4:RETURN ELSE IFS1(1)=S1(2)THEN WN=3:RETURN
1505 X=X+1:GOSUB315
1510 GOSUB840
1515 IFS1(1)>21THEN WN=4:RETURN
1520 IFX=5THEN WN=1:RETURN
1525 GOTO1495
1530 END SCREEN:
1535 PCLS:LINE(170,103)-(213,157),PSET,BF:COLOR0
1540 DRAW"BM173,110"+LE$(0)+"BM207,156"+LE$(0)
1545 R=1:H=185:V=124:GOSUB290
1550 COLOR1:A=1:Z=10
1555 TS$="BLACK JACK-":GOSUB700
1560 A=31:Z=20:TS$="BY JAMES RITCHIE CARROLL":GOSUB700
1565 A=5:Z=30:IFDA=1THEN GOSUB240:IFLOF(1)=1THEN GET#1,1:INPUT#1,HS,SB$:GOSUB230
1570 IFHS=0OR NX>HS THENTS$="INPUT YOUR NAME":GOSUB700:DRAW"BM1,40"+LE$(26)+"BR9":GOSUB610:HS=NX:IFDA=1THEN GOSUB240:WRITE#1,HS,SB$:PUT#1,1:GOSUB230
1575 Z=60:TS$="LAST SCORE:":GOSUB700:DRAW"BR":PN=NX:GOSUB820
1580 Z=70:TS$="HIGH SCORE:":GOSUB700:DRAW"BR":PN=HS:GOSUB820
1585 A=3:Z=80:TS$="BY-":GOSUB700:DRAW"BR":TS$=SB$:GOSUB705
1590 Z=120:TS$="PRESS:":GOSUB700
1595 A=9:Z=130:TS$="S-TO STOP GAME":GOSUB700
1600 Z=140:TS$="P-TO PLAY AGAIN":GOSUB700
1605 A=4:Z=180:TS$="RAINBOW-":GOSUB700
1610 A=19:Z=190:TS$="THE COLOR COMPUTER MAGAZINE":GOSUB700
1615 GOSUB200
1620 IFA$="S"THEN1635
1625 IFA$="P"THEN1160
1630 GOTO1615
1635 GOSUB1005:GOSUB240
1640 POKE282,255:CLOSE
1645 END
