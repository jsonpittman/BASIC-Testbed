1 'THE ASSEMBLY LINE: PART 8
2 'WRITTEN BY WILLIAM P. NEE
3 'COPYRIGHT (C) MARCH 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR 200,&H7000-1:PCLEAR 8
20 DIM U0(11),V0(11),U1(11),V1(11),U2(11),V2(11),U3(11),V3(11),DX(11),DY(11),XX(5),YY(5),A(11),B(11),C(11),D(11),E(11),F(11),G(11),H(11),AA(1),CC(1),N$(6)
30 IF PEEK(&H7019)<>134 THEN LOADM"WARP"
40 FOR N=0 TO 11:READ U0(N),V0(N),U1(N),V1(N),U2(N),V2(N),U3(N),V3(N):NEXT
50 FOR N=0 TO 11:READ DX(N),DY(N):NEXT
60 FOR N=0 TO 2:READ N$(N):NEXT
70 FOR N=4 TO 6:READ N$(N):NEXT:GOTO 330
80 PMODE3,1:PCLS:SCREEN1,1:CO=0
90 FOR R=0 TO 90 STEP 9:LINE(R,R)-(255-R,191-R),PSET,B:CO=(CO+1)AND 3:COLOR CO,1:NEXT
100 LINE(0,0)-(255,191),PSET
110 LINE(255,0)-(0,191),PSET
120 EXEC&HADFB:PCOPY 1 TO 5:PCOPY 2 TO 6:PCOPY 3 TO 7:PCOPY 4 TO 8:PCLS
130 FOR X=63 TO 191 STEP 64:LINE(X,0)-(X,191),PSET:NEXT
140 FOR Y=63 TO 127 STEP 64:LINE(0,Y)-(255,Y),PSET:NEXT
150 A$=INKEY$:IF A$=CHR$(94) THEN 190 ELSE IF A$="" THEN 730
160 A=VAL(A$):IF A<1 OR A>6 THEN  150
170 IF A<4 THEN A=A-1
180 GOTO 620
190 PCOPY 5 TO 1:PCOPY 6 TO 2:PCOPY 7 TO 3:PCOPY 8 TO 4
200 PMODE3,5:PCLS:SCREEN1,1
210 FOR N=0 TO 11
220 POKE &H7017,DX(N):POKE &H7018,DY(N)
230 A(N)=U1(N)-U0(N):POKE &H700F,A(N)
240 B(N)=U3(N)-U0(N):IF B(N)<0 THEN POKE &H7010,256+B(N) ELSE POKE &H7010,B(N)
250 C(N)=U0(N)-U1(N)+U2(N)-U3(N):IF C(N)<0 THEN POKE &H7011,256+C(N) ELSE POKE &H7011,C(N)
260 D(N)=U0(N):POKE &H7012,D(N)
270 E(N)=V1(N)-V0(N):IF E(N)<0 THEN POKE &H7013,256+E(N) ELSE POKE &H7013,E(N)
280 F(N)=V3(N)-V0(N):POKE &H7014,F(N)
290 G(N)=V0(N)-V1(N)+V2(N)-V3(N):IF G(N)<0 THEN POKE &H7015,256+G(N) ELSE POKE &H7015,G(N)
300 H(N)=V0(N):POKE &H7016,H(N)
310 EXEC &H7019:NEXT N
320 EXEC &HADFB
330 CLS:PRINT:PRINT"1> RUN PROGRAM",,"2> SAVE PICTURE",,"3> LOAD A PICTURE","4> END PROGRAM"
340 PRINT:PRINT"PRESS DESIRED KEY -"
350 A$=INKEY$:IF A$="" THEN 350
360 A=VAL(A$):IF A<1 OR A>4 THEN  330
370 ON A GOTO 380,420,420,410
380 RESTORE:FOR N=0 TO 11:READ U0(N),V0(N),U1(N),V1(N),U2(N),V2(N),U3(N),V3(N):NEXT
390 FOR N=0 TO 11:READ DX(N),DY(N):NEXT
400 GOTO 80
410 PMODE4:PCLEAR4:CLEAR200,&H7FFF:NEW
420 LINEINPUT"PICTURE NAME/EXT -";NM$
430 IF A=2 THEN PCOPY 5 TO 1:PCOPY 6 TO 2:PCOPY 7 TO 3:PCOPY 8 TO 4
435 IF A=2 THEN VERIFYON:SAVEM NM$,&HE00,&H25FF,&HE00:GOTO 330
440 PMODE3,1:PCLS:SCREEN1,1
450 LOADM NM$:GOTO 120
460 'U,V COORDINATES
470 DATA 0,0,31,0,31,63,0,63
480 DATA 32,0,63,0,63,63,32,63
490 DATA 64,0,95,0,95,63,64,63
500 DATA 96,0,127,0,127,63,96,63
510 DATA 0,64,31,64,31,127,0,127
520 DATA 32,64,63,64,63,127,32,127
530 DATA 64,64,95,64,95,127,64,127
540 DATA 96,64,127,64,127,127,96,127
550 DATA 0,128,31,128,31,191,0,191
560 DATA 32,128,63,128,63,191,32,191
570 DATA 64,128,95,128,95,191,64,191
580 DATA 96,128,127,128,127,191,96,191
590 DATA 0,0,32,0,64,0,96,0
600 DATA 0,64,32,64,64,64,96,64
610 DATA 0,128,32,128,64,128,96,128
620 'CURSOR ROUTINE
630 LINE(2*U1(A),V1(A))-(2*U2(A),V2(A)),PSET:LINE(2*U1(A+4),V1(A+4))-(2*U2(A+4),V2(A+4)),PSET
640 LINE(2*U3(A),V3(A))-(2*U2(A),V2(A)),PSET:LINE(2*U3(A+1),V3(A+1))-(2*U2(A+1),V2(A+1)),PSET
650 LINE(2*U1(A),V1(A))-(2*U2(A),V2(A)),PRESET:LINE(2*U3(A),V3(A))-(2*U2(A),V2(A)),PRESET
660 LINE(2*U3(A+1),V3(A+1))-(2*U2(A+1),V2(A+1)),PRESET:LINE(2*U1(A+4),V1(A+4))-(2*U2(A+4),V2(A+4)),PRESET
670 IF PEEK(341)=247 THEN POKE 341,255:V2(A)=V2(A)-1:V3(A+1)=V3(A+1)-1:V1(A+4)=V1(A+4)-1:V0(A+5)=V0(A+5)-1:GOTO 630
680 IF PEEK(342)=247 THEN POKE 342,255:V2(A)=V2(A)+1:V3(A+1)=V3(A+1)+1:V1(A+4)=V1(A+4)+1:V0(A+5)=V0(A+5)+1:GOTO 630
690 IF PEEK(343)=247 THEN POKE 343,255:U2(A)=U2(A)-1:U3(A+1)=U3(A+1)-1:U1(A+4)=U1(A+4)-1:U0(A+5)=U0(A+5)-1:GOTO 630
700 IF PEEK(344)=247 THEN POKE 344,255:U2(A)=U2(A)+1:U3(A+1)=U3(A+1)+1:U1(A+4)=U1(A+4)+1:U0(A+5)=U0(A+5)+1:GOTO 630
710 IF PEEK(338)=191 THEN POKE 338,255:LINE(2*U1(A),V1(A))-(2*U2(A),V2(A)),PSET:LINE(2*U1(A+4),V1(A+4))-(2*U2(A+4),V2(A+4)),PSET:LINE(2*U3(A),V3(A))-(2*U2(A),V2(A)),PSET:LINE(2*U3(A+1),V3(A+1))-(2*U2(A+1),V2(A+1)),PSET:GOTO 150
720 GOTO630
730 'DRAW NUMBERS"
740 FOR A=0 TO 2:GOSUB 790:NEXT
750 FOR A=4 TO 6:GOSUB 790:NEXT
760 FOR A=0 TO 2:GOSUB 800:NEXT
770 FOR A=4 TO 6:GOSUB 800:NEXT
780 DRAW"C4":GOTO 150
790 X=2*U2(A):DRAW"BM=X;,=V2(A);C4;XN$(A);":RETURN
800 X=2*U2(A):DRAW"BM=X;,=V2(A);C1;XN$(A);":RETURN
810 DATA "BF3BR4NG2D7NL3R3"
820 DATA "BF3U1R7D4L7D3R7"
830 DATA "BF3R7D3NL3D4L7"
840 DATA "BF3D3R7NU3D4"
850 DATA "BF3NR7D3R7D4L7"
860 DATA "BF3D7R7U3L7"
