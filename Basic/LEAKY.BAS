10 REM*THE LEAKY ROOF GAME
20 REM*BY J.E.RITTENHOUSE
22 CLS
25 GOSUB 600
30 PRINT:PRINT"******THE LEAKY ROOF GAME******"
40 PRINT:PRINT"DIRECTIONS:"
50 PRINT"USE THE RED BUCKET TO CATCH THE"
55 PRINT"DRIPS AND SCORE POINTS.IF YOU"
60 PRINT"MISS FIVE DRIPS (THEY'RE REALLY"
70 PRINT"BIG DRIPS) THE GAME IS OVER."
80 PRINT:PRINT"PRESS [A] TO MOVE LEFT AND"
85 PRINT"PRESS [L] TO MOVE RIGHT."
90 PRINT:GOSUB 660
95 PRINT:INPUT"PRESS [ENTER] TO BEGIN";ST$
100 T=0:B=0:F=288
105 CLS(0)
107 FOR H=1TO61:SOUND 50,3:H=H+RND(50)
109 IF H>61 THEN H=61
110 FOR V=1 TO 31
115 SET(H,V,3)
120 IF V=17 AND F=288+INT(H/2) THEN GOTO 128
122 IF V=17 AND F=288+INT(H/2)-1 THEN GOTO 128
124 IF V=17 AND F=288+INT(H/2)-2 THEN GOTO 128
125 GOTO 129
128 SOUND 100,3:SOUND 75,2:GOSUB 400:GOTO 107
129 GOSUB 300
130 RESET(H,V)
140 NEXT V
150 B=B+1:IF B=5 THEN GOTO 500
155 PRINT@450,"MISSED DRIPS=";B;
160 NEXT H
165 GOTO 107
300 C=128+16*(4-1)+11:D=128+16*(4-1)+3:E=128+16*(4-1)+7
310 IF F<288 THEN F=288
315 IF F>317 THEN F=317
320 PRINT@F,CHR$(C);CHR$(D);CHR$(E)
330 A$=INKEY$:IF A$=""THEN RETURN
335 IF A$="A"OR A$="L"THEN GOTO 340 ELSE RETURN
340 IF A$="L"THEN PRINT@F,"":F=F+RND(10):GOTO 315
350 IF A$="A"THEN PRINT@F,"":F=F-RND(10):GOTO 310
400 T=T+10
410 PRINT@386,CHR$(C);CHR$(D);CHR$(E);"=";T;
430 FOR X=1TO1000:NEXT X
440 RETURN
500 CLS:PRINT@288,"THE WATER'S UP TO YOUR EARS NOW.BETTER STOP BEFORE YOU DROWN."
505 PRINT:PRINT"YOUR SCORE WAS ";T
510 PRINT:INPUT"PLAY AGAIN (Y OR N)";P$
520 IF P$="Y"THEN GOTO 10 ELSE END
600 G=128+16*(4-1)+6
610 FOR X=0TO31
620 PRINT@0+X,CHR$(G);
640 NEXT X
650 RETURN
660 FOR X=0TO31
670 PRINT@416+X,CHR$(G);
680 NEXT X
690 RETURN
