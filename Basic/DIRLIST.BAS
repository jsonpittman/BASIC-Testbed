0 'DIRECTORY LISTER
1 'WRITTEN BY MIKE MOORE
2 'COPYRIGHT (C) JANUARY 1991
3 'BY FALSOFT, INC.
10 CLEAR800 'SET ASIDE STRING SPACE
20 INPUT"DISK NAME";N$
30 DIMR$(1):R$(0)=" " :R$(1)=CHR$(13) ' CARRIAGE RETURN
40 N=-2 ' DEVICE IS THE PRINTER
50 PRINT#N,N$+"   FREE";FREE(0) 'PRINT THE TITLE
60 PRINT#N," " 'BLANK LINE
70 DSKI$0,17,2,C$,A$:C$=LEFT$(C$,68) 'GET GRANULE POSITIONS
80 FOR S=3 TO 11:DSKI$0,17,S,A$,B$ 'READ DIRECTORY
90 A$=A$+LEFT$(B$,127) : L=255 'COLLECT NAMES TOGETHER
100 M$=LEFT$(A$,11)+"     " 'FILE NAME
110 IF ASC(M$)=255 THEN PRINT#N," ":END 'END OF NAMES
120 IF ASC(M$)=0 THEN 210 'THIS FILE WAS KILLED
130 A=(MID$(A$,13,1)=CHR$(255)) 'FILE TYPE
140 MID$(M$,13)=CHR$(66+A) '(A)SCII OR (B)INARY
150 G=1+ASC(MID$(A$,14)) : R=0 'FIND FIRST GRANULE
160 R=R+1:G=1+ASC(MID$(C$,G)) ' COUNT NEXT GRANULE
170 IF G<69 GOTO 160 'MORE GRANULES TO GET
180 B$=STR$(R) : MID$(M$,14,LEN(B$))=B$ 'PUT IN # OF GRANULES
190 M$=LEFT$(M$,8)+" "+RIGHT$(M$,8) 'SEPERATE NAME & EXTENSION
200 PRINT#N,M$+R$(P); : P=1-P 'PRINT
210 L=L-32 : IF L>0 THEN A$=RIGHT$(A$,L) : GOTO 100 'DROP A FILE
220 NEXT S 'GET NEXT SECTOR
