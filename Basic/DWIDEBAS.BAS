0 'COPYRIGHT 1990 FALSOFT, INC.
1 'DBLWIDE BY JOHN LUDWIG
2 '           COAL VALLEY,ILL.
3 'A PROGRAM TO PRINT
4 'DOUBLE-SIZED GRAPHICS
5 'SCREENS, SIDE BY SIDE
6 'FOR COCO 1,2,3
7 'REQUIRES 32K AND DMP105 OR DMP106
8 'PROGRAMMED FOR DISK DRIVE
10 PCLEAR8:CLEAR1024,19712:DIMP(6):DIMM$(7)'IT'S GOING TO BE A FULL COMPUTER
20 M$(1)="NAME OF THE ":M$(2)="LEFT ":M$(3)="RIGHT ":M$(4)="0 FOR DIRECTORY":M$(5)="SCREEN":M$(6)="NO. OF PIXELS BETWEEN SCREENS":M$(7)="(0 TO 20)"
30 IFPEEK(&H7D16) <>127THENLOADM"DBLWIDE"'IF SUB NOT IN MEMORY, GET IT
40 CLS:PRINT"INITIALIZING VARIABLES"
50 FORR=1 TO128:A$=A$+" ":B$=B$+" ":C$=C$+" ":D$=D$+" ":NEXT
60 DATA14,0,14,0,77,0
70 RESTORE:FORR=0TO5:READD:POKE&H7D00+R,D:NEXT'SET UP STARTS FOR SUBROUTINE
80 P(0)=VARPTR(A$)+2:P(2)=VARPTR(B$)+2:P(4)=VARPTR(C$)+2:P(6)=VARPTR(D$)+2'FIND STRINGS
90 B=&H7D0E:FORR=0 TO6STEP2:POKEB+R,PEEK(P(R)):POKEB+R+1,PEEK(P(R)+1):NEXT'GIVE POINTERS TO SUBROUTINE
100 CLS
110 GOSUB280:GOSUB300:IFT$="0"THENDIR:GOTO110
120 LOADMT$
130 GOSUB290:GOSUB300:IFT$="0"THENDIR:GOTO130
140 LOADMT$,16128
150 PRINTM$(6):PRINTM$(7):INPUTS
160 S$="":FORR=0 TOS:S$=S$+CHR$(128):NEXT
170 INPUT"PRINT BOLD (Y OR N)";BO$
180 INPUT"PRINT DOUBLE-STRIKE (Y OR N)";DS$
190 PMODE4,1:SCREEN1,1:PRINT#-2,CHR$(30);CHR$(27);CHR$(23)'DISPLAY GRAPHICS,SELECT TEXT,ELITE
200 IFBO$="Y"THENPRINT#-2,CHR$(27);CHR$(31);:GOTO220'START BOLD
210 PRINT#-2,CHR$(27);CHR$(32);'END BOLD
220 IFDS$="Y"THENPRINT#-2,CHR$(27);CHR$(21);CHR$(18):GOTO250'NO LF ON CR, SET GRAPHICS
230 PRINT#-2,CHR$(27);CHR$(22);CHR$(18)'LF ON CR, SET GRAPICS
240 FORR=1 TO55:EXEC&H7D16:PRINT#-2,A$;B$;S$;C$;D$:NEXT:GOTO270
250 FORR=1 TO55:EXEC&H7D16:PRINT#-2,A$;B$;S$;C$;D$;CHR$(13);A$;B$;S$;C$;D$;CHR$(13);CHR$(10):NEXT
260 'SELECT TEXT,     STD CHARACTER SET,LINEFEED ON CR,   END BOLD.
270 PRINT#-2,CHR$(30);CHR$(27);CHR$(19);CHR$(27);CHR$(22);CHR$(27);CHR$(32):END
280 PRINTM$(1);M$(2);M$(5):PRINTM$(4):RETURN
290 PRINTM$(1);M$(3);M$(5):PRINTM$(4):RETURN
300 INPUTT$:RETURN
