10 REM"BUYING GASOLINE"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1988"
30 CLS0
40 REM"DRAW THE THREE GAS PUMPS"
50 PRINT@35,"REG.";:PRINT@44," NO ";:PRINT@53,"PREM";
60 PRINT@67,"----";:PRINT@76,"LEAD";:PRINT@85,"----";
70 FOR X=6 TO 13:FOR Y=6 TO 14:SET(X,Y,4):NEXT Y,X
80 FOR X=24 TO 31:FOR Y=6 TO 14:SET(X,Y,3):NEXT Y,X
90 FOR X=42 TO 49:FOR Y=6 TO 14:SET(X,Y,2):NEXT Y,X
100 FOR Y=4 TO 12:SET(14,Y,5):SET(32,Y,5):SET(50,Y,5):NEXT Y
110 SET(15,13,5):SET(33,13,5):SET(51,13,5)
120 FOR T=1344 TO 1535:POKET,143:NEXT T
130 REM"CHOOSE THE CURRENT PRICES"
140 J=J+1
150 A=RND(100)+50:B=A+RND(20):C=B+RND(20)
160 A=A/100:B=B/100:C=C/100
170 PRINT@320,"":PRINT@352,"":PRINT@416,"":PRINT@448,""
180 IF J>10 THEN 340
190 IF A<1 THEN 150
200 PRINT@259,USING"#.##";A;:PRINT@268,USING"#.##";B;:PRINT@277,USING"#.##";C;
210 Q=RND(7)+2:REM"THE # OF GALLONS"
220 R=RND(3):IF R=1 THEN N=A:A$="REGULAR"
230 IF R=2 THEN N=B:A$="NO LEAD"
240 IF R=3 THEN N=C:A$="PREMIUM"
250 AN=N*Q
260 PRINT@320,"WHAT IS THE PRICE OF"Q"GALLONS  OF "A$" GAS TODAY?  $";
270 LINEINPUT W$:W=VAL(W$)
280 IF INT(W*100+.05)=INT(AN*100+.05) THEN PRINT@426,"CORRECT":RR=RR+1:PLAY"A":GOTO 300
290 PRINT@418,"SORRY,THE ANSWER IS $";AN
300 PRINT@453,"PRESS ENTER TO GO ON";
310 EN$=INKEY$
320 IF EN$=CHR$(13) THEN 130 ELSE 310
330 GOTO 330
340 PRINT@326,"THIS SET IS OVER."
350 PRINT@357,"YOUR SCORE WAS"RR*10;"%"
360 PRINT@448,"PRESS 'B' TO BEGIN OR 'E' TO END";
370 EN$=INKEY$
380 IF EN$="B" THEN RUN ELSE IF EN$="E" THEN CLS:END
390 GOTO 370
