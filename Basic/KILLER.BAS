10 'THE ASSASSINATION GAME
20 '(C) 1986 ANDY DATER
30 GOTO3050
40 FILES1:CLEAR12000:DIMD$(40,18),T$(40,17),SM$(17),P(4)
50 CLS:PRINT"LOADING DATA..."
60 OPEN"I",1,"TAG/DAT"
70 INPUT#1,R
80 IFR=0THEN130
90 FORX=1TOR
100 FORY=1TO17
110 LINEINPUT#1,D$(X,Y)
120 NEXTY,X
130 CLOSE
140 FORX=1TO11
150 READD$(0,X)
160 NEXTX
170 CLS:P$="THE ASSASSINATION GAME":GOSUB2800
180 PRINT
190 PRINTTAB(5)"1 - ADD PLAYER"
200 PRINTTAB(5)"2 - EDIT PLAYER"
210 PRINTTAB(5)"3 - DELETE PLAYER"
220 PRINTTAB(5)"4 - CHANGE POINTS"
230 PRINTTAB(5)"5 - DAILY BONUSES"
240 PRINTTAB(5)"6 - NON-KILL PENALTIES"
250 PRINTTAB(5)"7 - PRINT POINTS LIST"
260 PRINTTAB(5)"8 - SORT LIST"
270 PRINTTAB(5)"9 - LIST PLAYERS"
280 PRINTTAB(5)"0 - QUIT"
290 Q$=INKEY$:IFQ$<"0"ORQ$>"9"THEN290
300 A$="":B$="":N=0
310 ONVAL(Q$)+1GOSUB2690,340,500,720,1140,1630,1890,2110,950,2550
320 I$="":GOTO170
330 'ADD PLAYER
340 CLS:P$="ADD PLAYER":GOSUB2800
350 GOSUB2850
360 R=R+1
370 FORX=1TO11
380 L=76+32*X
390 GOSUB2970
400 IF(I$="Q"ORI$="")ANDX=1THENR=R-1:RETURN
410 D$(R,X)=I$
420 NEXTX
430 FORX=12TO15:D$(R,X)=STR$(0):NEXTX:D$(R,16)="ALIVE":D$(R,17)="Y"
440 PRINT@480,"ARE ENTRIES CORRECT? (Y/N/Q)";
450 A$=INKEY$:IFA$="N"THENN=R:GOTO630ELSEIFA$="Q"THENRETURNELSEIFA$<>"Y"THEN450
460 PRINT@480,"ADD ANOTHER? (Y/N)          ";
470 A$=INKEY$:IFA$="Y"THENPRINT@480,STRING$(31," ");:GOTO350ELSEIFA$<>"N"THEN470
480 RETURN
490 'EDIT PLAYER
500 CLS:P$="EDIT PLAYER":GOSUB2800
510 PRINT@96,"";:LINEINPUT"NAME: ";A$
520 IFA$="Q"THENRETURN
530 IFA$<>""THEN560
540 LINEINPUT"CODENAME: ";A$
550 IFA$=""THENRETURNELSE590
560 Q=INSTR(A$," ")
570 IFQ=0THENA$="":GOTO500
580 B$=RIGHT$(A$,LEN(A$)-Q):A$=LEFT$(A$,Q-1)
590 FORN=1TOR
600 IFA$=D$(N,1)ANDB$=D$(N,2)THEN630ELSEIFA$=D$(N,3)THEN630ELSENEXTN
610 PRINT"NOT FOUND."
620 EXEC44539:GOTO500
630 AN=1:AD=1:GOSUB2850
640 A$="":PRINT@448,STRING$(31," ");:PRINT@448,"";:LINEINPUT"CHANGE WHICH FIELD: ";A$
650 IFA$="Q"THENRETURNELSEA=VAL(A$)
660 IFA<1ORA>11ORA<>INT(A)THEN640
670 L=112+32*(A-1)
680 GOSUB2970
690 IFI$=""THENPRINT@L,D$(N,A);ELSED$(N,A)=I$
700 GOTO640
710 'DELETE PLAYER
720 CLS:P$="DELETE PLAYER":GOSUB2800
730 PRINT@96,"";:LINEINPUT"NAME: ";A$
740 IFA$="Q"THENRETURN
750 IFA$<>""THEN780
760 LINEINPUT"CODENAME: ";A$
770 IFA$=""THENRETURNELSE810
780 Q=INSTR(A$," ")
790 IFQ=0THENA$="":GOTO720
800 B$=RIGHT$(A$,LEN(A$)-Q):A$=LEFT$(A$,Q-1)
810 FORN=1TOR
820 IFA$=D$(N,1)ANDB$=D$(N,2)ORA$=D$(N,3)THEN850ELSENEXTN
830 PRINT"NOT FOUND."
840 EXEC44539:GOTO720
850 AD=1:GOSUB2850
860 PRINT@480,"ARE YOU SURE? (Y/N)";
870 A$=INKEY$:IFA$="N"THENRETURNELSEIFA$<>"Y"THEN870
880 FORX=N+1TOR
890 FORY=1TO17
900 D$(X-1,Y)=D$(X,Y)
910 NEXTY,X
920 R=R-1
930 RETURN
940 'SORT LIST
950 CLS:PRINT"SORTING..."
960 POKE65495,0
970 FORP=1TOR
980 PRINT@32,P;
990 SM$(1)=CHR$(255):SM$(2)=CHR$(255)
1000 FORA=1TOR
1010 PRINT@40,A;
1020 IFD$(A,2)+D$(A,1)<SM$(2)+SM$(1)THENFORX=1TO17:SM$(X)=D$(A,X):NEXTX:SB=A
1030 NEXTA
1040 FORX=1TO17:T$(P,X)=SM$(X):NEXTX
1050 D$(SB,1)=CHR$(255):D$(SB,2)=CHR$(255)
1060 NEXTP
1070 FORX=1TOR
1080 FORY=1TO17
1090 D$(X,Y)=T$(X,Y)
1100 NEXTY,X
1110 POKE65494,0
1120 RETURN
1130 'CHANGE POINTS
1140 CLS:P$="CHANGE POINTS":GOSUB2800
1150 PRINT@96,"";:LINEINPUT"NAME: ";A$
1160 IFA$="Q"THENRETURN
1170 IFA$<>""THEN1200
1180 LINEINPUT"CODENAME: ";A$
1190 IFA$=""THENRETURNELSE1230
1200 Q=INSTR(A$," ")
1210 IFQ=0THENA$="":GOTO1140
1220 B$=RIGHT$(A$,LEN(A$)-Q):A$=LEFT$(A$,Q-1)
1230 FORN=1TOR
1240 IFA$=D$(N,1)ANDB$=D$(N,2)THEN1270ELSEIFA$=D$(N,3)THEN1270ELSENEXTN
1250 PRINT"NOT FOUND."
1260 EXEC44539:GOTO1140
1270 PRINT@96,"NAME: "D$(N,1)" "D$(N,2)
1280 PRINT"CODENAME: "D$(N,3)
1290 PRINT
1300 FORX=1TO4:P(X)=VAL(D$(N,X+11)):NEXTX
1310 P(4)=P(1)+P(2)+P(3)
1320 PRINT@192,"1 - KILLS:    "P(1)
1330 PRINT"2 - BONUSES:  "P(2)
1340 PRINT"3 - PENALTIES:"P(3)
1350 PRINT"4 - TOTAL:    "P(4)
1360 PRINT"5 - STATUS:    "D$(N,16)
1370 PRINT"6 - ACTIVE:    "D$(N,17)
1380 PRINT@384,"CHANGE WHICH? (1-6)             "
1390 A$=INKEY$:IF(A$<"1"ORA$>"6")ANDA$<>"Q"ANDA$<>" "THEN1390ELSEA=VAL(A$)
1400 IFA$="Q"ORA$=" "THEN1570
1410 PRINT@384,STRING$(31," ");:PRINT@384,"";
1420 IFA=5THEN1470
1430 IFA=6THEN1530
1440 C=0:INPUT"CHANGE";C
1450 P(A)=P(A)+C
1460 GOTO1310
1470 PRINT"IS PLAYER <A>LIVE OR <D>EAD"
1480 I$=INKEY$:IFI$=""THEN1480
1490 IFI$="A"THEND$(N,16)="ALIVE":GOTO1320
1500 IFI$="D"THEND$(N,16)="DEAD":GOTO1320
1510 IFI$=CHR$(13)THENGOTO1320
1520 GOTO1480
1530 PRINT"IS PLAYER ACTIVE? (Y/N)"
1540 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN1540
1550 D$(N,17)=I$
1560 GOTO1320
1570 FORX=1TO4
1580 D$(N,X+11)=STR$(P(X))
1590 NEXTX
1600 IFA$=" "THEN1140
1610 RETURN
1620 'DAILY BONUSES
1630 CLS:P$="ADD DAILY POINTS":GOSUB2800
1640 PRINT
1650 INPUT"HOW MUCH TO ADD TO EACH ALIVE   PLAYER";A
1660 IFA=0THENRETURNELSECLS
1670 FORX=1TOR
1680 IFD$(X,16)="DEAD"ORD$(X,17)="N"THEN1720
1690 Q=Q+1
1700 PRINTD$(X,1)" "D$(X,2):PRINTTAB(10)D$(X,3)
1710 IFQ/7=INT(Q/7)THENPRINT@489,"PRESS <ENTER>";:EXEC44539:CLS
1720 NEXTX
1730 PRINT:PRINT"IS LIST CORRECT? (Y/N)"
1740 I$=INKEY$:IFI$=""THEN1740
1750 IFI$="Y"THEN1780
1760 IFI$="N"THENPRINT"PRESS <ENTER> TO RETURN":EXEC44539:RETURN
1770 GOTO1740
1780 FORX=1TOR
1790 IFD$(X,16)="DEAD"THEN1830
1800 D$(X,13)=STR$(VAL(D$(X,13))+A)
1810 D$(X,15)=STR$(VAL(D$(X,15))+A)
1820 N=N+1
1830 NEXTX
1840 PRINTN"PLAYERS CHANGED"
1850 X$=INKEY$
1860 IFINKEY$=""THEN1860
1870 RETURN
1880 'NON-KILL PENALTIES
1890 CLS:P$="NON-KILL PENALTIES":GOSUB2800
1900 PRINT
1910 PRINT@96,;:INPUT"HOW MUCH TO SUBTRACT FROM EACH  ALIVE PLAYER";S
1920 IFS=0THENRETURN
1930 IFS<>ABS(INT(S))THEN1890
1940 FORX=1TOR
1950 IFD$(X,16)<>"ALIVE"ORD$(X,17)="N"THEN2060
1960 PRINT@192,"NAME: "D$(X,1)" "D$(X,2)+STRING$(12-LEN(D$(X,2)),32)
1970 PRINT"CODENAME: "D$(X,3)"          "
1980 PRINT@288,"KILL POINTS:"D$(X,12)"          "
1990 PRINT"SUBTRACT"S"POINTS? (Y/N/Q)"
2000 A$=INKEY$:IFA$=""THEN2000
2010 IFA$="Q"THENRETURN
2020 IFA$="N"THEN2060
2030 IFA$<>"Y"THEN2000
2040 D$(X,14)=STR$(VAL(D$(X,14))-S)
2050 D$(X,15)=STR$(VAL(D$(X,15))-S)
2060 NEXTX
2070 PRINT
2080 PRINT"DONE."
2090 EXEC44539:RETURN
2100 'PRINT POINTS LIST
2110 CLS:P$="PRINT POINTS":GOSUB2800
2120 PRINT:PRINT"SORTING..."
2130 POKE65495,0
2140 TV=0:TR=0:A=0:NA=0:NU=0
2150 FORX=1TOR
2160 IFLEFT$(D$(X,16),1)="A"ANDD$(X,17)="Y"THENNA=NA+1
2170 NEXTX
2180 FORX=1TOR
2190 IFD$(X,17)="N"THEND$(X,18)="U":NU=NU+1
2200 NEXTX
2210 FORX=1TOR
2220 PRINT@0,X;
2230 FORY=1TOR
2240 IFVAL(D$(Y,15))=>TV ANDD$(Y,18)<>"U"THENTV=VAL(D$(Y,15)):TR=Y
2250 NEXTY
2260 IFLEFT$(D$(TR,16),1)="D"THEN2330
2270 A=A+1
2280 FORZ=13TO16
2290 T$(A,Z)=D$(TR,Z)
2300 T$(A,3)=D$(TR,3)
2310 NEXTZ
2320 GOTO2380
2330 NA=NA+1
2340 FORZ=13TO16
2350 T$(NA,Z)=D$(TR,Z)
2360 T$(NA,3)=D$(TR,3)
2370 NEXTZ
2380 D$(TR,18)="U":TV=0
2390 NEXTX
2400 FORX=1TOR
2410 D$(X,18)=""
2420 NEXTX
2430 POKE65494,0
2440 PRINT@128,"PRINTING..."
2450 FORX=1TO6:PRINT#-2,"":NEXTX
2460 PRINT#-2,TAB(11)"CODENAME"TAB(24)"KILLS"TAB(34)"BONUSES"TAB(46)"PENALTIES"TAB(60)"TOTAL"TAB(70)"STATUS"
2470 PRINT#-2,TAB(11)"--------"TAB(24)"-----"TAB(34)"-------"TAB(46)"---------"TAB(60)"-----"TAB(70)"------"
2480 PRINT#-2,""
2490 FORX=1TO(R-NU)
2500 PRINT#-2,TAB(4)T$(X,3)TAB(25)T$(X,12)TAB(36)T$(X,13)TAB(49)T$(X,14)TAB(61)T$(X,15)TAB(71)T$(X,16)
2510 NEXTX
2520 PRINT#-2,CHR$(12);
2530 RETURN
2540 'LIST PLAYERS
2550 CLS:P$="LIST PLAYERS":GOSUB2800
2560 PRINT
2570 PRINT"PRINT LIST TO PRINTER? (Y/N/Q)"
2580 A$=INKEY$:IFA$<>"Y"ANDA$<>"N"ANDA$<>"Q"THEN2580
2590 IFA$="Q"THENRETURN
2600 IFA$="Y"THENP=1ELSEP=0
2610 CLS:FORX=1TOR
2620 PRINTD$(X,1)" "D$(X,2):PRINTTAB(10)D$(X,3)
2630 IFP THENPRINT#-2,D$(X,1)" "D$(X,2)TAB(28)D$(X,3)TAB(45)D$(X,16)
2640 IFX/7=INT(X/7)THENPRINT@489,"PRESS <ENTER>";:FORQ=0TO1STEP0:Q$=INKEY$:IFQ$="Q"THENRETURNELSEIFQ$=""THENNEXTQ ELSECLS
2650 NEXTX
2660 PRINT:PRINTR"PLAYERS"
2670 EXEC44539:RETURN
2680 'QUIT
2690 CLS:PRINT"SAVE DATA? (Y/N)"
2700 A$=INKEY$:IFA$="N"THENENDELSEIFA$<>"Y"THEN2700
2710 CLS:PRINT"SAVING DATA..."
2720 OPEN"O",1,"TAG/DAT"
2730 PRINT#1,R
2740 FORX=1TOR
2750 FORY=1TO17
2760 PRINT#1,D$(X,Y)
2770 NEXTY,X
2780 END
2790 'CENTER ROUTINE
2800 T=16-LEN(P$)/2
2810 PRINTTAB(T)P$
2820 PRINTTAB(T)STRING$(LEN(P$),"-")
2830 RETURN
2840 'PRINT TITLES ROUTINE
2850 FORX=1TO11
2860 AN$=RIGHT$(STR$(X),1)+" - "
2870 IFX=10THENAN$="10- "
2880 IFX=11THENAN$="11- "
2890 PRINT@64+X*32,"";
2900 IFAN THENPRINTAN$;
2910 PRINTD$(0,X);
2920 IFAD THENPRINTD$(N,X)ELSEPRINT
2930 NEXTX
2940 AN=0:AD=0
2950 RETURN
2960 'INPUT DATA ROUTINE
2970 I$="":PRINT@L,STRING$(15,32)
2980 PRINT@L+LEN(I$),CHR$(191)
2990 A$=INKEY$:IFA$=""THEN2990
3000 IFA$=CHR$(8)ANDLEN(I$) THENI$=LEFT$(I$,LEN(I$)-1):PRINTA$;:GOTO2980
3010 IFA$=CHR$(21)THEN2970
3020 IFA$=CHR$(13)THENPRINT@L+LEN(I$)," ";:RETURN
3030 IFASC(A$)<32ORASC(A$)>122THEN2990ELSEI$=I$+A$:PRINT@L+LEN(I$)-1,A$;:GOTO2980
3040 DATA"FIRST NAME: ","LAST NAME:  ","CODENAME:   ","TELEPHONE:  ","HEIGHT:     ","WEIGHT:     ","HAIR COLOR: ","EYE COLOR:  ","CAR DRIVEN: ","ACTIVITIES: ","GRADE:      "
3050 PCLEAR1:GOTO40
