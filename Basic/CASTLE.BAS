0 GOTO20
1 FORX=1TO3000:NEXT:RETURN
2 A$=INKEY$:IFA$=""THEN2
3 PLAY"T2O1L8B":RETURN
4 PRINT"IT HITS YOU FOR"X"UNITS OF       DAMAGE":HP=HP-X:PLAYD$:RETURN
5 GOSUB1:GOTO200
6 PRINT"BUT LUCKILY IT MISSES":GOTO3000
7 PLAY"T2L4P2O3CP96CL3FL4P24CFAP64CFAP64CFAFAO4CO3AFCP64CP96CL3F":RETURN
8 PRINT"EXCUSE ME BUT I DON'T HAVE THAT "A$" IN MY VOCABULARY":GOTO410
9 PRINT"THAT IS NOT POSSIBLE":GOTO410
10 FORX=1TO1200:NEXT:RETURN
11 PRINT"PLEASE INDICATE WHICH "A$:GOTO410
20 CLEAR300:CLS3:PRINT@72,"C A S T L E  O F";:PRINT@140,"D E A T H";:PRINT@226,"BY:";:PRINT@295,"CHINARUT";:PRINT@363,"RUANGCHOTVIT";
50 HP=40:DIM DL(67),DR(67),B(67),R(67),LU(67),LD(67),E(67),M(67),P(67),WR(67),WL(67),A(67),O(69)
90 FORX=8TO55:READDL(X):NEXT:FORX=7TO54:READDR(X):NEXT:FORX=2TO63:READB(X):NEXT:FORX=2TO9:READR(X):NEXT:R(20)=1:FORX=28TO33:READR(X):NEXT:R(43)=1:R(55)=1:R(59)=1
150 FORX=3TO55:READLU(X):NEXT:FORX=14TO66:READLD(X):NEXT:FORX=57TO66:READWR(X):NEXT:FORX=57TO64:READWL(X):NEXT
175 E(5)=1:E(24)=1:E(26)=1:E(35)=1:A(2)=1:A(9)=1:A(19)=1:A(28)=1:P(2)=1:P(13)=1:P(15)=1:P(17)=1:P(21)=1:P(31)=1:P(26)=1:P(38)=1:P(44)=1:P(50)=1:FORX=5TO57:READM(X):NEXT
181 VL$="DROGETLOOOPEPULPUSUP DOWGO LEFRIGSAVLOADRISCORAI":NL$="   TRALEFRIGROPBUTSIDELEDOOWINGATPASLADSKESPEBROPOTKEYJEWMED
185 F$="YOU FIND ":M$="A MEDALLION":T$="A BROADSWORD":X$="A MAGICAL SPEAR":W$="A GOLDEN KEY":Z$="WHEN YOU RAISE THE KEY,A ":Y$=" MAGICALLY ":L$=" ON THE LEFT":R$=" ON THE RIGHT":G$="A LADDER GOING ":S$=STRING$(32,175):R=2:ML=2:D$="T2L4O1DDD
200 IFR=1THEN5200
210 IFR=12ORR=23ORR=34ORR=45ORR=56THEN5300
211 IFHP<=0THENQ=1:GOTO932
215 IFR=11ORR=47ORR=53THEN4000
220 IFG=1THEN240
230 IFM(R)=1THEN600
240 G=0:SI=0:PL=0:PR=0
250 CLS:PRINT"I SEE:",,S$
260 IF(RO=1ANDR=2)ORR=3THENPRINT"A TRAP DOOR
265 IFR=67THENPRINT"JEWELS",,"A SKELETON
270 IFDL(R)=1THENPRINT"A DOOR"L$
275 IFR=2THENPRINT"THE GATE
276 IFR=54ANDKD=0THEN300
277 IFWD=1ANDR=54THEN300
280 IFDR(R)=1THENPRINT"A DOOR"R$
290 IFR=41ORR=50THENPRINT"A SIDE DOOR
300 IFR(R)=1THENPRINT"A ROPE FROM THE CEILING
310 IFB(R)=1THENPRINT"A BUTTON ON THE WALL
320 IFWL(R)=1THENPRINT"A WINDOW"L$
325 IFWW=1ANDR=63THEN340
330 IFWR(R)=1THENPRINT"A WINDOW"R$
340 IFR=3ORR=6ORR=10OR(W1=1ANDR=19)ORR=16ORR=18ORR=27ORR=51THENPRINT"A SIDE WINDOW":SI=1
345 IFR=44ANDKL=0THEN360
350 IFLU(R)=1THENPRINTG$"UP
360 IFLD(R)=1THENPRINTG$"DOWN
370 IFE(R)=1THENPRINT"AN ELEVATOR
380 IFR=2ORR=61ORR=62ORR=65THENPRINT"A PASSAGE"L$:PL=1
385 IFR=60ORR=61ORR=64THENPRINT"A PASSAGE"R$:PR=1
390 IFO(1)=R THENPRINTT$
392 IFO(2)=R THENPRINTX$
394 IFO(3)=R THENPRINTW$
395 IFO(R+2)=R THENPRINT"A POTION
396 IFO(0)=R THENPRINTM$
410 PRINTS$;:LINEINPUT"NOW WHAT ->";A$
415 IFLEN(A$)<2THEN410
416 MO=MO+1:J$=LEFT$(A$,3)
417 IFJ$="STA"THEN990
418 IFJ$="SEA"THEN543
420 V$=LEFT$(A$,3):N$=""
421 IFLEN(A$)<6THEN455
430 N=INSTR(A$," "):N$=MID$(A$,N+1,3)
455 D=INSTR(A$," DO"):W=INSTR(A$," WI")
460 V=INSTR(VL$,V$):IFV=0THENA$="VERB":GOTO8
470 N=INSTR(NL$,N$):IFN=0THENA$="NOUN":GOTO8
480 V=(V+2)/3:ON V GOTO527,516,485,502,509,510,511,512,513,514,515,5400,5500,537,538,539
485 IFN=25ORD>9THENPRINT"IF YOU ASK ME, THEY ARE JUST    NORMAL WOODEN DOORS":GOTO410ELSEIFN=28ORW>9THENPRINT"HMMM...IT APPEARS TO BE VERY    DARK OUTSIDE":GOTO410
486 IFN=13ANDR(R)=1THENPRINT"IT LOOKS LIKE A NATURAL FIBER   ROPE, SISAL I THINK":GOTO410
487 IFN=16ANDB(R)=1THENPRINT"THE BUTTON LOOKS VERY WORN DOWN":GOTO410
488 IFN=22ANDE(R)=1THENPRINT"IT'S A ROTTEN,WOODEN ELEVATOR   WITH A RUSTED PULLEY ON IT":GOTO410
489 IFN=43ANDSP=1THENPRINT"IT'S A FINELY CRAFTED SPEAR     INDEED":GOTO410
490 IFN=46ANDSW=1THENPRINT"IT'S APPEARS TO BE VERY NEW AND HAS AN EYE OF THE TIGER NEAR THEHANDLE":GOTO410
491 IFN=49ANDP>0THENPRINT"THE POTION IS A VERY DARK BLUE  AND HAS A FOUL SMELL":GOTO410
492 IFN=52ANDK=1THENPRINT"THE KEY APPEARS TO BE MADE OUT  OF PURE GOLD":GOTO410
493 IFN=1THENGOTO200
494 IFN=31ANDR=2THENPRINT"IT IS A DOUBLE HINGED IRON GATE":GOTO410
495 IFN=34AND(PL=1ORPR=1)THENPRINT"MAYBE THE PASSAGE LEADS NOWHERE,WHO KNOWS?":GOTO410
496 IFN=37AND(LU(R)=1ORLD(R)=1)THENPRINT"THEY'RE CARVED WOODEN LADDERS":GOTO410
497 IFN=40ANDR=67THENPRINT"IT'S AN UGLY HUMAN SKELETON":GOTO410
498 IFN=55ANDR=67THENPRINT"I COULD SWEAR IT'S THOSE JEWELS YOU'RE AFTER!!":GOTO410
499 IFN=58ANDME=1THENPRINT"IT IS A VERY BEAUTIFUL SOLID    GOLD MEDALLION":GOTO410
500 IFN>42THENPRINT"YOU DO NOT HAVE IT"ELSEPRINT"THE PRESENCE OF THAT IS ABSENT
501 GOTO410
502 IFN=31THENPRINT"IT IS ALREADY OPEN SIR":GOTO410
503 IFW>9ORD>9THEN504ELSEIFN=25THENA$="DOOR":GOTO11ELSEIFN=28THENA$="WINDOW":GOTO11ELSE9
504 IFN=4AND(RO=1ORR=3)THENGOTO5550
505 IFN=7AND(DL(R)=1ORWL(R)=1)THENR=R-1:GOTO200
506 IFN=10AND(DR(R)=1ORWR(R)=1)THENR=R+1:IFR=67THENGOSUB6000:GOTO200ELSE200
507 IFN=19ANDD>9AND(R=41ORR=50)THENPRINT"YOU SMASH YOUR FACE INTO A      BRICKWALL AND TAKE A HIT POINT  OF DAMAGE":HP=HP-1:GOTO410
508 IFN=19ANDW>9ANDSI=1THENGOTO5750
509 IFN=13ANDR(R)=1THEN1060ELSEPRINT"YOU ARE UNABLE TO DO THAT FEAT":GOTO410
510 IFN=16ANDB(R)=1THEND=0:GOTO1049ELSEPRINT"THAT DIDN'T DO A THING":GOTO410
511 IFLU(R)=1THENR=R+11:GOTO200ELSEPRINT"WHAT??":GOTO410
512 IFLD(R)=1THENR=R-11:GOTO200ELSEPRINT"WHAT??":GOTO410
513 IFN=22THEN1000ELSE9
514 IFPL=1THENR=R-1:GOTO200ELSE9
515 IFPR=1THENR=R+1:GOTO200ELSE9
516 IFN=43ANDO(2)=R THENSP=1:O(2)=0:GOTO526
517 IFN=46ANDO(1)=R THENSW=SW+1:O(1)=0:GOTO526
518 IFN=49ANDO(R+2)=R THENP=P+1:O(R+2)=0:GOTO526
519 IFN=52ANDO(3)=R THENK=1:O(3)=0:GOTO526
520 IFN=58ANDO(0)=R THENME=1:PM=0:O(0)=0:GOTO526
521 IFN<>55ORR<>67THEN525
522 IFPM=1THEN7000
523 IFPM=0THENPRINT"THE SKELETON SUDDENLY RISES AND SAYS, DON'T YOU DARE TOUCH MY   JEWELS!!  HE THEN STRANGLES YOU TO DEATH SO YOU CAN ALSO LIE    PEACEFULLY NEXT TO HIM WITH HIS JEWELS.":HP=0:Q=1:R=2:GOTO932
525 PRINT"I AM UNCAPABLE OF TAKING THAT":GOTO410
526 PRINT"IT IS NOW TAKEN":GOTO410
527 IFN=43ANDSP=1THENSP=0:O(2)=R:GOTO536
528 IFN=46ANDSW>0THENSW=SW-1:O(1)=R:GOTO536
529 IFN=49ANDP>0THENP=P-1:O(R+2)=R:GOTO536
530 IFN=52ANDK=1THENK=0:O(3)=R:GOTO536
531 IFN=58ANDME=1THENME=0:O(0)=R:PRINT"FAINTLY, YOU HEAR A VOICE SAY   THANK YOU":PM=1:GOTO410
535 PRINT"I DON'T THINK YOU CAN DROP THAT":GOTO410
536 PRINT"YOU HAVE NOW DROPPED IT":GOTO410
537 IFN=49ANDP>0THENA=40-HP:PRINT"AHHH....THAT WAS REFRESHING. IT GIVES YOU"A"UNITS OF STRENGTH":HP=40:P=P-1:G=1:GOTO410ELSEIFP<=0ANDN=49THENPRINT"YOU DON'T HAVE ANY!!!":GOTO410ELSEPRINT"I WOULDN'T ADVISE THAT":GOTO410
538 MO=MO-1:PRINT"YOUR SCORE IS"MO"MOVES":GOTO410
539 IF(N=52ANDK=0)THENPRINT"YOU DON'T HAVE A KEY!":GOTO410
540 IFN=52ANDR=44THENGOSUB5620:GOTO200
541 IFN=52ANDR=54THENKD=1:GOSUB5610:GOTO200
542 PRINT"A NICE ATTEMPT":GOTO410
543 IFR=2THENPRINTF$"A TRAP DOOR":RO=1:GOTO5
544 IFR=52ORR=48THENPRINTF$G$"UP":LU(R)=1:GOTO5
546 IFR=19THENPRINTF$"A SIDE WINDOW":W1=1:GOTO5
547 IFR=58THENPRINTF$"A WINDOW"L$:WL(R)=1:GOTO5
548 IFR=3THENPRINTF$"AN ELEVATOR":E(3)=1:GOTO5
549 IFR=21ORR=36THENPRINTF$"A BUTTON":B(R)=1:GOTO5
550 IFR=39THENPRINTF$"A DOOR"L$:DL(R)=1:GOTO5
560 PRINT"YOU FIND NOTHING":GOTO410
600 IFR=5THENK$="LICH":MP=20
610 IFR=8THENK$="QUASSIT":MP=21
620 IFR=18THENK$="MIND FLARE":MP=15
630 IFR=22THENK$="BUG BEAR":MP=15
640 IFR=24THENK$="YETI":MP=15
650 IFR=29THENK$="VERANOPS":MP=14
660 IFR=30THENK$="NIGHT HAG":MP=15
670 IFR=32THENK$="BIAMAT":MP=28
680 IFR=39THENK$="GHAST":MP=15
690 IFR=41THENK$="SPHINX":MP=20
700 IFR=48THENK$="PSEUDO DRAGON":MP=28
710 IFR=52THENK$="DRAGONNE":MP=28
720 IFR=57THENK$="TIAMAT":MP=30
730 IFR=49THENK$="IMP":MP=15
740 IFR=17THENK$="YELLOW MOLD":MP=10
745 N$=RIGHT$(K$,2):GOSUB5700:CLS:IFN$="AT"THENPRINTK$" IS IN THE ROOM!":GOTO760
750 PRINT"THERE IS A "K$:PRINT"IN THE ROOM!
760 MP=MP+RND(5):F=1
765 IFF=0THEN200ELSE770
770 PRINT"YOU ATTACK":E=RND(7)-1
780 IFE=0THENM=1:GOTO860
790 IFSW>0THENA=RND(6):E=E+A
792 IFSP=1THENA=RND(3):E=E+A
800 PRINT"YOU HIT "H$K$:PRINT"FOR"E"UNITS":MP=MP-E:PLAY"T2L4O3AAA":IFMP=<0THEN920
805 PRINTH$K$" ATTACKS
810 X=RND(7)-1:IFX=0THENM=2:GOTO860
820 PRINTH$K$" HITS YOU":PRINT"FOR"X"UNITS":HP=HP-X:PLAYD$:IFHP=<0THEN932
830 PRINT"YOU HAVE"HP"POINTS OF STRENGTH":PRINTH$K$" HAS"MP:PRINT"POINTS OF STRENGTH
840 PRINT"TRY TO ESCAPE(Y/N)
845 GOSUB2
850 IFA$="N"THEN940
852 IFA$="Y"THEN5630ELSE845
858 PRINTH$K$" STOPS YOU":GOTO940
860 IFM=1THENPRINT"YOU MISSED":GOTO810
880 IFM=2THENPRINT"THE "K$" MISSES":GOTO830
920 GOSUB7
930 IFMP=<0THENPRINT"YOU HAVE KILLED "H$:PRINTK$:M(R)=0:GOSUB1:F=0:IFR=57THEN6500ELSE200
932 IFHP=<0THENML=ML-1
935 IFML=0THENPRINT"YOU ARE DEAD":GOTO980
936 PRINT"YOU HAVE NO MORE HP'S","LIVES LEFT:";ML-1:HP=40:IFQ THENGOSUB1ELSE840
937 GOTO200
940 GOTO765
980 PRINT:PRINT"PLAY AGAIN(Y/N)
985 GOSUB2:IFA$="Y"THENRUNELSEIFA$="N"THENCLS:ENDELSE985
990 PRINT"YOU HAVE:":PRINTHP"UNITS OF STRENGTH":PRINTP"POTIONS":PRINTML-1;:IFML=2THENPRINT"LIFE";ELSEPRINT"LIVES";
991 PRINT" LEFT":IFSW>0THENPRINT" "T$
992 IFSP=1THENPRINT" "X$
993 IFK=1THENPRINT" "W$
994 IFME=1THENPRINT" "M$
995 GOTO410
1000 IFR=3THENR=24ELSEIFR=24THENR=3
1010 IFR=5THENR=26ELSEIFR=26THENR=5
1020 IFR=35THEN5000
1025 IFE(R)=1THEN200ELSEPRINT"ARE YOU CRAZY, I DON'T SEE AN   ELEVATOR AROUND HERE":GOTO410
1049 IFR=2ORR=55THEND=1
1060 IFA(R)=1ANDD=0THENX=RND(3):A=RND(4)-1:PRINTX;"ARROWS SHOOT OUT AND ";:IFA=0THEN1065ELSEPRINT"HIT YOU  FOR"A"UNITS OF DAMAGE":HP=HP-A:PLAYD$:GOTO3000
1061 GOTO1070
1065 PRINT"LUCKILY, THEY MISS YOU":GOTO3000
1070 IFP(R)=1THENPRINT"A POTION DROPS ONTO THE FLOOR":O(R+2)=R:P(R)=0:GOSUB5720:GOTO3000
1075 IFB37=1ANDR=37THEN2900
1076 IFR3=1ANDR=3THEN2900
1080 IFR=3ORR=37THENPRINTT$" LIES ON THE FLOOR":O(1)=R:IFR=3THENR3=1:GOSUB5720:GOTO3000
1081 IFR=37THENB37=1:GOSUB5720:GOTO3000
1084 IFB7=1ANDR=7THEN2900
1085 IFR=7THENPRINTX$" LIES IN FRONT OFYOU":O(2)=R:B7=1:GOSUB5720:GOTO3000
1090 IFR=14ORR=43THENPRINT"A AXE SWINGS OUT AT YOU!":A=RND(2)-1:X=RND(2):IFA=0THEN6ELSEGOSUB4:GOTO200
1095 IFB59=1ANDR=59THEN2900
2000 IFR=59THENPRINT"THERE IS NOW "W$" ON THEFLOOR":O(3)=R:B59=1:GOSUB5720:GOTO3000
2005 IFR=4THENPRINT"YOU FALL DOWN A TRAP DOOR":R=7:GOTO3000
2010 IFR=20THENPRINT"SUDDENLY YOU ARE HYPERSPACED     INTO ANOTHER ROOM!":R=46:GOTO3000
2012 IFR=40THEN2025
2015 IFR=25ORR=36THENPRINT" A SLEEPING GAS SEEPS INTO THE    ROOM AND WHEN YOU WAKE UP, YOU  ARE IN ANOTHER ROOM!
2020 IFR=25THENR=48ELSEIFR=36THENR=40
2021 IFR=40ORR=48THENGOSUB1:GOTO3000
2025 IFR=33THENPRINT"AN KNIFE SHOOTS OUT AT YOU!":X=RND(2):A=RND(2)-1:IFA=0THEN6ELSEGOSUB4:GOTO200
2035 IFR=42THENPRINT"A FLAME SHOOTS OUT!":A=RND(2)-1:X=2:IFA=0THEN6ELSEGOSUB4:GOTO200
2037 IFWW=1ANDR=63THEN2900
2038 IFWD=1ANDR=54THEN2900
2040 IFR=40ORR=54ORR=63THENPRINT"A WALL SLAMS RIGHT IN FRONT OF   YOUR FACE!":GOSUB6010
2045 IFR=40THENPRINT"YOU ARE TRAPPED! IT COSTS YOU A LIFE TO GET OUT":HP=0:Q=1:GOTO932
2050 IFR=54THENWD=1:GOTO3000
2055 IFR=63THENWW=1:GOTO3000
2800 IFR=55ANDD=1THENPRINT" A CHANDELIER DROPS ON YOUR      HEAD! YOU BLEED TO DEATH!":HP=0:Q=1:GOTO932
2900 PRINT"NOTHING HAPPENS
3000 GOSUB10:D=0:GOTO200
4000 PRINT" AS YOU GO DOWN THE LADDER,YOU   GET YOURSELF INTO A TRAP!
4005 IFR=11THENA=RND(2)
4010 IFR=47THENA=RND(3)
4020 IFR=53THENA=RND(4)
4035 PRINT"YOU TAKE"A"UNITS OF DAMAGE","TRYING TO ESCAPE":HP=HP-A:PLAYD$
4050 IFR=11THENR=22
4060 IFR=47THENR=58
4070 IFR=53THENR=64
4080 GOTO3000
5000 PRINT" YOU ARE STUCK IN THE ELEVATOR!":A=RND(3)
5010 PRINT"YOU TAKE"A"UNITS OF DAMAGE","TRYING TO ESCAPE":HP=HP-A:PLAYD$
5110 GOTO3000
5200 PRINT"YOU ARE BACK OUTSIDE!,BUT YOU   SAY TO YOURSELF, I MUST GET     THOSE PRECIOUS JEWELS":R=2:GOSUB1:GOTO200
5300 GOSUB6000:PRINT" YOU FALL INTO THE MOAT,AND GET  EATEN ALIVE BY CROCODILES!":HP=0:Q=1:R=2:GOTO932
5400 GOSUB5600:IFN=-1THEN5410ELSE5420
5410 OPEN"O",#N,A$:FORX=5TO57:PRINT#N,M(X):NEXT:FORX=2TO50:PRINT#N,P(X):NEXT:FORX=0TO69:PRINT#N,O(X):NEXT:PRINT#N,R,HP,ML,SW,SP,G,P,F,K,WD,WW,B37,R3,B7,B59,MO:CLOSE#N:END
5420 OPEN"O",#N,A$:FORX=5TO57:WRITE#N,M(X):NEXT:FORX=2TO50:WRITE#N,P(X):NEXT:FORX=0TO69:WRITE#N,O(X):NEXT:WRITE#N,R,HP,ML,SW,SP,G,P,F,K,WD,WW,B37,R3,B7,B59,MO:CLOSE#N:END
5500 GOSUB5600
5510 OPEN"I",#N,A$:FORX=5TO57:INPUT#N,M(X):NEXT:FORX=2TO50:INPUT#N,P(X):NEXT:FORX=0TO69:INPUT#N,O(X):NEXT:INPUT#N,R,HP,ML,SW,SP,G,P,F,K,WD,WW,B37,R3,B7,B59,MO:CLOSE#N:A=1:GOTO210
5550 IFR=2THENR=3ELSEIFR=3THENR=2
5560 GOTO200
5600 CLS:PRINT"tAPE,dISK,OR rETURN TO GAME
5601 GOSUB2:IFA$="R"THEN200ELSEIFA$="T"THENN=-1ELSEIFA$="D"THENN=1ELSE5601
5602 INPUT"MAY I ASK THE NAME OF YOUR GAME";A$
5605 RETURN
5610 PRINTZ$"DOOR"R$Y$"OPENS":IFWD=1THENPRINT" BUT A WALL IS BLOCKING IT
5615 GOSUB1:RETURN
5620 IFWD=0THEN538
5621 PRINTZ$"LADDER"Y$"APPEARS":KL=1:GOSUB1:RETURN
5630 IFHP<41THENA=RND(7)
5635 IFHP<31THENA=RND(5)
5640 IFHP<21THENA=RND(3)
5645 IFHP<11THENA=RND(2)
5650 IFHP<5THENA=1
5655 IFA=1THENG=1:GOTO200
5660 GOTO858
5700 IFN$="AT"THENH$=""ELSEH$="THE "
5710 RETURN
5720 PLAY"T4O3L4CCCL3GL4CL2G":RETURN
5750 IFR=3THENR=6ELSEIFR=6THENR=3
5755 IFR=10THENR=19ELSEIFR=19THENR=10
5760 IFR=16THENR=18ELSEIFR=18THENR=16
5765 IFR=27THENR=51ELSEIFR=51THENR=27
5770 PRINT" YOU CLIMB OUT OF THE WINDOW AND GO INTO ANOTHER WINDOW":GOSUB1:GOTO200
6000 A$="BAGFEDC":PLAY"T9L4O5XA$;O4XA$;O3XA$;
6010 PLAY"L4P2T53O1V30":FORX=1TO30:PLAY"V-D":NEXT:PLAY"V15":RETURN
6500 PRINT"AFTER A FEW SECONDS, YOU WATCH  AS THE CORPSE OF TIAMAT TURNS   INTO "M$:O(0)=57:GOSUB1:GOTO200
7000 PRINT" YOU DID IT! YOU TAKE THE JEWELS AND GO HOME RICH FOR LIFE!      YOU FINISHED THE ADVENTURE      IN"MO"MOVES":GOTO980
8000 DATA1,1,,,,,1,1,,1,,,,,1,,,1,1,,,1,,1,1,1,,,1,,1,,,,1,,1,,,,,1,,,1,,,1
8001 DATA1,1,,,,,1,1,,1,,,,,1,,,1,1,,,1,,1,1,1,,,1,,1,1,,,1,,1,,,,,1,,,1,,,1
8002 DATA1,,1,,,1,,,,,,1,1,1,,1,,1,,,,,,1,1,,,,,,,,,,,1,1,,1,,1,,1,,,,,,1,,,,1,1,,,,,,,,1
8003 DATA1,1,,,,,,1,1,,,1,,1
8004 DATA1,1,1,1,1,1,1,1,1,,1,,1,1,1,,,,,1,,,,1,,,1,1,,,1,,1,,,1,,,,,1,1,,,1,,,1,,,1,,1
8005 DATA1,1,1,1,1,1,1,1,1,,1,,1,1,1,,,,,1,,,,1,,,1,1,,,1,,1,,,1,,,,,1,1,,,1,1,,1,,1,1,,1
8006 DATA1,1,1,,,1,1,,,1
8007 DATA1,,1,1,,,1,1
8008 DATA1,,,1,,,,,,,,,1,1,,,,1,,1,,,,,1,1,,1,,,,,,,1,,1,,,,,,,1,1,,,1,,,,,1
8100 ' CASTLE OF DEATH
8101 ' BY CHINARUT RUANGCHOTVIT
8102 ' 242 DAVIDSON AVENUE
8103 ' RAMSEY, NEW JERSEY 07446
8104 ' JUNE-AUGUST 1987
