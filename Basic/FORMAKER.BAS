0 ' COPYRIGHT 1990 FALSOFT, INC
10 '************************************
20 '*            FORM MAKER            *
30 '*            ==========            *
40 '*        for IBM Graphic and       *
50 '*        compatible printers       *
60 '*      by BENJAMIN W. BRUNOTTE     *
70 '*          COPYRIGHT 1989          *
80 '************************************
90 GOTO170
100 HCOLOR1,4:IF Z$=" " THEN 150
110 IF Z$>" " AND Z$<="z" AND CS=0 THEN 150 ELSE IF Z$>" " AND Z$<="z" AND CS=1 THEN CS=0:OF=1:GOTO 140
120 IF Z$=>CHR$(179) AND Z$<=CHR$(223) AND CS=1 THEN 150 ELSE IF Z$=>CHR$(179) AND Z$<=CHR$(223) AND CS=0 THEN CS=1:OF=46:GOTO 140
130 HSCREEN0:CLS5:LOCATE0,11:PRINT"CHARACTER ERROR !!!!":PRINT"  CHARACTER #";STR$(ASC(Z$)):STOP
140 FOR L=0 TO 44:FOR B=0 TO 7:N=ASC(MID$(CS$(L+OF),B+1,1)):POKE (61605+(L*8)+B),N:NEXT B,L
150 IF Z$=>" " AND Z$<="z" THEN HPRINT(CL-XO,RW-YO),Z$ ELSE HPRINT(CL-XO,RW-YO),CHR$(ASC(Z$)-146)
160 RETURN
170 POKE 65497,0:PALETTE0,63:PALETTE1,0:PALETTE2,63:PALETTE3,36
180 WIDTH40:CLS5:CLEAR7000:DIM LT$(66),CS$(90):VERIFYON:HBUFF1,16:ON BRK GOTO 580:LOCATE0,7:PRINT"  ";STRING$(36,"*"):PRINT"  *";STRING$(12,32);"FORM MAKER";STRING$(12,32);"*"
190 PRINT"  *";STRING$(12,32);"==========";STRING$(12,32);"*":PRINT"  *";STRING$(8,32);"for IBM Graphic and";STRING$(7,32);"*":PRINT"  *";STRING$(8,32);"compatible printers";STRING$(7,32);"*"
200 PRINT"  *      by BENJAMIN W. BRUNOTTE     *":PRINT"  *";STRING$(10,32);"COPYRIGHT 1989";STRING$(10,32);"*":PRINT"  ";STRING$(36,"*"):LOCATE0,0
210 FOR L=1 TO 90:CS$(L)="":NEXT L:FOR L=0 TO 44:FOR B=0 TO 7:N=PEEK(61605+(L*8)+B):CS$(L+1)=CS$(L+1)+CHR$(N):READ N:CS$(L+46)=CS$(L+46)+CHR$(N):NEXT B,L
220 HCOLOR1,4:HSCREEN1:HGET(0,0)-(7,7),1
230 HCOLOR1,4:HSCREEN1:XO=1:YO=1:DP=0:FOR B=1 TO 66:LT$(B)=STRING$(80,32):NEXT B:X=0:Y=0:Z$=" ":GOTO 250
240 HCOLOR1,4:HSCREEN1:FOR RW=YO TO YO+23:FOR CL=XO TO XO+39:Z$=MID$(LT$(RW),CL,1):GOSUB 100:NEXT CL,RW:X=0:Y=0:DP=0
250 SOUND255,1:NX=0:NY=0:XX=X*8:YY=Y*8:IF DP=1 THEN PALETTE3,36:HPUT(XX,YY)-(XX+7,YY+7),1,NOT ELSE PALETTE3,0:HPUT(XX,YY)-(XX+7,YY+7),1,NOT
260 IF PEEK(341)=191 THEN IK$=INKEY$:SOUND110,5:IF DP=0 THEN DP=1:Z$=MID$(LT$(YO+Y),XO+X,1):GOTO510 ELSE DP=0:GOTO510
270 IF PEEK(342)<>191 THEN 330
280 HSCREEN0:CLS5:PRINT"PRESS AN ARROW KEY TO MOVE TO ANOTHER":PRINT"SECTION OF THE FORM OR":PRINT"PRESS <ENTER> TO STAY IN SAME SECTION"
290 IK$=INKEY$:IF IK$=CHR$(13) THEN 240
300 IF IK$=CHR$(8) THEN XO=XO-20 ELSE IF IK$=CHR$(9) THEN XO=XO+20 ELSE IF IK$=CHR$(10) THEN YO=YO+21 ELSE IF IK$=CHR$(94) THEN YO=YO-21 ELSE 290
310 IF XO<1 THEN XO=1 ELSE IF XO>41 THEN XO=41 ELSE IF YO<1 THEN YO=1 ELSE IF YO>43 THEN YO=43
320 GOTO 240
330 IF PEEK(343)<>191 THEN 390 ELSE Z=179
340 HCOLOR1,4:HPUT(XX,YY)-(XX+7,YY+7),1,PSET:Z$=CHR$(Z):CL=XO+X:RW=YO+Y:GOSUB100
350 IF INKEY$=CHR$(13) THEN 380 ELSE IF PEEK(343)=247 THEN Z=Z-1 ELSE IF PEEK(344)=247 THEN Z=Z+1 ELSE 350
360 IF Z<179 THEN Z=223 ELSE IF Z>223 THEN Z=179
370 GOTO 340
380 Z$=CHR$(Z):GOTO 490
390 IF PEEK(344)=191 THEN HCOLOR1,4:GOTO 670
400 IF PEEK(343)=247 THEN NX=-1 ELSE IF PEEK(344)=247 THEN NX=1 ELSE IF PEEK(342)=247 THEN NY=1 ELSE IF PEEK(341)=247 THEN NY=-1
410 IF NX<>0 OR NY<>0 THEN 510
420 IK$=INKEY$:IF IK$="" THEN 260
430 IF IK$="g" AND PEEK(343)=191 THEN 330
440 IF (IK$="@" AND PEEK(341)=191) OR IK$="^" THEN 260
450 IF IK$<>CHR$(12) THEN 480
460 HSCREEN0:CLS5:PRINT"DO YOU WANT TO CLEAR FORM (Y/N)?"
470 IK$=INKEY$:IF IK$="Y" OR IK$="y" THEN 230 ELSE IF IK$="N" OR IK$="N" THEN 240 ELSE 470
480 IF IK$<" " OR IK$>"z" THEN 260 ELSE Z$=IK$
490 IF DP=0 THEN NX=1
500 MID$(LT$(YO+Y),XO+X,1)=Z$
510 HPUT(XX,YY)-(XX+7,YY+7),1,PSET:CL=XO+X:RW=YO+Y:GOSUB 100:X=X+NX:Y=Y+NY
520 IF X<0 THEN X=39:Y=Y-1
530 IF X>39 THEN X=0:Y=Y+1
540 IF Y<0 THEN Y=23
550 IF Y>23 THEN Y=0
560 IF DP=1 THEN MID$(LT$(YO+Y),XO+X,1)=Z$ ELSE Z$=MID$(LT$(YO+Y),XO+X,1)
570 CL=XO+X:RW=YO+Y:GOSUB100:GOTO250
580 HCOLOR1,4:HSCREEN0:CLS5:PRINT:PRINT" S = SAVE TO DISK":PRINT" L = LOAD FROM DISK":PRINT" R = RETURN TO SAME FORM":PRINT" N = START NEW FORM":PRINT" X = EXIT FROM PROGRAM":PRINT:PRINT"PRESS S, L, R, N, OR X KEY.";
590 IK$=INKEY$:IF IK$="R" OR IK$="r" THEN POKE 65497,0:GOTO 240 ELSE IF IK$="N" OR IK$="n" THEN POKE 65497,0:GOTO230 ELSE IF IK$="X" OR IK$="x" THEN POKE 65496,0:PALETTE CMP:CLEAR 500:PRINT:PRINT:PRINT" GOODBYE":END
600 IF IK$<>"S" AND IK$<>"L" AND IK$<>"s" AND IK$<>"l" THEN 590 ELSE CLS
610 PRINT:PRINT"ENTER FILENAME: ";:LINEINPUT F$:IF LEN(F$)>8 THEN PRINT"  TOO LONG - REDO":GOTO610
620 POKE 65496,0:F$=F$+"/FMI":OPEN"D",#1,F$,1:FIELD#1,1 AS A$:R=1:IF IK$="L" OR IK$="l" THEN 650
630 PRINT"SAVING ";F$:FOR B=1 TO 66:LOCATE0,4:PRINT"LINE";B
640 FOR I=1 TO 80:LSET A$=MID$(LT$(B),I,1):PUT#1,R:R=R+1:NEXT I,B:CLOSE:GOTO580
650 PRINT"LOADING ";F$:FOR B=1 TO 66:LOCATE0,4:PRINT"LINE";B
660 FOR I=1 TO 80:GET#1,R:R=R+1:MID$(LT$(B),I,1)=A$:NEXT I,B:CLOSE:XO=1:YO=1:DO=0:POKE 65497,0:GOTO240
670 HCOLOR1,4:HSCREEN0:CLS5:PRINT:PRINT"DO YOU WANT TO PREVIEW FORM (Y/N)?"
680 IK$=INKEY$:IF IK$="N" OR IK$="n" THEN 750 ELSE IF IK$<>"Y" AND IK$<>"y" THEN 680
690 PRINT:PRINT"PRESS <ENTER> TO CONTINUE AFTER EACH      SECTION OF THE FORM.":FOR B=1 TO 250*5:NEXTB
700 XO=1:FOR YO=1 TO 43 STEP21:HSCREEN4
710 FOR Y=0 TO 23:RW=YO+Y:IF LT$(RW)=STRING$(80,32) THEN 730
720 FOR X=0 TO 79:CL=XO+X:Z$=MID$(LT$(RW),CL,1):GOSUB100:NEXTX
730 NEXTY:SOUND10,5:SOUND150,3
740 IK$=INKEY$:IF IK$<>CHR$(13) THEN 740 ELSE NEXT YO
750 HSCREEN0:CLS5:PRINT:PRINT"PRINT FORM (Y/N)?";:XO=1:YO=1:DP=0
760 IK$=INKEY$:IF IK$="N" OR IK$="n" THEN 240 ELSE IF IK$<>"Y" AND IK$<>"y" THEN 760
770 CLS5:PRINT:PRINT"READY PRINTER AND SELECT BAUD RATE:":PRINT"    1)  300 BAUD":PRINT"    2)  600 BAUD":PRINT"    3) 1200 BAUD":PRINT"    4) 2400 BAUD":PRINT"    5) 4800 BAUD":PRINT"    6) 9600 BAUD"
780 IK$=INKEY$:IF IK$<"1" OR IK$>"6" THEN 780
790 POKE 65496,0:ON VAL(IK$) GOTO 800,810,820,830,840,850
800 POKE150,180:GOTO860
810 POKE150,87:GOTO860
820 POKE150,41:GOTO860
830 POKE150,18:GOTO860
840 POKE150,7:GOTO860
850 POKE150,1
860 PRINT#-2,CHR$(27);"U1";:'UNIDIRECTIONAL PRINTING
870 FOR B=1 TO 66:PRINT"PRINTING LINE";B
880 IF LT$(B)=STRING$(80,32) THEN PRINT#-2,CHR$(13); ELSE PRINT#-2,LT$(B)
890 NEXTB:GOTO580
900 DATA 24,24,24,24,24,24,24,24
910 DATA 24,24,24,24,248,24,24,24
920 DATA 24,24,248,24,248,24,24,24
930 DATA 54,54,54,54,246,54,54,54
940 DATA 0,0,0,0,254,54,54,54
950 DATA 0,0,248,24,248,24,24,24
960 DATA 54,54,246,6,246,54,54,54
970 DATA 54,54,54,54,54,54,54,54
980 DATA 0,0,254,6,246,54,54,54
990 DATA 54,54,246,6,254,0,0,0
1000 DATA 54,54,54,54,254,0,0,0
1010 DATA 24,24,248,24,248,0,0,0
1020 DATA 0,0,0,0,248,24,24,24
1030 DATA 24,24,24,24,31,0,0,0
1040 DATA 24,24,24,24,255,0,0,0
1050 DATA 0,0,0,0,255,24,24,24
1060 DATA 24,24,24,24,31,24,24,24
1070 DATA 0,0,0,0,255,0,0,0
1080 DATA 24,24,24,24,255,24,24,24
1090 DATA 24,24,31,24,31,24,24,24
1100 DATA 54,54,54,54,55,54,54,54
1110 DATA 54,54,55,48,63,0,0,0
1120 DATA 0,0,63,48,55,54,54,54
1130 DATA 54,54,247,0,255,0,0,0
1140 DATA 0,0,255,0,247,54,54,54
1150 DATA 54,54,55,48,55,54,54,54
1160 DATA 0,0,255,0,255,0,0,0
1170 DATA 54,54,247,0,247,54,54,54
1180 DATA 24,24,255,0,255,0,0,0
1190 DATA 54,54,54,54,255,0,0,0
1200 DATA 0,0,255,0,255,24,24,24
1210 DATA 0,0,0,0,255,54,54,54
1220 DATA 54,54,54,54,63,0,0,0
1230 DATA 24,24,31,24,31,0,0,0
1240 DATA 0,0,31,24,31,24,24,24
1250 DATA 0,0,0,0,63,54,54,54
1260 DATA 54,54,54,54,255,54,54,54
1270 DATA 24,24,255,24,255,24,24,24
1280 DATA 24,24,24,24,248,0,0,0
1290 DATA 0,0,0,0,31,24,24,24
1300 DATA 255,255,255,255,255,255,255,255
1310 DATA 0,0,0,0,255,255,255,255
1320 DATA 240,240,240,240,240,240,240,240
1330 DATA 15,15,15,15,15,15,15,15
1340 DATA 255,255,255,255,0,0,0,0
