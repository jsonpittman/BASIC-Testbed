0 'SAUCER DEFENSE 12/07/86 BY JOHN T. WELLS, HERNDON VA VERS1.13
10 POKE65497,0
20 ON BRK GOTO 810
30 HSCREEN 2
40 HBUFF 1,900:HBUFF 2,900:HBUFF 3,900:HBUFF 4,900
50 HCLS 8
60 WI=5000:CC=0:SC=0:XS=160:CT=1000:BO=300:AD=100
70 X=100:X1=50:Y=100:SX=150:SY=141:C$="H4G3U3L4E4H4E4U2F4E4D3R4G3F2R3G4F3L3D2L2"
80 L1$="T155;V31;O4;12;11;10;9;8;7;6;5;4;3;2;1":L2$="T200;V31;O4;1;2;3;4;5;6;7;8;9;10;11;12"
90 GOSUB830:'****DO TITLE PAGE****
95 IF AN<1 OR AN>4 THEN AN=1
100 IF AN=1 THEN DF=20:D1=4:D2=16:DI=10
110 IF AN=2 THEN DF=18:D1=4:D2=14:DI=12
120 IF AN=3 THEN DF=16:D1=4:D2=12:DI=14
130 IF AN=4 THEN DF=14:D1=4:D2=10:DI=16
140 EX$="L255T255O1V31;7;3;3;5;1;2;7;5;5;4;5;7;V25;5;5;5;7;3;7;4;5;2;V20;3;5;7;7;1;1;2;7;4;V15;3;5;2;1;2;2;5;5;V10;1;7;4;2;5;8;1;3;5;7;3;5;3;V5;4;6;7;3;7;2;8"
150 C0=0:C1=1:C2=2:C3=3:C4=4:C5=5:C6=6:C7=7
160 HDRAW"C3;BM165,82;L8U3D6L1U6D3R16U3R1D6U6D4U1L16R16L4H4G4":HCIRCLE(165,82),3,3:HPAINT(166,83),3,3:HPAINT(166,81),3,3
170 HCIRCLE(128,82),8,C4,.4:HPAINT(128,82),C0,C4
180 HGET(0,0)-(40,35),1
190 HGET(150,70)-(180,105),2
200 HGET(110,76)-(140,91),3
210 HPAINT(128,82),C7,C4:HGET(110,76)-(140,91),4
220 HPUT(150,70)-(180,105),1,PSET
230 HPUT(110,76)-(140,91),1,PSET
240 HDRAW"C4;BM0,190;E10;R5;F5;E8;R3;E5;R2;F15;R3;E10;R5;F6;R7;F4;R2;E18;R1;F5;R3;F6;E7;R5;F12;E2;R6;F3;E12;R4;F10;R10;E3;R2;F5;R3;E16;F6;R5;F10;R22;E5;F6;R12;E4;F3;R8;E6;F6;R20"
250 HPAINT(15,190),C6,4
260 HCOLOR 1,8:HPRINT(10,0),"SCORE: ":HPRINT(19,0),SC
270 SA=188:SB=10'**** SATELLITES
280 FOR CC=1 TO 25:XX=RND(300):YY=RND(60)+10:HCIRCLE(XX,YY),RND(3),C4:HPAINT(XX,YY),4,C4:NEXT CC
290 '**** MAIN LOOP ****
300 A=0:B=0
310 XA=X+A:XB=X1+B
320 ZZ=SX-XA:A=A+(ZZ/(RND(4)+2))
330 Z1=SX-XB:B=B+(Z1/(RND(4)+2))
340 DE=XA-XB:IF DE<40 THEN A=A+20:B=B-20
350 GOSUB410
360 IF PO<>XS AND BUTTON(2)=1 THEN GOSUB520
370 HPUT(X+A,Y)-(X+A+35,Y+15),1,PSET
380 HPUT(X1+B,Y)-(X1+B+35,Y+15),1,PSET
390 GOTO310
400 '**** SAUCER FLYING ****
410 HPUT(X+A,Y)-(X+A+30,Y+15),3,PSET
420 IF SC>=CT THEN HPUT(X1+B,Y)-(X1+B+30,Y+15),4,PSET
430 RN=RND(DF):IF RN<D1 THEN GOSUB740 ELSE IF RN>D2 AND SC>CT THEN GOSUB 1080
440 '**** OUR MISSILE BATTERY ****
450 PO=XS:QQ=JOYSTK(0):J0=JOYSTK(2):J1=JOYSTK(3):IF J0>53THENXS=SX+14ELSEIFJ0<12THENXS=SX-14
460 IF XS<45THENXS=45ELSEIFXS>240THENXS=240
470 HPUT(SX,SY)-(SX+30,SY+25),1,PSET
480 SX=XS
490 HPUT(SX,SY)-(SX+30,SY+25),2,PSET
500 RETURN
510 '**** SHOOT MISSILE ****
520 YY=SY-35
530 PLAY L1$:HLINE(SX+15,YY+40)-(SX+15,YY),PSET:PP=HPOINT(SX+15,YY-2):HCOLOR1,8:HLINE(SX+15,YY+40)-(SX+15,YY),PRESET
540 IF PP=C0 OR PP=C4 THEN GOSUB570
550 RETURN
560 '**** SAUCER EXPLOSION ****
570 D$="C2;BM"+STR$(INT(SX+15))+","+STR$(INT(YY+5))
580 HDRAW D$+C$:HPAINT(SX+15,YY),C1,2
590 FOR I=1TO10:NEXTI
600 HPAINT(SX+15,YY+5),C3,2
610 HDRAW D$+C$
620 PLAY EX$
630 HPUT(SX-5,YY-15)-(SX+35,YY+15),1,PSET
640 SC=SC+AD
650 HCOLOR8,1:HPRINT(19,0),SC-AD:HCOLOR1,8:HPRINT(10,0),"SCORE: ":HPRINT(19,0),SC
660 WW=SC/WI:IF WW>0 AND INT(WW)=WW THEN 1220 ELSE CC=SC/CT:IF CC>0 AND INT(CC)=CC THEN GOSUB1150:GOTO160
670 RETURN
680 '**** MISSILE BATTERY EXPLOSION ****
690 HCIRCLE(SX+15,YP),10,C6:HPAINT(SX+15,YP+2),C3,C6:PLAY EX$
700 SC=SC-AD
710 HCOLOR8,1:HPRINT(19,0),SC+AD:HCOLOR1,8:HPRINT(10,0),"SCORE: ":HPRINT(19,0),SC
720 RETURN
730 '**** FIRST SAUCER SHOOT ****
740 REM PLAY L2$:'**** DELETE  REM FOR SAUCER FIRING SOUND ****
750 XP=SX+15:YP=SY+10:DP=X+A+18-XP:IF ABS(DP)>DI THEN XP=SX+30:'IF DISTANCE TOO GREAT THEN SAUCER'S SHOT MISSES
760 HLINE(X+A+18,Y+9)-(XP,YP),PSET:P1=HPOINT(XP,YP+1)
770 HCOLOR1,8
780 HLINE(X+A+18,Y+9)-(XP,YP),PRESET
790 IF P1=3 THEN GOSUB690:GOTO800
800 RETURN
810 PALETTE CMP:WIDTH32:POKE65496,0:END
820 '**** TITLE PAGE ****
830 HCOLOR5,8:GOSUB910
840 LINE INPUT AN$
850 HCOLOR8,8:GOSUB910
860 HCOLOR5,8:GOSUB1040
870 LINE INPUT AN$:PLAY L3$:AN=VAL(AN$)
880 HCOLOR8,8:GOSUB1040
890 HCOLOR1,8
900 RETURN
910 XZ=8:YZ=8
920 L3$="L255O1V31;12;11;10;9;8;7"
930 PLAY L3$:PLAY L3$:PLAY L3$
940 HPRINT(XZ,YZ),"S A U C E R   D E F E N S E"
950 FOR TT=1 TO 200:NEXT TT
960 PLAY L3$:PLAY L3$
970 HPRINT(XZ+7,YZ+2),"BY"
980 FOR TT=1 TO 200:NEXT TT
990 PLAY L3$:PLAY L3$:PLAY L3$:PLAY L3$
1000 HPRINT(XZ,YZ+4),"J O H N  T.  W E L L S"
1010 PLAY L3$:PLAY L3$:PLAY L3$
1020 HPRINT(XZ,YZ+6),"PRESS <ENTER> TO START!"
1030 RETURN
1040 HPRINT(XZ-5,YZ),"DIFFICULTY? 1(EASIEST) - 4(HARDEST)":HPRINT(XZ-5,YZ+2),"PRESS YOUR CHOICE AND <ENTER>"
1050 PLAY L3$:PLAY L3$:PLAY L3$
1060 RETURN
1070 '**** SECOND SAUCER SHOOT ****
1080 REM PLAY L2$:'**** DELETE REM FOR SAUCER FIRING SOUND ****
1090 XP=SX+15:YP=SY+10:DP=X1+B+18-XP:IF ABS(DP)>DI THEN XP=SX+30
1100 HLINE(X1+B+18,Y+9)-(XP,YP),PSET:P1=HPOINT(XP,YP+1)
1110 HCOLOR1,8
1120 HLINE(X1+B+18,Y+9)-(XP,YP),PRESET
1130 IF P1=3 THEN GOSUB690
1140 RETURN
1150 '**** BONUS AND CHANGE COLORS ****
1160 HPRINT(XZ+2,YZ+2),BO:HPRINT(XZ+8,YZ+2),"POINTS BONUS!":SC=SC+BO
1170 FOR SS=1 TO 10:PLAY L1$:PLAY L3$:NEXT SS
1180 IF AN=1 THEN DI=DI+.5 ELSE IF AN=2 THEN DI=DI+1 ELSE IF AN=3 THEN DI=DI+1.5 ELSE DI=DI+2
1190 HCLS8
1200 C0=RND(7):C1=RND(7):C2=RND(7):C3=RND(7):C4=RND(7):C5=RND(7):C6=RND(7):C7=RND(7)
1210 RETURN
1220 '**** QUIT OR CONTINUE ****
1230 HCLS4:HCOLOR 8,8:FOR CO=YZ-8 TO YZ+2:FOR CR=XZ-6 TO XZ+24 STEP 6:HPRINT(CR,CO),SC:PLAY L3$:NEXT CR,CO
1240 GOSUB 1280
1250 LINE INPUT AN$
1260 HCOLOR8,8:GOSUB 1280
1270 IF AN$="C" THEN SC=SC+BO:HCLS8:GOTO160 ELSE 810
1280 HPRINT(XZ,YZ+4),SC:HPRINT(XZ+8,YZ+4),"   POINTS ! ! ! ! !"
1290 PLAY L3$:PLAY L3$:PLAY L3$
1300 HPRINT(XZ,YZ+7),"CONTINUE <C> OR QUIT <Q> ?":HPRINT(XZ,YZ+9),"PRESS <C> OR <Q> AND <ENTER>"
1310 RETURN
