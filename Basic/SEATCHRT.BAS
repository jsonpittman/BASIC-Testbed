10 '**************
20 '*  SEATING   *
30 '*   CHART    *
40 '* GENERATOR  *
50 '*     BY     *
60 '*D.A.TUROWSKI*
70 '* NOV. 1987  *
80 '**************
90 CLEAR 5000
95 NS=0
100 CLS(RND(8)):PRINT@32*8+4,"seating"+CHR$(128)+"chart"+CHR$(128)+"generator";
110 PRINT@32*9+14,"by";
120 PRINT@32*10+9,"d"+CHR$(128)+"a"+CHR$(128)+"turowski";
121 PRINT@32*13+10,"disk"+CHR$(128)+"version";
125 PRINT@32*15+2,"suggestions"+CHR$(128)+"by"+CHR$(128)+"r"+CHR$(128)+"s"+CHR$(128)+"lamendola";
130 SCREEN 0,1:FOR YY=1 TO 2500:NEXT YY
140 FOR XX=32*16-1 TO 0 STEP -1:PRINT@XX,CHR$(150);:NEXT XX:CLS
141 PRINT"THIS PROGRAM WILL GENERATE A    SEATING CHART FOR YOUR CLASS.   IN ORDER TO BEGIN THIS PROGRAM, YOU MUST ENTER THE NUMBER OF    ROWS OF SEATS AND THE NUMBER OF SEATS IN EACH ROW IN YOUR CLASS.":PRINT"PRESS ANY KEY TO CONTINUE...":EXEC44539
142 PRINTSTRING$(32,128):CLS:PRINT"IF POSSIBLE, TRY TO INCORPORATE THIS SIZE INTO THE NAME YOU GIVETO THIS LIST WHEN ASKED IN THE  PROGRAM, SUCH AS 'PER2-5X6' TO  DESIGNATE 5 ROWS WITH 6 SEATS!"
143 PRINTSTRING$(32,128):PRINT"THIS UTILITY ALSO HAS AN EDITINGFEATURE TO ALLOW CHANGES OR     DELETIONS!":PRINT@32*14,"PRESS ANY KEY TO CONTINUE...":EXEC44539
150 CLS(RND(8)):PRINT"ENTER THE NUMBER OF ROWS OF     SEATS IN THE CLASSROOM (MAXIMUM IS 5)":INPUT R:GOSUB 4000
151 IF R>5 THEN PRINT"SORRY, YOU MUST ENTER A NUMBER  LESS THAN OR EQUAL TO 5 FOR THE SEATING CHART TO FIT ON THE     PRINTER!":FOR YY=1 TO 3500:NEXT YY:GOTO 150
152 IF R<1 THEN PRINT"SORRY, INVALID NUMBER OF ROWS.  TRY AGAIN!!!":FOR YY=1 TO 3000:NEXT YY:GOTO 150
160 PRINT:PRINT"ENTER THE NUMBER OF SEATS       (MAXIMUM) IN EACH ROW";:INPUT S:PRINTSTRING$(32,"=")
165 IF R=3 THEN V=16 ELSE V=0
170 DIM LN$(R,S),F$(R,S)
171 CLS(RND(8)):INPUT"DO YOU HAVE THIS SEATING CHART  PREVIOUSLY SAVED ON DISK";R$:IF LEFT$(R$,1)="Y" THEN 3000
175 CLS(RND(8)):PRINTSTRING$(32,128);
180 PRINT"YOU WILL NOW BE ASKED TO ENTER  THE NAMES OF THE STUDENTS IN    YOUR CLASS BY ENTERING THEIR    FIRST NAME AND THEN THEIR LAST  NAME."
181 PRINTSTRING$(32,128);:FOR YY=1 TO 2500:NEXT YY
190 PRINT"note-->FOR THIS PROGRAM, ROW 1, SEAT 1 REFERS TO THE FARTHEST   ROW ON YOUR LEFT AS YOU STAND   IN FRONT OF YOUR CLASS AND OF   COURSE THE FIRST SEAT IN THAT   ROW."
195 PRINTSTRING$(32,128);:FOR YY=1 TO 3000:NEXT YY
200 FOR XX=1 TO 1000:NEXT XX:PRINT@32*14,"PRESS ANY KEY TO CONTINUE.....":EXEC44539
210 CLS(RND(8))
220 FOR X=1 TO R
230 FOR Y=1 TO S
240 PRINT"ENTER first name FOR:":PRINT" ROW ";X;", SEAT";Y:INPUT F$(X,Y):IF LEN(F$(X,Y))>15 THEN PRINT"NAME TOO LONG!  PLEASE RE-ENTER!":GOTO 240
250 PRINT"ENTER last  name FOR:":PRINT" ROW ";X;", SEAT";Y:INPUT LN$(X,Y):IF LEN(LN$(X,Y))>15 THEN PRINT"NAME TOO LONG!! PLEASE RE-ENTER!":GOTO 250
251 PRINTF$(X,Y);" ";LN$(X,Y):PRINT"correct (y/n)":INPUT R$:IF F$(X,Y)<>"" THEN NS=NS+1
252 IF LEFT$(R$,1)="Y" THEN 255 ELSE 240
255 PRINTSTRING$(32,"=")
260 NEXT Y
270 NEXT X
271 CLS(RND(8)):PRINT STRING$(32,"-")
275 PRINTSTRING$(32,128):PRINT"PLEASE ENTER CLASS NAME AT THIS TIME (MATH, ENGLISH, ETC.)":LINE INPUT CL$
276 PRINT"NOW ENTER THE PERIOD NUMBER":INPUT P$
277 PRINT"NOW ENTER THE TEACHER'S NAME":INPUT T$
280 PRINTSTRING$(32,128);:PRINT"DATA ENTRY COMPLETED!!":PRINT"DO YOU WANT TO SAVE OR          RE-SAVE THIS LIST OF NAMES?":LINE INPUT R$:IF LEFT$(R$,1)="Y" THEN 2000
281 GOSUB 6010:CLS(RND(8)):PRINT@32*8,STRING$(32,128);:PRINT@32*8+6,"printing"+CHR$(128)+"in"+CHR$(128)+"progress";
284 Z=LEN(CL$):TP=(80-Z)/2
285 PRINT#-2,T$,,,,"PERIOD ";P$:PRINT#-2,TAB(TP);CL$:
286 PRINT#-2:PRINT#-2:PRINT#-2,TAB(2);"<<BACK OF ROOM>>";TAB(63);"<<BACK OF ROOM>>"
287 PRINT#-2,TAB(V);STRING$(LL,"-")
288 PRINT#-2
290 FOR Y=S TO 1 STEP -1
300 FOR X=1 TO R
310 PRINT#-2,TAB(V);F$(X,Y),
320 NEXT X
325 PRINT#-2
340 FOR X=1 TO R
350 PRINT#-2,TAB(V);LN$(X,Y),
360 NEXT X
370 PRINT#-2:PRINT#-2:PRINT#-2,TAB(V);STRING$(LL,"-"):PRINT#-2
380 NEXT Y
381 PRINT#-2,TAB(32);"<<FRONT OF ROOM>>":PRINT#-2:PRINT#-2:PRINT#-2,CL$;TAB(40);"NUMBER OF STUDENTS =";NS
384 PRINT@32*8,STRING$(32,128);:PRINT@32*8+7,"printing"+CHR$(128)+"completed";
385 PRINT@32*13,"DO YOU WANT ANOTHER COPY (Y/N)";:INPUT R$:IF LEFT$(R$,1)="Y" THEN 281
386 PRINT@32*13,"DO YOU WANT TO RE-RUN THIS      PROGRAM";:INPUT R$:IF LEFT$(R$,1)="Y" THEN RUN ELSE END
390 END
2000 REM ROUTINE TO SAVE LIST OF NAMES FOR SEATING CHART
2005 PRINT STRING$(32,"=")
2010 INPUT"ENTER A NAME FOR THIS LIST.     THE NAME MUST HAVE 8 OR LESS    LETTERS!";N$
2020 IF LEN(N$)>8 THEN PRINT "INVALID FILENAME- TRY AGAIN!":GOTO 2010
2030 OPEN "O",#1,N$
2040 FOR X=1 TO R
2050 FOR Y=1 TO S
2060 WRITE#1,F$(X,Y),LN$(X,Y)
2070 NEXT Y
2080 NEXT X
2085 CLOSE #1
2090 PRINTN$;" IS SAVED TO DISK!!":SOUND 200,2:FOR XX=1 TO 1500:NEXT XX:FL=1:GOTO 3015
3000 REM ROUTINE TO LOAD PREVIOUS FILE NAMES
3010 PRINTSTRING$(32,"-"):INPUT"ENTER THE NAME OF THE           LIST";N$
3011 IF LEN(N$)>8 THEN PRINT"INVALID FILENAME, PLEASE        RE-ENTER!!":GOTO 3010
3015 PRINTSTRING$(32,"-"):NS=0
3020 OPEN "I",#1,N$
3030 FOR X=1 TO R
3040 FOR Y=1 TO S
3050 INPUT#1, F$(X,Y),LN$(X,Y)
3055 IF FL=1 THEN 3065
3060 PRINT F$(X,Y);" ";LN$(X,Y)
3065 IF F$(X,Y)<>"" THEN NS=NS+1
3070 NEXT Y
3080 NEXT X
3090 CLOSE #1
3091 IF FL=1 THEN 281
3095 PRINTSTRING$(32,"-")
3100 REM EDITING ROUTINE
3110 INPUT"DO YOU WANT TO CHANGE ANY OF    THESE SEATS AT THIS TIME";R$:IF LEFT$(R$,1)="Y" THEN 3120 ELSE 275
3120 PRINT"PLEASE ENTER THE LOCATION OF THESTUDENT'S SEAT THAT YOU WANT TO CHANGE":INPUT"ROW #";X:INPUT"SEAT #";Y
3130 PRINT "THE STUDENT IS:":PRINTF$(X,Y);" ";LN$(X,Y):INPUT "IS THIS CORRECT";R$:IF LEFT$(R$,1)="Y" THEN 3140 ELSE 3120
3140 PRINT:PRINT"ENTER THE first NAME OF THE     STUDENT WHO WILL now BE         SITTING IN THIS SEAT instead of ";F$(X,Y);" ";LN$(X,Y)
3150 INPUT F$(X,Y)
3160 PRINT"NOW ENTER THEIR last NAME":INPUT LN$(X,Y)
3170 PRINT"STUDENT IS REPLACED BY:":PRINTF$(X,Y);" ";LN$(X,Y)
3180 PRINT"ANY MORE CHANGES (Y/N)":INPUT R$:IF LEFT$(R$,1)="Y" THEN 3120ELSE 275
4000 REM ROUTINE TO SET PRINTED LINE WIDTH
4010 IF R=5 THEN LL=80
4020 IF R=4 THEN LL=64
4030 IF R=3 THEN LL=48
4040 IF R=2 THEN LL=32
4050 IF R=1 THEN LL=16
4060 RETURN
6000 REM ROUTINE TO CHECK FOR PRINTER READINESS
6010 CLS:PRINT@32*3,"1)TURN ON PRINTER":SOUND 200,3:FORXX=1TO1000:NEXT XX:IF PEEK(65314)/2<>INT(PEEK(65314)/2) THEN PRINT@32*11+6,"printer is not on line!!!":PRINT@32*12,"turn printer on at this time!!!":FORXX=1TO1000:NEXT XX:GOTO6010
6020 CLS:PRINT@32*5,"2)LINE UP SHEET IN PRINTER WITH  PRINT HEAD AT THIS TIME":SOUND 230,2
6030 PRINT@32*10,"   PRESS ANY KEY TO CONTINUE":EXEC44539
6040 PLAY"O3;V31;L10;A;V16;A;V8;A;V3;A;V1;L20;A"
6050 RETURN
