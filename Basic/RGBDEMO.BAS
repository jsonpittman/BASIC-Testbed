10 CLEAR300
20 ON BRK GOTO 50
30 RGB:WIDTH32:PALETTE 12,54:PALETTE 13,0
40 TEST$=STRING$(192,255)
50 CL=0
60 PALETTE 7,CL
70 CLS
80 GOSUB 90:GOTO 280
90 PRINTTAB(13);CHR$(191);"=OFF  ";CHR$(207);"=ON"
100 PRINTTAB(5);"BIT #   5  4  3  2  1  0"
110 PRINTTAB(5);"VALUE  32 16  8  4  2  1"
120 PRINTTAB(5);"COLOR   R  G  B  R  G  B"
130 PRINTTAB(13);
140 IFCL AND32THENPRINTCHR$(207);ELSEPRINTCHR$(191);
150 PRINT"  ";
160 IFCL AND16THENPRINTCHR$(207);ELSEPRINTCHR$(191);
170 PRINT"  ";
180 IFCL AND8THENPRINTCHR$(207);ELSEPRINTCHR$(191);
190 PRINT"  ";
200 IFCL AND4THENPRINTCHR$(207);ELSEPRINTCHR$(191);
210 PRINT"  ";
220 IFCL AND2THENPRINTCHR$(207);ELSEPRINTCHR$(191);
230 PRINT"  ";
240 IFCL AND1THENPRINTCHR$(207);ELSEPRINTCHR$(191);
250 PRINT:PRINTTAB(13);"CURRENT COLOR="CL
260 PRINT@224,TEST$;
270 RETURN
280 PRINT@416,"<C>ODE <B>ITS <S>CAN <H>ELP":PRINT"<E>ND";
290 A$=INKEY$:IFA$=""THEN290
300 PRINT@416,STRING$(64,32);
310 IFA$="S"THEN 820
320 IFA$="C"THEN 370
330 IFA$="B"THEN 400
340 IFA$="E"THENCLS:RGB:END
350 IFA$="H"THEN480
360 GOTO280
370 PRINT@416,"ENTER COLOR CODE";:INPUTCL
380 IFCL<0ORCL>63ORCL<>INT(CL)THEN390ELSE60
390 SOUND1,5:PRINT@416,STRING$(32,32);:GOTO370
400 CL=0:PRINT@416,"ENTER BIT NUMBER TO SET HIGH"
410 PRINT"PRESS ENTER WHEN DONE"
420 A$=INKEY$:IFA$=""THEN420
430 IFA$=CHR$(13)THEN60
440 IFA$<"0"ORA$>"5"THEN420
450 PRINTA$;
460 B=2^VAL(A$):CL=CL OR B
470 GOTO420
480 CLS
490 PRINT"THIS PROGRAM WILL DEMONSTRATE"
500 PRINT"THE WAY THE COCO 3 DETERMINES"
510 PRINT"THE RGB COLORS. EACH COLOR CON-"
520 PRINT"SISTS OF 6 BITS, TWO BITS FOR"
530 PRINT"EACH PRIMARY COLOR OF <R>ED,"
540 PRINT"<G>REEN AND <B>LUE. BY TURNING"
550 PRINT"A BIT ON OR OFF, YOU TURN THE"
560 PRINT"ASSOCIATED COLOR ON OR OFF."
570 PRINT"THE PURE R G OR B COLOR IS"
580 PRINT"CREATED BY TURNING BOTH COLOR"
590 PRINT"BITS ON WITH THE OTHERS OFF."
600 PRINT"BY TURNING ON OR OFF DIFFERENT"
610 PRINT"COMBINATIONS OF BITS, YOU CAN"
620 PRINT"CREATE ALL 64 COLORS AVAILABLE."
630 PRINT:PRINT"PRESS A KEY";
640 A$=INKEY$:IFA$=""THEN640
650 CLS
660 PRINT"YOU CAN EITHER ENTER THE COLOR"
670 PRINT"CODE DIRECTLY BY PRESSING <C>"
680 PRINT"OR ENTER THE BITS YOU WANT ON"
690 PRINT"BY PRESSING <B>. THE BLOCK IN"
700 PRINT"THE MIDDLE OF THE SCREEN WILL"
710 PRINT"SWITCH TO THE COLOR YOU SELECT."
720 PRINT"IN THE <C> MODE, SIMPLY ENTER A"
730 PRINT"NUMBER FROM 0 TO 63. IN THE <B>"
740 PRINT"MODE, PRESS 0-5 TO TURN ON BIT"
750 PRINT"0 TO 5.  PRESS ENTER WHEN YOU"
760 PRINT"ARE DONE AND THE COLOR WILL BE"
770 PRINT"DISPLAYED. THE <S>CAN MODE WILL"
780 PRINT"DISPLAY ALL THE COLORS IN ORDER."
790 PRINT"PRESS A KEY"
800 A$=INKEY$:IFA$=""THEN800 ELSE60
810 RGB:CLS:END
820 FOR CL=0 TO 63
830 PALETTE 7,CL:PRINT@0,"";
840 GOSUB 90
850 FOR DLAY=1 TO 500:NEXT
860 NEXT CL
870 CL=0
880 GOTO 50
