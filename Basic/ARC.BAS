0 'ARCHIE 1.1 (main module), (C) 1986 BRUCE BELL, THIS PROGRAM IS MADE AVAILABLE 'AS IS' AND IS NON-WARRANTED.
5 DIMA(2),B(400),N$(22):DEF FND(S)=INT(WW*S/12-.5):DEF FNJ(H)=(ABS(H)-1)*SGN(H)
10 D$(0)="ARCHIE  ":D$(1)="B. BELL ":D$(2)="02/10/87":M$="CIRCLESQUAREBOXFILLINE  DRAW  ERASE PAINT TYPE  CIRWALSQRWALMOVE  COPY  FURN  FIX   SCALE PRINT DISK  CASSETSCREENHELP  PALETEDEFALTTRASH END   "
15 SP$=" ^[":U$=" ###X### ":ME$=CHR$(34):S=4:SS=3:M=2:PC=1:CP=0:PP=10:SC=1:WW=4.5:PQ=PEEK(492):G=PEEK(188)*256:G1=PEEK(253):G2=G1+24
20 PMODE4,5:PCLS5:PMODE4,1:PCLS5:DRAW"BM50,50C0U6F6L3M+2,+4LM-2,-4L2":PAINT(52,48),0,0:GET(50,44)-(56,54),A,G:PCLS5:SCREEN1,1
25 POKE65495-PQ,0
30 POKE178,PC:LINE(192,0)-(204,7),PSET,BF:PRINT@26,MID$(M$,1+6*(M-1),6);:PRINT@23,MID$(SP$,SP+1,1);:COLOR5,0
35 POKE255,255:PRINT@9,USING"##.#"+ME$;SS;:POKE255,0:XM=0:YM=8:GOSUB200:PSET(X,Y,C):IFQ$=""THEN65ELSEIFQ$="\"THENC=5:GOTO330ELSEIFQ$="["THENC=5:GOTO60ELSEIFQ$=" "THENC=5:GOSUB235:GOTO295
40 IFQ$<>"R"ANDQ$<>"P"THEN50ELSEGOSUB285:FORT=1TO999:NEXT:IFQ$="P"THENGET(80,0)-(184,7),B,G:GOSUB235:GOSUB200:Z=1ELSE50
45 GOSUB230:PUT(X1,Y1)-(X1+104,Y1+7),B,AND:GOSUB200:IFZ=2THENZ=0:GOTO35ELSE45
50 IFQ$="^"THENSP=(SP+1)AND1:X3=X1:Y3=Y1:GOTO30ELSEIFQ$=CHR$(10)THENSP=(SP+2)AND2:GOTO30ELSEIFQ$="C"THENCN=1:GOSUB230
55 GOTO35
60 GOSUB230:GOTO30
65 GOSUB235:ONM GOTO70,90,90,90,115,125,135,140,70,90,155,155,180,180
70 IFCN=0THENX=X1:Y=Y1 ELSECN=0:X=X2:Y=Y2
75 X2=X:Y2=Y:YM=8:Z=1:POKE253,G2:D=FND(S)
80 W=ABS(X-X1):IFW=0GOSUB200:GOTO80ELSEHW=ABS(Y-Y1)/W:CIRCLE(X,Y),W,0,HW:IFM=9ANDW>=D THENCIRCLE(X,Y),W-D,0,HW
85 GOSUB265:GOSUB200:IFZ=2THENZ=0:POKE253,G1:GOTO35ELSEGOSUB230:GOTO80
90 IFCN=0THENX=X1:Y=Y1 ELSECN=0:X=X2:Y=Y2
95 X2=X:Y2=Y:YM=8:Z=1:POKE253,G2:D=FND(S)
100 IFM=4THENLINE(X,Y)-(X1,Y1),PRESET ELSE IFM=3THENPOKE178,PC:LINE(X,Y)-(X1,Y1),PSET,BF ELSE LINE(X,Y)-(X1,Y1),PRESET,B
105 IFM=10ANDD>0THENDX=SGN(X1-X)*D:DY=SGN(Y1-Y)*D:LINE(X+DX,Y+DY)-(X1-DX,Y1-DY),PRESET,B
110 GOSUB265:GOSUB200:IFZ=2THENZ=0:POKE253,G1:C=PPOINT(X,Y):GOTO35ELSEGOSUB230:GOTO100
115 YM=8:Z=1
120 X=X1:Y=Y1:PRESET(X,Y):PRINT@10,USINGU$;X,Y:GOSUB200:IFZ=2THENZ=0:GOTO35ELSE120
125 XM=2:YM=10:Z=1:COLOR0,5
130 GOSUB200:LINE(X1-2,Y1-2)-(X1+2,Y1+2),PSET,BF:PRINT@10,USINGU$;X1,Y1:LINE(X1-2,Y1-2)-(X1+2,Y1+2),PRESET,BF:IFZ=2THENZ=0:C=PPOINT(X,Y):GOTO35ELSE130
135 POKE178,PC:PAINT(X1,Y1),,0:C=PPOINT(X1,Y1):POKE178,3:GOTO35
140 IFY1>180THEN30ELSEP=X1/8+Y1+Y1/6*10
145 PRINT@0,"PRESS <ENTER> AT END OF THE LINE";:PRINT@P,"";:LINEINPUTS$:IFP>479THEN30ELSEPRINT@0,"<CLEAR> TO QUIT:<SPACE> TO CONT.";
150 Q$=INKEY$:IFQ$=""THEN150ELSEIFQ$=CHR$(12)THEN30ELSEIFQ$=" "THENP=P+32:GOTO145
155 YM=8:Z=1:IFCN THENCN=0:X=X2:Y=Y2:GOTO175ELSEPOKE253,G2:X=X1:Y=Y1
160 GOSUB230:GOSUB200:IFZ=1THENPOKE179,53:LINE(X,Y)-(X1,Y1),PRESET,B:X2=INT(400-(ABS(X-X1)+1)*(ABS(Y-Y1)+1)/40):PRINT@16,X2:IFX2<0THENPLAY"L255O1A":GOTO160ELSE160
165 POKE253,G1:IFX2<0THENPRINT@14,"AREA TOO LARGE!";:SOUND100,1:FORT=1TO500:NEXT:Z=0:GOTO30
170 X2=X:Y2=Y:GET(X,Y)-(X1,Y1),B,G:LINE(X,Y)-(X1,Y1),PSET,B:DX=X1-X:DY=Y1-Y:X1=X:Y1=Y:XM=ABS((DX-ABS(DX))/2):YM=ABS((DY-ABS(DY))/2)+8:GOSUB200:Z=1
175 GOSUB200:GOSUB230:PUT(X1,Y1)-(X1+DX,Y1+DY),B,AND:IFZ=1THEN175ELSEZ=0:IFM=11THENLINE(X2,Y2)-(X2+DX,Y2+DY),PSET,BF:PUT(X1,Y1)-(X1+DX,Y1+DY),B,AND:GOTO35ELSE35
180 Z=1:POKE253,G2:PRINT@31,CHR$(95-O);:C=0
185 X=X1:Y=Y1:DRAW"BM=X;,=Y;C0S=S;A=A;":ONW GOSUB620,625,630,635,640,645,650,655,660,665,670,680,685,690,695,700,705,710,715,555,555,555,555,555,565,570,575,580,580,585,590,595,600,605,610,610,615,615:DRAW"S4A0"
190 PRINT@14,USINGU$;X,Y;:GOSUB200:IFZ<2GOSUB230:GOTO185ELSEPLAY"O3L9GE":C=PPOINT(X,Y):GOSUB200:IFQ$=CHR$(9)THENO=(O+2)AND2:PRINT@31,CHR$(95-O);:GOSUB230:GOTO180ELSEIFQ$=" "THENZ=0:POKE253,G1:PLAY"L40CDEFGABO4C":GOTO30
195 IFQ$="["THENZ=0:POKE253,G1:GOSUB230:GOTO30ELSEGOSUB230:A=(A+1)AND3:GOTO180
200 H=JOYSTK(0):V=JOYSTK(1):PRINT@0,H;V:IFM=8THENH=INT(H/2):V=INT(V/4.3)+1:X1=H*8:Y1=V*12:GOTO215ELSEIFSP=2THENX1=INT(H*4.06):Y1=INT(V*2.91)+8ELSEIFSP=1THENX1=X3+H-32:Y1=Y3+V-32ELSEH=INT(H/9.2)-3:H=FNJ(H):V=INT(V/9.2)-3:V=FNJ(V):X1=X1+H:Y1=Y1+V
205 IFX1<XM THENX1=XM:H=0ELSEIFX1>255THENX1=255:H=0
210 IFY1<YM THENY1=YM:V=0 ELSEIFY1>191THENY1=191:V=0
215 Q$=INKEY$:PK=PEEK(65280):IFQ$<>""ORPK=126ORPK=254THENRETURNELSEIFZ=1THENZ=2:RETURN
220 PSET(X,Y,C):X=X1:Y=Y1:C=PPOINT(X,Y):PSET(X,Y,C+1):PRINT@14,USINGU$;X,Y;:GOTO200
225 PRINT@256,STRING$(21,32):EXEC31410:DRAW"BM5,0C0R166D104L166NU104BR4DR163U101C5RD102L164":GOSUB240:GOSUB360:RETURN
230 FORI=5TO8:PCOPYI TOI-4:NEXT:C=PPOINT(X,Y):RETURN
235 FORI=1TO4:PCOPYI TOI+4:NEXT:RETURN
240 IFME THENSS=30.48/S:ME$="C"ELSESS=12/S:ME$=CHR$(34)
245 PRINT@257,USING"##.#"+ME$+"]";SS;:DX=256:DY=184:PP=264:GOSUB275:PP=9:RETURN
250 PRINT@0,D$(0);:PRINTTAB(27)"=";:PRINTUSING"###"+ME$;8*SS;:POKE178,0:LINE(200,0)-(208,7),PSET,BF:RETURN
255 DRAW"BM0,9C0R90D112L90":COLOR5:LINE(1,10)-(89,120),PSET,BF:POKE255,255:PRINT@33,D$(0);:POKE255,0:RETURN
260 H=JOYSTK(0):V=INT(JOYSTK(1)/8):K=V*32+64:PRINT@K,"\";:PK=PEEK(65280):IFPK=255ORPK=127THENPRINT@K," ";:GOTO260ELSERETURN
265 IFJ THENPRINT@14,USINGU$;X1,Y1;:RETURNELSEIFM=4THEN280ELSEDX=(ABS(X-X1)+1):DY=(ABS(Y-Y1)+1):IFM<9ORM>10THEN275ELSEDX=DX-S*WW/12:IFDX<0THENDX=0
270 DY=DY-S*WW/12:IFDY<0THENDY=0
275 DX=DX/S:DY=DY/S:IFME THENXF=DX*.305:YF=DY*.305:GOTO290ELSEXF=INT(DX):XI=(DX-XF)*12:YF=INT(DY):YI=(DY-YF)*12:GOTO290
280 DX=((ABS(X-X1)+1)/S)^2:DY=((ABS(Y-Y1)+1)/S)^2:DX=SQR(DX+DY):XF=INT(DX):XI=(DX-XF)*12:PRINT@10,USING"    ###'##   ";XF,XI;
285 IFM=4THENIFME THENPRINT@10,USING"    ###.##  ";XF*.305+XI*.025:RETURNELSEPRINT@10,USING"    ###'##   ";XF,XI:RETURN
290 IFME THENPRINT@PP,USING"###.##X###.##";XF;YF;:RETURNELSEPRINT@PP,USING"###'##X###'##";XF,XI,YF,YI;:RETURN
295 GOSUB225
300 GET(XX,YY)-(XX+6,YY+10),B,G:PUT(XX,YY)-(XX+6,YY+10),A,AND:Q$=INKEY$:IFQ$=" "THENP=M-1
305 Q$=INKEY$:IFQ$=" "THENP=M-1
310 PK=PEEK(65280):IFQ$=" "ORPK=126ORPK=254THENPLAY"L50O4C":IFP<12THENM=P+1:GOTO60ELSEIFP>23THEN320ELSEPUT(XX-8,YY-15)-(XX+8,YY+3),B,NOT:ONP-11 GOTO520,530,515,495,405,410,400,365,355,335,330,325
315 H=INT(JOYSTK(0)/8):V=INT(JOYSTK(1)/16)*8:P=H+V:IFP=P1 THEN305ELSEPUT(XX,YY)-(XX+6,YY+10),B,PSET:XX=H*20+18:YY=V*3+18:P1=P:GOTO300
320 PC=CP+P-24:POKE178,PC:LINE(192,0)-(204,7),PSET,BF:IFM=3ORM=7THEN60ELSE315
325 POKE65494,0:SCREEN0,0:CLS8:PRINT"you can continue by typing","CONT <PRESS ENTER>",,,"or you can cold start by typing POKE113,0:EXEC40999 <ENTER>",,,"BYE-BYE!":END:POKE65495-PQ,0:SCREEN1,1:GOSUB230:GOTO295
330 PCLS5:GOTO30
335 POKE255,255:PCLS5:PRINT@0,"DEFAULT MENU":PRINT:PRINT" FILENAME: "D$(0)," DESIGNER: "D$(1)," DATE: "D$(2),," WALL:";:IFME THENPRINTUSING"##.## CM";WW*2.54ELSEPRINTWW"IN"
340 PRINT" AUTO SIZE: "MID$("ON OFF",J*3+1,3),," LPRINT I.D.: "MID$("YESNO ",L*3+1,3)," MEASURE: "MID$("U.S.  METRIC",ME*6+1,6)," RETURN":POKE255,0
345 GOSUB260:IFV<4THENPRINT@K+20,"";:IFV<3THENINPUTD$(V):D$(V)=LEFT$(D$(V)+STRING$(8,32),8):GOTO335ELSEINPUTWW:IFME THENWW=INT(WW*39.37+.5)/100:GOTO335ELSE335
350 IFV=7THENGOSUB230:GOTO295ELSEIFV=4THENJ=(J+1)AND1:GOTO335ELSEIFV=5THENL=(L+1)AND1:GOTO335ELSEME=(ME+1)AND1:GOTO335
355 CP=INT(JOYSTK(0)/2)*8:GOSUB360:PK=PEEK(65280):IFPK=255ORPK=127THEN355ELSE295
360 V=CP:FORH=13TO162STEP20:POKE178,V:LINE(H,79)-(H+10,89),PSET,BF:V=V+1:NEXT:RETURN
365 PCLS5:PRINT@24,"COMMAND"TAB(57)"MODE:":PRINT@246,"_ COLORS"TAB(54)"_ IN/PIX &"TAB(24)"MAX SIZE":GOSUB225:FORK=0TO23:PRINT@322+K*8,MID$(M$,K*6+1,6);:NEXT:DRAW"BM9,116C0R240D74L240NU74BR4DR237U71":GOSUB395
370 PCLS5:PRINT@0,"KEYBOARD INPUT:"TAB(24)"DESIGN"TAB(57)"MODE:"TAB(32)"<SHFT>-<[>= UNDO","<SHFT>-<CLR>= TRASH","<SPACE>= COMMAND MODE",,,"CURSOR MODE (DEFAULT= RELATIVE):[= FULL SCREEN IMMEDIATE","^= SINGLE PIX IMMEDIATE",,,
375 PRINT"]= TOGGLE L-R DOOR HINGES","<SPACE>= SET FIXTURE/FURNITURE",,,"R= RECALL LAST DIMENSION","P= PRINT LAST DIMENSION","C= CONTINUE FUNCTION":GOSUB395
380 PCLS5:PRINT@12,"SPECIAL INSTRUCTIONS* MOVE & COPY",,"1. ENCLOSE TARGET AREA","2. RELEASE BUTTON","3. MOVE TO NEW AREA","4. PRESS BUTTON AND FINE ADJUST",,,
385 PRINT"* FIX & FURN",,"1. SELECT DIAGRAM","2. RELEASE & PRESS BUTTON TO RO-   TATE",,"4. PRESS ] TO SWITCH HINGES","5. PRESS <SPACE> TO SET POSITION"
390 PRINT"* <SPACE>: CHANGE COMMAND TO DE-  SIGN MODE W/O FUNCTION CHANGE":GOSUB395:GOSUB230:GOTO295
395 T=(T+1)AND1:PRINT@121,"PRESS":PRINT@152,"BUTTON>":DRAW"BM190,34C=T;R58D22L58U22":PK=PEEK(65280):IFPK=127ORPK=255THEN395ELSERETURN
400 SC=(SC+1)AND1:SCREEN1,SC:GOTO295
405 IFPEEK(49152)<>68THENPRINT@257,"WHERE'S DISK BASIC?":SOUND100,1:FORT=1TO1500:NEXT:GOTO295ELSEVERIFYON
410 GOSUB255:PRINT@65,"SAVE":PRINT" LOAD":PRINT" RETURN ^":IFP=16THENPRINT" DRIVE#"DN
415 GOSUB260:IFV>3THEN415ELSEIFV=2THEN490ELSEIFV=3THENIFP=16THENDN=(DN+1)AND3:PRINT@167,DN;:DRIVEDN:GOTO415ELSE415
420 POKE65494,0
425 IFV=1THEN435ELSEGOSUB230:GOSUB250:POKEG+8,24/S:FORI=1TO16:POKEG+I+8,ASC(MID$(D$(1)+D$(2),I,1)):NEXT:IFP=16THENIFFREE(DN)>2THENSAVEMD$(0)+".ARC",G,G+6143,380:GOTO25ELSEPRINT@22,"DISK FULL":SOUND100,1:GOSUB225:GOTO410
430 CSAVEMD$(0),G,G+6143,380:GOTO25
435 PCLS5:IFP=17THENPRINT@256,"PREPARE TAPE AND ENTER FILENAME BELOW. ENTER ^ TO ABORT...":I=22:GOTO460
440 N=0:CLS:POKE255,255:PRINT@0,TAB(7)"ARCHIE FILE DRAWER"STRING$(6,32):POKE255,0
445 FORSE=3TO11:DSKI$DN,17,SE,A$,B$:A$=A$+LEFT$(B$,127):FORI=0TO7:K=I*32:B$=MID$(A$,K+1,8):C$=LEFT$(B$,1):IFC$=CHR$(255)THENI=8:SE=12ELSEIFC$>CHR$(0)THENIFMID$(A$,K+9,3)="ARC"THENPRINT"  "B$,:N$(N)=B$:N=N+1
450 NEXTI,SE:PRINT@384," *LOAD"," *ABORT (^)";:N=N-1
455 H=INT(JOYSTK(0)/32):V=INT(JOYSTK(1)/5.3)*2:I=H+V:K=I*16+32:PRINT@K,"\";:PK=PEEK(65280):IFPK=255ORPK=127THENPRINT@K," ";:GOTO455
460 IFI=23GOTO490ELSEIFI<22ANDI>N THEN455ELSEIFI<22THENNF$=N$(I)+".ARC":GOTO475ELSEIFI=22THENPRINT@448,"FILENAME     ";:INPUTNF$:IFNF$="^"THENPRINT@448,STRING$(32,32):GOTO490ELSEIFP=17THENNF$=LEFT$(NF$+STRING$(8,32),8):GOTO475
465 E1=INSTR(1,NF$,"."):E2=INSTR(1,NF$,"/"):IFE1=0ANDE2=0THENNF$=NF$+".ARC":GOTO465ELSEIFE2>0THENMID$(NF$,E2,1)=".":GOTO465ELSENF$=LEFT$(LEFT$(NF$,E1-1)+STRING$(8,32),8)+MID$(MID$(NF$,E1+1)+"   ",1,3)
470 OPEN"D",#1,NF$,1:LF=LOF(1):CLOSE:IFLF=0THENKILLNF$:PRINT@448,"UNABLE TO FIND":GOTO455
475 IFP=16THENLOADMNF$:D$(0)=LEFT$(NF$,INSTR(2,NF$,".")-1)ELSECLOADMNF$:D$(0)=NF$
480 PK=PEEK(G+8):D$(1)="":D$(2)="":FORI=1TO8:D$(1)=D$(1)+CHR$(PEEK(G+I+8)):D$(2)=D$(2)+CHR$(PEEK(G+I+16)):NEXT:IFPK>0THENS=24/PK:IFME THENSS=30.48/S ELSESS=12/S
485 GOTO25
490 POKE65495-PQ,0:GOSUB230:GOTO295
495 GOSUB255:PRINT@65,"PRINT":PRINT" GRID":PRINT" RETURN"
500 GOSUB260:IFV>2THEN500ELSEIFV=2THENGOSUB230:GOTO295ELSEGOSUB230:GOSUB250:IFV=1THENFORH=0TO255STEP8:FORV=8TO191STEP8:PSET(H,V,0):NEXTV,H
505 POKE65494,0:IFL=0THENPRINT#-2,D$(0)CHR$(13)D$(1)CHR$(13)D$(2)CHR$(13)CHR$(13)
510 EXEC31462:GOSUB230:GOTO25
515 GOSUB240:S=24/(INT(JOYSTK(0)/2.8)+2):PK=PEEK(65280):IFPK=255ORPK=127THEN515ELSE295
520 S1=S:S=4:PCLS5:DRAW"BM0,8C0R46":PRINT@0,"SELECT:":PRINT@32,"CHAIRS",,"& SOFAS:"TAB(18)"5'   6'   7'":PRINT@192,"BEDS:  SNGL FULL QUN  KNG CAL":PRINT@320,"TABLES: RND RECT OCT COF  TV":PRINT@448,"BED-  CHST  DSK DRSR NS","ROOM:"
525 FORI=0TO3:FORK=0TO4:X=64+K*36:Y=22+48*I+(1ANDI):DRAW"BM=X;,=Y;S4A0":ONK+1+I*5 GOSUB620,625,630,635,640,645,650,655,660,665,670,680,685,690,695,700,705,710,715,720:NEXTK,I:S=S1:GOTO545
530 PCLS5:DRAW"BM0,8C0R46":PRINT@0,"SELECT:":PRINT@64,"DOORS: 2'0  2'4  2'6  2'8  3'0":O=0:DRAW"BM60,22S8A0":FORD=8TO12:GOSUB555:DRAW"BR10":NEXT
535 PRINT@192,"BATH: COMM.  SHOWERS    TUB":PRINT@320,"KIT: STOVE  REF  W/D    SINKS":FORI=0TO1:FORK=1TO5:DRAW"BM"+STR$(6+K*44)+","+STR$(70+48*I):ONK+I*5GOSUB565,570,575,580,560,585,590,595,600,605:NEXTK,I
540 PRINT@427,"WALL"TAB(21)"CORNER":PRINT@448,"FIRE-"TAB(26),"PLACE:":DRAW"BM50,178":GOSUB610:DRAW"BM138,178":GOSUB615:GOSUB720
545 H=INT(JOYSTK(0)/12.8):V=INT(JOYSTK(1)/16):PRINT@K+I," ";:I=H*5+8:K=96+V*128:PRINT@K+I,"\";:PK=PEEK(65280):IFPK=255ORPK=127THEN545
550 W=(V*5+H+1)+(P-12)*19:D=H+8:M=P+1:IFH+V=7THEN490ELSE60
555 E=D-2:DRAW"C5R=D;UL=D;DC0":IFO=0THENDRAW"UE=E;UF2D=E;"ELSEDRAW"U=E;E2DF=E;D":'door
560 RETURN
565 DRAW"R8U3L2GL2HL2ND3BRBU2U4E2R2F2D4G2L2H2":RETURN:'toilet
570 DRAW"R13U13L7G6D7BE6EFGH":RETURN:'corner shower stalls
575 DRAW"NE13R13U13L2BL4LBL4L2NF13D13":RETURN:'wall shower
580 DRAW"R20U10L2H2L12G2L2D10BR2BU4U3E2R12F2D3G2L12H2":RETURN:'bathtub
585 DRAW"R10U8L10D8BR2BU3EFGHBR4EFGHBU3EFGHBL4EFGH":RETURN:'stove
590 DRAW"R10U8NL10HL8GD8":RETURN:'refrig (30")
595 DRAW"R8U8NL8HL6GD8":RETURN:'washer, dryer, dishwasher (24")
600 DRAW"R12U8L12D8BRBU3U2ER2FND2BR2ND2ER2FD2GL2HBL2GL2H":RETURN:'dbl sink
605 DRAW"R8U8L8D8BE2R4EU3HL4GD3FBDRBR2R":RETURN:'single sink
610 DRAW"R18U6L2NL2U2H4L6G4D2NR2L2D6":RETURN:'wall fireplace
615 DRAW"NE3H4E13F4GNG2F2D6G4L6H2":RETURN:'corner fireplace
620 DRAW"HU8ER8FD8GLNL7U7HL4GD7 BR20":RETURN:wall chair
625 DRAW"LH6UE6RF6DGNG5H5LG3DF5":RETURN:'corner chair
630 DRAW"HU8ER18FD8GLNL17U7HL14GD7":RETURN:'sofa (5')
635 DRAW"HU8ER22FD8GLNL21U7HL18GD7":RETURN:'sofa (6')
640 DRAW"HU8ER28FD8GLNL27U7HL24GD7":RETURN:'sofa (7')
645 DRAW"R13U25L13ND25BF2R9D4L9NU4BG2R13BD3M-13,-1":RETURN:'sgl bed
650 DRAW"R18U25L18ND25BF2R6ND4BR2R6D4L6NU4BL2L6NU4BG2R18D3M-18,-1":RETURN:'full bed
655 DRAW"R20U27L20ND27BF2R7ND4BR2R7D4L7NU4BL2L7NU4BG2R20D3M-20,-1":RETURN:'queen bed
660 DRAW"R25U27L25ND27BF2R9ND4BR3R9D4L9NU4BL3L9NU4BG2R25D3M-25,-1":RETURN:'king bed
665 DRAW"R24U28L24ND28BF2R9ND4BR2R9D4L9NU4BL2L9NU4BG2R24D3M-24,-1":RETURN:'ca king bed
670 R=7*S/4:IF(Y-R)<8THENY=8
675 CIRCLE(X,Y-R),R,0:RETURN:'dining table (round)
680 DRAW"R20U12L20D12":RETURN:"dining table (rect)
685 DRAW"BU4U6E4R6F4D6G4L6H4":RETURN:'dining table (octagon)
690 DRAW"R16U8L16D8":RETURN:'coffee table
695 DRAW"R12U7L12D7":RETURN:'tv
700 DRAW"R12U6L12D6":RETURN:'chest
705 DRAW"R14U6L14D6":RETURN:'desk
710 DRAW"R20U6L20D6":RETURN:'dresser
715 DRAW"R6U6L6D6":RETURN:'night stand
720 PRINT@474,"CANCEL";:DRAW"BM206,166S4R48D10L48U10"
725 RETURN
