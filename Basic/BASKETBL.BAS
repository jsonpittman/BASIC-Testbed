10 REM"THE BASKETBALL CAPITOL'S DRILL- STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1989"
15 M=12:DIM A$(M),B$(M):FOR T=1 TO M:READ A$(T),B$(T):NEXT T:REM" *** THIS IS A NEW LINE"
20 CLS0:X=RND(-TIMER):FOR X=1408 TO 1471:POKE X,246:NEXT X
30 FOR X=1377 TO 1383:POKE X,172:NEXT X
40 FOR X=1400 TO 1406:POKE X,172:NEXT X
50 FOR Y=1156 TO 1348 STEP 32:POKE Y,239:NEXT Y
60 FOR Y= 1179 TO 1371 STEP 32:POKE Y,239:NEXT Y
70 FOR X=2 TO 16:FOR Y=0 TO 7:SET(X,Y,8):NEXT Y,X:POKE 1060,191
80 FOR X=47 TO 61:FOR Y=0 TO 7:SET (X,Y,8):NEXT Y,X:POKE 1083,191
90 PRINT@99,"000";:PRINT@122,"000";
100 POKE 1196,223:POKE 1203,197
110 FOR Y=1228 TO 1296 STEP 32:POKE Y,218:NEXT Y
120 POKE 1323,214:POKE 1354,214:POKE 1324,221:POKE 1357,217
130 POKE 1260,222:POKE 1229,214:POKE 1259,214:POKE1290,212
140 FOR Y=1204 TO 1300 STEP 32:POKE Y,202:NEXT Y
150 POKE 1331,198:POKE1362,198:POKE 1332,205:POKE 1365,201
160 POKE 1268,206:POKE 1237,198:POKE 1206,194
170 POKE 1267,204:POKE 1234,201:POKE 1201,193
180 P=RND(M):PRINT@416,A$(P);"'S CAPITOL":IF A=20 OR B=20 THEN 330 ELSE 190
190 PRINT@448,"":PRINT@448,"IS... ";:POKE1199,148:REM-THE BALL
200 TIMER=0:K=TIMER:REM 20 SECOND TIMER
210 K=TIMER:IF K>1200 THEN PRINT@454,"TIME IS UP":PLAY"L4D-D-D-":GOTO 260
220 EN$=INKEY$
230 IF EN$="" THEN 210
240 LINEINPUT DD$:REM-STUDENT'S ANSWER
250 IF DD$=B$(P) THEN 290 ELSE 260
260 FOR Y=1199 TO 1080 STEP-30: POKE Y,148:SOUND 230,2:POKE Y,128:NEXT Y
270 FOR T=1 TO 3:POKE 1083,239:PLAY"L10A":POKE 1083,191:PLAY"G":NEXT T:B=B+2:PRINT@122,B;
280 PRINT@454,B$(P):GOTO 310
290 FOR Y=1199 TO 1080 STEP-34:POKE Y,148:SOUND 230,2:POKE Y,128:NEXT Y
300 FOR T=1 TO 3:POKE 1060,239:PLAY"L10F":POKE 1060,191:PLAY"F":NEXT T:A=A+2:PRINT@99,A;
310 EN$=INKEY$
320 IF EN$=CHR$(13) THEN 180 ELSE IF EN$="E" THEN 350 ELSE 310
330 PRINT@416,STRING$(64,255);:PRINT@422,"THIS ROUND IS OVER";:PLAY"L20GFEDCCC"
340 EN$=INKEY$:IF EN$=CHR$(13) THEN RUN ELSE IF EN$="E" THEN 350 ELSE 340
350 CLS:END:REM ***THE NEW DATA BEGINS HERE
360 DATA ALBERTA,EDMONTON,BRITISH COLUMBIA,VICTORIA,MANITOBA,WINNIPEG
370 DATA NEW BRUNSWICK,FREDERICKTON,NEW FOUNDLAND,ST.JOHN'S,NORTHWEST TERRITORIES,YELLOWKNIFE
380 DATA NOVA SCOTIA,HALIFAX,ONTARIO,TORONTO,PRINCE EDWARD IS.,CHARLOTTETOWN
390 DATA QUEBEC,QUEBEC CITY,SASKATCHEWAN,REGINA,YUKON TERRITORY,WHITEHORSE
