0 'COPYRIGHT 1989  FALSOFT,INC
1 PMODE0:PCLEAR1
2 CLEAR14000
3 DIMT$(300,3)
5 DIMA$(10,2):DIMA(10,3)
6 DATA DATE,PLACE,AMOUNT
10 CLS(0):PRINT@64,"":PRINT@64,"IF YOU HAVE NO ACCOUNTS ON FILE,PRESS <Y> AND YOU WILL GO TO THEADD ACCOUNTS SUBROUTINE.          PRESS <N> TO GO TO MAIN MENU"
11 I$=INKEY$
12 IFI$="Y"ORI$="y"THENGOSUB3000:GOTO15
13 IFI$="N"THEN15
14 IFI$<>"Y"ORI$<>"N"THEN11
15 OPEN"I",#1,"ACCOUNTS/DAT"
16 FORX=1TO10:FORY=1TO2:INPUT#1,A$(X,Y):NEXTY:FORZ=1TO3:INPUT#1,A(X,Z):NEXTZ:NEXTX
17 CLOSE#1
19 CLS(0):PRINT@73,"credit";:PRINT@80,"cards";
20 PRINT@168,"VIEW DATA   (1)";:PRINT@200,"PRINT DATA  (2)";:PRINT@232,"ADD ACCOUNT (3)";:PRINT@264,"ADD DATA    (4)";:PRINT@296,"EDIT DATA   (5)";:PRINT@359,"CHOOSE A FUNCTION";
30 I$=INKEY$
31 IFI$=""THEN30
32 ONVAL(I$)GOSUB1000,2000,3000,4000,5000
34 GOTO19
1000 CLS(6):PRINT@11,"view";:PRINT@16,"data";
1005 CLOSE#2
1010 GOSUB 6000
1020 GOSUB6100
1025 IFAN=0THENCLOSE#2:RETURN
1030 CLS(6):PRINT@11,"view";:PRINT@16,"data";
1040 GOSUB6060
1050 PRINT@160,"INPUT TRANSACTION NUMBER";:INPUTVT
1055 IFVT=0THEN1190
1060 IF VT<0 OR VT>TN THEN 1050
1070 PRINT@160,"":PRINT@160,"UP & DOWN ARROWS TO VIEW  Q=QUIT";
1080 FOR X=1TO3
1090 READD$
1100 PRINT@192+(X*32),D$+"="+T$(VT,X)
1105 PRINT@240,"TRANSACTION";VT
1110 NEXTX
1120 RESTORE
1130 I$=INKEY$
1140 IF I$="Q" THEN 1190
1150 IF I$=CHR$(10) THEN VT=VT-1
1155 IF VT<1 THEN VT=TN
1160 IF I$=CHR$(94) THEN VT=VT+1
1165 IF VT>TN THEN VT=1
1170 IFI$=CHR$(10) OR I$=CHR$(94) THEN 1080
1180 GOTO 1130
1190 CLOSE#2:TN=0:VT=0
1200 GOTO 1000
2000 REM PRINT ROUTINE
2010 CLS(2):PRINT@12,"print";:PRINT@18,"data";
2020 GOSUB6000
2030 GOSUB6100
2040 IFAN=0THENCLOSE#2:RETURN
2050 CLS(2):PRINT@12,"print";:PRINT@18,"data";
2060 GOSUB6060
2070 CLOSE#2
2080 PRINT@160,"ENTER THE RANGE OF TRANSACTIONS"
2090 PRINT@224,"START AT=";:INPUTST:PRINT@240,"END WITH=";:INPUTET
2095 IFST<1 OR ET>TN THEN 2090
2100 PRINT#-2,CHR$(27)CHR$(23)
2110 PU$="$#####.##"
2120 PRINT#-2,A$(AN,1)
2130 PRINT#-2,A$(AN,2)
2135 PRINT#-2,""
2140 PRINT#-2,"ACCOUNT LIMIT    =";:PRINT#-2,USINGPU$;A(AN,1)
2150 PRINT#-2,"BEGINNING BALANCE=";:PRINT#-2,USINGPU$;A(AN,2)
2160 PRINT#-2,"MONTHLY PER. RATE=";:PRINT#-2,USING"#####.###";A(AN,3)
2170 PRINT#-2,"CURRENT BALANCE  =";:PRINT#-2,USINGPU$;A(AN,2)+CU
2180 PRINT#-2,"AVAILABLE CREDIT =";:PRINT#-2,USINGPU$;A(AN,1)-(A(AN,2)+CU)
2184 PRINT#-2,""
2185 H$="    DATE      PLACE                AMOUNT           DATE      PLACE                AMOUNT"
2186 PRINT#-2,H$
2190 FOR X=ST TO ET STEP 2
2195 IF(X+1)=82 OR (X+1)=162 OR (X+1)=242 THENPRINT#-2,CHR$(12):PRINT#-2,"":PRINT#-2,"":PRINT#-2,H$H$
2200 PRINT#-2,USING"###";X;:PRINT#-2," ";T$(X,1);"  ";T$(X,2);" ";:PRINT#-2,USINGPU$;VAL(T$(X,3));
2213 Y=X+1
2214 IF Y>TN THEN PRINT#-2,"":GOTO2216
2215 PRINT#-2,"    ";:PRINT#-2,USING"###";Y;:PRINT#-2," ";T$(Y,1);"  ";T$(Y,2);" ";:PRINT#-2,USINGPU$;VAL(T$(Y,3))
2216 IFLEFT$(T$(X,2),7)="PAYMENT"THENTP=TP+VAL(T$(X,3))
2217 IFLEFT$(T$(Y,2),7)="PAYMENT"THENTP=TP+VAL(T$(Y,3))
2218 IFLEFT$(T$(X,2),6)="CREDIT"THENTC=TC+VAL(T$(X,3))
2219 IFLEFT$(T$(Y,2),6)="CREDIT"THENTC=TC+VAL(T$(Y,3))
2220 IFLEFT$(T$(X,2),8)="INTEREST"THENTI=TI+VAL(T$(X,3))
2221 IFLEFT$(T$(Y,2),8)="INTEREST"THENTI=TI+VAL(T$(Y,3))
2222 IF VAL(T$(X,3))>0 THEN T=T+VAL(T$(X,3))
2223 IF VAL(T$(Y,3))>0 THEN T=T+VAL(T$(Y,3))
2224 NEXTX
2225 PRINT#-2,"":PRINT#-2,"TOTAL PAYMENTS  =";:PRINT#-2,USINGPU$;TP
2226 PRINT#-2,"TOTAL CREDITS   =";:PRINT#-2,USINGPU$;TC
2227 PRINT#-2,"TOTAL INTEREST  =";:PRINT#-2,USINGPU$;TI
2228 PRINT#-2,"TOTAL CHARGES   =";:PRINT#-2,USINGPU$;T-TI
2230 PRINT#-2,CHR$(12)
2240 TP=0:TC=0:TI=0:T=0:GOTO2000
3000 CLS(3):I$=""
3005 PRINT@11,"add accounts";
3010 GOSUB6000
3020 PRINT@384,"INPUT THE ACCOUNT NUMBER";:INPUTAN
3025 IFAN=0THENRETURN
3030 PRINT@384,"":PRINT@384,"ACCOUNT NAME";:INPUTA$(AN,1)
3035 GOSUB6000
3040 PRINT@384,"":PRINT@384,"ACCOUNT LIMIT";:INPUTA(AN,1)
3050 PRINT@384,"":PRINT@384,"BEGINNING BALANCE";:INPUTA(AN,2)
3060 PRINT@384,"":PRINT@384,"MONTHLY INTEREST %";:INPUTA(AN,3)
3070 PRINT@384,"":PRINT@384,"LOST CARD PHONE #";:INPUTA$(AN,2)
3080 PRINT@384,"":PRINT@384,"ADD ANOTHER ACOUNT (Y/N)";:INPUTY$
3090 IFY$="Y"THEN3000
3100 OPEN"O",#1,"ACCOUNTS/DAT"
3110 FORX=1TO10
3120 FORY=1TO2:WRITE#1,A$(X,Y):NEXTY
3130 FORZ=1TO3:WRITE#1,A(X,Z):NEXTZ
3140 NEXTX
3150 CLOSE#1:RETURN
4000 CLS(4):GOSUB6000
4010 PRINT@13,"add";:PRINT@17,"data";
4020 GOSUB6100
4030 IFAN=0THENCLOSE#2:RETURN
4090 CLS(4):PRINT@13,"add";:PRINT@17,"data";
4098 GOSUB6060
4099 IFTN>300THEN4200
4100 PRINT@96,"ENTER DATA FOR TRANSACTION";TN+1
4103 FIELD#2,8ASTD$(1),20ASTD$(2),8ASTD$(3)
4105 TN=TN+1
4110 FORX=1TO3
4120 READD$
4130 PRINT@128+(X*32),"ENTER THE ";D$;:INPUTT$(TN,X)
4132 IFT$(TN,X)="Q"THENRESTORE:GOTO4200
4134 IFT$(TN,2)="PAYMENT"ORT$(TN,2)="CREDIT"THENC=VAL(T$(TN,3)):C=C*-1:T$(TN,3)=STR$(C)
4140 LSETTD$(X)=T$(TN,X)
4150 NEXTX
4152 RESTORE
4154 PUT#2,TN
4156 GOSUB6060
4160 PRINT@320,"ENTER ANOTHER TRANSACTION (Y/N)"
4170 I$=INKEY$
4180 IFI$="Y"THEN4090
4190 IFI$="N"THEN4200
4195 IFI$<>"Y"ORI$<>"N"THEN4170
4200 CU=0:TN=O:CLOSE#2
4210 GOTO4000
4215 GOSUB6000
5000 CLS(5):PRINT@12,"edit";:PRINT@17,"data";
5010 GOSUB6000
5020 GOSUB6100
5025 IFAN=0THENCLOSE#2:RETURN
5030 CLS(5):PRINT@12,"edit";:PRINT@17,"data";
5040 GOSUB6060
5050 PRINT@160,"INPUT TRANSACTION # TO EDIT";:INPUTVT
5060 IF VT<0 OR VT>TN THEN 5050
5065 IFVT=0THEN5270
5070 PRINT@160,"PRESS 1 2 OR 3 TO EDIT OR Q=QUIT";
5080 FORX=1TO3
5090 READD$
5100 PRINT@192+(X*32),"<";X;">";D$+"="+T$(VT,X)
5110 NEXTX
5120 RESTORE
5130 I$=INKEY$
5140 IF I$="Q" THENRETURN
5150 IF VAL(I$)<1 OR VAL(I$)>3 THEN 5130 ELSE 5160
5160 FORX=1TO3:PRINT@192+(X*32),"":NEXTX
5170 FORX=1TOVAL(I$):READD$:NEXTX
5180 PRINT@224,"CURRENT "+D$+"="+T$(VT,VAL(I$))
5190 PRINT@289,"CHANGE TO=";:INPUTT$(VT,VAL(I$))
5200 IFT$(VT,VAL(I$))="Q"THEN5000
5203 IFVAL(T$(VT,3))<0ANDLEFT$(T$(VT,2),7)="PAYMENT"ORVAL(T$(VT,3))<0ANDLEFT$(T$(VT,2),6)="CREDIT"THEN5210
5205 IFLEFT$(T$(VT,2),7)="PAYMENT" OR LEFT$(T$(VT,2),6)="CREDIT" THEN C=VAL(T$(VT,3)):C=C*-1:T$(VT,3)=STR$(C):GOTO5210
5206 IFLEFT$(T$(VT,2),7)<>"PAYMENT"ORLEFT$(T$(VT,2),6)<>"CREDIT"THENC=ABS(VAL(T$(VT,3))):T$(VT,3)=STR$(C)
5210 GOSUB6060
5240 FIELD#2,8ASTD$(1),20ASTD$(2),8ASTD$(3)
5250 FORX=1TO3:LSETTD$(X)=T$(VT,X):NEXTX
5260 PUT#2,VT
5270 RESTORE:TN=0:VT=0:CLOSE#2
5280 GOTO5000
6000 REM PRINTS ACCOUNTS ON SCREEN
6010 FORX=1TO5
6020 PRINT@(2+(64*X)),X;LEFT$(A$(X,1),10);
6030 PRINT@(16+(64*X)),(X+5);LEFT$(A$(X+5,1),10);
6040 NEXTX
6050 RETURN
6060 CU=0:FORX=1TOTN:CU=CU+VAL(T$(X,3)):NEXTX
6065 IP=(A(AN,3)/100)*(A(AN,2)+CU)
6070 PRINT@384,"CREDIT LINE=";A(AN,1):PRINT"USED=";A(AN,2)+CU:PRINT"AVAILABLE=";INT(A(AN,1)-(A(AN,2)+CU))
6075 PRINT"APPROXIMATE INTEREST=";:PRINTUSING"####.##";IP;
6080 PRINT@32,TN;"TRANSACTIONS ON ";A$(AN,1)
6090 RETURN
6100 TN=0:PRINT@384,"INPUT THE ACCOUNT NUMBER";:INPUTAN
6110 IFAN<0 OR AN>10 THEN 6100
6112 IF AN=0 THEN RETURN
6115 IFA$(AN,1)=""THEN6100
6120 PRINT@384,"":PRINT@384,"LOADING DATA FOR ";A$(AN,1)
6130 IFLEN(A$(AN,1))>8THENF$=LEFT$(A$(AN,1),8)ELSEF$=A$(AN,1)
6135 FORX=1TO300:T$(X,3)="":NEXTX
6140 OPEN"D",#2,F$+"/DAT",36
6150 IFLOF(2)=0THENRETURN
6160 FIELD#2,8ASTD$(1),20ASTD$(2),8ASTD$(3)
6170 TN=TN+1
6180 GET#2,TN
6190 FORX=1TO3
6200 T$(TN,X)=TD$(X)
6210 NEXTX
6230 PRINT@480,"RECORD #";TN;
6240 IF LOF(2) <> TN THEN 6170
6250 RETURN
