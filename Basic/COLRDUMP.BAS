10 '***************************
20 '* SCREEN DUMP FOR GEMINI  *
30 '*  AND SG-10 PRINTERS IN  *
40 '*   PMODE 3 AND PMODE 1   *
50 '*           BY            *
60 '*     PIERRE SALVAIL      *
70 '*   95 CHARLOTTE, SOREL   *
80 '* QUEBEC, CANADA, J3P 1G7 *
90 '***************************
100 CLS:PRINT:INPUT" PMODE";PM:IFPM<>1 AND PM<>3 THEN RUN ELSE PMODE PM,1
110 CLS:PRINT:PRINT" gEMINI 10X"+CHR$(13)+" sG 10":GOSUB310:IFA$="G"THENG$=CHR$(27)+"K"+CHR$(176)+CHR$(1):SP=48:GOTO140
120 IF A$="S"THENG$=CHR$(27)+"g"+CHR$(5)+CHR$(224)+CHR$(1):SP=96:GOTO140
130 GOTO110
140 F(1)=2:F(3)=1
150 C1(1)=0:C2(1)=0
160 C1(2)=8:C2(2)=2
170 C1(3)=10:C2(3)=5
180 C1(4)=13:C2(4)=7
190 C1(5)=15:C2(5)=15
200 SCREEN1,0:GOSUB310
210 CLS:FORC=1TO5:READC$:PRINT@140+32*C,STR$(C);"- ";C$:NEXT
220 FORC=1TO4:READC$:PRINT@129,C$;"=":GOSUB310:R(C)=VAL(A$):NEXT
230 PRINT@392,"PRESS ANY KEY":GOSUB310:SCREEN1,0
240 PRINT#-2,CHR$(27);"A";CHR$(8)
250 FORX=0TO255STEP4
260 PRINT#-2,G$;
270 FORS=1TO SP:PRINT#-2,CHR$(0);:NEXT
280 FORY=191TO0STEP-F(PM)
290 FORP=1TO F(PM):PRINT#-2,CHR$(C1(R(PPOINT(X,Y)))*16 OR C1(R(PPOINT(X+2,Y))));CHR$(C2(R(PPOINT(X,Y)))*16 OR C2(R(PPOINT(X+2,Y))));:NEXTP,Y:PRINT#-2:NEXTX
300 CLS:END
310 A$=INKEY$:IFA$=""THEN310ELSERETURN
320 DATA WHITE,LIGHT GRAY,GRAY,DARK GRAY,BLACK
330 DATA GREEN,YELLOW,BLUE,RED
