10 REM ******PHONE NUMBERS******
20 REM *****BY DEVON COPLEY*****
30 REM STARTED  1/24/85
40 REM FINISHED 1/26/85
50 REM REVISED  4/13/85
60 CLEAR 12000
70 TN=0:DIM NA$(255),NU$(255),NO$(255)
80 REM MAIN LOOP
90 CLS
100 PRINT"**********PHONE NUMBERS*********"
110 PRINT"1)INPUT NUMBERS AND NAMES"
120 PRINT"2)LIST ALL NUMBERS"
130 PRINT"3)SEARCH FOR NAME"
140 PRINT"4)SEARCH FOR NUMBER"
150 PRINT"5)DUMP ALL NUMBERS TO PRINTER"
160 PRINT"6)CHANGE NAME,NUMBER,OR COMMENT"
170 PRINT"7)DELETE NAME AND NUMBER"
180 PRINT"8)SAVE NUMBERS FILE"
190 PRINT"9)LOAD NUMBERS FILE"
200 PRINT"10)EXIT PROGRAM"
210 PRINT"11)CLEAR MEMORY"
220 PRINT:IF A$="" THEN A$="IN MEMORY ONLY"
230 PRINT"CURRENT FILE:"A$
240 INPUT"SELECT ONE";I
250 IF I=11 THEN CLS:PRINT"you are about to clear memory.  this will erase all files in    memory.":PRINT"IF YOU WISH TO DO THIS, TYPE THEENTIRE WORD 'YES' NOW. ANYTHING ELSE WILL TAKE YOU BACK TO THE  MENU."
260 IF I=11 THEN LINEINPUT Z$:IF Z$="YES" THEN RUN
270 IF I=10 THEN CLS:PRINT"TYPE 'CONT' TO GO BACK INTO THE PROGRAM WITHOUT LOSING YOUR     DATA.":STOP
280 IF I<0 OR I>9 THEN 90
290 IF ((I>1 AND I<>9)AND TN<2) THEN CLS:PRINT"MORE THAN ONE RECORD IS REQUIREDTO USE THIS OPTION.":GOSUB 1640:GOTO 90
300 ON I GOSUB 320,500,610,730,870,1050,1220,1390,1530
310 GOTO 90
320 CLS:PRINT"     INPUT NUMBERS AND NAMES":PRINT:PRINT
330 PRINT"BE SURE TO USE THE CORRECT FOR- MAT WHEN TYPING IN PHONE NUM-   BERS. HERE IS AN EXAMPLE:               (555)-555-5555"
340 PRINT"IF NO AREA CODE IS SPECIFIED,   THE DEFAULT AREA CODE WILL BE   USED."
350 PRINT:INPUT"WHAT IS THE DEFAULT AREA        CODE";AC$:IF LEN(AC$)<>3 THEN 350
360 AC$="("+AC$+")-"
370 GOSUB 1640:CLS
380 X=TN:IF TN=0 THEN X=1
390 PRINT:PRINT:INPUT"NAME";NA$
400 INPUT"NUMBER";NU$
410 IF LEN(NU$)<8 THEN 400
420 IF LEN(NU$)=8 THEN NU$=AC$+NU$
430 IF MID$(NU$,10,1)=" " THEN MID$(NU$,10,1)="-"
440 IF MID$(NU$,6,1)=" " THEN MID$(NU$,6,1)="-"
450 INPUT"COMMENT";NO$:IF NO$="" THEN NO$="NONE":PRINT@137,NO$:ELSE PRINT
460 PRINT:INPUT"CORRECT";C$:IF LEFT$(C$,1)="N" THEN 390
470 NU$(X)=NU$:NA$(X)=NA$:NO$(X)=NO$
480 TN=TN+1
490 INPUT"MORE FILES";C$:IF LEFT$(C$,1)="N" THEN RETURN ELSE X=X+1:CLS:GOTO 390
500 CLS:PRINT"        LIST ALL NUMBERS"
510 PRINT:INPUT"SPEED DELAY(0-100)";D:IF (D<0 OR D>100) THEN 510
520 D=D*10
530 PRINT:PRINT
540 FOR X=1 TO TN
550 CLS
560 PRINT:PRINT"NAME:";NA$(X):PRINT:PRINT"NUMBER:";NU$(X):PRINT:PRINT"COMMENT:";NO$(X)
570 PRINT
580 FOR TD=1 TO D:NEXT TD
590 NEXT X
600 GOSUB 1640:RETURN
610 CLS:PRINT"        SEARCH FOR NAME"
620 PRINT:INPUT"ENTER NAME";NA$
630 FOR X=1 TO TN
640 IF NA$(X)=NA$ THEN 680
650 NEXT X
660 PRINT"SORRY, THAT NAME IS NOT IN THE  FILE.":GOSUB 1640
670 RETURN
680 PRINT"NAME:";NA$(X)
690 PRINT"NUMBER:";NU$(X)
700 PRINT"COMMENT:";NO$(X)
710 GOSUB 1640
720 RETURN
730 CLS:PRINT"       SEARCH FOR NUMBER"
740 PRINT:PRINT"ENTER NUMBER TO SEARCH FOR. BE  SURE TO INCLUDE ALL DASHES."
750 PRINT"ENTERING 'X' INSTEAD OF A DIGIT WILL ALLOW ANY DIGIT IN THAT    PLACE.":LINEINPUT NU$
760 NP=0
770 FOR X=1 TO TN
780 NN$=NU$(X)
790 FL=0:IF LEN(NU$)<>LEN(NN$) THEN NEXT X
800 FOR Y=1 TO LEN(NU$):IF MID$(NU$,Y,1)=MID$(NN$,Y,1) THEN FL=FL+1:ELSE IF MID$(NU$,Y,1)="X" THEN FL=FL+1
810 NEXT Y
820 IF FL=LEN(NU$) THEN CLS:PRINT:PRINT"NAME:"NA$(X):PRINT:PRINT"NUMBER:"NU$(X):PRINT:PRINT"COMMENT:"NO$(X):NP=NP+1
830 IF FL=LEN(NU$) THEN GOSUB 1640
840 IF X<TN THEN NEXT X
850 IF NP<>0 THEN RETURN
860 PRINT"SORRY, THAT NUMBER IS NOT IN THEFILE.":GOSUB 1640:RETURN
870 CLS:PRINT"   DUMP ALL NUMBERS TO PRINTER"
880 PRINT:PRINT"PRINT COMMENTS(Y/N)";:INPUT I$
890 IF LEFT$(I$,1)="Y" THEN CO=1 ELSE CO=0
900 PRINT:INPUT"DOUBLE SPACED(Y/N)";I$:IF LEFT$(I$,1)="Y" THEN DS=1 ELSE DS=0
910 PRINT:PRINT"PRESS 'P' TO PRINT NOW, OR ANY  OTHER KEY TO RETURN TO THE MENU."
920 X$=INKEY$:IF X$="" THEN 920
930 IF X$<>"P" THEN RETURN
940 PRINT:INPUT"HOW MANY CHARACTERS ACROSS DOES YOUR PRINTER HAVE";CA
950 PRINT"PRINTING..."
960 FOR X=1 TO TN
970 PRINT#-2,NA$(X);
980 FOR GG=1 TO CA-(LEN(NA$(X))+LEN(NU$(X))):PRINT#-2,".";:NEXT GG
990 PRINT#-2,NU$(X)
1000 IF CO=1 THEN PRINT"COMMENT:"NO$(X)
1010 IF DS=1 THEN PRINT#-2,""
1020 NEXT X
1030 PRINT"DONE!":GOSUB 1640
1040 RETURN
1050 CLS:PRINT"     CHANGE NAME OR NUMBER"
1060 PRINT:INPUT"ENTER NAME OR NUMBER TO SEARCH  FOR";NN$
1070 FOR X=1 TO TN
1080 IF (NN$=NA$(X) OR NN$=NU$(X)) THEN 1100 ELSE NEXT X
1090 PRINT"SORRY, THAT NAME/NUMBER IS NOT  IN THE FILE.":GOSUB 1640:RETURN
1100 PRINT"NAME:"NA$(X)
1110 PRINT"NUMBER:"NU$(X)
1120 PRINT"COMMENT:"NO$(X)
1130 PRINT:INPUT"DO YOU WANT:                    1) A NEW NAME FOR THE FILE      2) A NEW NUMBER FOR THE FILE    3) A NEW COMMENT FOR THE FILE   4) RETURN TO THE MAIN MENU      PICK ONE";IN:IF(IN>4 OR IN<1) THEN 1130
1140 IF IN=4 THEN RETURN
1150 ON IN GOTO 1160,1180,1200
1160 PRINT:INPUT"NEW NAME";NA$(X)
1170 GOSUB 1640:RETURN
1180 INPUT"NEW NUMBER";NU(X)
1190 GOSUB 1640:RETURN
1200 INPUT"NEW COMMENT";NO$(X)
1210 GOSUB 1640:RETURN
1220 CLS:PRINT"     DELETE NAME AND NUMBER"
1230 PRINT:PRINT"ENTER NAME";:INPUT NA$
1240 INPUT"ENTER NUMBER";NU$
1250 PRINT"SEARCHING..."
1260 FOR X=1 TO TN
1270 IF(NA$(X)=NA$ AND NU$=NU$(X)) THEN 1290 ELSE NEXT X
1280 PRINT"THERE IS NO FILE UNDER THAT NAMEAND NUMBER.":GOSUB 1640:RETURN
1290 PRINT"NAME:"NA$(X)
1300 PRINT"NUMBER:"NU$(X)
1310 PRINT"COMMENT:"NO$(X)
1320 PRINT:PRINT"STILL WANT TO DELETE(Y/N)";:INPUT IN$:IF LEFT$(IN$,1)<>"Y" THEN RETURN
1330 PRINT"DELETING..."
1340 FOR Y=(X+1) TO TN
1350 NA$(Y-1)=NA$(Y):NU$(Y-1)=NU$(Y):NO$(Y-1)=NO$(Y)
1360 NEXT Y
1370 TN=TN-1
1380 GOSUB 1640:RETURN
1390 CLS:PRINT"        SAVE NUMBERS FILE"
1400 PRINT:PRINT"THIS OPTION WILL SAVE TWO FILES.THE FILENAME YOU ENTER, AND A   BACKUP."
1410 PRINT:LINEINPUT"HIT <ENTER> TO START SAVING, OR <R> AND <ENTER> TO RETURN TO THEMENU.";IN$
1420 IF IN$="R" THEN RETURN
1430 INPUT"FILENAME";A$
1440 FOR GG=1 TO 2
1450 IF GG=1 THEN OPEN"O",#1,A$:ELSE A$=A$+".BAK":OPEN"O",#1,A$
1460 WRITE#1,TN
1470 FOR X=1 TO TN
1480 WRITE#1,NA$(X),NU$(X),NO$(X)
1490 NEXT X
1500 CLOSE
1510 NEXT GG
1520 GOSUB 1640:RETURN
1530 CLS:PRINT"       LOAD NUMBERS FILE"
1540 PRINT:LINEINPUT"HIT <ENTER> TO LOAD OR <R> AND  <ENTER> TO RETURN TO THE MAIN   MENU.";IN$
1550 IF IN$="R" THEN RETURN
1560 INPUT"FILENAME";A$
1570 OPEN"I",#1,A$
1580 INPUT#1,TN
1590 FOR X=1 TO TN
1600 INPUT#1,NA$(X),NU$(X),NO$(X)
1610 IF EOF(1)=-1 THEN 1620 ELSE NEXT X
1620 CLOSE:GOSUB 1640:RETURN
1630 END:REM END OF PROGRAM AND MAJOR SUBROUTINES
1640 PRINT@480,"    PRESS ANY KEY TO CONTINUE";
1650 X$=INKEY$:IF X$="" THEN 1650
1660 RETURN
