0 '      SCRATCH GOLFER
1 '  (C) 1986 BY LARRY DUGGINS
2 '      2706 SYMPHONY WAY 
3 '      W. CARROLLTON OH          COPYRIGHT 1989, FALSOFT,INC
4 GOSUB342
5 CLS:PD=PEEK(&HC000):IF PD=68 THEN INPUT"DOES YOUR COMPUTER ACCEPT THE   SPEED UP POKE WITH YOUR DRIVE   CONNECTED (Y/N)";AQ$ELSE INPUT"DOES YOUR COMPUTER ACCEPT THE   SPEED UP POKE (Y/N)";AQ$
6 IF AQ$="Y"THEN POKE65495,0
7 X=RND(-TIMER):CLS:INPUT"WHAT TYPE OF SHOT DO YOU WANT ASYOUR NORMAL ONE...PRESS (D) FOR DRAW   (F) FOR FADE";SS$
8 IF SS$="D"OR SS$="F"THEN12ELSE7
9 DATA0,1,2,3,4,2,1,5,3,4,5,2,3,4
10 DATA4,1,0,3,2,3,4,5,0,1
11 DATA5,536,4,468,5,596,3,152,4,440,4,416,4,424,3,212,4,376,4,420,4,292,4,388,5,532,4,368,3,240,4,424,5,640,3,164
12 WD=RND(7)+1:WS=RND(20)+1:IF WD=1 THEN WD$="N"ELSEIF WD=2 THEN WD$="NE"
13 IF WD=3 THEN WD$="E"ELSEIF WD=4 THEN WD$="SE"
14 IF WD=5 THEN WD$="S"ELSEIF WD=6 THEN WD$="SW"
15 IF WD=7 THEN WD$="W"ELSEIF WD=8 THEN WD$="NW"
16 G$="H5L1H1L2H2U1H2U1L1H3L1H1L1H1L1G1D1G1D1G1D1G1D1G1D1G2D1G2D1F1D1F1D1F1D1F2D1F3D1F3D1F3R3F2R3E2R3E5U3E3U3H3U2":F1$="G3D2G2D3F3R4F3R4F6R3E4R3F2R2E4R4E3R5F14R4F12R4F12R4F10R3F2R3F1R2E16R3E12R3E10R3E8R3E8R3"
17 GW$="E2R2F3R2F3R2F2D2G5D2G2L3G6L2H4U3H5U3E8":FW$="G14L2G6L2G4D3G5D3G8D3G5D4G2D3G1D2G1D1F2D2F2D2G2D2G1D3F2D3F3D2G1D2R2E1R3E4R2U2E2U2E2U2H2U3E6U2H2U3H2U2H3U2E5U2E7U2E4U2E4U2E3U2E4U2E6U2E2U2L3":GS$="L2G2L2G3D3G3D3F4R4F2R4E3R3U2E6U2H4U1H3G2L3
18 SR$="R3F2R3F2R3F2R1F2D2G3L2H3L2H4L2H2U4":SF$="H3U2H2L2H2U2L2G3D2G2D3F3D2F2R4F2R2E3U4":FB$="L3G2L2G2L2G5D1G2D2F6":LB$="F3R3D2F3R3E3U2H6L2H3L4H2L4G3L2G6L3G4L2G2D6F4D3F4R6E3H4"
19 FT$="H1L1H1L2H2L1H2H3U2H4U2H3L2G3L2G4D2F6R3F5R2F3R2F4R2U2R2E3U2":SM$="H3L2H1L1G2L1G1D1G1D1F2R1F1R2E2R2U1E2":SL$="H4L2H2L2D3L2D3F3D2F3D2L2D2F3R3E3U2E2R2U3H8":FL$="R2D5R4D3R5D2R6E2R7E2R5E3R5D3R7F8R5F7R6F5R3E6R3E5R3E3F3D4R5E7U4E8U3E7R5F9R3F10R4E7R5F10
20 G4$="F9R2F4R2F3R2F1R4F1R3E1R2E1R3E2R2E2U1H2U2H1U1H3U1L2H1L1H2L2H2L4H1L2H1L2H4L1H2L1G2L2G4D2G2D2":GR$="U2H2U1H2U1L3H1L3G2L2G2D3F4D2F3R4E3R2E2U1"
21 CLS:PRINT@200,"***ONE MOMENT***":DIM LE(18),ST(18),TS(18),PS(18),NP(18),WD(30),PA(18),S1(18),SC(18),TR(18):HO=1:CP=72:CT=1:RP=109:PV=1134:SZ$=STRING$(32,131):BS$=STRING$(64,32):BL$="  ":AT$=STRING$(32,42):GF=1
22 FOR X=0 TO 23:WD(X)=X:NEXTX:FOR X=1 TO 24:READ A:NEXT X:FOR X=1 TO 18:READ PA(X),LE(X):NEXT X:RESTORE
23 FX=0:FY=0:FT=0:GOSUB241
24 PMODE3,1:PCLS1:RF=.99:ON HO GO SUB26,33,42,54,63,69,73,84,79,88,92,95,100,104,107,110,114,119
25 GOTO121
26 LINE(13,157)-(23,170),PSET,BF
27 DRAW"BM70,109;XF1$;BM70,109;XFL$;"
28 CIRCLE(230,99),14,4:HX=226:HY=103:CIRCLE(HX,HY),2,3
29 CIRCLE(104,138),5,2:CIRCLE(122,143),5,2:CIRCLE(120,136),5,2:CIRCLE(115,139),5,2:CIRCLE(110,132),5,2
30 PAINT(210,138),3,4
31 COLOR1,1:DRAW"BM10,180;XD1$;BM70,109;XF1$;BM70,109;XFL$;"
32 GOTO121
33 HX=192:HY=125:LINE(26,41)-(42,49),PSET,BF
34 CIRCLE(196,133),16,4,1.45:CIRCLE(HX,HY),2,3
35 H$="L2U1L3U1L4U1H3U1L2H3U1L2U1L5U1H2L4U1H2L7H1L4H2L4H1L5G2L4G2L4H3L4H2L3G2L3"
36 R$="R2F2D3R5F5R4F4R4F2D2F2D2E1U2R4F5R3E4R3E2R3E6R3E4"
37 DRAW"BM98,91;XR$;BM152,60;A2;XFT$;BM206,114;A3;XGW$;BM136,71;XSM$;BM190,113;A0XH$;BM128,75;XSM$;":PAINT(170,72),2,4:PAINT(218,110),3,4:PAINT(116,95),2,4
38 PAINT(120,74),2,4:CIRCLE(170,108),24,4,1.45,.17,.49:PAINT(172,130),3,4
39 CIRCLE(170,108),24,1,1.45,.17,.49:PAINT(137,76),2,4
40 COLOR1,4:DRAW"BM152,60;A2;XFT$;BM206,114;A3;XGW$;BM98,91;A0;XR$;BM190,113;XH$;BM136,71;A3;XSM$;BM128,75;A0XSM$;"
41 GOTO121
42 HX=72:HY=18:CIRCLE(HX,HY),2,3
43 LINE(232,139)-(242,149),PSET,BF
44 W$="F3D2F6D3F3D3F4D4G6L5G4L6U3G2L4U4E4U3R2E2U3R4H4U2G1U4E2H4U3H3"
45 DRAW"BM118,0;A0;XW$;BM160,100;XSR$;BM166,95;XSL$;":PAINT(163,103),2,4:PAINT(166,98),2,4:PAINT(170,106),2,4:PAINT(160,107),2,4
46 PAINT(118,4),3,4
47 DRAW"BM76,33;A1;XG$;"
48 DRAW"BM176,82;A2XSF$;"
49 DRAW"BM138,90;A1XFW$;"
50 PAINT(84,55),2,4:PAINT(180,86),2,4
51 CIRCLE(59,29),12,,1.22,.10,.7:PAINT(54,27),3,4:CIRCLE(59,29),12,1,1.22,.10,.7
52 COLOR1,1:DRAW"BM118,0;A0XW$;BM138,90;A1XFW$;BM160,100;A0XSR$;BM166,95;XSL$;BM176,82;A2XSF$;"
53 GOTO121
54 HX=110:HY=76:CIRCLE(HX,HY),2,3:LINE(52,100)-(59,108),PSET,BF
55 WL$="F30D4F10D4F10D3F7D2F9D5F3D3F7R5F3R5F4R3E2U7"
56 WR$="F10D2F8R3F4D2F8R2F10R3F4R3F1R4F2R5F4R3F2R4F1R5F1R3F1R3F1R4"
57 WG$="D1F4R2F6R1F5D1F3R1F7R2F2D2F3"
58 WB$="R5F2R5F1R6F2R4F2R6F3R5F1R3F1R4E2U3H5"
59 DRAW"BM20,0;A0XWL$;BM30,0;XWR$;BM82,60;XWG$;BM82,60;XWB$;":PAINT(25,0),3,4
60 DRAW"BM120,78;XGR$;"
61 COLOR1,1:DRAW"BM20,0;XWL$;BM30,0;XWR$;BM82,60;XWG$;BM82,60;XWB$;"
62 GOTO121
63 HX=85:HY=46:LINE(138,149)-(148,157),PSET,BF:CIRCLE(80,40),16,4:CIRCLE(HX,HY),2,3
64 DRAW"BM111,86;S6;XSL$;":LS$="H3L2H2G3D2L2F3R3F2R3E2U2":DRAW"BM93,86;XLS$;":PAINT(112,95),2,4:PAINT(93,89),2,4
65 DRAW"BM124,75;S8;XSR$;":PAINT(142,86),2,4
66 GT$="G3D2G2D2F3R2F1R3F2D2F2R3E2R2E3U2E2U3H2":DRAW"BM66,50;S4;XGT$;":TR$="E3R2E2R2F3R2F2D3R2D2G3L2G2L2H1L3HL3H2":DRAW"BM96,43;XTR$;":PAINT(66,55),2,4:PAINT(96,47),2,4
67 COLOR1,1:DRAW"BM111,86;S6XSL$;BM93,86;XLS$;BM124,75;S8XSR$;BM66,50;S4XGT$;BM96,43;XTR$;"
68 GOTO121
69 HX=62:HY=122:LINE(205,40)-(208,46),PSET,BF:CIRCLE(60,124),12,4:CIRCLE(HX,HY),2,3
70 DRAW"BM118,82;A1S6XSF$;BM134,108;A0XSF$;BM63,106;A2XSF$;":PAINT(118,80),2,4:PAINT(130,110),2,4:PAINT(68,107),3,4
71 COLOR1,1:DRAW"BM118,82;A1;XSF$;BM134,108;A0;XSF$;BM63,106;A2;XSF$;"
72 GOTO121
73 HX=166:HY=40:LINE(50,130)-(58,138),PSET,BF:CIRCLE(HX,HY),2,3:CIRCLE(168,42),13,4
74 DRAW"BM104,89;A1;S4;XFT$;BM124,104;A0;S6;XSM$;BM126,108;A2;XSM$;BM126,88;A3;XSM$;BM151,79;A1;XSR$;BM155,45;A0;XSM$;BM154,39;A1;XSM$;BM184,40;XSM$;BM186,55;A0;XSM$;":PAINT(101,86),2,4
75 PAINT(117,105),2,4:PAINT(129,108),2,4:PAINT(124,93),2,4
76 PAINT(148,84),2,4:PAINT(147,45),2,4:PAINT(150,32),2,4:PAINT(183,37),2,4:PAINT(185,54),2,4
77 COLOR1,1:DRAW"BM104,89;A1;S4;XFT$;BM124,104;A0;S6;XSM$;BM126,108;A2;XSM$;BM126,88;A3;XSM$;BM151,79;A1;XSR$;BM154,39;A1;XSM$;BM184,40;XSM$;BM155,45;A0;XSM$;BM186,55;A0;XSM$;"
78 GOTO121
79 HX=60:HY=122:CIRCLE(HX,HY),2,3:LINE(170,40)-(179,50),PSET,BF:CIRCLE(58,121),11,4
80 DRAW"BM124,122;A1S4XSL$;":PAINT(120,120),2,4
81 DRAW"BM118,35;A0XFW$;BM54,101;A2XSF$;BM140,91;A1XSR$;BM119,99;A3XSM$;BM108,112;A2XSM$;":PAINT(111,111),2,4:PAINT(121,106),2,4:PAINT(116,40),3,4:PAINT(63,107),2,4:PAINT(138,94),2,4:COLOR1,1:DRAW"BM118,35;A0XFW$;BM54,101;A2XSF$;BM140,91;A1XS$;
82 DRAW"BM119,99;A3;XSM$;BM108,112;A2;XSM$;BM124,122;A1;XSL$;BM140,91;A1XSR$;
83 GOTO121
84 HX=68:HY=65:LINE(172,56)-(178,64),PSET,BF:CIRCLE(68,62),10,4:CIRCLE(HX,HY),2,3
85 CIRCLE(75,75),4,2:CIRCLE(70,78),4,2:CIRCLE(63,74),4,2:CIRCLE(54,70),4,2
86 DRAW"BM68,49;A2S4XFT$;":PAINT(82,53),2,4:COLOR1,1:DRAW"BM68,49;XFT$;"
87 GOTO121
88 HX=107:HY=50:CIRCLE(HX,HY),12,4:LINE(139,148)-(151,158),PSET,BF:CIRCLE(HX,HY),2,3
89 DRAW"BM108,37;A0XFB$;BM110,37;XLB$;BM122,81;S6;XSL$;BM146,91;A2;XSM$;":PAINT(96,40),2,4:PAINT(118,87),2,4:PAINT(158,92),3,4
90 COLOR1,1:DRAW"BM108,37;A0;S4;XFB$;BM110,37;XLB$;BM122,81;S6;XSL$;BM146,91;A2;XSM$;"
91 GOTO121
92 HX=146:HY=40:DRAW"BM157,42;A0S4XGR$;":LINE(14,70)-(22,82),PSET,BF:CIRCLE(HX,HY),2,3
93 DRAW"BM90,124;A2XFW$;BM166,38;XSR$;":PAINT(164,35),2,4:PAINT(110,104),3,4:COLOR1,1:DRAW"BM90,124;A2;XFW$;BM166,38;XSR$;"
94 GOTO121
95 HX=48:HY=65:CIRCLE(HX,HY),2,3:LINE(176,130)-(184,140),PSET,BF:CIRCLE(50,62),11,4
96 WO$="S6L2H3L6H3U4E3R2E3R6F6D4G6":DRAW"BM106,99;A0XWO$;":PAINT(100,91),3,4:DRAW"BM106,99;C1XWO$;"
97 DRAW"BM38,63;C4A3S6XSM$;":CIRCLE(96,101),4,2:PAINT(42,70),2,4
98 COLOR1,1:DRAW"BM38,63;A3;XSM$;BM70,63;S4A0XWF$;"
99 GOTO121
100 HX=198:HY=178:LINE(120,45)-(132,53),PSET,BF:CIRCLE(HX,HY),2,3
101 DRAW"BM198,169;S4;XGS$;BM106,123;A3;XFW$;BM180,101;A3;XFT$;BM177,128;A2XSM$;":PAINT(122,138),3,4:PAINT(182,104),2,4:PAINT(181,130),2,4
102 COLOR1,1:DRAW"BM106,123;A3;XFW$;BM180,101A3XFT$;BM177,128;A2XSM$;"
103 GOTO121
104 HX=105:HY=139:CIRCLE(HX,HY),2,3:LINE(140,42)-(150,50),PSET,BF:CIRCLE(HX,HY),9,4,1.3
105 DRAW"BM126,129;S6A0XSL$;BM83,120;A3;S8;XSR$;BM132,121;S4;A2;XGS$;":PAINT(134,118),2,4:PAINT(124,140),3,4:PAINT(90,112),3,4:COLOR1,1:DRAW"BM126,129;S6A0XSL$;BM83,120;A3;S8;XSR$;BM132,121;S4;A2;XGS$;"
106 GOTO121
107 HX=124:HY=128:CIRCLE(HX,HY),2,3:CIRCLE(128,126),14,4,.66:LINE(124,64)-(132,72),PSET,BF
108 CIRCLE(110,126),5,2:CIRCLE(110,120),5,2:CIRCLE(105,123),5,2:CIRCLE(112,115),5,2:CIRCLE(113,131),5,2:CIRCLE(118,136),5,2:CIRCLE(107,128),5,2:CIRCLE(126,138),5,2
109 GOTO121
110 HX=108:HY=153:CIRCLE(HX,HY),3,3:LINE(96,40)-(112,48),PSET,BF:CIRCLE(110,152),11,4,1.33
111 C1$="H6U3H6U3H5L2U4E12U3E7U3E12R3U3R4H8U4E15R2"
112 DRAW"BM90,180;S3;A3;XF1$;BM90,180;S4;A0;XC1$;":PAINT(95,175),3,4:COLOR1,1:DRAW"BM90,180;S3;A3;XF1$;BM90,180;S4;A0;XC1$;"
113 GOTO121
114 HX=108:HY=34:CIRCLE(HX,HY),2,3:CIRCLE(110,30),13,4:LINE(240,175)-(252,183),PSET,BF
115 CIRCLE(192,114),5,2:CIRCLE(210,124),5,2:CIRCLE(160,120),5,2:CIRCLE(159,130),5,2:CIRCLE(150,123),5,2:CIRCLE(207,113),5,2
116 DRAW"BM140,64;S8;A2;XSM$;BM116,56;A3;XSM$;BM130,28;S4;XGR$;BM88,41;A2;XGR$;":PAINT(120,68),2,4:PAINT(156,58),2,4:PAINT(100,46),2,4:PAINT(132,34),2,4
117 COLOR1,1:DRAW"BM140,64;S8;A2;XSM$;BM116,56;A3;XSM$;BM130,28;S4;XGR$;BM88,41;A2;XGR$;BM190,124C3XSM$;":PAINT(196,122),3,3
118 GOTO121
119 HX=116:HY=94:CIRCLE(HX,HY),10,4:CIRCLE(HX,HY),2,3:LINE(150,128)-(158,136),PSET,BF
120 WC$="G4D2G2D2F2D3F2D2F3R3F2R3E3R3E3R2E2U3E2U3H6U2L3U2L4H2L2G4":DRAW"BM106,79;S6A0XWC$;":PAINT(108,86),3,4:COLOR1,1:DRAW"BM106,79;S6A0XWC$;"
121 CLS:OB=0:POKE1024,191:PRINT@1,"COURSE YARDAGE = 7,088 YDS.":POKE1056,191:PRINT@33,"COURSE PAR = 72"
122 POKE1088,239:PRINT@65,"HOLE";HO;:POKE1097,239:PRINT@74,"PAR";PA(HO):POKE1105,239:PRINT@82,"LENGTH";LE(HO);"YDS"
123 POKE1120,175:PRINT@97,"WIND- SPEED";WS;"** DIRECTION ";WD$:POKE359,126:PRINT@128,SZ$;
124 PRINT@288,SZ$:PRINT@326,"AVERAGE DISTANCES";:PRINT@353,"WOODS: DR=265  3W=241";:PRINT@385,"IRONS:  1=223   2=211   3=202";:PRINT@425,"4=193   5=184   6=175";:PRINT@457,"7=166   8=157   9=148";:PRINT@482,"      10=133";
125 TG=0:IF RF<>.99 THENPRINT@179,CHR$(175);"YOU HAVE A":IF RF=1 THENPRINT@212,"GOOD LIE":GOTO129
126 IF RF=.99THEN129ELSEIF RF>.7 AND RF<1THENPRINT@212,"FAIR LIE":GOTO129
127 IF RF>.5 AND RF<.8THENPRINT@212,"BAD LIE";:GOTO129
128 PRINT@179,CHR$(191);"BAD LIE   ":PRINT@212,"BE CAREFUL":TG=1
129 IF RF<>.99 THENPRINT@243,CHR$(239);"DISTANCE TO":PRINT@276,"HOLE";DH*4;"YDS";
130 A$=INKEY$:IF A$=""THENPRINT@161,"WHICH CLUB**** ";:FOR T=1 TO 20:NEXTT:POKE1198,127:FOR T=1 TO 80:NEXTT:GOTO130
131 IFA$=CHR$(83)THENSCREEN1,0:PSET(SX,SY,3):FOR T=1 TO 50:NEXTT:PSET(SX,SY,2):FOR T=1 TO 50:NEXTT:B$=INKEY$:IF B$=""THEN131ELSESCREEN0,0:GOTO130
132 SOUND30,1:IF A$=CHR$(13)THEN134ELSENN=NN+1:C$(NN)=A$:GOTO130
133 FOR T=1 TO 400:NEXTT:POKE1232,32:GOTO130
134 FOR N=1 TO NN:C$=C$(1)+C$(2):NEXT N:PLAY"T230;O5AO4BO5F":PRINT@176,C$;:IF FL=1 THEN IF SS$="D"THEN SS$="F"ELSEIF SS$="F"THEN SS$="D"
135 IFTG<>1THEN138ELSEIF C$="DR"ORC$="3W"THENRF=.2-RF:GOTO138
136 C=VAL(C$):IF C<5THENRF=.3-RF:GOTO138
137 IF C<10THENRF=.4-RF ELSERF=RND(4)+3:RF=RF/10
138 A$=INKEY$:IF A$=""THENPRINT@193,"DIRECTION***** ";:FOR T=1 TO 20:NEXTT:POKE1230,127:FOR T=1 TO 80:NEXTT:GOTO138
139 IF A$=CHR$(83)THENSCREEN1,0:PSET(SX,SY,3):FOR T=1 TO 100:NEXTT:PSET(SX,SY,2):B$=INKEY$:IF B$=""THEN139ELSESCREEN0,0:GOTO138
140 SOUND30,1:IF A$=CHR$(13)THEN141ELSE PR=PR+1:DR$(PR)=A$:GOTO138
141 FOR N=1 TO PR:DR$=DR$(1)+DR$(2):NEXT N:PLAY"T230;O5AO4BO5F":PRINT@208,DR$;
142 A$=INKEY$:IF A$=""THENPRINT@225,"LENGTH OF ARC* ";:FOR T=1 TO 20:NEXTT:POKE1262,127:FORT=1 TO 80:NEXTT:GOTO142
143 IF A$=CHR$(83)THENSCREEN1,0:PSET(SX,SY,3):FOR T=1 TO 50:NEXT:PSET(SX,SY,2):FOR T=1 TO 50:NEXT:B$=INKEY$:IF B$=""THEN143ELSESCREEN0,0:GOTO142
144 IF A$>"7"OR A$<"2"THENSOUND1,5:A$="":GOTO142
145 LA=VAL(A$):PLAY"T230;O5AO4BO5F":PRINT@239,LA;:IF LA<5THENTS$="":PRINT@257,"TYPE OF SHOT** ";:PRINT@272,"*";:GOTO150
146 A$=INKEY$:IF A$=""THENPRINT@257,"TYPE OF SHOT** ";:FOR T=1 TO 20:NEXTT:POKE1294,127:FORT=1 TO 80:NEXTT:GOTO146
147 IF A$=CHR$(83)THENSCREEN1,0:B$=INKEY$:IF B$=""THEN147ELSESCREEN0,1:GOTO146
148 IF A$="F"OR A$="D"THEN149 ELSE146
149 TS$=A$:PLAY"T230;O5AO4BO5F":PRINT@272,TS$;
150 PRINT@320,BS$
151 PRINT@416,"*(PRESS <ENTER> TO PLAY HOLE)":PRINT@448,"*(PRESS <SPACE BAR> TO CHANGE     ANY VALUES    ";
152 A$=INKEY$:IF A$=""GOTO152
153 IF A$=CHR$(13)THEN156
154 IF A$=CHR$(32)THENPRINT@176,BL$;:PRINT@208,BL$;:PRINT@239,BL$;:PRINT@271,BL$;:PR=0:NN=0:C$(1)="":C$(2)="":DR$(1)="":DR$(2)="":GOTO130
155 PRINT@271,BL$;:GOTO146
156 DR=VAL(DR$):IF INT(WD/2)=WD/2THEN163
157 IF WD=1 OR WD=5 THEN158ELSE160
158 IF DR=0 OR DR=1 OR DR=23 THENIF WD=1 THENDW=-2*WS ELSEDW=2*WS
159 IF DR>10 AND DR<14 THENIFWD=5 THENDW=-2*WS ELSEDW=2*WS
160 IF WD=3 OR WD=7 THEN161ELSE163
161 IF DR>4 AND DR<8 THENIFWD=3 THENDW=-2*WS ELSEDW=2*WS
162 IF DR>16 AND DR<20 THENIFWD=7 THENDW=-2*WS ELSEDW=2*WS
163 IF WD=2 OR WD=6 THEN164 ELSE166
164 IF DR>1 AND DR<5 THENIFWD=2 THENDW=-2*WS ELSEDW=2*WS
165 IF DR>13 AND DR<17 THENIFWD=6 THENDW=-2*WS ELSEDW=2*WS
166 IF WD=4 OR WD=8 THEN167 ELSE
167 IF DR>7 AND DR<11 THENIFWD=4 THENDW=-2*WS ELSEDW=2*WS
168 IF DR>19 AND DR<23 THENIFWD=8 THENDW=-2*WS ELSEDW=2*WS
169 IN(1)=5:IN(2)=4:IN(3)=5:IN(4)=5:IN(5)=4:IN(6)=5
170 IF WD=1THENX1(1)=2:X1(2)=5:X1(3)=14:X1(4)=17
171 IF WD=2THENX1(1)=5:X1(2)=8:X1(3)=17:X1(4)=20
172 IF WD=3THENX1(1)=8:X1(2)=11:X1(3)=20:X1(4)=23
173 IF WD=4THENX1(1)=11:X1(2)=14:X1(3)=23:X1(4)=2
174 IF WD=5THENX1(1)=14:X1(2)=17:X1(3)=2:X1(4)=5
175 IF WD=6THENX1(1)=17:X1(2)=20:X1(3)=5:X1(4)=8
176 IF WD=7THENX1(1)=20:X1(2)=23:X1(3)=8:X1(4)=11
177 IF WD=8THENX1(1)=23:X1(2)=2:X1(3)=11:X1(4)=14
178 IF C$="DR"THENCC=460:IF ST>0 THENGF=(RND(5)+5)/10
179 IF C$="3W"THENCC=410
180 IF C$="1"THENCC=380
181 IF C$="2"THENCC=360
182 IF C$="3"THENCC=345
183 IF C$="4"THENCC=330
184 IF C$="5"THENCC=315
185 IF C$="6"THENCC=300
186 IF C$="7"THENCC=285
187 IF C$="8"THENCC=270
188 IF C$="9"THENCC=255
189 IF C$="10"THENCC=230
190 IF LA=7THENR=RND(100):CO=-R
191 IF LA=6THENR=RND(40):CO=R-25
192 IF LA=5THENR=RND(40):CO=R
193 IF LA<5THENR=20:CO=-(R)
194 IF LA>4 THENLF=1 ELSELF=.1
195 IF LA<5 THENNT=0:GOTO229
196 R1=RND(13)+1:IF TS$=SS$ THENR2=RND(9)+15:R3=RND(3):IF R3<>3THEN199
197 R4=RND(10):IFR4<4THENTS$=SS$:GOTO199
198 FOR N=1 TO R1:READNT:NEXTN:GOTO200
199 FOR N=1 TO R2:READNT:NEXTN
200 RESTORE
201 FOR X=X1(CT)TOX1(CT)+IN(CT):IF WD(X)=DR THEN203ELSENEXTX
202 CT=CT+1:IF CT>6THEN CT=0ELSE201
203 IF CT=2 OR CT=5 THEN AF=INT(WS/4.1)ELSE AF=INT(WS/12+.3)
204 IF CT>3THEN IF TS$="F"THEN AF=-AF
205 IF CT=0 THEN206ELSEIF CT>0 AND CT<4 THEN IF TS$="D"THEN AF=-AF
206 CT=1:NT=INT(NT*LA/10*1.92+AF)
207 IF NT<0THEN FL=1:IF TS$="D"THEN TS$="F"ELSE IF TS$="F"THEN TS$="D"
208 NT=ABS(NT):AF=0:C=VAL(C$)
209 IF C>6 AND C<11 THENIF NT>1 THEN NT=1
210 IF C>3 AND C<7 THENIFNT>2 THENNT=2
211 IF C>0 AND C<4 THENIF NT>3 THEN NT=3
212 IF TS$="F"THEN 221
213 IF INT(DR/2)<>DR/2THEN217
214 IFDR=0 OR DR=2 OR DR=12 OR DR=16THENXC=3:YC=6:GOTO229
215 IFDR=4 OR DR=10 OR DR=16 OR DR=22THENXC=6:YC=6:GOTO229
216 IFDR=6 OR DR=8 OR DR=18 OR DR=20THENXC=8:YC=2:GOTO229
217 IFDR=1 OR DR=13THENXC=0:YC=9:GOTO229
218 IFDR=3 OR DR=11 OR DR=17 OR DR=23THENXC=4:YC=7:GOTO229
219 IFDR=5 OR DR=9 OR DR=15 OR DR=21THENXC=7:YC=4:GOTO229
220 IFDR=7 OR DR=19THENXC=8:YC=0:GOTO229
221 IF INT(DR/2)<>DR/2THEN225
222 IFDR=0 OR DR=10 OR DR=12 OR DR=22THENXC=2:YC=8:GOTO229
223 IFDR=2 OR DR=8 OR DR=14 OR DR=20THENXC=6:YC=6:GOTO229
224 IFDR=4 OR DR=6 OR DR=16 OR DR=18THENXC=8:YC=2:GOTO229
225 IFDR=1 OR DR=9 OR DR=15 OR DR=21THENXC=4:YC=7:GOTO229
226 IFDR=3 OR DR=7 OR DR=13 OR DR=19THENXC=7:YC=4:GOTO229
227 IFDR=5 OR DR=17THENXC=8:YC=0:GOTO229
228 IFDR=11 OR DR=23THENXC=0:YC=9
229 IF INT(DR/2)=DR/2 THEN233
230 IF DR=5 OR DR=7 OR DR=17 OR DR=19THENXI=10:YI=2.34:GOTO237
231 IF DR=3 OR DR=9 OR DR=15 OR DR=21THENXI=4.34:YI=4.345:GOTO237
232 IF DR=1 OR DR=11 OR DR=13 OR DR=23THENXI=1.34:YI=5.34:GOTO237
233 IF DR=0 OR DR=12THENXI=0:YI=5.34:GOTO237
234 IF DR=6 OR DR=18 THENXI=11:YI=0:GOTO237
235 IF DR=2 OR DR=10 OR DR=14 OR DR=22THENXI=3:YI=5.26:GOTO237
236 IF DR=4 OR DR=8 OR DR=16 OR DR=20 THENXI=7:YI=3.34
237 PRINT@176,BL$;:PRINT@208,BL$;:PRINT@239,BL$;:PRINT@271,BL$;:PRINT@416,BS$;:PMODE3,1:SCREEN1,0:DI=INT(CC*LA/10*RF*GF+(DW*LF))+CO
238 RF=1:GF=1
239 AC=INT(AC*LA/10*1.3+.5):PP=DI/22:FP=PP-INT(PP):PP=INT(PP+.5)
240 IF FT=1THENGOTO260
241 IF HO=1THENSX=18:SY=161:BX=SX:BY=SY
242 IF HO=2THENSX=38:SY=45:BX=SX:BY=SY
243 IFHO=3THENSX=236:SY=142:BX=SX:BY=SY
244 IFHO=4THEN SX=57:SY=103:BX=SX:BY=SY
245 IFHO=5THEN SX=145:SY=153:BX=SX:BY=SY
246 IFHO=6THEN SX=204:SY=43:BX=SX:BY=SY
247 IFHO=7THEN SX=56:SY=132:BX=SX:BY=SY
248 IF HO=8THEN SX=174:SY=60:BX=SX:BY=SY
249 IFHO=9THEN SX=174:SY=46:BX=SX:BY=SY
250 IFHO=10THEN SX=145:SY=150:BX=SX:BY=SY
251 IFHO=11THEN SX=20:SY=78:BX=SX:BY=SY
252 IFHO=12THEN SX=178:SY=132:BX=SX:BY=SY
253 IFHO=13THEN SX=126:SY=50:BX=SX:BY=SY
254 IFHO=14THEN SX=145:SY=47:BX=SX:BY=SY
255 IFHO=15THEN SX=128:SY=68:BX=SX:BY=SY
256 IFHO=16THENSX=104:SY=45:BX=SX:BY=SY
257 IFHO=17THENSX=246:SY=179:BX=SX:BY=SY
258 IFHO=18THENSX=155:SY=131:BX=SX:BY=SY
259 IF FT=0THENFT=1:RETURN
260 IF DR>-1 AND DR<7 THEN QX=1:QY=-1:GOTO264
261 IF DR>6 AND DR<13 THEN QX=1:QY=1:GOTO264
262 IF DR>12 AND DR<19 THEN QX=-1:QY=1:GOTO264
263 IF DR>18 AND DR<24 THEN QX=-1:QY=-1
264 RX=SX:RY=SY:IY=YI-INT(YI):IX=XI-INT(XI)
265 FOR T=1 TO 300:NEXT T:CX=PP-NT:CX=PP-NT:TS(ST)=DI:PLAY"T100V31O4AB":PSET(SX,SY,1):PSET(BX,BY,4)
266 FOR N=1 TO PP:CX=CX-1:IF CX<0THEN SX=SX+XC*QX:SY=SY+YC*QY
267 IF CX>=0 THEN SX=SX+(INT(XI+IX+TX)*QX):TX=TX+IX:IF TX>1THENTX=0
268 IF CX>=0 THENSY=SY+(INT(YI+IY+TY)*QY):TY=IY+TY:IF TY>.66THENTY=0
269 IF SX<0 OR SX>255 OR SY<0 OR SY>191 THENSOUND1,5:CLS:PRINT@197,"****OUT OF BOUNDS****":FOR T=1 TO 900:NEXTT:OB=1:PS=PS+1:SX=RX:SY=RY:C$(1)="":C$(2)="":DR$(1)="":DR$(2)="":NN=0:PR=0:IF ST=0THENSX=BX:SY=BY:ST=ST+1:GOTO284:ELSE284
270 IF PPOINT(SX,SY)=2THENPSET(SX,SY):FOR T=1 TO80:NEXT T:PSET(SX,SY,2):NEXT N:TR(HO)=TR(HO)+1:RF=RND(9):RF=RF/10:GOTO293
271 IFPPOINT(SX,SY)=3THENPSET(SX,SY,2):FORT=1 TO80:NEXTT:PSET(SX,SY,3):NEXTN:IFABS(HX-SX)<5 AND ABS(HY-SY)<5 THEN273ELSEPLAY"T200O1ABO4CDO1EF":PS=PS+1:TW=TW+1:WH=WH+1:CIRCLE(SX,SY),3,2:FORT=1 TO 300:NEXTT:CIRCLE(SX,SY),3,3:GOTO290
272 PSET(SX,SY):FOR T=1 TO 60:NEXT T:PRESET(SX,SY):NEXT N
273 PSET(SX,SY,2)
274 RF=RND(100):IF RF>92 THENRF=.8 ELSE IF RF<4 THENRF=.6 ELSERF=1
275 IF SX<HX THENSP=1ELSESP=-1
276 IF SY<HY THENSE=1ELSESE=-1
277 IF SX>HX+18 THENCX=HX+20:GOTO280
278 IF SX<HX-18 THENCX=HX-20:GOTO280
279 CX=SX
280 IF SY>HY+18 THENCY=HY+20:GOTO283
281 IF SY<HY-18 THENCY=HY-20:GOTO283
282 CY=SY
283 FOR NX=CX TO HX STEPSP:FOR NY=CY TO HY STEP SE:IF PPOINT(NX,NY)=4 THEN FG=1 ELSE NEXT NY,NX:FG=0
284 DD=0:LX=ABS(SX-HX):LY=ABS(SY-HY):DH=INT(SQR(LX^2+LY^2)):DX=ABS(BX-SX)/2:DY=ABS(BY-SY):DD=INT(SQR(DX^2+DY^2)):DD=INT(DD*4+.5):TS(ST)=DD
285 LX=ABS(SX-HX):LY=ABS(SY-HY):LX=LX/2:DH=INT(SQR(LX^2+LY^2)):IF OB=1 THEN121
286 CIRCLE(SX,SY),1,4:ST=ST+1
287 A$=INKEY$:CIRCLE(SX,SY),1,2:FOR T=1 TO 50:NEXT T:CIRCLE(SX,SY),1,3:IF A$<>CHR$(83)THEN287ELSEPR=0:DD=0:NN=0:C$(1)="":C$(2)="":DR$(1)="":DR$(2)="":PRINT@180,"          ";:PRINT@212,"         ";:PRINT@244,"           ";:PRINT@276,"            ";
288 SCREEN0,0:IF FG=0 THENCLS:LP=DH:GOTO295
289 GOTO124
290 IF ST=0 THENS1(HO)=1
291 IF CX>=0 THENSX=SX-XI*QX:SY=SY-YI*QY:IF PPOINT(SX,SY)<>3 AND (ABS(HX-SX)>11 OR ABS(HY-SY)>11) THEN294 ELSE291
292 SX=SX-XC*QX:SY=SY-YC/2*QY:IF PPOINT(SX,SY)<>3 AND PPOINT(SX,SY)<>2 AND (ABS(HX-SX)>11 OR ABS(HY-SY)>11) THEN294ELSE CX=CX+1:GOTO291
293 IF ST=0THENS1(HO)=1
294 GOTO275
295 DW=0:PS(HO)=PS:PG=RND(100):ST(HO)=ST
296 IF PG>22 AND PG<28 THEN NP=1:GOTO305
297 IF DH>10 AND PG>50 AND PG<54 THENNP=4
298 IF DH=0 THENPLAY"T8O5GBDO3FACO5CEG":SCREEN0,1:CLS:PRINT@102,"YOU HOLED OUT!":FOR T=1 TO 600:NEXTT:GOTO305
299 IF DH>0 AND DH<3 THEN IF PG>9 THENNP=1 ELSENP=2:GOTO305
300 IF DH>2 AND DH<5 THEN IF PG>49 THENNP=1 ELSENP=2:GOTO305
301 IF DH>4 AND DH<7 THEN IF PG>69 THENNP=1 ELSENP=2:GOTO305
302 IF DH>6 AND DH<9 THEN IF PG>29 THENNP=2 ELSENP=1:GOTO305
303 IF DH>8 AND DH<11 THEN IF PG>49 THENNP=2 ELSENP=3:GOTO305
304 IF DH >10 THEN IF PG>69 THENNP=2 ELSENP=3:GOTO305
305 NP(HO)=NP:SC(HO)=ST+NP+PS:PT=PT+NP
306 SCREEN0,0:CLS:PRINT@7,"STATS FOR HOLE ";HO;:PRINT@33,"PAR ";PA(HO);"     LENGTH OF HOLE ";LE(HO);:PRINT@64,SZ$;
307 PRINT@97,"PENALTY STROKES************";PS:XP=XP+PS:PRINT@129,"SCORE FOR THE HOLE*********";SC(HO);:PRINT@160,SZ$;
308 IF HO=4 OR HO=8 OR HO=15 OR HO=18 THENPRINT@225,"DRIVE LAND IN FAIRWAY?***** --";:GOTO310 
309 TL=TL+TS(0):PRINT@225,"DRIVE LAND IN FAIRWAY?***** ";:IF S1(HO)=1 THENPRINT"NO"ELSEPRINT"YES";:GD=GD+1
310 PRINT@193,"LENGTH OF TEE SHOT*********";TS(0);
311 PRINT@257,"# OF TIMES IN ROUGH/TRAP***";TR(HO);:BR=BR+TR(HO):PRINT@289,"# OF TIMES IN WATER********";WH;:PRINT@320,SZ$;:PRINT@353,"REACHED GREEN IN REGULATION ";:IF ST+PS<=PA(HO)-2 THENPRINT"YES":GR=GR+1ELSEPRINT"NO";
312 PRINT@385,"NUMBER OF PUTTS************";NP;
313 IF HO<10 THENYF=YF+SC(HO)ELSEYB=YB+SC(HO)
314 A$=INKEY$:IF A$=""THEN314ELSEST=0:YS=YF+YB:CLS
315 PRINT@9,"***SCORECARD***":PRINT@64,"HOLE  PAR SCR    HOLE  PAR SCR";:FH=96:BH=113:FP=102:FS=106:BP=119:BS=123
316 FOR X=1 TO 9:PRINT@FH,X:FH=FH+32:PRINT@FP,PA(X):FP=FP+32:PRINT@FS,SC(X):FS=FS+32:NEXTX:PRINT@384,"TOTAL  36 ";YF;
317 FOR X=10 TO 18:PRINT@BH,X:BH=BH+32:PRINT@BP,PA(X):BP=BP+32:PRINT@BS,SC(X):BS=BS+32:NEXTX:PRINT@401,"TOTAL  36 ";YB;
318 TP=TP+PA(HO):SR=YS-TP:IF SR<0 THENPOKEPV,ABS(SR)+48 ELSEIF SR=0 THENPRINT@RP," E";ELSEPRINT@RP,SR;
319 IF HO=9 THENRP=93:PV=1118
320 RP=RP+32:PV=PV+32
321 A$=INKEY$:IF A$=""THEN321ELSEIF HO=18 THEN322 ELSEHO=HO+1:PS=0:NP=0:WH=0:GOTO23
322 CLS:PD=PEEK(&HC000):IF PD<>68THEN326ELSEINPUT"DO YOU WANT  TO SAVE YOUR STATS TO DISKETTE Y/N";F1$:IF F1$="N"THEN325ELSEPRINT:INPUT"MAKE SURE YOUR DRIVE IS ON AND  A DISK IS IN IT....PRESS RETURN WHEN READY";F2$
323 INPUT"IS THIS YOUR FIRST SAVE OF THIS FILE Y/N";F3$:IF F3$="N"THEN324ELSEZS=85:ZD=6:ZL=245:ZG=6:ZP=36:ZG=10:ZW=8:ZX=10:OPEN"O",#1,"FINLSTAT/DAT":WRITE#1,ZS,ZD,ZL,ZG,ZP,ZR,ZW,ZX:CLOSE#1:GOTO325
324 OPEN"I",#1,"FINLSTAT/DAT":INPUT#1,ZS,ZD,ZL,ZG,ZP,ZR,ZW,ZX:CLOSE#1
325 CLS
326 PRINT@10,"FINAL STATS":PRINT@65,"SCORE**********************";YS;:PRINT@97,"# OF DRIVES IN FAIRWAY*****";GD;:PRINT@129,"AVERAGE LENGTH OF DRIVE****";INT(TL/14);:PRINT@161,"GREENS REACHED REGULATION**";GR;:PRINT@193,"# OF PUTTS*****************";PT;
327 PRINT@225,"# OF TIMES IN ROUGH / TRAPS";BR;:PRINT@257,"# OF TIMES IN WATER********";TW;:PRINT@289,"PENALTY STROKES************";XP
328 FOR T=1 TO 600:NEXTT:IFMT=3THEN340
329 IF F1$="N"OR PD<>68 THEN340ELSEPRINT@352,"**FLASHING ASTERISKS=NEW RECORD"
330 IF YS<ZS THENPRINT@93,"**";:IFMT=2 THENZS=YS
331 IF GD>ZD THENPRINT@125,"**";:IFMT=2 THENZD=GD
332 IF INT(TL/14)>ZL THENPRINT@157,"** ";:IFMT=2 THENZL=INT(TL/14)
333 IF GR>ZG THENPRINT@189,"**";:IFMT=2 THENZG=GR
334 IF PT<ZP THENPRINT@221,"**";:IFMT=2 THENZP=PT
335 IF BR<ZR THENPRINT@253,"**";:IFMT=2 THENZR=BR
336 IF TW<ZW THENPRINT@285,"**";:IFMT=2 THENZW=TW
337 IF XP<ZX THENPRINT@317,"**";:IFMT=2 THENZX=XP
338 FOR T=1 TO 200:NEXTT
339 PRINT@352,"  ";
340 IF F1$="Y"THENMT=MT+1:IF MT<4THEN326ELSEOPEN"O",#1,"FINLSTAT/DAT":WRITE#1,ZS,ZD,ZL,ZG,ZP,ZR,ZW,ZX:CLOSE#1
341 PRINT:PRINT:INPUT"PLAY AGAIN Y/N";F4$:IF F4$="Y"THENRUN0ELSEEND
342 XB=1216:YY=1247:X=480:CLS:A$=STRING$(32,32):T1$="         SCRATCH GOLFER":N$="         LARRY DUGGINS"
343 PRINT@X,T1$:X=X-32:IFX>193THENFORT=1 TO 30:NEXT:PLAY"T100;A":GOTO343
344 X=480
345 PRINT@X,A$:X=X-32:IFX>129THENFORT=1 TO 30:NEXT:PLAY"T160;O2A":GOTO345
346 FORT=1 TO 100:NEXT
347 FOR L=1 TO 16:PLAY"T190O5AED":POKEXB-1,96:POKEYY+1,96:FORT=1 TO 30:NEXT:POKEXB,66:POKEYY,89:XB=XB+1:YY=YY-1:NEXT
348 PLAY"T200":FORFT=1TO20:PLAY"T+CE":NEXTFT:PRINT@256,N$
349 FORT=1 TO 800:NEXT:RETURN
