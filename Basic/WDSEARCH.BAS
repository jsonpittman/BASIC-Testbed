10 '****************************
20 '*                          *
30 '* WORD SEARCH PUZZLE MAKER *
40 '*   BY BENJAMIN BRUNOTTE   *
50 '*      COPYRIGHT 1987      *
60 '*                          *
70 '****************************
80 PMODE 0,1:PCLEAR 1:' **FREE UP MORE MEMORY
90 CLEAR 14000:CLS:DIM A$(30,30),W$(25),DX(8,2),DD(28):K=RND(-TIMER):' **RESERVE STRING SPACE & DIMENSION ARRAYS
100 ' ***SET UP ARRAYS FOR PICKING DIRECTION TO PLACE WORD IN PUZZLE
110 FOR I=1 TO 8:READ DX(I,1),DX(I,2):NEXT I
120 FOR I=1 TO 28:READ DD(I):NEXT I
130 DATA 0,1,1,1,1,0,1,-1,0,-1,-1,-1,-1,0,-1,1
140 DATA 2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,1,3,5,7
150 ' ***INSTRUCTIONS
160 CLS:PRINT@167,"WORD SEARCH PUZZLE":PRINT:PRINT:PRINT"DO YOU NEED INSTRUCTIONS (Y/N)?"
170 IK$=INKEY$:IF IK$="Y" THEN CLS ELSE IF IK$="N" THEN CLS:GOTO 220 ELSE 170
180 PRINT"   THIS PROGRAM IS A WORD SEARCHPUZZLE GENERATOR!  THE PROGRAM  TAKES A LIST OF WORDS, REMOVES  ANY NON-ALPHABETICAL CHARACTERS EXCEPT SPACES, AND BUILDS A WORDSEARCH PUZZLE WITH THEM.":PRINT
190 PRINT"   IF THE COMPUTER CAN NOT FIT AWORD IN THE PUZZLE, IT WILL ASK IF YOU WANT TO START OVER.  THE WORD WILL BE DROPPED FROM THE   LIST IF YOU ANSWER 'N0'.  IF    NECESSARY, TRY USING FEWER WORDSOR A LARGER PUZZLE SIZE."
200 PRINT:INPUT"   PRESS enter TO START....";IK$:CLS
210 ' ***SET UP PUZZLE
220 FOR I=1 TO 25:W$(I)="":NEXT:CLS:' **CLEAR WORD LIST ARRAY
230 PRINT:PRINT:INPUT"WIDTH OF PUZZLE: ";W:MD=W:IF W>30 THEN PRINT"TOO WIDE.  MUST BE 30 OR LESS.":GOTO 230 ELSE IF W<5 THEN PRINT"MUST BE AT LEAST 5 WIDE.":GOTO 230:' **SET PUZZLE WIDTH
240 PRINT:PRINT:INPUT"LENGTH OF PUZZLE: ";L:IF L>W THEN MD=L
250 IF L<5 THEN PRINT"MUST BE AT LEAST 5 LONG.":GOTO 240 ELSE IF L>30 THEN PRINT"TOO LONG.  MUST BE 30 OR LESS.":GOTO 240:' **SET PUZZLE LENGTH
260 PRINT:PRINT:PRINT"ENTER A HEADING TO BE PRINTED   AT THE TOP OF THE PUZZLE.       (40 CHARACTERS MAXIMUM)":LINE INPUT HD$:IF LEN(HD$)>40 THEN PRINT"TOO MANY CHARACTERS.":GOTO 260:' **INPUT HEADING FOR PUZZLE
270 ' ***INPUT WORD LIST
280 CLS:PRINT:PRINT:PRINT"  THERE MUST BE AT LEAST 2 WORDSIN THE LIST AND NOT MORE THAN   25.":PRINT"  ENTER A WORD AT EACH QUESTION MARK.":PRINT"  TO REDO THE PREVIOUS WORD,    TYPE A HYPHEN (-).":PRINT"  WHEN FINISHED, TYPE A PERIOD  (.)."
290 FOR I=1 TO 25:PRINT"NEXT WORD: ";
300 INPUT T$:' **INPUT A WORD
310 IF T$="-" THEN I=I-1:PRINT"REDO: ";W$(I):GOTO 300:' **CHECK FOR HYPHEN
320 IF T$="." THEN IF I<3 THEN PRINT"MUST HAVE AT LEAST 2 WORDS.":GOTO 300 ELSE M=I-1:GOTO 490:' **CHECK FOR PERIOD
330 IF LEN(T$)=0 THEN PRINT"INPUT ERROR.":PRINT"REDO: ";:GOTO 300 ELSE J=1:' **MAKE SURE A WORD WAS ENTERED
340 TE$=MID$(T$,J,1):IF TE$<"A" OR TE$>"Z" THEN 360:' **CHECK FOR LETTERS ONLY
350 J=J+1:IF J<=LEN(T$) THEN 340 ELSE 410:' **DONE?
360 IF TE$=T$ THEN T$="":GOTO 330:' **ONLY 1 NON-ALPHABETICAL CHARACTER IN WORD
370 IF TE$=" " THEN 350:' **SKIP IF A SPACE
380 IF J=LEN(T$) THEN T$=LEFT$(T$,J-1):GOTO 410:' **REMOVE NON-ALPHA CHARACTERS
390 IF J=1 THEN T$=RIGHT$(T$,LEN(T$)-1):GOTO 340
400 T$=LEFT$(T$,J-1)+RIGHT$(T$,LEN(T$)-J):J=J-1:GOTO 350
410 IF LEN(T$)>MD THEN PRINT"WORD IS TOO LONG.":PRINT"REDO: ";:GOTO 300:' **CHECK IF WORD TOO LONG FOR PUZZLE
420 ' ***CHECK WORD LIST FOR DUPLICATE WORD
430 IF I=1 THEN 460:' **SKIP CHECK IF FIRST WORD
440 FOR K=1 TO I-1:IF W$(K)=T$ THEN PRINT"DUPLICATE WORD.":PRINT"REDO: ";:GOTO 300
450 NEXT K
460 W$(I)=T$:CLS:PRINT"WORD LIST :":FOR K=1 TO I:PRINTW$(K),:NEXT K:PRINT:' **PRINT WORD LIST ON SCREEN
470 NEXT I:M=25
480 ' ***PUT WORDS IN PUZZLE
490 CLS0:PRINT@199,"one";CHR$(128);"moment";CHR$(128);"please";:FOR J=1 TO L:FOR K=1 TO W:A$(J,K)=CHR$(45):NEXT K:NEXT J:' **PUT HYPHENS IN ALL PUZZLE SPACES
500 CLS:PRINT@37,"BUILDING THE PUZZLE.":FOR I=1 TO M-1:FOR J=I+1 TO M:IF LEN(W$(I))<LEN(W$(J)) THEN W$(0)=W$(I):W$(I)=W$(J):W$(J)=W$(0):' **ARRANGE WORDS LONGEST TO SHORTEST
510 NEXT J,I
520 FOR I=1 TO M:PRINT@37,"BUILDING THE PUZZLE.":PRINT:PRINT"WORKING ON WORD ";W$(I):LN=LEN(W$(I)):NT=0
530 SD=DD(RND(28)):' **SELECT DIRECTION AND STARTING SPACE FOR WORD
540 SX=RND(W):X1=SX+(LN-1)*DX(SD,1):IF X1<1 OR X1>W THEN 530
550 SY=RND(L):X1=SY+(LN-1)*DX(SD,2):IF X1<1 OR X1>L THEN 530
560 NT=NT+1:IF NT<>W*L*2 THEN 590:' **NUMBER OF TRIES TO PUT WORD IN PUZZLE
570 PRINT:PRINT"CAN NOT FIT ",W$(I)," IN THE PUZZLE.":PRINT"START OVER (Y/N)?":' **START OVER OR DROP WORD
580 IK$=INKEY$:IF IK$="Y" THEN CLS:PRINT@8,"STARTING OVER":GOTO 520 ELSE IF IK$="N" THEN CLS:PRINT@96,W$(I);" REMOVED.":W$(I)="":GOTO 650 ELSE 580
590 J=SY:K=SX:FOR P=1 TO LN:IF A$(J,K)=CHR$(45) OR A$(J,K)=MID$(W$(I),P,1) THEN 600 ELSE IF MID$(W$(I),P,1)=" " THEN 610 ELSE 530:' **CHECK IF WORD FITS AND SKIP SPACES
600 J=J+DX(SD,2):K=K+DX(SD,1)
610 NEXT P
620 J=SY:K=SX:FOR P=1 TO LN:IF MID$(W$(I),P,1)=" " THEN 640 ELSE A$(J,K)=MID$(W$(I),P,1):' **REPLACE HYPHENS WITH LETTERS OF WORD IF IT FITS AND SKIP SPACES
630 J=J+DX(SD,2):K=K+DX(SD,1)
640 NEXT P
650 NEXT I
660 TB=(80-(2*W))/2:' **TAB SETTING FOR 80 COLUMN PRINTER
670 POKE 150,41:' **1200 BAUD
680 CLS:PRINT"READY PRINTER TO PRINT PUZZLE.":INPUT"HOW MANY COPIES";CP
690 PT=1:' **SET PRINT COUNTER TO 1
700 FOR I=1 TO M-1:FOR J=I+1 TO M:IF W$(J)<W$(I) THEN W$(0)=W$(I):W$(I)=W$(J):W$(J)=W$(0):' **PUT WORDS IN ALPHABETICAL ORDER
710 NEXT J,I:X$="SOLUTION"
720 PRINT#-2,CHR$(27);CHR$(18);:' **NORMAL CORRESPONDENCE PRINT
730 PRINT"PRINTING ANSWER COPY";PT:GOSUB 800:IF PT<CP THEN PT=PT+1:GOTO 730:' **PRINT EACH COPY
740 PRINT"FILLING IN PUZZLE.":FOR J=1 TO L:FOR K=1 TO W:IF A$(J,K)=CHR$(45) THEN CK=RND(26)+64:A$(J,K)=CHR$(CK):' **REPLACE HYPHENS WITH RANDOM LETTERS
750 NEXT K,J
760 PT=1:X$=" ":' **RESET PRINT COUNTER
770 PRINT"PRINTING PUZZLE COPY";PT:GOSUB 800:IF PT<CP THEN PT=PT+1:GOTO 770:' **PRINT EACH COPY
780 CLS:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT" N = NEW PUZZLE":PRINT:PRINT" Q = QUIT":PRINT:PRINT"PRESS KEY FOR CHOICE....."
790 IK$=INKEY$:IF IK$="N" THEN RUN ELSE IF IK$="Q" THEN END ELSE 790
800 PRINT#-2,CHR$(13);CHR$(13);CHR$(13);CHR$(13):PRINT#-2,TAB((80-(2*LEN(HD$)))/2);:' **CENTER HEADING 5 LINES FROM TOP OF PAPER
810 PRINT#-2,CHR$(15);:' **UNDERLINE
820 PRINT#-2,CHR$(27);CHR$(14);:' **ELONGATION
830 PRINT#-2,HD$;:' **PRINT HEADING
840 PRINT#-2,CHR$(14);:' **END UNDERLINE
850 PRINT#-2,CHR$(27);CHR$(15):' **END ELLONGATION
860 PRINT#-2,TAB((80-LEN(X$))/2);X$;CHR$(13);CHR$(13)
870 FOR J=1 TO L:PRINT#-2,TAB(TB);:' **CENTER EACH LINE
880 FOR K=1 TO W:PRINT#-2,A$(J,K);" ";:NEXT K:' **PRINT EACH LETTER IN A LINE
890 PRINT#-2,CHR$(13);:NEXT J
900 FOR I=1 TO 3:PRINT#-2:NEXT I:B$="Find these words hidden in the puzzle above:":DD=(80-LEN(B$))/2:PRINT#-2,TAB(DD);:' **CENTER MESSAGE 3 LINES DOWN
910 PRINT#-2,CHR$(15);:' **UNDERLINE
920 PRINT#-2,B$;
930 PRINT#-2,CHR$(14):' **END UNDERLINE
940 MD=0
950 ' ***PRINT WORD LIST IN COLUMNS BELOW MESSAGE
960 FOR I=1 TO M:IF W$(I)="" THEN 1000
970 PRINT#-2,TAB(DD+MD);W$(I);
980 MD=MD+16:IF MD=48 THEN MD=0:PRINT#-2,CHR$(13);
990 IF POS(-2)>MD+DD THEN MD=MD+16:GOTO 980
1000 NEXT I
1010 PRINT#-2,CHR$(12);:' **PRINTER FORM FEED
1020 RETURN
