0 'COPYRIGHT 1990 FALSOFT, INC.
5 REM  ********************************
10 REM ***                          ***
15 REM *** CHEMISTRY PERIODIC TABLE ***
20 REM ***                          ***
25 REM *********** WRITTEN BY *********
30 REM ***                          ***
35 REM ***      R.G. MEIJERING      ***
40 REM ***    WINNIPEG, MANITOBA    ***
45 REM ***          CANADA          ***
50 REM ***                          ***
55 REM ********************************
60 REM ***                          ***
65 REM ***        COPY RIGHT        ***
70 REM ***                          ***
75 REM ***      APRIL 18th, 1989    ***
80 REM ***                          ***
85 REM ********************************
100 REM *** INITIAL SCREEN & MEMORY ***
105 POKE65497,0:WIDTH40:CLEAR2000:PALETTE0,0:CLS1:PALETTE8,48:CLS1:RUN1600
200 REM *** SCREEN PROMPTS LINE LOCATIONS ***
205 C1=7:R1=0:R2=1:GOSUB305:RETURN
210 C1=21:R1=0:R2=1:GOSUB305:RETURN
215 C1=5:R1=22:GOSUB310:RETURN
220 C1=14:R1=22:GOSUB310:RETURN
225 C1=0:R1=19:C2=8:R2=20:GOSUB325:RETURN
230 C1=0:R1=10:C2=8:R2=11:GOSUB325:RETURN
300 REM *** SCREEN PROMPTS ***
305 CLS1:LOCATEC1,R1:ATTR0,0:PRINT"Chemistry Periodic Table":LOCATEC1,R2:PRINT"------------------------":RETURN
310 LOCATEC1,R1:PRINT"Press any key to Continue.";:GOSUB400:RETURN
315 LOCATE0,22:PRINT"Press [ C ] - Continue or [ M ] - Menu";:GOSUB400:IF I$="C" THEN RETURN:ELSE IF I$="M" THEN 1600:ELSE IF I$<>"C" OR I$<>"M" THEN 315
320 LOCATE0,22:PRINT"[ C ] continue  [ E ] exit  [ R ] re-do";:GOSUB400:IF I$="C" OR I$="E" OR I$="R" THENRETURNELSE320
325 LOCATEC1,R1:PRINT"  NOTE: the keyboard is in LOWER case...":LOCATEC2,R2:PRINT"Use SHIFT for UPPER case.":POKE282,0:RETURN
330 GOSUB205:LOCATE5,3:PRINT"Question Option Selection:":LOCATE5,10:PRINT"Enter the following:":LOCATE0,12:RETURN
335 GOSUB205:LOCATE0,5:PRINT"Equation Balancing:":LOCATE0,6:PRINT"------------------":RETURN
340 LOCATE0,10:PRINT"One minute Please...To set up Questions":RETURN
345 IF FA=1 OR FA=4 THEN 350 ELSE RETURN
350 LOCATE0,13:ATTR6,4:PRINT" HINT: ";:ATTR0,0:LOCATE0,15:PRINTHI$:RETURN
400 REM *** INKEYS ***
405 I$=INKEY$:IF I$="" OR I$=" " THEN 405 ELSE RETURN
410 GOSUB405:I=VAL(I$):IF I<1 OR I>7 THEN 410 ELSE RETURN
415 GOSUB405:IF I$="Y" OR I$="N" THEN420ELSE415
420 IF I$="Y" THEN IN$=" Yes ":IN=1:RETURN
425 IF I$="N" THEN IN$=" No  ":IN=2:RETURN
430 LOCATE0,8:LINEINPUT"Your Answer: ";W$:RETURN
435 LOCATE0,17:LINEINPUT"Answer: ";W$:RETURN
500 REM *** QUESTIONS OPTIONS ***
505 GOSUB520:GOSUB525:GOSUB540:GOSUB545:GOSUB550:GOTO555
510 GOSUB520:GOSUB525:GOSUB540:GOSUB545:GOTO555
515 GOSUB520:LE=3:GOSUB530:GOSUB535:GOSUB545:GOTO555
520 GOSUB330:INPUT"Number of Questions [ 25 - 106 ]";NQ:IF NQ<25 OR NQ>106 THEN 520 ELSE RETURN
525 IF NQ=>25 AND NQ<27 THEN LE=4:ELSE IF NQ>26 AND NQ<54 THEN LE=3:ELSE IF NQ>54 AND NQ<79 THEN LE=2:ELSE IF NQ>79 THEN LE=1
530 IF LE=1 THEN540ELSE GOSUB330:PRINT"Level of Questions [ 1 - ";LE;" ]";:INPUT"";QL:IF QL>0 OR QL<(LE+1) THENRETURNELSE530
535 GOSUB330:INPUT"Number of Chem. / Quest. [ 2 - 8 ] ";EN:IF EN<2 OR EN>8 THEN 535
540 GOSUB330:INPUT"Number of Tries / Question [ 1 - 5 ]";NT:IF NT<1 OR NT>5 THEN540ELSERETURN
545 GOSUB330:PRINT"List Errors [ Yes / No ]? ";:GOSUB415:LE$=IN$:LO=IN:RETURN
550 GOSUB330:PRINT"List Spelling Errors [ Yes / No ]? ";:GOSUB415:SP$=IN$:SP=IN:RETURN
555 GOSUB330:PRINT"Give Percentage Rating [ Yes / No ]? ";:GOSUB415:GP$=IN$:GP=IN
560 GOSUB330:PRINT"Print - Out Required [ Yes / No ]? ";:GOSUB415:PO$=IN$:PO=IN
565 GOSUB330:LOCATE0,6:PRINT"Number of Questions:.....";:ATTR6,4:PRINTNQ;:ATTR0,0:LOCATE0,8:PRINT"Question Level:..........";:ATTR6,4:PRINTQL;:ATTR0,0:LOCATE0,10:PRINT"Number of Chem. / Quest:.";:ATTR6,4:PRINTEN;:ATTR0,0
570 LOCATE0,12:PRINT"Number of Tries:.........";:ATTR6,4:PRINTNT;:ATTR0,0:IF SP$="" THEN SP$=" No  "
575 LOCATE0,14:PRINT"List Errors:.............";:ATTR6,4:PRINTLE$;:ATTR0,0:LOCATE0,16:PRINT"List Spelling Errors:....";:ATTR6,4:PRINTSP$;:ATTR0,0:LOCATE0,18:PRINT"Give Percentage:.........";:ATTR6,4:PRINTGP$;:ATTR0,0
580 LOCATE0,20:PRINT"Print - Out:.............";:ATTR6,4:PRINTPO$;:ATTR0,0:GOSUB320:IF I$="C" THEN585:ELSE IF I$="E" THEN 1600:ELSE IF I$="R" THEN ON I GOTO 505,510,510,505,515
585 IF I>4 THEN 3000
600 REM *** INITIALIZE QUESTION OPTION VARIABLES ***
605 NS=NQ+1:DIM QT(NS):DIM TE$(NS):DIM SE$(NS):DIM QS(NS):TR=1
610 IF NQ<27 THEN SL=(QL-1)*26 ELSE SL=INT((26.5)*(QL-1))
615 RESTORE:FORZ=0TO(SL-1):READA,B$,C$,D$,E:NEXTZ:GOSUB205:GOSUB230:GOSUB215:NN=NQ+SL
620 FORT=SL TO NN:READA,B$,C$,D$,E:ON I GOSUB 1700,1800,1900,2000,815,915:NEXTT:RESTORE:GOSUB215
700 REM *** PERCENTAGE ROUTINE ***
705 GOSUB205:GOSUB1000:IF GP=1 THEN710ELSE800
710 RA=RA-1:PE=INT((100/NQ)*RA):LOCATE0,3:PRINT"Percentage Rating:";:LOCATE0,4:PRINT"*****************";:LOCATE5,6:PRINT"Number Right:........ ";RA;:LOCATE5,8:PRINT"Your Percentage:..... ";PE;"%";:IF NT=1 THEN 720 ELSE 715
715 PA=INT((100/NQ)*(RA-AT)):LOCATE0,11:PRINT"Actual Percentage Rating:";:LOCATE0,12:PRINT"************************";:LOCATE5,14:PRINT"Number of Attemps:.... ";AT;:LOCATE5,16:PRINT"Final Percentage Rating:.. ";PA;"%";:
720 IF PO=1 AND NT<2 THEN GOSUB1110:ELSE IF PO=1 AND NT>1 THEN GOSUB 1120
725 IF FG=2 THEN RETURN
730 GOSUB215
800 REM *** ERROR PRINT - OUT ROUTINE ***
805 GOSUB1000:IF LO=1 AND ER>0 THEN 810 ELSE 900
810 I=5:Z=1:X=1:SL=1:EP=1:NN=105:GOTO1200
815 IF QT(EP)=A THEN 1225 ELSE RETURN
820 LOCATE56,LT:PRINTTE$(EP):EP=EP+1:GOTO 1230
900 REM *** SPELLING ERROR PRINT - OUT ROUTINE ***
905 GOSUB1000:IF SP=1 AND SW>0 THEN 910 ELSE 100
910 I=6:Z=2:ER=SW:X=2:SL=1:NN=105:EP=1:GOTO1200
915 IF QS(EP)=A THEN 1225 ELSE RETURN
920 LOCATE56,LT:PRINTSE$(EP):EP=EP+1:GOTO 1230
1000 REM *** PAPER PRINT - OUT INITIAL ROUTINE ***
1005 POKE65496,0:POKE65497,126:POKE65496,126:POKE150,18:POKE152,64:POKE155,80:POKE282,255:RETURN
1100 REM *** PRINTER PRINT - OUT ***
1105 PRINT#-2:PRINT#-2,TAB(21),"Chemistry Periodic Table":PRINT#-2,TAB(21),"------------------------":PRINT#-2:RETURN
1110 GOSUB1105:PRINT#-2:PRINT#-2,TAB(5)"Percentage Rating:":PRINT#-2,TAB(5)"*****************":PRINT#-2:PRINT#-2:PRINT#-2,TAB(5)"Number of Questions:..............";NQ:PRINT#-2:PRINT#-2,TAB(5)"Question Level:...................";QL:PRINT#-2
1115 PRINT#-2,TAB(5)"Number of Tries / Question:.......";NT:PRINT#-2:PRINT#-2,TAB(5)"Number Correct:...................";RA:PRINT#-2:PRINT#-2,TAB(10)"Your Percentage Rating:..........";PE;"%":PRINT#-2:RETURN
1120 GOSUB1110:PRINT#-2,TAB(5)"Actual Perecentage Rating:":PRINT#-2,TAB(5)"*************************":PRINT#-2:PRINT#-2:PRINT#-2,TAB(5)"Number of Attempts:..............";AT:PRINT#-2:PRINT#-2,TAB(20)"Final Perecentage Rating:.....";PA;"%":PRINT#-2:RETURN
1125 PRINT#-2:PRINT#-2,TAB(5)"Error Listing:":PRINT#-2,TAB(5)"*************":PRINT#-2:PRINT#-2:RETURN
1130 PRINT#-2:PRINT#-2,TAB(5)"Spelling Error Listing:":PRINT#-2,TAB(5)"**********************":PRINT#-2:RETURN
1135 ON Z GOSUB1125,1130:PRINT#-2:PRINT#-2,TAB(5)"Chemical Name";TAB(22)"Symbol";TAB(39)"State";TAB(56)"Your Mistake":PRINT#-2:PRINT#-2:RETURN
1140 IF Z=1 THEN O$=TE$(EP) ELSE O$=SE$(EP)
1145 PRINT#-2,TAB(5)B$;TAB(24)C$;TAB(39)D$;TAB(57)O$:RETURN
1150 PRINT#-2,TAB(3)"Chemical Equation";TAB(31)"Correct Answer";TAB(61)"Your Answer":PRINT#-2:RETURN
1155 PRINT#-2,TAB(C3)QN$(T);TAB(C4)RB$(T);TAB(C5)WA$(T):PRINT#-2,TAB(C4)RC$(T);TAB(C5)WB$(T):RETURN
1200 REM *** SCREEN SPELLING & ERROR PRINT OUT ROUTINE ***
1205 IF PO=1 THEN GOSUB 1135 ELSE 1220
1210 GOTO1220
1215 GOSUB220
1220 WIDTH80:LT=5:GOSUB210:RESTORE:LOCATE5,3:PRINT"Chemical";:LOCATE22,3:PRINT"Symbol";:LOCATE39,3:PRINT"State";:LOCATE56,3:PRINT"Your Mistake";:GOTO620
1225 LOCATE5,LT:PRINTB$;:LOCATE24,LT:PRINTC$;:LOCATE38,LT:PRINTD$;:ON Z GOTO 820,920
1230 LT=LT+1:IF LT>21 THEN 1215
1235 IF PO=1 THEN GOSUB1140
1240 IF EP>ER THEN 1245 ELSE RETURN
1245 GOSUB220:ON X GOTO 900,100
1300 REM *** CORRECT ROUTINE ***
1305 LOCATE14,10:ATTR6,4:PRINT" CORRECT! ";:FORG=1TO700:NEXTG:RA=RA+1:GOTO1515
1400 REM *** SPELLING & ERROR ROUTINE ***
1405 IF SP=2 THEN 1435
1410 IF LEN(W$)<>LEN(B$) THEN WE=WE+1
1415 FORK=1TO(LEN(B$)):IF MID$(W$,K,1)<>MID$(B$,K,1) THEN WE=WE+1
1420 NEXTK:IF WE>2 THEN 1435 ELSE 1425
1425 LOCATE11,10:ATTR6,4:PRINT" SPELLING ERROR! ";:FORG=1TO700:NEXTG:AT=AT+1:IF TR=NT THEN 1430 ELSE 1505
1430 SW=SW+1:QS(SW)=A:SE$(SW)=W$:GOTO1515
1435 LOCATE14,10:ATTR6,4:PRINT"  ERROR!  ";:FORG=1TO700:NEXTG:AT=AT+1:IF TR=NT THEN 1440 ELSE 1505
1440 ER=ER+1:TE$(ER)=W$:QT(ER)=A:GOTO1515
1500 REM *** RESET ROUTINE ***
1505 TR=TR+1:WE=0:IF I<4 THEN 1510 ELSE 1520
1510 ON I GOTO 1700,1800,1900
1515 TR=1:WE=0:RETURN
1520 ON U GOTO 1700,1800,1900
1600 REM *** MAIN MENU ***
1605 GOSUB205:LOCATE0,3:PRINT"Select (1 to 5):":GOSUB1610:GOSUB1615:GOSUB1620:GOSUB1625:GOSUB1630:GOSUB410:ATTR6,4:ON I GOSUB1610,1615,1620,1625,1630:ATTR0,0:GOSUB315:ON I GOTO 505,510,510,505,515
1610 LOCATE5,8:PRINT"1] Quiz on Chemical Names";:RETURN
1615 LOCATE5,10:PRINT"2] Quiz on Chemical Symbols";:RETURN
1620 LOCATE5,12:PRINT"3] Quiz on Physical Properties";:RETURN
1625 LOCATE5,14:PRINT"4] Quiz on All Three";:RETURN
1630 LOCATE5,16:PRINT"5] Balance Equations";:LOCATE0,22:RETURN
1700 REM *** QUESTION #1 ***
1705 GOSUB205:LOCATE3,5:PRINT"What is the chemical name for this   symbol [ ";C$;" ] which is a ";D$;".":GOSUB430:IF W$="" OR W$=" " THEN 1705:ELSE IF W$=B$ THEN 1300 ELSE 1400
1800 REM *** QUESTION #2 ***
1805 GOSUB205:LOCATE3,5:PRINT"What is the chemical symbol for this  chemical: ";B$;" which is a ";D$;".";:GOSUB430:IF W$="" OR W$=" " THEN 1805:ELSE IF W$=C$ THEN 1300 ELSE 1435
1900 REM *** QUESTION #3 ***
1905 GOSUB205:LOCATE3,5:PRINT"What is the physical state of this   chemical element: ";C$;:GOSUB430:IF W$=D$ THEN 1300 ELSE 1435
2000 REM *** QUESTION #4 ***
2005 U=RND(3):ON U GOTO 1700,1800,1900
3000 REM *** OPTION #5 BALANCE EQUATIONS ***
3005 GOSUB335:GOSUB340:FG=1:FA=QL:GOTO3100
3010 GOSUB230:GOSUB215:FG=2:RETURN
3100 REM *** INITIALIZE SYMBOL & VALUE SEARCH ***
3105 NN=NQ+1:DIM PC$(EN),QC(EN),HT(EN),QN$(NN),WA$(NN),WB$(NN),RB$(NN),RC$(NN):FORF=1TONN:FORT=1TOEN
3110 RN=RND(106):SL=RN+1:FORV=1TOSL:READA,B$,C$,D$,E:NEXTV:IF RN=A THEN3115 ELSE 3110
3115 RESTORE:IF E=0 THEN 3110
3120 PC$(T)=C$:QC(T)=E:HI$=HI$+PC$(T)+"="+MID$((STR$(QC(T))),2,1)+" ":NEXTT
3200 REM *** BALANCING FORMAT ***
3205 FORR=4TO1 STEP-1:FORT=1TOEN:IF QC(T)=R THEN 3215
3210 NEXTT:NEXTR
3215 FL=0:FORT=1TOEN:ON R GOTO 3705,3605,3505,3405
3220 IF FL=0 THEN 3805
3300 REM *** RE - BALANCE ELEMENTS ***
3305 FORT=1TOEN:ON R GOTO 5,3705,3515,3415
3400 REM *** BALANCE TO 4 th ELEMENT ***
3405 IF QC(T)=4 THEN QC(T)=0:ELSE IF QC(T)=3 THEN FL=1:ELSE IF QC(T)=1 THEN QC(T)=4
3410 NEXTT:GOTO3220
3415 IF QC(T)=3 THEN QC(T)=4:ELSE IF QC(T)=0 THEN QC(T)=3
3420 NEXTT:FL=0:GOTO3220
3500 REM *** BALANCE TO 3 rd ELEMENT ***
3505 IF QC(T)=3 THEN QC(T)=0:ELSE IF QC(T)=2 THEN FL=1:ELSE IF QC(T)=1 THEN QC(T)=3
3510 NEXTT:GOTO3220
3515 IF QC(T)=2 THEN QC(T)=3:ELSE IF QC(T)=0 THEN QC(T)=2
3520 NEXTT:FL=0:GOTO3220
3600 REM *** BALANCE TO 2 nd ELEMENT ***
3605 IF QC(T)=2 THEN QC(T)=0:ELSE IF QC(T)=1 THEN QC(T)=2
3610 NEXTT:FL=0:GOTO3220
3700 REM *** BALANCE TO 1 st ELEMENT ***
3705 IF QC(T)=1 THEN QC(T)=0
3710 NEXTT:GOTO3220
3800 REM *** QUESTION VARIABLE SET UP ***
3805 FORT=1TOEN:QU$=QU$+PC$(T)+"+":IF QC(T)=0 THEN 3810 ELSE 3815
3810 AN$=AN$+PC$(T):AP$=AP$+PC$(T):AQ$=AQ$+STRING$((LEN(PC$(T))),32):GOTO3820
3815 AN$=AN$+PC$(T)+MID$((STR$(QC(T))),2,1):AP$=AP$+PC$(T)+" ":AQ$=AQ$+STRING$((LEN(PC$(T))),32)+MID$((STR$(QC(T))),2,1)
3820 NEXTT:L=LEN(QU$)-1:QU$=MID$(QU$,1,L)+"=?"
3900 REM *** QUESTION OUT - PUT ***
3905 IF FG=1 THEN GOSUB 3010
3910 GOSUB335:LOCATE12,9:PRINT"Your Question:":CO=INT(40-(LEN(QU$)))/2:LOCATECO,11:PRINTQU$:GOSUB345:GOSUB435:IF W$="" OR W$=" " THEN 3910 :ELSE IF W$=AN$ THEN 4005 ELSE 4105
4000 REM *** CORRECT ROUTINE ***
4005 LOCATE14,18:ATTR6,4:PRINT" CORRECT! ";:ATTR0,0:FORG=1TO100:NEXTG:RA=RA+1:GOTO4305
4100 REM *** ERROR ROUTINE ***
4105 LOCATE14,18:ATTR6,4:PRINT"  ERROR!  ";:ATTR0,0:AT=AT+1:TR=TR+1:IF TR=NT THEN 4205
4110 FORZ=1TO500:NEXTZ:IF FA=2 THEN FA=4
4115 GOTO3910
4200 REM *** CHEMICAL SUB SCRIPT INPUT ROUTINE ***
4205 ER=ER+1:TL=LEN(QU$)-2:QN$(ER)=MID$(QU$,1,TL):WA$(ER)=W$:WB$(ER)=W$:RB$(ER)=AP$:RC$(ER)=AQ$
4210 LW=LEN(W$):FORZ=1TOLW:SC=ASC(MID$(W$,Z,1)):IF SC>59 THEN MID$(WB$(ER),Z,1)=" "ELSE GOSUB4220
4215 NEXTZ:GOTO4305
4220 MID$(WA$(ER),Z,1)=" ":RETURN
4300 REM *** RE - SET ROUTINE ***
4305 IF FA=4 THEN FA=2
4310 HI$="":AN$="":AP$="":AQ$="":QU$="":TR=0:NEXTF:GOSUB1000:GOSUB215
4315 IF GP=1 THEN GOSUB700:GOSUB215
4320 IF LO=1 AND PO=2 AND ER>0 THEN 4410
4325 IF LO=1 AND PO=1 AND ER>0 THEN 4335 ELSE 100
4330 IF LO=2 THEN 100
4335 GOSUB1100:GOSUB1150:GOTO4410
4400 REM *** ERROR PRINT - OUT ***
4405 WIDTH80:LT=5:GOSUB210:LOCATE3,3:PRINT"Chemical Equation:";:LOCATE31,3:PRINT"Correct Answer:":LOCATE64,3:PRINT"Your Answer":RETURN
4410 GOSUB4405:FORT=1TOER:C3=INT(24-LEN(QN$(T)))/2:C4=INT(24-LEN(RB$(T)))/2+27:C5=INT(24-LEN(WB$(T)))/2+56:LOCATEC3,LT:PRINTQN$(T);:LOCATEC4,LT:PRINTRB$(T);:LOCATEC5,LT:PRINTWA$(T);:LT=LT+1:LOCATEC4,LT:PRINTRC$(T);:LOCATEC5,LT:PRINTWB$(T);
4415 IF PO=1 THEN GOSUB1155
4420 LT=LT+1:IF LT>20 THEN 4430
4425 NEXTT:GOSUB215:RUN100
4430 GOSUB215:GOTO4405
9300 REM *** DATA ***
9305 DATA 0,0,0,0,0
9310 DATA 1,Hydrogen,H,Gas,1
9315 DATA 2,Helium,He,Gas,0
9320 DATA 3,Lithium,Li,Solid,1
9325 DATA 4,Beryllium,Be,Solid,2
9330 DATA 5,Boron,B,Solid,3
9335 DATA 6,Carbon,C,Solid,4
9340 DATA 7,Nitrogen,N,Gas,3
9345 DATA 8,Oxygen,O,Gas,2
9350 DATA 9,Flourine,F,Gas,1
9355 DATA 10,Neon,Ne,Gas,0
9360 DATA 11,Sodium,Na,Solid,1
9365 DATA 12,Magnesium,Mg,Solid,2
9370 DATA 13,Aluminum,Al,Solid,2
9375 DATA 14,Silicon,Si,Solid,4
9380 DATA 15,Phosphorus,P,Solid,3
9385 DATA 16,Sulfur,S,Solid,2
9390 DATA 17,Chlorine,Cl,Gas,1
9395 DATA 18,Argon,Ar,Gas,0
9400 DATA 19,Potassium,K,Solid,1
9405 DATA 20,Calcium,Ca,Solid,2
9410 DATA 21,Scandium,Sc,Solid,2
9415 DATA 22,Titanium,Ti,Solid,2
9420 DATA 23,Vanadium,V,Solid,2
9425 DATA 24,Chromium,Cr,Solid,2
9430 DATA 25,Manganese,Mn,Solid,2
9435 DATA 26,Iron,Fe,Solid,2
9440 DATA 27,Cobalt,Co,Solid,2
9445 DATA 28,Nickel,Ni,Solid,2
9450 DATA 29,Copper,Cu,Solid,2
9455 DATA 30,Zinc,Zn,Solid,2
9460 DATA 31,Gallium,Ga,Liquid,2
9465 DATA 32,Germanium,Ge,Soild,2
9470 DATA 33,Arsenic,As,Solid,3
9475 DATA 34,Selenium,Se,Solid,2
9480 DATA 35,Bromine,Br,Liquid,1
9485 DATA 36,Krypton,Kr,Gas,0
9490 DATA 37,Rubidium,Rb,Solid,1
9495 DATA 38,Strontium,Sr,Solid,2
9500 DATA 39,Yttrium,Y,Solid,2
9505 DATA 40,Zirconium,Zr,Solid,2
9510 DATA 41,Niobium,Nb,Solid,2
9515 DATA 42,Molybdenum,Mo,Solid,2
9520 DATA 43,Technetium,Tc,Synthetic,2
9525 DATA 44,Ruthenium,Ru,Solid,2
9530 DATA 45,Rhodium,Rh,Solid,2
9535 DATA 46,Palladium,Pd,Solid,2
9540 DATA 47,Silver,Ag,Solid,2
9545 DATA 48,Cadmiun,Cd,Solid,2
9550 DATA 49,Indium,In,Solid,2
9555 DATA 50,Tin,Sn,Solid,2
9560 DATA 51,Antimony,Sb,Solid,2
9565 DATA 52,Tellurium,Te,Solid,2
9570 DATA 53,Iodine,I,Solid,1
9575 DATA 54,Xenon,Xe,Gas,0
9580 DATA 55,Cesium,Cs,Liquid,1
9585 DATA 56,Barium,Ba,Solid,2
9590 DATA 57,Lanthanum,La,Solid,2
9595 DATA 58,Cerium,Ce,Solid,2
9600 DATA 59,Praseodymium,Pr,Solid,2
9605 DATA 60,Neodymium,Nd,Solid,2
9610 DATA 61,Promethium,Pm,Synthetic,2
9615 DATA 62,Samarium,Sm,Solid,2
9620 DATA 63,Europium,Eu,Solid,2
9625 DATA 64,Gadolinium,Gd,Solid,2
9630 DATA 65,Terbium,Tb,Solid,2
9635 DATA 66,Dyprosium,Dy,Solid,2
9640 DATA 67,Homium,Ho,Solid,2
9645 DATA 68,Erbium,Er,Solid,2
9650 DATA 69,Thulium,Tm,Solid,2
9655 DATA 70,Ytterbium,Yb,Solid,2
9660 DATA 71,Lutetium,Lu,Solid,2
9665 DATA 72,Hafnium,Hf,Solid,2
9670 DATA 73,Tantalum,Ta,Solid,2
9675 DATA 74,Tungsten,W,Solid,2
9680 DATA 75,Rhenium,Re,Solid,2
9685 DATA 76,Osmium,Os,Solid,2
9690 DATA 77,Iridium,Ir,Solid,2
9695 DATA 78,Platinum,Pt,Solid,2
9700 DATA 79,Gold,Au,Solid,2
9705 DATA 80,Mercury,Hg,Liquid,2
9710 DATA 81,Thallium,Tl,Solid,2
9715 DATA 82,Lead,Pb,Solid,2
9720 DATA 83,Bismuth,Bi,Solid,2
9725 DATA 84,Polonium,Po,Solid,2
9730 DATA 85,Astatine,At,Solid,1
9735 DATA 86,Radon,Rn,Gas,0
9740 DATA 87,Francium,Fr,Liquid,1
9745 DATA 88,Radium,Ra,Solid,2
9750 DATA 89,Actinium,Ac,Solid,2
9755 DATA 90,Thorium,Th,Solid,2
9760 DATA 91,Protactinium,Pa,Solid,2
9765 DATA 92,Uranium,U,Solid,2
9770 DATA 93,Neptunium,Np,Synthetic,2
9775 DATA 94,Plutonium,Pu,Synthetic,2
9780 DATA 95,Americium,Am,Symthetic,2
9785 DATA 96,Curium,Cm,Synthetic,2
9790 DATA 97,Berkelium,Bk,Synthetic,2
9795 DATA 98,Californium,Cf,Synthetic,2
9800 DATA 99,Einsteinium,Es,Synthetic,2
9805 DATA 100,Fermium,Fm,Synthetic,2
9810 DATA 101,Mendelevium,Md,Synthetic,2
9815 DATA 102,Nobelium,No,Synthetic,2
9820 DATA 103,Lawrencium,Lr,Synthetic,2
9825 DATA 104,Unnilquadium,Unq,Synthetic,2
9830 DATA 105,Unnilpentium,Unp,Synthetic,2
9835 DATA 106,Unnilhexium,Unh,Synthetic,2
9900 REM *** SAVE PROGRAM ***
9905 FORF=1TO9:MOTORON:AUDIO ON:FORK=1TO2500:NEXTK:CSAVE"CHEMV.1":NEXTF:MOTOROFF
