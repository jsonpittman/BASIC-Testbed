10 POKE 27,118:POKE 28,00
20 EXEC:CLEAR 1000:PCLEAR4:DIMC$(80)
30 EX$=CHR$(PEEK(2388))+CHR$(PEEK(2389))
40 OPEN"D",#1,"ACCOUNTS/DAT",32:RN=0
50 FIELD#1,10 AS A$, 22 AS B$
60 FOR X=1 TO 80 STEP2:RN=RN+1:GET#1,RN:C$(X)=A$:C$(X+1)=B$
70 IF LOF(1)=RN THEN 90
80 NEXTX
90 CLOSE#1
100 PRINT@266,"PLEASE WAIT";
110 FOR X=1TO RN*2
120 FOR Y=1 TO 22
130 A$=RIGHT$(C$(X),1)
140 F=LEN(C$(X))-1
150 IF A$<>" " THEN 180
160 IF A$=" " THEN C$(X)=LEFT$(C$(X),F)
170 NEXT Y
180 NEXT X
200 UL$=CHR$(27)+CHR$(45)+CHR$(1)
210 NU$=CHR$(27)+CHR$(45)+CHR$(0)
220 EE$=CHR$(14)
230 TA$="19"+EX$+" TAX INFORMATION"
240 NA$="PUT YOUR NAME HERE ON LINE 240"
250 GOTO 270
260 INPUT "IF YOU WANT TO CONTINUE, PRESS              <ENTER>";K
270 POKE32544+54,0:EXEC
280 PRINT@9,"*** MENU ***"
290 PRINT@64,"1-ADD"
300 PRINT@128,"2-CODES"
310 PRINT@192,"3-NUMBER"
320 PRINT@256,"4-DATE"
330 PRINT@320,"5-NAME"
340 PRINT@85,"6-ACCOUNT"
350 PRINT@149,"7-BAR GRAPH"
360 PRINT@213,"8-SEARCH"
370 PRINT@277,"9-LOAD"
380 PRINT@340,"10-SETUP"
390 PRINT@421,"SELECTION (1-8)";:INPUT N
400 ON N GOSUB 2030,1800,420,580,700,870,1270,2460,2400,2440
410 GOTO 260
420 EXEC:INPUT "ENTER NUMBER";G
430 T=0:L=0
440 RESTORE
450 READ C
460 IF C=-1 THEN 540 ELSE 470
470 READ D,P$,A,T$,CM$
480 IF L>=448 THEN A$=INKEY$:IF A$="" THEN 480
490 IF L>=448 THEN EXEC:L=0
500 IF G=C THEN GOSUB 1780:PRINTCM$:L=L+32
510 IF G<=5 AND G=C THEN T=T+A:L=L+32
520 IF G>5 AND G=C THEN L=L+32
530 GOTO 450
540 PRINT
550 IF G<=5 THEN PRINT "TOTAL=";T
560 PRINT
570 RETURN
580 EXEC:INPUT "BEGINNING DATE";W
590 INPUT "ENDING DATE";Y
600 EXEC:L=0
610 RESTORE
620 READ C
630 IF C=-1 THEN 690 ELSE 640
640 READ D,P$,A,T$,CM$
650 IF L>=448 THEN A$=INKEY$:IF A$="" THEN 650
660 IF L>=448 THEN EXEC:L=0
670 IF W<=D AND Y>=D THEN GOSUB 1780:L=L+32
680 GOTO 620
690 RETURN
700 EXEC:INPUT"GIVE THE NAME OF THE PARTY PAID OR THE NAME OF THE PARTY FROM   WHOM MONEY WAS RECEIVED";R$
710 PRINT
720 INPUT "WAS MONEY RECEIVED FROM (1) OR  PAID TO (2) THIS PARTY";U
730 ON U GOTO 740,1180
740 EXEC:T=0:L=0
750 RESTORE
760 READ C
770 IF C=-1 THEN 830 ELSE 780
780 READ D,P$,A,T$,CM$
790 IF L>=448 THEN A$=INKEY$:IFA$="" THEN 790
800 IF L>=448 THEN EXEC:L=0
810 IF C=0 AND R$=P$ THEN GOSUB 1780:T=T+A:L=L+32
820 GOTO 760
830 IF U=1 THEN PRINT "TOTAL RECEIVED FROM "R$" IS-":PRINTUSING"$$####.##";T
840 IF U=2 THEN PRINT "TOTAL PAID TO "R$" IS-":PRINTUSING"$$####.##";T
850 PRINT
860 RETURN
870 EXEC:INPUT "WHAT ACCT. DO YOU WANT";M$
880 INPUT "OUTPUT TO PRINTER (Y/N)";YN$:IFYN$="Y" THEN POKE32544+54,255:ELSE IF YN$="N" THEN 920
890 PO=PEEK(65314):IF PO/2<>INT(PO/2) THEN 900 ELSE 915
900 EXEC:PRINT@265,"TURN ON PRINTER";
910 A$=INKEY$:IF A$="" THEN 910
915 L2=LEN(NA$):L1=((80-(2*L2))/2)
920 IF YN$="Y"AND M$="ALL" THEN PRINT#-2,TAB(20)EE$;TA$:PRINT#-2:PRINT#-2,TAB(L1)EE$;NA$:PRINT#-2:PRINT#-2
930 IF M$="ALL" THEN 940 ELSE 950
940 FOR I=1 TO 80 STEP 2:M$=C$(I):GOSUB 950:NEXTI
950 IF M$="" THEN 1170
960 EXEC:T=0:L=0
970 RESTORE
980 READ C
990 IF C=-1 THEN 1060 ELSE 1000
1000 READ D,P$,A,T$,CM$
1010 IF L>=448 AND YN$<>"Y"THEN A$=INKEY$:IFA$="" THEN 1010
1020 IF L>=448 THEN EXEC:L=0
1030 IF T$=M$ THEN GOSUB 1780:L=L+32:T=T+A
1040 IFT$=M$ AND YN$="Y" GOSUB 1750
1050 GOTO 980
1060 FOR X=1 TO 80
1070 IF M$=C$(X) THEN M$=C$(X+1):GOTO 1090
1080 NEXT
1090 PRINT
1100 IF M$<>"BUS/PROF TRAVEL" THEN PRINT "TOTAL-"M$:PRINT USING "$$#####.##";T
1110 IF M$="BUS/PROF TRAVEL" THEN PRINT "TOTAL BUSINESS/PROFESSIONAL           MILEAGE IS-";T
1120 IF M$="BUS/PROF TRAVEL" AND YN$="Y" THEN PRINT#-2,"TOTAL ";UL$;"BUSINESS/PROFESSIONAL MILEAGE";NU$;" IS-";TAB(53)T;"MILES"
1130 IF M$<>"BUS/PROF TRAVEL" AND YN$="Y" THEN PRINT#-2,"TOTAL FOR ";UL$;M$;NU$;" IS-";TAB(45):PRINT#-2,USING "$$#####.##";T
1140 IF YN$="Y" THEN PRINT#-2:PRINT#-2:GOTO 1170
1150 FOR K=1 TO 400:NEXTK
1160 PRINT:POKE32544+54,0
1170 RETURN
1180 EXEC:T=0:L=0
1190 RESTORE
1200 READ C
1210 IF C=-1 THEN 840 ELSE 1220
1220 READ D,P$,A,T$,CM$
1230 IF L>=448 THEN A$=INKEY$:IFA$="" THEN1230
1240 IF L>=448 THEN EXEC:L=0
1250 IF C>0 AND R$=P$ THEN GOSUB 1780:T=T+A:L=L+32
1260 GOTO 1200
1270 EXEC:INPUT "NAME";Z$
1280 PMODE4:PCLS:SCREEN1,1
1290 DRAW "BM50,50 NR5D25NR5D25NR5D25NR5D25R130
1300 DRAW "BM30,48 R4D2L4D2R4BU4BR2R4D4L4U4BR6R4D4L4U4"
1310 DRAW "BM32,102 U4BR2R4D4L4U4BR6R4D4L4U4"
1320 DRAW "BM93,165 U4F3E3D4BU4BR3R4D4L4U4BR7ND4F4U4BR3R4BL2D4BU4BR5D4BR4U4BD2L4"
1330 L=60
1340 RESTORE
1350 READ C
1360 IF C=-1 THEN 1430
1370 READ D,P$,A,T$,CM$
1380 IF P$=Z$ AND A>300 THEN PRINT@200,"ENTRY TOO LARGE":V$=INKEY$:IF V$="" THEN 1380 ELSE 1460
1390 IF P$=Z$ THEN 1400 ELSE 1420
1400 LINE (L,150)-(L+5,150-A/2),PSET,BF
1410 L=L+10
1420 GOTO 1350
1430 A$=INKEY$:IF A$=""THENGOTO1430
1440 INPUT "DO Y0U WANT A SCREEN DUMP";QY$
1450 IF QY$="Y" THEN POKE32544+54,255:GOTO1470 ELSE 1460
1460 EXEC:RETURN
1470 PO=PEEK(65314):IF PO/2<>INT(PO/2) THEN EXEC:PRINT@265,"TURN ON PRINTER";:ELSE 1490
1480 A$=INKEY$:IFA$="" THEN 1480
1490 EXEC:PRINT@267,"PRINTING";
1500 PMODE4:PCLS:SCREEN0,0
1510 DRAW "BM67,155 NU5R25NU5R25NU5R25NU5R25U130
1520 DRAW "BM65,174 U4R2D4R2U4BL4BU2U4R4D4L4BU6U4R4D4L4"
1530 DRAW "BM118,172 L4BU2U4R4D4L4BU6U4R4D4L4"
1540 DRAW "BM200,122 L4E3H3R4BL4BU3U4R4D4L4BU7NR4E4L4BU3U4BD2R4BL4BU5R4BU4L4BR2D4"
1550 L=145
1560 RESTORE
1570 READ C
1580 IF C=-1 THEN 1640
1590 READ D,P$,A,T$,CM$
1600 IF P$=Z$ THEN 1610 ELSE 1630
1610 LINE (167,L)-(167-A/2,L-5),PSET,BF
1620 L=L-10
1630 GOTO 1570
1640 X=0:Y=0:PRINT#-2,TAB(10)Z$
1650 PRINT#-2,CHR$(27)+"1"
1660 FOR X=3558 TO 3558+21
1670 PRINT#-2,CHR$(27)+"K"+CHR$(191)+CHR$(0);
1680 FOR Y=X+(191*32) TO X STEP-32
1690 PRINT#-2,CHR$(PEEK(Y));
1700 NEXT Y
1710 PRINT#-2
1720 NEXT X
1730 PRINT#-2,CHR$(27)+"2"
1740 POKE32544+54,0:EXEC:RETURN
1750 'PRINT ROUTINE
1760 PRINT#-2,USING"####   ####   %    %   #####.##   %    %    %                               %";C,D,P$,A,T$,CM$
1770 RETURN
1780 PRINTUSING"#### #### %    % ####.## %    %";C,D,P$,A,T$
1790 RETURN
1800 EXEC:PRINT@42,"CODES INFO"
1810 PRINT@96,"1-NUMBER CODES"
1820 PRINT@160,"2-PLACES"
1830 PRINT@224,"3-NAMES"
1840 PRINT@288,"4-CATEGORY CODES"
1850 INPUT CN
1860 IF CN=1 THEN NM$="NUMBER":GOSUB 1910
1870 IF CN=2 THEN NM$="PLACES":GOSUB 1910
1880 IF CN=3 THEN NM$="NAMES":GOSUB 1910
1890 IF CN=4 THEN NM$="ACCOUNTS":GOSUB 1910
1900 RETURN
1910 EXEC
1920 OPEN"D",#1,NM$,32:RN=0:L=0
1930 FIELD #1,10 AS A$, 22 AS B$
1940 RN=RN+1
1950 GET#1,RN
1960 PRINT@L,A$;B$
1970 L=L+32
1980 IF INT(L/448)=L/448 THEN V$=INKEY$:IF V$="" THEN 1980
1990 IF L>=448 THEN EXEC:L=0
2000 IF LOF(1)<>RN THEN1940
2010 CLOSE#1
2020 RETURN
2030 EXEC:PRINT@233,"PLEASE WAIT":A$=""
2040 AA=PEEK(25)*256+PEEK(26)
2050 BB=PEEK(AA)*256+PEEK(AA+1)
2060 CC=PEEK(AA+2)*256+PEEK(AA+3)
2070 IF BB=0 THEN 2090
2080 A1=AA:AA=BB:C1=CC:GOTO 2050
2090 FOR I=A1 TO A1+3:POKE I,0:NEXT
2100 LL=A1+3
2110 VT=PEEK(27)*256+PEEK(28)
2120 SP=INT((VT-LL)/64)
2130 EXEC:PRINT TAB(10)"space=";SP
2140 IF A$="-1" THEN 2370
2150 PRINT" ***INPUT -1 FOR LAST ENTRY***"
2160 INPUT "NUMBER";A1$
2170 IF A1$="-1" THEN Z=0:GOTO 2290
2180 IF A1$="CODES" GOSUB 1800:GOTO2160
2190 INPUT "DATE";A2$
2200 INPUT "NAME/PLACE";A3$
2210 IFA3$="CODES"GOSUB1800:GOTO2200
2220 INPUT "AMT./MILES";A4$
2230 INPUT "ACCT.";A5$
2240 IFA5$="CODES"GOSUB1800:GOTO2230
2250 INPUT "COMMENT";A6$
2260 INPUT"DO YOU WANT TO MAKE CORRECTIONS BEFORE CONTINUING? (Y/N)";B$:IF B$="Y" THEN 2160
2270 A$=A1$+","+A2$+","+A3$+","+A4$+","+A5$+","+A6$
2280 GOTO2300
2290 A$=A1$
2300 AA=A1+4:FOR I=1 TO 64:POKE AA,32:AA=AA+1:NEXT
2310 AA=A1+64:DD=INT(AA/256):D1=AA-(DD*256):POKE A1,DD:POKE A1+1,D1
2320 DD=INT(C1/256):D1=C1-(DD*256):POKE A1+2,DD:POKE A1+3,D1:POKE A1+4,134:A1=A1+5
2330 FOR I=1 TO PEEK(VARPTR(A$)):BB=PEEK(256*PEEK(VARPTR(A$)+2)+PEEK(VARPTR(A$)+3)+I-1)
2340 POKE A1,BB:A1=A1+1:NEXTI
2350 FOR I=AA-1 TO AA+3:POKE I,0:NEXT
2360 A1=AA:C1=C1+1:SP=SP-1:GOTO 2130
2370 EXEC:PRINT@11,"S-A-V-E":PRINT:INPUT"ENTER YEAR (LAST 2 DIGITS)";F2$
2375 IF VAL(F2$)<80 OR VAL(F2$)>99 THEN 2370
2380 F1$="TAXINFO"+CHR$(47)+F2$
2390 SAVE F1$:RETURN
2400 EXEC:INPUT"SURE";S$:IF S$<>"Y" THEN RETURN
2410 EXEC:PRINT@11,"L-O-A-D":PRINT:INPUT"ENTER YEAR (LAST 2 DIGITS)";F2$
2420 F1$="TAXINFO"+CHR$(47)+F2$
2430 LOAD F1$,R
2440 EXEC:INPUT"SURE";S$:IF S$<>"Y" THEN RETURN
2450 LOAD"SETUP",R
2460 EXEC:CT=0:SN$="":RESTORE:PRINT@8,"S E A R C H":LINEINPUT"     ENTER TARGET STRING          ";SN$:LN=10001
2470 READ C
2480 IF C=-1 THEN PRINT"SEARCH OVER"ELSE GOTO 2490
2485 ZZ$=INKEY$:IF ZZ$="" THEN 2485
2487 RETURN
2490 READ D,P$,A,T$,CM$:IFVAL(SN$)=D OR SN$=P$ OR VAL(SN$)=A OR SN$=T$ OR SN$=MID$(CM$,1,LEN(SN$)) THEN PRINT "LINE #";LN
3000 LN=LN+1:GOTO2470
10001 DATA -1
