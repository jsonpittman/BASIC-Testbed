0 ' COPYRIGHT 1989  FALSOFT,INC
10 ''''''''''''''''''''''''''''''
20 '        Lunar Lander        '
30 '       By: Jeff Donze       '
40 '       Copyright 1988       '
50 ''''''''''''''''''''''''''''''
60 CLS:ON BRK GOTO 130:ON ERR GOTO 100
70 POKE &HFFA2,&H34:LOADM"LANDER1"
80 POKE &HFFA2,&H37:LOADM"LANDER2"
90 POKE &HFFA2,&H3A:GOTO 140
100 POKE &HFFA2,&H3A:CLOSE
110 IF ERNO=26 THEN PRINT "FILE NOT FOUND IN";ERLIN:STOP
120 PRINT "ERROR #";ERNO;" IN LINE";ERLIN:STOP
130 STOP
140 CLEAR:ON BRK GOTO 150
150 POKE 65497,0:POKE &HE0D4,&H34:LPOKE &H68000,255:LPOKE &H68001,255
160 FOR N=1 TO 11:HBUFF N,684:NEXT N
170 DIM XO(10),YO(10),HT(10),VT(10),XT(10,3),YT(10,3),TC(4,3),C(15),M(5)
180 PD$="C12U3RD2EURXPX$;XPX$;XPX$;XPX$;XPX$;XPX$;BLD2LGR2BR"
190 PX$="C13G3RE3BRC12G3RE3BR"
200 POKE &HEF0B,&HA8
210 RG$="00635607090836043652546336070700"
220 CM$="00631632110907040738366352161600"
230 FOR N=0 TO 15:C(N)=VAL(MID$(CM$,N*2+1,2)):PALETTE N,0:NEXT N
240 HSCREEN 2
250 FOR N=1 TO 5:READ M(N):NEXT N
260 DATA 1,6,1,1,6
270 FOR N=0 TO 4:FOR M=0 TO 3:READ TC(N,M):NEXT M,N
280 DATA 0,0,0,0,0,0,0,8,0,0,8,9,0,8,9,10,8,9,10,11
290 FOR N=0 TO 10:READ XO(N),YO(N),HT(N),VT(N)
300 FOR M=0 TO 3:READ XT(N,M),YT(N,M):NEXT M,N
310 DATA 24,6,-.965,.298,- 9,-7, 6,-7, 7, 3,-5,10
320 DATA 22,7,-.866,.500,-11,-4, 4,-8, 8, 1,-2,10
330 DATA 18,8,-.707,.707,-11,-1, 2,-9, 8,-1, 1,11
340 DATA 13,8,-.500,.866,-11, 2, 0,-9, 7,-3, 3,10
350 DATA  9,8,-.298,.965,-11, 4,-3,-9, 6,-5, 6, 9
360 DATA  8,7,+.000,1.00,- 9, 7,-5,-8, 5,-6, 8, 7
370 DATA  6,6,+.298,.965,- 7, 9,-7,-6, 3,-7,10, 5
380 DATA  7,6,+.500,.866,- 4,11,-8,-4, 1,-8,10, 3
390 DATA  8,6,+.707,.707,- 1,11,-9,-2,-1,-8,11,-1
400 DATA  8,6,+.866,.500,  2,11,-9, 0,-3,-7,10,-3
410 DATA  8,6,+.965,.298,  4,11,-9, 3,-5,-6, 9,-6
420 ' Title Screen
430 ON BRK GOTO 440
440 POKE &HE0D4,&H34
450 GOSUB 1620:HCLS:R=RND(-123):GOSUB 2350
460 HCOLOR 1:GOSUB 1450:GOSUB 1610
470 FOR X=25 TO 280 STEP 8
480 TN=INT(ABS(SIN(X/30)*4)+.5):GOSUB 1430
490 HPUT (X,85)-(X+34,120),3,OR
500 FOR T=1 TO 40
510 IF BUTTON(0)=1 THEN 540
520 NEXT T:HPUT (X,85)-(X+34,120),3,OR
530 NEXT X:GOTO 470
540 HCOLOR 0:GOSUB 1450
550 HPUT (X,85)-(X+34,120),3,OR
560 HCOLOR 1:HPRINT (14,2),"Lunar Lander"
570 HPRINT (5,4),"As remote pilot, it is your job"
580 HPRINT (3,5),"to land the unmanned cargo ship at"
590 HPRINT (3,6),"the designated landing site.  For"
600 HPRINT (3,7),"each successful landing you will"
610 HPRINT (3,8),"be paid according to the landing"
620 HPRINT (3,9),"site and the cargo class.  You"
630 HPRINT (3,10),"will also be given a bonus for"
640 HPRINT (3,11),"conserved fuel.  Every 5th landing"
650 HPRINT (3,12),"you will be promoted to a higher"
660 HPRINT (3,13),"cargo class with higher fuel"
670 HPRINT (3,14),"consumption.  Slow the ship by"
680 HPRINT (3,15),"using a rightward thrust, and"
690 HPRINT (3,16),"it will drop from orbit.  Use"
700 HPRINT (3,17),"right joystick.  Pull down to"
710 HPRINT (3,18),"thrust, and adjust angle with left"
720 HPRINT (3,19),"and right positioning.  Good luck!"
730 HPRINT (9,22),"Press button to start."
740 IF BUTTON(0)=0 THEN 740
750 ' New Game
760 LV=0:CC=1:SC=0
770 ' New Screen
780 LV=LV+1:IF LV>5 THEN LV=1:IF CC<5 THEN CC=CC+1
790 GOSUB 1490:HCOLOR 1:HPRINT (2,0),"Landing Site:"+STR$(LV)
800 HPRINT (22,0),"Cargo Class:"+STR$(CC)
810 HV=8:VV=0:G=.1:X=30:Y=30:HC=.1:VC=.1:FL=100:FC=CC*.1:O=1:IH=HV
820 PN=10:TN=0:GOSUB 1430:GOSUB 1390:GOTO 860
830 ' Main Loop
840 GOSUB 1390:IF PN<PR THEN PN=PN+1 ELSE IF PN>PR THEN PN=PN-1
850 HPUT (XD,YD)-(XD+34,YD+35),LN+1,OR
860 LN=PN:XD=INT((X-XO(PN))/2)*2:YD=Y-YO(PN)
870 T=0:FOR N=0 TO 3:T=T OR HPOINT(X+XT(PN,N),Y+YT(PN,N)):NEXT N
880 HPUT (XD,YD)-(XD+34,YD+35),PN+1,OR:GOSUB 1430
890 GF=G:IF O THEN GF=G*(IH-HV):IF HV<4 OR Y>34 THEN O=0
900 X=X+HV:Y=Y+VV:HV=HV-HT(PN)*TN*HC:VV=VV+GF-VT(PN)*TN*VC
910 IF O THEN IF X>290 THEN X=28
920 F=FL-TN*FC:IF F<0 THEN F=0
930 IF F<FL THEN HLINE(312,112-FL)-(317,112-F),PRESET,BF
940 FL=F:IF T<12 THEN 840
950 ' Touch Down
960 HPUT (XD,YD)-(XD+34,YD+35),LN+1,OR:HCOLOR M(LV)
970 IF Y<25 THEN 1150 ELSE IF T=15 AND (X<20 OR X>290) THEN 1240
980 TA=HPOINT(X-8,Y+7) AND 14:TB=HPOINT(X+7,Y+7) AND 14
990 IF VV<1 AND ABS(HV)<.2 AND PN=5 AND TA=12 AND TB=12 THEN 1290
1000 TN=0:GOSUB 1430:FOR N=1 TO 4
1010 HCIRCLE (X,Y),N*5,12-N:HCIRCLE (X,Y),N*5-5,12-N
1020 HPAINT (X,Y-N*5+3),12-N,12-N:NEXT N
1030 PLAY "O1L255":FOR O=1 TO 2
1040 FOR M=1 TO 4:TN=M:GOSUB 1430:TN=M-1:GOSUB 1430:PLAY "ABC":NEXT M
1050 FOR M=4 TO 1 STEP-1:TN=M-1:GOSUB 1430:TN=M:GOSUB 1430:PLAY "ABC":NEXT M,O
1060 TN=0:GOSUB 1430
1070 HPRINT (4,10),"You have crashed the ship!"
1080 HPRINT (4,11),"Being very displeased to see"
1090 HPRINT (4,12),"their expensive ship destroyed,"
1100 HPRINT (4,13),"the company has fired you."
1110 HPRINT (4,16),"Career Earnings: $"+RIGHT$(STR$(SC),LEN(STR$(SC))-1)
1120 PLAY "O2L10AGFGFEFL2C"
1130 HPRINT (4,18),"Press button to play again."
1140 FOR T=1 TO 6000:IF BUTTON(0)=0 THEN NEXT:GOTO 450 ELSE 760
1150 HPUT (XD,YD)-(XD+34,YD+35),LN+1,PSET
1160 HPRINT (6,7),"The ship has left the gravita-"
1170 HPRINT (4,8),"tional pull of the moon, and you"
1180 HPRINT (4,9),"were not able to land it. The"
1190 HPRINT (4,10),"ship was lost in space forever."
1200 HPRINT (6,12),"Due to the loss of a costly"
1210 HPRINT (4,13),"craft and its cargo, you were"
1220 HPRINT (4,14),"dismissed from the company."
1230 GOTO 1110
1240 HPRINT (4,7),"Your ship has drifted out of"
1250 HPRINT (4,8),"radio range, and you were unable"
1260 HPRINT (4,9),"to prevent its destruction on"
1270 HPRINT (4,10),"the lunar surface."
1280 GOTO 1200
1290 HPUT (XD,YD)-(XD+34,YD+13),LN+1
1300 LP=1000+500*LV+100*CC:FB=INT((FL)*10):SC=SC+LP+FB
1310 HPRINT (4,6),"Great Landing!"
1320 HPRINT (4,8),"Landing Pay: $"+RIGHT$(STR$(LP),LEN(STR$(LP))-1)
1330 HPRINT (4,10),"Fuel Consumtion:"+STR$(INT(100-FL))+"%  Bonus: $"+RIGHT$(STR$(FB),LEN(STR$(FB))-1)
1340 HPRINT (4,12),"Total Earnings: $"+RIGHT$(STR$(SC),LEN(STR$(SC))-1)
1350 PLAY"O3L12FGAGABO+CP16O-BP16O+L2C"
1360 HPRINT (4,14),"Press button for next landing."
1370 IF BUTTON(0)=0 THEN 1370 ELSE 780
1380 ' Get Postion and Thrust
1390 PR=INT(JOYSTK(0)*10/63)
1400 IF JOYSTK(1)>31 AND FL>0 THEN TN=INT((JOYSTK(1)-32)*5/32) ELSE TN=0
1410 RETURN
1420 ' Set Thrust Palette Colors
1430 FOR N=8 TO 11:PALETTE N,C(TC(TN,N-8)):NEXTN:RETURN
1440 ' Title Message
1450 HPRINT (14,4),"Lunar Lander"
1460 HPRINT (13,6),"By  Jeff Donze"
1470 HPRINT (14,18),"Press button.":RETURN
1480 ' Draw Game Screen
1490 GOSUB 1620:HCLS
1500 PALETTE 8,63:HCOLOR 8:HPRINT (11,0),"One Moment Please."
1510 R=RND(-12349-LV):GOSUB 2350
1520 POKE &HE0D4,&H37
1530 ON LV GOSUB 1630,1760,1910,2040,2190
1540 POKE &HE0D4,&H34
1550 HCOLOR 15:HLINE (0,10)-(15,191),PSET,BF
1560 HLINE (300,10)-(319,191),PSET,BF
1570 HCOLOR 1:HLINE (311,11)-(318,113),PSET,B
1580 HCOLOR 6:HLINE (312,12)-(317,112),PSET,BF
1590 PALETTE 8,0
1600 HCOLOR 15:HLINE (0,0)-(319,10),PSET,BF
1610 FOR N=0 TO 15:PALETTE N,C(N):NEXT N:RETURN
1620 FOR N=0 TO 15:PALETTE N,0:NEXT N:RETURN
1630 SX=52:SY=92:GOSUB 2390
1640 HDRAW "C14BM0,150R15E2RFDF2L2U2F2DRFDFDFDR2D3R3FD2F2R2FR2"
1650 HDRAW "R4FR6FR8FR10ER4ER2EUER2U2RDRE2R2DR2F2D2R2F2R2E2RE2"
1660 HDRAW "RE2RE2UEUEU2EU3EU5EU8E2RD3FR2FR3FR2FR3F2D2FDFR3F2R"
1670 HDRAW "FRFDFR2FD2R2FR3ER3ER2ER3ER2E2RE2R2D2FRD2FDFDFR2ER2"
1680 HDRAW "E2R2E2R2FR2F2D2R2DFDFD2FD3F2RFR2FR3FR4R30R4ER4ER3E"
1690 HDRAW "R2ER2ERERE2R2ER2ER3ER4FR3FR2F2RF2R2R40"
1700 HPAINT (0,191),14,14
1710 HDRAW "C7BM0,174R15FR5FR4FR5FR6FR5FR7FR6FR8FR9FR8FR7FRFR5"
1720 HDRAW "ERER2ER4ER3ERE2UEU2EU3EU4E2RER3FRF2DFDFD3ERFDFRF2R"
1730 HDRAW "FR3F2UFRFR4ER2E2RER2ER3ER4ER6ER5FR8FR7FR8ER30FR5F2"
1740 HDRAW "R2ER3ER5EREUER2FRFR3FR5FR6FR5FR4ERER3ER5ER20"
1750 HPAINT (0,191),7,7:HDRAW "BM219,178XPD$;":RETURN
1760 SX=202:SY=105:GOSUB 2410
1770 HDRAW "C14BM0,130R15E2RE2RER2R3FRFDF2D2FD2R2FDFRFR2FR3ER2"
1780 HDRAW "ER3ER2E2UEU2EU3EU2E2RERER2ER3FR3F2D2R2DFD2FD3FD4F2"
1790 HDRAW "D4FD3FD2FD2FD3FD4FD3FD2FD3FD2FDFRFR2FR3FR25ERER2ER"
1800 HDRAW "EREUEU2EU3EU2EU3EU4EU5EU4EU3ERER2ER3ER2EU5EU6EU7EU"
1810 HDRAW "EU3EUE2ERE2ER2D2R2F2D2F3D2F3R2FR2F2RF2RFRFR2FR2FR3"
1820 HDRAW "ER2ERE2RE2RE3REUEU2EU3EU4EU5EU6ED3FD5DF4DF3DF4DF3D"
1830 HDRAW "F3D2FD2FDF2RFR3FR4FR5FR6FR5FR3FR2FR3FR4FR5FR6R20"
1840 HPAINT (0,191),14,14
1850 HDRAW "C7BM0,168R15E3RE2RE3RE4REF2RF2DF3DF2DF3RFR2ERE2RE2"
1860 HDRAW "RER2ER3FRFR2F2DF2DFDFRF2DRFR2FR3FR4FR45ER4ER3ER4ER"
1870 HDRAW "ER3ERE2RE3RE4RE2RE3RE4RE3RE2RER2RE2R4F2R2ERE3RE4RE"
1880 HDRAW "RE3RE2R2ER2DFD4FD3FD2F2R3FR2RFRF2RFR2FR4FR5FR3FR4F"
1890 HDRAW "R4FR5FR8FR6FR5FR7R20"
1900 HPAINT (0,191),7,7:HDRAW "BM105,184XPD$;":RETURN
1910 SX=100:SY=105:GOSUB 2430
1920 HDRAW "C14BM0,140R15ER5ER4ER3ER4ER2ER4ER3ER2ERE2ERER2ERE2"
1930 HDRAW "RE2RER2ERER2ER3ER4FRF2RF3RF2RFDF2DFDFD3FD2F2RFR2FR"
1940 HDRAW "ERERE2RE3RE2RE3REUEUE2UE3UE2UEU2EU3EU2R2FRF2RF3RF2"
1950 HDRAW "RF2RF3RFDFD2FD6FD7FD8FD8R30EU2EU6EU6F2DFDF2DF3DFD2"
1960 HDRAW "FDF2DF3DF4DF2DFRFR2FR3FR4FR2FR3ER3ER2ERER2ERER2E2R"
1970 HDRAW "E3RE3RE2RER2ER3FR2FRF2RF3RF4RFR2FR30"
1980 HPAINT (0,191),14,14
1990 HDRAW "C7BM0,164R15E3RE3RE3RE2RE3RE4RF2DF3DF2DFRFR2ERE2R2"
2000 HDRAW "ERE2RE3F2E2RE2F3DF2DF4DF2DF5DF4D2FDFRFR2FR3FR4FR5F"
2010 HDRAW "R6ER4ER3ER5ER2ER4ER3ER2ER3ER5ER6FR5FR4FR2FR4FR5FR6"
2020 HDRAW "FR7FR5FR6FR4FR6ER5ER4ER7ER4ER6ER4ER8ERER6F2R40"
2030 HPAINT (0,191),7,7:HDRAW "BM170,161XPD$;":RETURN
2040 SX=250:SY=105:GOSUB 2390
2050 HDRAW "C14BM0,165R15ER4F2R3FR2ER2ER4ER3ER2ER2ERERE2RE3RE2"
2060 HDRAW "RE3RE2RE3RERE2RER2ER3ERE2RERE2RE2FRF2FRF2RF3RF2RFR"
2070 HDRAW "F2RFDFD3FD4FD3FD6FD4FD7FR30EU5EU4EU6EUEU2EUEU3EU2E"
2080 HDRAW "U2EUEU2E2UEU2EU2E2F3DFDF2DF3E2R2EREUEU2EUE2UE3UE2U"
2090 HDRAW "E3UE2UE3UE2UER2ERE3UE4RE3UE2F2DF2DF3DFD2FD3FD2FD3F"
2100 HDRAW "DFD3FD2FRF2E3UE3UE2UE3UE2FD2F2DF3DFDF2DFDF3DF2RF3R"
2110 HDRAW "F3RFR2ERE2RE4RE3RE2RE5R20"
2120 HPAINT (0,191),14,14
2130 HDRAW "C7BM0,173R25ER5ER4ER5ER3ER2ER4ER5FR2FR3FR3FR2FR2F2"
2140 HDRAW "RFRFR2FRFR8FRFRFR6FR3FR2R45ER4ER3ER2ERER3ER2ERERE2"
2150 HDRAW "RE2RERE2RERE2UE2UE3UE2UEU2EU3EDFD2FDF2DF3DF4DF3DF2"
2160 HDRAW "DF3E2RE3RE2RE4RE3RE2RERE2RERE2UE2F2DF2DF3DF2DF4DF3"
2170 HDRAW "DF3DF2E4RE3RE2RE4RE3RE5RER20"
2180 HPAINT (0,191),7,7:HDRAW"BM126,185XPD$;":RETURN
2190 SX=85:SY=105:GOSUB 2410
2200 HDRAW "C14BM0,140R15E2RE2RE2RE2F2R2F2RF2RF2RFRFR2FRFR2FR2"
2210 HDRAW "FR3FR4ER3ER2ERER2ERE2RE2UE2UE2UE2UE2F3DF3DF2DF2DF2"
2220 HDRAW "E2RE2RE3RE2F2RF2RF2E2RE2RE3UE2UE2F2RF2RFRF2RFR2FR2"
2230 HDRAW "ER2ERE2UE2UE2F2DF2DF2D2FD2FD2FD4F2DFD3G2D3FD6GD3F2"
2240 HDRAW "DG2D4R30U2EU6HU2EU2H2UE2U4EU5EU3EU2E2UE2UE2U2EU2EU"
2250 HDRAW "E2F2R2FDF2DF2DF3DFRFR2ERE2RE2RE3RE2RE3RE2F2DF2DF3D"
2260 HDRAW "F2E2UE2UE3F2RF2DF2E3RE3RE3RE3RE3RE3R20"
2270 HPAINT (0,191),14,14
2280 HDRAW "C7BM0,165R15F2RF2RF2F2RE2RE2RE2RE2RE2RE2E2R4F3R2FR"
2290 HDRAW "ER2ER3ER2ERERE2RE3RE2F2R3F2RF2RF2RF2DF3DF2R4F2E2RE"
2300 HDRAW "RE2RE3RE2RE2F2RF2DF2DF3DF2DFR2ER2ERE2RE3RE2F2RF2RF"
2310 HDRAW "RF2RF2RF2RF2RR30E2RE2RE2R3ER4ER3FR3FR2ER3ERER2ERE2"
2320 HDRAW "RE2RE3RE2RE3RE2RE3RE2F2RF2DF2DF3DF2E3RE2UE3UE2UE2R"
2330 HDRAW "E3F4R20":HPAINT (0,191),7,7:HDRAW "BM173,181XPD$;"
2340 RETURN
2350 FOR N=1 TO 150:HSET (RND(285)+10,RND(181)+10,RND(5)):NEXT N
2360 FOR N=1 TO 3:X=RND(285)+15:Y=RND(171)+10
2370 HDRAW "BM=X;,=Y;C5NR4NL4ND4NU4C4NDNUNLNR":HSET (X,Y,1):NEXT N
2380 RETURN
2390 HCIRCLE (SX,SY),17,15:HPAINT (SX,SY),1,15:HPAINT (SX,SY),0,15
2400 HCIRCLE (SX,SY),17,0:HPUT (SX-44,SY-17)-(SX+44,SY+16),1,OR:RETURN
2410 HCIRCLE (SX,SY),40,15:HPAINT (SX,SY),1,15:HPAINT (SX,SY),0,15
2420 HCIRCLE (SX,SY),40,0:HPUT (SX-40,SY-40)-(SX+38,SY+39),3,OR:RETURN
2430 HCIRCLE (SX,SY),29,15:HPAINT (SX,SY),1,15:HPAINT (SX,SY),0,15
2440 HCIRCLE (SX,SY),29,0:HPUT (SX-30,SY-30)-(SX+28,SY+29),2,OR:RETURN
