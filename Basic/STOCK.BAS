0 CLS0:PRINT@138,"STOCK TRACKER";:PRINT@138+32,"BY MARK EVANS";:PRINT@136+96,"COPYRIGHT (C)  1986";:FORX=1TO2000:NEXT:CLS:PRINT@96,"THIS PROGRAM IS DESIGNED TO AID IN THE TRACKING OF STOCK PRICES.IT IS not CLAIMED AS A METHOD"
1 PRINT"OF INVESTING IN STOCKS. FURTHER,THE AUTHOR IS not RESPONSIBLE   FOR THE OUTCOME FROM ERRONEOUS  CALCULATIONS RESULTING FROM THISPROGRAM.":FORX=1TO10000:NEXT
2 '
3 '        STOCK TRACKER
4 '
5 'COPYRIGHT(C) 1986,MARK EVANS
6 '
7 '
8 '
9 '
10 '
11 '
12 PCLEAR1:CLEAR1400:DIMS(5,130),D$(130)
13 CLS3:PRINT"1. DAILY ENTER SEQUENCE":PRINT"2. SAVE DATA":PRINT"3. PRINT OPTIONS":PRINT"4. CREATE FILES":PRINT"5. LOAD DATA":PRINT"6. END PROGRAM"
14 PRINT"ENTER SELECTION...";
15 R$=INKEY$:IFR$="1"THEN17ELSEIFR$="2"THEN79ELSEIFR$="3"THEN34ELSEIFR$="4"THEN85ELSEIFR$="5"THENGOTO18ELSEIFR$="6"THENCLS0:END
16 GOTO15
17 PT$="DAILY ENTER SEQUENCE...":GOSUB19:GOTO25
18 PT$="LOAD DATA...":GOSUB19:GOTO13
19 CLS0:PRINTPT$;:PRINT@64,"";:INPUT"ENTER NAME OF STOCK DATA FILE ";DN$:PRINT@128,"PRESS PLAY ON TAPE RECORDER...  PRESS <SPACEBAR> WHEN READY...";
20 R$=INKEY$:IFR$=" "THEN21ELSE20
21 ' LOAD DATA
22 OPEN"I",-1,DN$
23 INPUT#-1,NE:FORQ=1TO5:INPUT#-1,S$(Q):NEXT:FORW=1TONE:INPUT#-1,D$(W),S(1,W),S(2,W),S(3,W),S(4,W),S(5,W):NEXT:CLOSE(-1)
24 RETURN
25 'DATA ENTRY
26 CLS:NE=NE+1:INPUT"DATE(MM/DD/YY)";D$(NE):GOSUB95:CLS:PRINT@0,D$(NE):FOR S=1TO5:PRINT@32*(S+1),USING"%               %";S$(S),:PRINT"PRICE";:INPUTS(S,NE):NEXT
27 PRINT@32*12:PRINT"ANY CORRECTIONS(Y/N)?";
28 R$=INKEY$:IFR$="Y"THENPRINT:INPUT"WHICH NUMBER(1-5)";N:PRINTS$(N),:PRINT"PRICE";:INPUTS(N,NE):CLS0:PRINTD$(NE):FORS=1TO5:PRINTS$(S),S(S,NE):NEXT:GOTO27
29 IFR$=""THEN28
30 CLS0:PRINT"REWIND DATA TAPE...             PRESS RECORD AND PLAY...        PRESS ENTER WHEN READY..."
31 R$=INKEY$:IFR$=CHR$(13)THENGOSUB80ELSE31
32 CLS0:PRINT"DATA HAS BEEN SAVED...          PRESS ANY KEY TO RETURN TO MENU.ALL DAILY ENTRY FUNCTIONS ARE   COMPLETE."
33 R$=INKEY$:IFR$=""THEN33ELSE13
34 IF TT=9187 THEN 43 ELSE TT=9187:POKE65495,0:FORW=1TO5:T(W)=0:NEXT
35 CLS0:PRINT"PLEASE STANDBY..."
36 ZA=0:FORQ=1TO5:HP(Q)=0:LP(Q)=5000:FORW=1TONE:ZA=ZA+1
37 T(Q)=T(Q)+S(Q,W)
38 PRINT@25,USING"######";5*NE-ZA
39 IF S(Q,W)>HP(Q)THEN HP(Q)=S(Q,W)
40 IF S(Q,W)<LP(Q)THEN LP(Q)=S(Q,W)
41 NEXT:NEXT
42 FORQ=1TO5:AP(Q)=T(Q)/NE:NEXT:POKE65494,0
43 CLS0:PRINT"1. GENERAL INFORMATION          2. DAILY PRICES                 3. RETURN TO MENU"
44 R$=INKEY$:IFR$="3"THEN13ELSEIFR$="1"THEN45ELSEIFR$="2"THEN58ELSE44
45 CLS0:INPUT"PRINT TO:1. SCREEN                       2. PRINTER             ";EW:IFEW=1THEN46ELSEIFEW=2THEN53ELSE45
46 WQ=1
47 CLS0:PRINT@0,USING"%             %                 CURRENT PRICE: $####.##         DATE: %        %                                                YEAR AVERAGE: $####.##          YEAR HIGH: $####.##";S$(WQ),S(WQ,NE),D$(NE),AP(WQ),HP(WQ)  
48 PRINTUSING"YEAR LOW:  $####.##";LP(WQ):PRINT"--------------------------------";
49 PRINT"PRESS <^> TO ADVANCE AND <DOWN  ARROW> TO REVIEW.               PRESS <ENTER> TO END SEQUENCE."
50 R$=INKEY$:IFR$="^"THENWQ=WQ+1ELSEIFR$=CHR$(10)THENWQ=WQ-1ELSEIFR$=CHR$(13)THEN34ELSE50
51 IF WQ>5THENWQ=5:SOUND 200,2 ELSE IF WQ<1THENWQ=1:SOUND1,1
52 GOTO47
53 CLS0:PRINT"TURN PRINTER ON AND POSITION    PAPER...  PRESS <ENTER>..."
54 R$=INKEY$:IFR$=CHR$(13)THEN55ELSE54
55 PRINT#-2,TAB(35);"STOCK DATA":PRINT#-2,STRING$(80,"-"):PRINT#-2,"STOCK";TAB(20);"DATE";TAB(30);"CUR. PRICE";TAB(45);"YEAR MEAN";TAB(60);"YEAR HIGH /  LOW":PRINT#-2,STRING$(80,"-")
56 FOR QQ=1 TO 5:PRINT #-2,USING"%              %    %       %   $####.##      $####.##      $####.##   $####.##";S$(QQ),D$(NE),S(QQ,NE),AP(QQ),HP(QQ),LP(QQ):NEXT
57 PRINT#-2,STRING$(80,"-"):GOTO34
58 CLS0:PRINTUSING"1. %                %           2. %                %           3. %                %           4. %                %           5. %                %";S$(1),S$(2),S$(3),S$(4),S$(5)
59 PRINT@32*6,"MAKE SELECTION AS TO WHICH YOU  WANT DAILY PRICES ON.  PRESS    <ENTER> TO RETURN TO MENU."
60 WQ$=INKEY$:IFWQ$=CHR$(13)THEN34ELSEIFVAL(WQ$)>=1AND VAL(WQ$)<=5 THEN 61 ELSE 60
61 CLS0:KK=VAL(WQ$):PRINT"WHERE DO YOU WANT DATA PRINTED.       1. SCREEN OR                    2. PRINTER"
62 RR$=INKEY$:IFRR$="1"THEN D=0 ELSE IF RR$="2"THEND=-2 ELSE 62
63 CLS0:PRINT"PRESS ENTER WHEN READY TO PRINT DATA."
64 R$=INKEY$:IFR$=CHR$(13)THEN66ELSE64
65 S(KK,0)=S(KK,1)
66 IF D=-2 THEN PRINT#-2,CHR$(15)
67 IFD=-2THENPRINT#-2,USING"DAILY STOCK QUOTES FOR %           %";S$(KK):PRINT#-2,CHR$(14):PRINT#-2,"DATE";TAB(18);"PRICE":PRINT#-2,STRING$(40,"-"):FORPP=1TONE:H=S(KK,PP)-S(KK,PP-1):PRINT#-2,USING"%      %        $####.### +####.###";D$(PP),S(KK,PP),H
68 IF D=-2THENNEXT:GOTO34
69 FOR A=1TO NE STEP14
70 CLS
71 IF NE-A>14THEN QQ=A+14 ELSE QQ=NE
72 FORAA=A TO QQ:PRINTUSING"%       %  $####.### +####.###";D$(AA),S(KK,AA),S(KK,AA)-S(KK,AA-1):NEXT
73 PRINT@32*15,"PRESS <SPACEBAR> TO CONTINUE.";
74 R$=INKEY$:IFR$=" "THEN75ELSE74
75 NEXTA
76 PRINT@32*15,"PRESS A KEY TO RETURN TO MENU.";
77 R$=INKEY$:IFR$=""THEN77ELSE34
78 GOTO78
79 CLS0:INPUT"REWIND DATA TAPE...             PRESS RECORD AND PLAY...        PRESS ENTER WHEN READY...       ";EE$:GOSUB80:GOTO13
80 MOTORON:FORX=1TO2500:NEXT:OPEN"O",-1,DN$
81 PRINT#-1,NE
82 FORQ=1TO5:PRINT#-1,S$(Q):NEXT:FORW=1TONE:PRINT#-1,D$(W),S(1,W),S(2,W),S(3,W),S(4,W),S(5,W):NEXT
83 CLOSE(-1)
84 RETURN
85 CLS:PRINT"1 CREATE NEW FILE":PRINT"2 BACKUP FILE":PRINT"3 RETURN TO MAIN MENU"
86 R$=INKEY$:IFR$="1"THEN87ELSEIFR$="2"THEN92ELSEIFR$="3"THEN13ELSE86
87 INPUT"ENTER FILENAME";DN$:CLS0
88 PRINT"ENTER THE NAMES OF 5 STOCKS TO  BE TRACKED:"
89 PRINT@64,"";:FORX=1TO5:PRINTUSING"STOCK #";X;:INPUTS$(X):NEXT
90 CLS0:FORX=1TO5:PRINTS$(X):NEXT:PRINT@32*11,"ARE THESE CORRECT(Y/N)?";
91 R$=INKEY$:IFR$=""THEN91ELSEIFR$="N"THEN85ELSEIFR$="Y"THEN79
92 CLS:INPUT"HOW MANY DAYS BACK DO YOU WANT  TO SAVE";BD:IF BD>NE OR BD<1 OR BD<>INT(BD) THEN 92ELSECLS0:INPUT"FILE NAME";DN$:DN$=MID$(DN$,1,8)
93 PRINT@96,"";:PRINT"PRESS PLAY AND RECORD. PRESS    <ENTER>";:LINEINPUTR$:MOTORON:FORX=1TO2500:NEXT:MOTOROFF:OPEN"O",-1,DN$:PRINT#-1,BD:FORX=1TO5:PRINT#-1,S$(X):NEXT:FORQ=BD TO1STEP-1:NB=1+NE-Q:PRINT#-1,D$(NB),S(1,NB),S(2,NB),S(3,NB),S(4,NB),S(5,NB)
94 NEXT:CLOSE:GOTO13
95 IF LEN(D$(NE))<>8THEN105
96 FORQ=1TO8:E$(Q)=MID$(D$(NE),Q,1):NEXT
97 IF E$(3)="/"ANDE$(6)="/"THEN98ELSE105
98 IF E$(1)="1"ORE$(1)="0"THEN99ELSE105
99 E=ASC(E$(2)):IFE>=48AND E<=57 THEN100ELSE105
100 FORW=4TO5:E=ASC(E$(W)):IFE>=48ANDE<=57THEN101ELSE105
101 NEXT
102 IF E$(7)="8"ORE$(7)="9"THEN103ELSE105
103 E=ASC(E$(8)):IFE>=48ANDE<=57 THEN 104 ELSE 105
104 RETURN
105 PRINT"ENTER DATE IN FORMAT  MM/DD/YY.":INPUTD$(NE):GOTO95
106 '       VARIABLES:
107 ' D$(X)=DATE    R$=INKEY$
108 ' S(X,Y)=STOCK PRICE FOR
109 '        STOCK X, DATE Y
110 ' S$(X)=STOCK NAME
111 ' DN$=DATA FILE NAME
112 ' NE=NUMBER OF ENTERIES
113 ' HP(X)=HIGH PRICE      FOR
114 ' LP(X)=LOW PRICE   STOCK X
115 ' T(X)=TOTAL OF STOCK 
116 '      PRICES FOR STOCK X
117 ' AP(X)=AVERAGE PRICE FOR
118 '       STOCK X
119 ' LINES 500-680:D=PRINT
120 '                 DEVICE
121 '               KK=STOCK #
122 ' BN= # OF ENTERIES BACK TO
123 '    BE USED IN DATA BACKUP
100 '**************************
101 '*** CHARLES MAY ***
102 '*** RT. 1 BOX 234P ***
103 '*** DESOTO, TX.,75115 ***
104 '*** (214) 223-3767 ***
105 '*************************
110 '
500 '*************************
510 '** INITIALIZE VARIABLES **
520 '*************************
540 CLEAR 1000
550 F1$="QTY                     AMOUNT  RECEIPTS  PROFIT    UNITS"
560 F2$="QTY    UNIT    QTY   QTY  PRSON   UNIT    AMOUNT  PERSONAL   THIS     THIS       ON"
570 F3$=" STOCK NO.      UNIT DESCRIPTION             PURCH   COST   SOLD  RETRN  USE   RETAIL  REFUNDED    USE     ITEM     ITEM     HAND"
580 F4$="%         % %                              % ##### #####.## ##### ##### ##### #####.## #####.## #####.## #####.## #####.##   #####"
590 F5$=STRING$(133,"=")
600 F6$="  FINAL"
610 F7$="INVENTORY    TOTAL     GROSS    RETURNS &   PERSONAL    GROSS"
620 F8$=" BALANCE   PURCHASES  RECEIPTS  ALLOWANCES    USE       PROFIT"
630 F9$=" #####.##  #####.##   #####.##  #####.##    #####.##   #####.##"
890 PRINT#-2,CHR$(27)CHR$(20)
900 GOSUB5000
1000 '************************
1010 '*** ROUTINE SELECTIONS***
1020 '************************
1040 GOSUB 2000
1060 IF Z$="5" THEN 1105
1070 ON VAL(Z$) GOSUB 3000,4000,6000,7000
1080 GOSUB 2000
1100 GOTO 1060
1105 PRINT#-2,CHR$(27)CHR$(19)
1110 CLS:CLOSE#1
1140 PRINT@200,"END OF PROGRAM"
1150 END
2000 '************************
2010 '***   MENU   ***
2020 '*************************
2040 CLS
2050 PRINT TAB(10)"M E N U"
2060 PRINT " "
2070 PRINT "CODE    SELECTION"
2080 PRINT " "
2090 PRINT"1.-ENTER NEW STOCK NUMBERS"
2100 PRINT"2.-INVENTORY ACTIVITY"
2110 PRINT"3.-PRINT REPORT"
2115 PRINT"4.-REVIEW STATUS"
2120 PRINT"5.-EXIT "
2130 PRINT " "
2140 PRINT "ENTER CODE 1-5 :";
2150 Z$=INKEY$: IF Z$=""THEN2150
2160 IF Z$>="1"ANDZ$<="5"THEN 2170 ELSE 2000
2170 RETURN
3000 '*************************
3005 '*** DATA ENTRY ***
3010 '**************************
3020 K=LOF(1):CLS:PRINT"THE LAST RECORD NUMBER WAS ";K
3025 PRINT" "
3030 PRINT "STRIKE ANY KEY TO CONTINUE"
3040 Z$=INKEY$:IFZ$=""THEN3040
3050 CLS:PRINT"ENTER NEW STOCK NUMBER"
3060 LINE INPUT"11 CHARACTERS MAX  "; A$
3070 RN=VAL(RIGHT$(A$,3))
3080 PRINT"ENTER STOCK DESCRIPTION"
3090 LINE INPUT B$
3100 INPUT"NUMBER OF UNITS BOUGHT";C
3110 L=C
3130 INPUT"ENTER UNIT COST";D
3140 INPUT"ENTER UNIT RETAIL PRICE";E
3150 PRINT"ARE ALL ENTRIES CORRECT? Y/N"
3155 Z$=INKEY$:IFZ$=""THEN 3155
3160 IF Z$="N" THEN CLS:GOTO 3080
3170 IF Z$="Y"THEN 3200 ELSE 3150
3200 LSET AA$=A$ :'STOCK NO.
3205 LSET BB$=B$ :'DESCRIPTION
3210 LSET CC$=MKN$(C) :'QTY UNITS
3215 LSET DD$=MKN$(D) :'UNIT COST
3220 LSET EE$=MKN$(E) :'UNIT RETAIL
3225 LSET GG$=MKN$(0) :'PERSONAL USE
3230 LSET HH$=MKN$(0) :'RECEIPTS
3235 LSET JJ$=MKN$(0) :'REFUNDED
3240 LSET KK$=MKN$(0) :'$ AMT PERSONAL
3245 LSET LL$=MKN$(L) :'UNITS ON HAND
3250 LSET PP$=MKN$(P) :'UNIT PROFIT
3255 LSET RR$=MKN$(0) :'QTY RETURN
3260 LSET SS$=MKN$(0) :'QTY SOLD
3280 PUT #1, RN
3290 PRINT"DO YOU HAVE ANOTHER ENTRY Y/N?"
3300 Z$=INKEY$:IFZ$=""THEN3300
3320 IF Z$="Y" THEN 3050
3330 IF Z$="N" THEN 3370 ELSE 3290
3370 RETURN
4000 '*************************
4010 '*** ACTIVITY ***
4020 '*************************
4030 CLS:PRINT"ENTER STOCK NUMBER"
4040 INPUT A$
4050 RN=VAL(RIGHT$(A$,3))
4060 GET #1,RN
4070 A$=AA$:B$=BB$:C=CVN(CC$):D=CVN(DD$):E=CVN(EE$):H=CVN(HH$):J=CVN(JJ$):L=CVN(LL$):S=CVN(SS$):P=CVN(PP$)
4080 CLS:PRINTTAB(10)A$
4090 PRINTB$
4120 PRINTTAB(5)"1.-PURCHASES"
4130 PRINTTAB(5)"2.-SALES"
4140 PRINTTAB(5)"3.-RETURNS/ALLOWANCES"
4150 PRINTTAB(5)"4.-PERSONAL USE"
4160 PRINTTAB(5)"5.-REVIEW ONLY"
4170 PRINTTAB(5)"6.-EXIT TO MAIN MENU"
4180 PRINT" "
4190 PRINT"SELECT 1 - 6"
4200 Z$=INKEY$:IF Z$=""THEN4200
4210 IF Z$>="1"ANDZ$<="6"THEN4215 ELSE 4080
4215 IF Z$="6"THEN 4690
4220 CLS:PRINTTAB(10) A$
4230 PRINT B$
4240 PRINT @64,"QUANTITY UNITS ON HAND ";L
4250 PRINT"TOTAL UNITS BOUGHT ";C
4260 PRINT"UNIT COST ";D
4270 PRINT"UNIT RETAIL ";E
4275 PRINT"NUMBER UNITS SOLD";S
4290 PRINT" ":PRINT" "
4300 ON VAL(Z$) GOTO 4310,4380,4470,4560,4660,4690
4310 INPUT"ENTER NUMBER UNITS BOUGHT ";N
4320 PRINT"ARE YOU SURE? Y/N"
4330 Y$=INKEY$:IFY$=""THEN4330
4340 IF Y$="N"THEN4310ELSEIFY$="Y"THEN4350ELSE4320
4350 C=C+N:L=L+N:N=0
4360 LSET CC$=MKN$(C):LSET LL$=MKN$(L)
4370 GOTO 4650
4380 INPUT"ENTER NUMBER UNITS SOLD ";N1
4390 PRINT"ENTER ACTUAL SALES PRICE PER "
4400 INPUT "UNIT ";N2
4410 PRINT"ARE YOU SURE? Y/N"
4420 Y$=INKEY$:IFY$=""THEN4420
4430 IF Y$="N"THEN4380ELSEIFY$="Y"THEN4435ELSE4410
4435 H=H+(N1*N2): LSET HH$=MKN$(H)
4440 L=L-N1:S=S+N1:P1=(N1*N2)-(D*N1):P=P+P1:P1=0:N1=0:N2=0
4450 LSET LL$=MKN$(L):LSETPP$=MKN$(P):LSET SS$=MKN$(S)
4460 GOTO 4650
4470 INPUT"ENTER NUMBER UNITS RETURNED FOR REFUND";N3
4475 INPUT"HOW MANY CAN BE RESOLD?";N4
4480 PRINT"ENTER AMOUNT YOU REFUNDED"
4485 INPUT N5
4490 PRINT"ARE YOU SURE? Y/N"
4500 Y$=INKEY$:IFY$=""THEN4500
4510 IFY$="N"THEN4470ELSEIFY$="Y"THEN4520ELSE4490
4520 L=L+N4 : R=R+N4 : J=J+N5 : S=S-N4: N4=0: N5=0
4525 LSET LL$=MKN$(L): LSET RR$=MKN$(R) : LSET JJ$=MKN$(J) : LSET SS$=MKN$(S)
4530 GOTO 4650
4560 PRINT"ENTER NUMBER UNITS YOU/YOUR     FAMILY USED OR YOU GAVE AWAY"
4570 INPUT N6
4580 PRINT"ARE YOU SURE?  Y/N"
4590 Y$=INKEY$:IFY$=""THEN4590
4600 IF Y$="N"THEN4560ELSEIFY$="Y"THEN4620ELSE4580
4620 L=L-N6:K=K+(N6*D):G=G+N6:N6=0
4630 LSET LL$=MKN$(L):LSETKK$=MKN$(K):LSET GG$=MKN$(G)
4640 GOTO 4650
4650 PUT #1,RN
4655 N=0:N1=0:N2=0:N3=0:N4=0:N5=0:N6=0
4660 PRINT @ 448,"DO YOU HAVE ANOTHER ACTIVITY?"
4670 Y$=INKEY$:IFY$=""THEN4670
4680 IF Y$="Y"THEN4030ELSEIFY$="N"THEN4690ELSE4660
4690 RETURN
5000 '*************************
5010 '***** OPEN FILES ********
5020 '*************************
5030 OPEN "D",#1,"INV",116
5040 FIELD 1, 11 AS AA$,32 AS BB$,5 AS CC$,8 AS DD$,5 AS SS$,5 AS RR$,5 AS GG$,8 AS EE$,8 AS JJ$,8 AS KK$,8 AS HH$,8 AS PP$,5 AS LL$
5050 RETURN
6000 '*************************
6010 '*** PRINT REPORT ***
6020 '*************************
6030 CLS:PRINT"ENTER TODAY'S DATE"
6040 PRINT"EXAMPLE:  JANUARY 10, 1988"
6050 LINE INPUT W$
6055 W$="INVENTORY REPORT FOR PERIOD ENDING "+W$
6060 W=LEN(W$):W1=INT((133-W)/2)
6070 CLS:PRINT"I'M WORKING ON YOUR REPORT"
6080 PRINT#-2,TAB(W1)W$
6090 PRINT#-2," "
6100 PRINT#-2,TAB(73)F1$
6105 BI=BI+(L*D)
6110 PRINT#-2,TAB(46)F2$
6120 PRINT#-2,F3$
6130 PRINT#-2," "
6140 FOR RN=1TOLOF(1):GET#1,RN
6150 A$=AA$ : B$=BB$ : C=CVN(CC$) : D=CVN(DD$) : E=CVN(EE$) : G=CVN(GG$) : H=CVN(HH$) : J=CVN(JJ$) : K=CVN(KK$) : L=CVN(LL$) : P=CVN(PP$) : R=CVN(RR$) : S=CVN(SS$)
6160 PRINT#-2,USINGF4$;A$;B$;C;D;S;R;G;E;J;K;H;P;L
6170 TI=TI+(L*D):'FINAL INV.BAL.
6180 PR=PR+(C*D):'TOTAL PURCH
6190 TH=TH+H:'GROSS RECEIPTS
6200 TJ=TJ+J:'TOTAL RET/ALLOW
6210 TK=TK+K:'TOTAL PERSONAL USE
6220 TP=TP+P:'GROSS PROFIT
6230 NEXT RN
6240 PRINT#-2,F5$
6250 PRINT#-2," "
6260 PRINT#-2,TAB(55)"G R A N D   T O T A L S"
6270 PRINT#-2,F6$
6280 PRINT#-2,F7$
6290 PRINT#-2,F8$
6300 PRINT#-2,USINGF9$;TI;PR;TH;TJ;TK;TP
6990 RETURN
7000 '**************************
7010 '*** REVIEW STATS ***
7020 '**************************
7030 CLS:INPUT"ENTER STOCK NUMBER ";RN
7040 GET #1,RN
7050 A$=AA$ : B$=BB$ : C=CVN(CC$) : D=CVN(DD$) : E=CVN(EE$) : G=CVN(GG$) : H=CVN(HH$) : J=CVN(JJ$) : K=CVN(KK$) : L=CVN(LL$) : P=CVN(PP$) : R=CVN(RR$) : S=CVN(SS$)
7060 CLS:PRINTTAB(10)A$
7070 PRINTB$
7080 PRINT@64,"QTY. UNITS PURCHASED ";C
7090 PRINT"UNIT COST ";D
7100 PRINT"QTY. UNITS SOLD ";S
7110 PRINT"UNITS RETURNED ";R
7120 PRINT"UNITS PERSONAL USE ";G
7130 PRINT"RETAIL PRICE ";E
7140 PRINT"$ AMOUNTS REFUNDED ";J
7150 PRINT"$ AMOUNTS PERSONAL USE ";K
7160 PRINT"TOTAL RECEIPTS ";H
7170 PRINT"TOTAL PROFIT ";P
7180 PRINT"UNITS ON HAND ";L
7190 PRINT"ANOTHER REVIEW? Y/N"
7200 Y$=INKEY$:IFY$=""THEN7200
7210 IF Y$="Y"THEN7000ELSEIFY$="N"THEN7220ELSE7190
7220 RETURN
