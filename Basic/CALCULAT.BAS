0 ' COPYRIGHT 1990 FALSOFT, INC
1 '   ********************
2 '   *     CALCULAT     *
3 '   *        BY        *
4 '   *    BILL MINZE    *
5 '   *  BROOKLYN  N.Y.  *
6 '   ********************
7 X=PEEK(65314):IF INT(X/2)=X/2 THEN X$="Y"
8 IF X$="Y" THEN 14:CLS
9 CLS:PRINT@128,"**THERE IS NO PRINTER ON LINE***"
10 PRINT@192,"*** ARE YOU USEING A PRINTER ***"
11 PRINT@267,"YES OR NO"
12 X$=INKEY$:IF X$=""THEN 12
13 IF X$="Y" OR X$="N"THEN 14 ELSE PRINT@327,"ANSWER WITH Y OR N":GOTO 12
14 CLS
15 PRINT@434,"S         0.00"
16 PRINT@480,"E         0.00"
17 N$="########.##":GOTO 34
18 Z$=INKEY$:IF Z$=""THEN18
19 IF Z$=CHR$(92)THEN81
20 IF Z$>CHR$(47) AND Z$<CHR$(58)THEN 71
21 IF Z$=CHR$(46)THEN71
22 IF Z$=CHR$(43) OR Z$=CHR$(59) THEN RETURN
23 IF Z$=CHR$(45) OR Z$=CHR$(47)THEN RETURN
24 IF Z$=CHR$(42) OR Z$=CHR$(58)THEN RETURN
25 IF Z$=CHR$(61) OR Z$=CHR$(13) THEN58
26 IF Z$=CHR$(67)THEN64
27 IF Z$=CHR$(12) THEN 68
28 PRINT@0,"*USE NUMBERS OR MATH SIGNS ONLY*":SOUND100,10:SOUND150,10:GOTO 18
29 IF Z$=CHR$(43) OR Z$=CHR$(59)THEN 34
30 IF Z$=CHR$(45) THEN 40
31 IF Z$=CHR$(42) OR Z$=CHR$(58)THEN46
32 IF Z$=CHR$(47) THEN 52
33 IF Z$=CHR$(61) OR Z$=CHR$(13)THEN59
34 GOSUB 18:ST=ST+P
35 PRINT@434,"S":PRINT@437,USING N$;ST
36 IF C=0 THENPRINT@384,"L+":PRINT@387,USING N$;P ELSE PRINT@384,""
37 PRINT@480,"E         0.00"
38 IF X$="Y"THEN PRINT#-2,USING N$;P;:PRINT#-2," +"
39 C=0:P=0:Z0$="0":B=0:GOTO 29
40 GOSUB 18:ST=ST-P
41 PRINT@434,"S":PRINT@437,USING N$;ST
42 IF C=0 THEN PRINT@384,"L-":PRINT@387,USING N$;P ELSE PRINT@384,""
43 PRINT@480,"E         0.00"
44 IF X$="Y"THEN PRINT#-2,USING N$;P;:PRINT#-2," -"
45 C=0:P=0:Z0$="0":B=0:GOTO 29
46 GOSUB 18:ST=ST*P
47 PRINT@434,"S":PRINT@437,USING N$;ST
48 IF C=0 THEN PRINT@384,"L*":PRINT@387,USING N$;P ELSE PRINT@384,""
49 PRINT@480,"E         0.00"
50 IF X$="Y"THEN PRINT#-2,USING N$;P;:PRINT#-2," *"
51 C=0:P=0:Z0$="0":B=0:GOTO 29
52 GOSUB 18:IF P=0 THEN GOTO 79
53 ST=ST/P:PRINT@434,"S":PRINT@437,USING N$;ST
54 IF C=0 THEN PRINT@384,"L/":PRINT@387,USING N$;P ELSE PRINT@384,""
55 PRINT@480,"E         0.00
56 IF X$="Y"THEN PRINT#-2,USING N$;P;:PRINT#-2," /"
57 C=0:P=O:Z0$="0":B=0:GOTO 29
58 RETURN
59 PRINT@434,"T":PRINT@437,USING N$;ST
60 PRINT@384,"T":PRINT@387,USING N$;ST
61 PRINT@480,"E         0.00"
62 IF X$="Y"THEN PRINT#-2,USING N$;ST;:PRINT#-2," T"CHR$(10)
63 P=0:Z0$="0":C=1:B=0:GOTO 34
64 PRINT@434,"S":PRINT@437,USING N$;ST
65 PRINT@384,""
66 PRINT@480,"E         0.00"
67 Z0$="0":P=0:C=1:GOTO 18
68 P=0:Z0$="0":ST=0:C=0:B=0
69 IF X$="Y"THEN PRINT#-2,"     CLEAR"CHR$(10):PRINT#-2,USING N$;P;:PRINT#-2," T"
70 GOTO14
71 IF C=1 THEN 76
72 IF Z$=CHR$(46) THEN B=0
73 B=B+1:IF B=9 THEN PRINT@0,"*ONLY 8 DIGETS LEFT OF DECIMAL*":SOUND100,10:SOUND150,10:GOTO 64
74 Z0$=Z0$+Z$:P=VAL(Z0$)
75 PRINT@448,"E":PRINT@451,USING N$;P:GOTO 18
76 PRINT@0,"  USE MATH SIGN FIRST + - * /"
77 SOUND100,10:SOUND150,10:C=0
78 GOTO18
79 PRINT@0,"YOU CAN NOT DEVIDE BY ZERO ENTER  NUMBER <1> NOW TO SAVE YOUR    PREVIOUS ENTRIES, AND CONTINUE"
80 SOUND100,10:SOUND150,10:GOTO52
81 IFX$="Y"THEN X$="N"ELSEX$="Y"
82 GOTO 18
