10 REM"DEWEY DECIMAL SYSTEM"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1987
30 PR$=STRING$(32,255)
40 DIM A$(10),B$(10),C$(10),D$(21),E$(21)
50 FOR T=1 TO 10:READ A$(T),B$(T),C$(T):NEXT T
60 FOR T=1 TO 21:READ D$(T),E$(T):NEXT T
70 CLS5
80 N=N+1:IF N>10 THEN 210
90 FOR T=1 TO 10:PRINTTAB(0)A$(T);"."TAB(3)B$(T)TAB(13)C$(T):NEXT T
100 PRINT@320,PR$;
110 PRINT@352,"  WHERE CAN WE LEARN MORE ABOUT"
120 R=RND(21)
130 PRINT@384,D$(R)
140 PRINT@412,"? ";:LINEINPUT AN$
150 PRINT@448,PR$;
160 IF AN$=E$(R) THEN PRINT@425,"CORRECT";:CR=CR+1
170 IF AN$<>E$(R) THEN PRINT@421,"SORRY,"E$(R)" IS CORRECT";
180 PRINT@484,"press enter to go on";
190 EN$=INKEY$
200 IF EN$=CHR$(13) THEN 70 ELSE 190
210 CLS:PRINT"    DEWEY DECIMAL SYSTEM"
220 PRINT:PRINT"YOUR SCORE WAS "CR*10"% THIS TIME."
230 PRINT:PRINT"       PRESS G TO GO AGAIN            OR E TO END THE GAME."
240 EN$=INKEY$
250 CR=0:N=0
260 IF EN$="G" THEN RUN ELSE IF EN$="E" THEN END ELSE 240
270 DATA A,000-999,GENERAL WORKS,B,100-199,PHILOSOPHY,C,200-299,RELIGION,D,300-399,SOCIAL SCIENCES
280 DATA E,400-499,LANGUAGE,F,500-599,PURE SCIENCE,G,600-699,APPLIED SCIENCE,H,700-799,FINE ARTS
290 DATA I,800-899,LITERATURE,J,900-999,HISTORY AND TRAVEL
300 DATA THE FRENCH LANGUAGE,E,BONGO DRUMS,H,THE STORY OF THE U.S.FLAG,J
310 DATA WAYS OF THINKING,B,ALMANACS,A,BIBLE STORIES,C
320 DATA STORIES ABOUT GODDESSES,C,WASHINGTON'S LIFE STORY,J,TURTLES AND TORTISES,F
330 DATA POETRY OF THE 1800'S,I,SENDING SOMEONE TO THE MOON,G,ATOMIC ENERGY,G
340 DATA BRAZIL'S HISTORY,J,TORNADOES,F,HOW TO MAKE PUPPETS,H
350 DATA HISTORY OF ANCIENT ROME,J,THE CARE OF PETS,G,CHEMISTRY EXPERIMENTS,F
360 DATA FAMOUS FAIRY TALES,D,REFERENCE BOOKS,A,HISTORY OF TELEVISION,G
