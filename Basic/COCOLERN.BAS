0 '***********************
1 'LEARN COCO LEARN 
2 'BY BILL ENGLISH
3 '   R. R. 1 BOX 407
4 '   GREENFIELD, IN 46140
5 '***********************
20 CLEAR 1000,30000
25 R=RND(-TIMER)
30 E=30000:L=E:FOR X=L TO L+100:POKE X,0:NEXT X:MU$="O3L100A":MO$="L32G"
40 GOSUB 2560:'INITIAL QUESTIONS
50 GOSUB 2440:'DRAW GAME BOARD
60 GOTO 740
70 '*********
80 'STORE MOVES
90 NM=MN:IF WF=-1 THEN PB=4 ELSE PB=-4
100 CC=0:ML=L:KM(0)=0:FL=0
110 IF CC=NM THEN RETURN
120 P=PEEK(ML)
130 IF P<>M(CC) THEN 200
140 KM(CC)=ML
150 IF CC/2=INT(CC/2) THEN NE=2 :GOTO 180
160 NE=3
170 MZ=ML:GOSUB 640:'STORE PROB
180 ML=ML+NE:CC=CC+1
190 GOTO 110
200 PZ=PEEK(ML+1)
210 IF PZ=0 THEN KI=ML:KM(CC)=ML:GOTO 240
220 ML=ML+PZ
230 GOTO 120
240 KK=CC
250 P=PEEK(KM(KK)+1)
260 IF P<>0 THEN 300
270 KK=KK-1
280 IF KK<0 THEN KM=E:FL=E:GOSUB 530:RETURN
290 GOTO 250
300 FL=P+KM(KK)
310 EL=0
320 FOR X=CC TO NM-1
330 IF X/2=INT(X/2) THEN NE=2 ELSE NE=3
340 EL=EL+NE
350 NEXT X
360 FOR X=E-1 TO FL STEP -1
370 PK=PEEK(X)
380 POKE X+EL,PK
390 NEXT X
400 KM=FL
410 FOR X=FL-1+EL  TO FL STEP -1: POKE X,0:NEXT X
420 '********
430 GOSUB 530
440 BG=L
450 IF BG>=KI THEN 520
460 PZ=PEEK(BG+1)
470 IF PZ+BG<FL THEN 490
480 POKE BG+1,PZ+EL
490 IF PEEK(BG)>100 THEN NE=3 ELSE NE=2:'FOUND COMPUTER MOVE
500 BG=BG+NE
510 GOTO 450
520 RETURN
530 FOR X=CC TO NM-1
540 POKE KM,M(X)
550 POKE KM+1,0
560 IF X/2=INT(X/2) THEN NE=2:GOTO 590
570 NE=3
580 MZ=KM:GOSUB 640:'STORE PROB
590 KM=KM+NE
600 E=E+NE
610 NEXT X
620 POKE KI+1,FL-KI
630 RETURN
640 'STORE PROBABILITY
650 PM=PEEK(MZ+2)
660 IF PM=1 AND PB=-4 THEN 710
670 IF PM=0 THEN PM=5
680 PM=PM+PB
690 IF PM<0 THEN PM=1
700 IF PM>99 THEN PM=99
710 POKE MZ+2,PM
720 RETURN
730 '**************
740 'INITIALIZATION FOR EACH GAME
750 PRINT@386,USING"###";TC;:PRINT@410,USING"###";TH;
760 PRINT@483,USING"###";ZC;:PRINT@507,USING"###";ZH;
770 FOR KD=7 TO 9:GOSUB 1980:CL=159:GOSUB 2020:NEXT KD
780 FOR KD=4 TO 6:GOSUB 1980:CL=143:GOSUB 2020:NEXT KD
790 FOR KD=1 TO 3:GOSUB 1980:CL=175:GOSUB 2020:NEXT KD
800 FOR P=1 TO 9:P(P)=INT((P+2)/3)-2:NEXT P
810 WF=0:MN=0:'WIN FLAG AND MOVE NUMBER
815 SCREEN 0,1:FOR JZ=1 TO 10:PLAY"L255GEC":NEXT JZ:SCREEN 0,0
820 'GET HUMAN MOVE
830 H1=254:H2=286
840 IF HR=1 THEN PRINT@153,"RANDOM";
850 IF HR=0 THEN PRINT@153,"HUMAN";
855 PRINT@185,CHR$(159)"YOUR"CHR$(159);
860 PRINT@217,CHR$(159)"MOVE"CHR$(159);
870 PRINT@H1,"";
880 PRINT@H2,"";
890 PRINT@H1,CHR$(191);:PRINT@H2,CHR$(191);
900 IF HR=0 THEN 910 ELSE PA=RND(6)+3:A$=STR$(PA):GOTO 920
910 A$=INKEY$:IF A$="" THEN 910
920 HF=VAL(A$)
930 IF HR=0 THEN PLAY MU$
940 PRINT@H1,RIGHT$(A$,1);
950 IF P(HF)<>1 THEN 870
960 PRINT@H2,"";
970 IF HR=0 THEN 980 ELSE PA=RND(6):A$=STR$(PA):GOTO 990
980 A$=INKEY$:IF A$="" THEN 980
990 HT=VAL(A$)
1000 IF HR=0 THEN PLAY MU$
1010 PRINTRIGHT$(A$,1);
1020 IF HF=7 AND HT=3 THEN 870
1030 IF HF=6 AND HT=4 THEN 870
1040 IF HF-HT=3 AND P(HT)=0 THEN 1070
1050 IF (HF-HT=4 OR HF-HT=2) AND P(HT)=-1 THEN 1070
1060 GOTO 870
1070 GOSUB 1410:REM MAKE MOVE
1080 PRINT@217,"      ";:PRINT@185,"      ";
1090 P(HF)=0:P(HT)=1:'UPDATE GAME BOARD
1100 M(MN)=10*HF+HT:'STORE MOVE AWAY
1110 MN=MN+1
1120 IF MN>1 THEN GOSUB 1600:'CHECK FOR WINNER
1130 IF WF<>0 THEN 740:'IF WINNER START OVER
1140 'COMPUTER MOVES AT RANDOM
1150 GOSUB 2080:'SEE IF GAME IN MEMORY
1160 C1=230:C2=262
1170 PRINT@194,"MOVE";
1180 IF RF=1 THENCF=RND(6):PRINT@129,"RANDOM";
1190 IF RF<>1 THEN PRINT@129,"MEMORY";:PLAY"L255EC"
1200 IF P(CF)<>-1 THEN 1180
1210 CF$=STR$(CF)
1220 PRINT@C1,RIGHT$(CF$,1);
1230 IF RF=1 THEN CT=RND(6)+3
1240 IF CT=7 AND CF=3 THEN 1230
1250 IF CT=6 AND CF=4 THEN 1180
1260 IF CT-CF=3 AND P(CT)=0 THEN 1300
1270 IF (CT-CF=4 OR CT-CF=2) AND P(CT)=1 THEN 1300
1280 GOTO 1180
1290 PLAY MU$
1300 CT$=STR$(CT)
1310 PRINT@C2,RIGHT$(CT$,1);
1320 PLAY MU$
1330 GOSUB1520:'MAKE MOVE
1340 PRINT@194,"    ";
1350 P(CF)=0:P(CT)=-1
1360 M(MN)=10*CF+CT+100
1370 MN=MN+1
1380 IF MN>1 THEN GOSUB 1750:'CHECK FOR WINNER
1390 IF WF<>0 THEN 740
1400 GOTO 820
1410 'MAKE HUMAN MOVE
1420 FOR Q=1 TO ZT:NEXT Q:PLAY MO$
1430 KD=HF:GOSUB 1980
1440 CL=143
1450 GOSUB 2020
1460 KD=HT
1470 FOR Q=1 TO ZT:NEXT Q:PLAY MO$
1480 GOSUB1980
1490 CL=159
1500 GOSUB 2020
1510 RETURN
1520 'MAKE COMPUTER MOVE
1530 FOR Q=1 TO ZT:NEXT Q:PLAY MO$
1540 KD=CF:GOSUB1980
1550 CL=143:GOSUB2020
1560 FOR Q=1 TO ZT:NEXT Q:PLAY MO$
1570 KD=CT:GOSUB1980
1580 CL=175:GOSUB2020
1590 RETURN
1600 'CHECK FOR WINNER HUMAN
1610 'BLOCKED MOVE
1620 IF P(1)=-1  AND P(3)=-1  AND P(5)=-1  AND P(4)=1  AND P(6)=1 AND P(8)=1   AND P(2)=0 AND P(7)=0 AND P(9)=0 THEN 1690
1630 IF P(2)=-1 AND P(5)=1 AND P(1)=0 AND P(3)=0 AND P(4)=0 AND P(6)=0 AND P(7)=0 AND P(8)=0 AND P(9)=0  THEN 1690
1640 'CHECK FOR OUTRIGHT WIN
1650 FOR P=1 TO 3
1660 IF P(P)=1 THEN 1690
1670 NEXT P
1680 GOTO 1740
1690 FOR XZ=1 TO 20:PRINT@89,"WINNER";:PLAY"L100A":PRINT@89,"      ";:NEXT XZ
1700 TH=TH+1
1710 ZH=ZH+1:ZC=0
1720 WF=1
1730 GOSUB 80:'STORE GAME AWAY
1740 RETURN
1750 'CHECK FOR COMPUTER WIN
1760 'BLOCK MOVE
1770 IF P(1)=-1 AND P(5)=-1 AND P(4)=1 AND P(8)=1 AND P(2)=0 AND P(3)=0 AND P(6)=0 AND P(7)=0 AND P(9)=0 THEN 1920
1780 IF P(4)=-1 AND P(5)=-1 AND P(1)=0 AND P(2)=0 AND P(3)=0 AND P(6)=0 AND P(7)=0 AND P(8)=0 AND P(9)=0 THEN 1920
1790 IF P(5)=-1 AND P(6)=-1 AND P(1)=0 AND P(2)=0 AND P(3)=0 AND P(4)=0 AND P(7)=0 AND P(8) =0 AND P(9) =0 THEN 1920
1800 IF P(2)=-1  AND P(3)=-1 AND P(5)=1  AND P(6)=1  AND P(1)=0 AND P(4)=0 AND P(7)=0 AND P(8)=0 AND P(9)=0  THEN 1920
1810 IF P(1)=-1 AND P(2)=-1  AND P(4)=1  AND P(5)=1 AND P(3)=0 AND P(6)=0 AND P(7)=0 AND P(8)=0 AND P(9)=0 THEN 1920
1820 IF P(1)=-1 AND P(6)=-1  AND P(4)=1 AND P(9)=1   AND P(2)=0 AND P(3)=0 AND P(5)=0 AND P(7)=0 AND P(8)=0   THEN 1920
1830 IF P(3)=-1 AND P(4)=-1  AND P(7)=1 AND P(6)=1 ANDP(1)=0 AND P(2)=0 AND P(5)=0 AND P(8)=0 AND P(9)=0 THEN 1920
1840 IF P(2)=-1 AND P(4)=-1 AND P(5)=1 AND P(7)=1 AND P(1)=0 AND P(3)=0 AND P(6)=0 ANDP(8)=0 AND P(9)=0 THEN 1920
1850 IF P(2)=-1 AND P(6)=-1  AND P(5)=1 AND P(9)=1ANDP(1)=0 AND P(3)=0 AND P(4)=0 ANDP(7)=0ANDP(8)=0  THEN 1920
1860 IF P(1)=-1 AND P(6)=-1 AND P(4)=1 AND P(9)=1 AND P(2)=0 AND P(3)=0 AND P(5)=0 AND P(7)=0 AND P(8)=0 THEN 1920
1870 IF P(3)=-1 AND P(5)=-1 AND P(6)=1 AND P(8)=1 AND P(1)=0 AND P(2)=0 AND P(4)=0 AND P(7)=0 AND P(9)=0 THEN 1920
1880 FOR P=7TO 9
1890 IF P(P)=-1 THEN 1920
1900 NEXT P
1910 GOTO 1970
1920 FOR XZ=1 TO 20:PRINT@65,"WINNER";:PLAY"L100;E":PRINT@65,"      ";:NEXT XZ
1930 WF=-1
1940 ZC=ZC+1:ZH=0
1950 GOSUB 80:'STORE GAME AWAY
1960 TC=TC+1
1970 RETURN
1980 RO=INT((KD-1)/3):'COMPUTE ROW(0 TO 2)
1990 IF KD/3=INT(KD/3) THEN CO=3 ELSE IF (KD+1)/3=INT((KD+1)/3) THEN CO=2 ELSE CO=1
2000 R=4*RO+4:C=5*(CO-1)+9:'COMPUTE PROPER ROW AND COLUMN TO DRAW PIECE IN
2010 RETURN
2020 'DRAW ACTUAL MOVE 
2030 FOR R1=R TO R+1
2040 FOR C1=C TO C+3
2050 PRINT@R1*32+C1,CHR$(CL);
2060 NEXT C1,R1
2070 RETURN
2080 'COMPUTER MOVE BY LEARNING
2090 NM=MN:CC=0:ML=L
2100 P=PEEK(ML)
2110 IF P=0 THEN RF=1:RETURN:'NO MOVES STORED
2120 IF P=M(CC) THEN 2180
2130 PZ=PEEK(ML+1)
2140 IF PZ=0 THEN RF=1:RETURN:'MOVE NOT STORED
2150 ML=ML+PZ
2160 IF ML>E THEN RF=1:RETURN:'SET RANDOM MOVE FLAG
2170 GOTO 2100
2180 IF CC/2=INT(CC/2) THEN NE=2 ELSE NE=3
2190 ML=ML+NE
2200 CC=CC+1
2210 IF CC=NM THEN 2230
2220 GOTO 2100
2230 K=0:TP=0
2240 SM(K)=PEEK(ML):SP(K)=PEEK(ML+2)
2250 P=PEEK(ML+1)
2260 IF P=0 THEN 2300
2270 K=K+1
2280 ML=ML+P
2290 GOTO 2240
2300 FOR X=0 TO K:TP=TP+SP(K):NEXT X:'ADD UP PROBS
2310 RP=RND(TP):IF TP<3 THEN RF=1:RETURN:'IF NOT MUCH TO CHOOSE FROM GO BACK TO PICK MOVE AT RANDOM
2320 TP=0
2330 FOR X=0 TO K
2340 TP=TP+SP(K)
2350 IF TP>RP THEN 2370
2360 NEXT X
2370 CM=SM(K)-100:CF=INT(CM/10):CT=CM-10*CF
2380 RF=-1:RETURN:'SET RANDOM FLAG TO MEMORY
2390 REM STORE GAME AWAY
2400 NM=MN
2410 IF WF=-1 THEN PB=4 ELSE PB=-4:'CHOOSE PROBABILITY
2420 GOSUB 80
2430 RETURN
2440 'DRAW BOARD
2450 CLS
2460 B$=STRING$(16,128):M$="MOVE":F$="FROM":T$="TO":TT$="TOTAL":W$="WINS"
2470 C$=STRING$(8,175):O$=STRING$(8,159):PRINT@56,O$;:PRINT@24,O$;:PRINT@32,C$;:PRINT@0,C$;
2480 PRINT@40,"LEARN COCO LEARN";:PRINT@8,STRING$(8,169);:PRINT@16,STRING$(8,153);
2490 PRINT@72,B$;:PRINT@200,B$;:PRINT@328,B$;:PRINT@456,B$;
2500 FOR R=2 TO 13:FOR C=8 TO 23 STEP 5:PRINT@R*32+C,CHR$(128);:NEXT C,R
2510 FOR R=3 TO 11 STEP 4:FOR C=10 TO 20 STEP 5:I=I+1:PRINT@R*32+C,I;:NEXT C,R
2520 PRINT@98,"COCO";:PRINT@120,"OPPONENT";:PRINT@225,F$;:PRINT@249,F$;
2530 PRINT@259,T$;:PRINT@283,T$;:PRINT@322,TT$;:PRINT@346,TT$;:PRINT@354,W$;:PRINT@378,W$;
2540 PRINT@488,"CONSECUTIVE WINS";
2550 RETURN
2560 CLS
2570 PRINT@40,"LEARN COCO LEARN";
2580 PRINT:PRINT"DO YOU WISH TO PLAY AGAINST THE COMPUTER YOURSELF OR TO WATCH     THE GAME PLAYED AT RANDOM"
2590 PRINT"     TYPE H FOR HUMAN GAME           OR R FOR RANDOM GAME"
2600 PRINT@235,"H OR R";
2610 Z$=INKEY$:IF Z$="" THEN 2610
2620 IF Z$="H" THEN HR=0 ELSE IF Z$="R" THEN HR=1 ELSE 2600
2630 PRINT"  ";:IF Z$="H" THEN PRINT"HUMAN" ELSE PRINT"RANDOM"
2640 PRINT:PRINT"    SELECT THE SPEED OF PLAY    (1 THRU 5, 1 IS FAST 5 IS SLOW)"
2650 PRINT@363,"1 THRU 5";
2660 Z$=INKEY$:IF Z$="" THEN 2660
2670 Z=VAL(Z$)
2680 IF Z>5 OR Z<1 THEN 2650 ELSE Z=INT(Z)
2690 PRINT"  ";Z
2700 ZT=(Z-1)*80
2710 FOR ZZ=1 TO 1000:NEXT ZZ
2720 RETURN
