1 'SPACE KAMIKAZE
2 'WRITTEN BY T.C. TAULLI
3 'COPYRIGHT (C) APRIL 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 CLEAR 2000:DIM T(200),H(200),R(200),XX$(40),LA(200):Y$=CHR$(128):U=0
20 CLS5
30 DATA161,162,163,165,170,175
40 FOR X=1 TO 6:READ Q(X):NEXT X
50 FOR X=0 TO 10:PRINT@X*32,STRING$(32,128);:NEXTX
60 FOR X=1 TO 32:XX$=XX$+CHR$(Q(RND(6))):NEXTX
70 PRINT@320,XX$;
80 N$=CHR$(155):B=134
90 PRINT@B,N$;
100 PRINT@384,"HIT(S)";
110 PRINT@448,"DAMAGE";
120 PRINT@416,"SCORE";
130 GOSUB 610
140 FOR X=1 TO 200
150 C=PEEK(65280)
160 IF RND(7)=1 THEN G=G+1:T(G)=(RND(9)*32)-1
170 FOR Y=1+U TO G
180 C=PEEK(65280)
190 IF T(Y)=0 THEN 420
200 IF H(Y)=7 THEN PRINT@LA(Y),Y$;:T(Y)=0:U=U+1:GOTO 420
210 IF PEEK(1024+T(Y))=159 THEN GAME=1
220 PRINT@LA(Y),Y$;:PRINT@T(Y),CHR$(162);:LA(Y)=T(Y):T(Y)=T(Y)-5:H(Y)=H(Y)+1
230 IF C=254 OR C=126 THEN GOSUB 440
240 IF RND(4)=1 THEN T(Y)=T(Y)+32:GOTO 260
250 IF RND(4)=1 THEN T(Y)=T(Y)-32
260 IF T(Y)>320 THEN T(Y)=320
270 IF T(Y)<0 THEN PRINT@LA(Y),Y$;:T(Y)=0:U=U+1
280 IF PEEK(1024+B)=162 THEN DA=DA+1:PRINT@448,"DAMAGE";DA;:PLAY"T222;O5;V31;GF"
290 IF DA=5 THEN 560
300 XX$=XX$+CHR$(Q(RND(6)))+CHR$(Q(RND(6)))+CHR$(Q(RND(6)))+CHR$(Q(RND(6)))+CHR$(Q(RND(6)))
310 XX$=MID$(XX$,5,32):PRINT@320,XX$;
320 J1=INT(JOYSTK(0)/31.5):J2=INT(JOYSTK(1)/31.5)
330 C=PEEK(65280)
340 IF J2=0 THEN PRINT@B,CHR$(128);:B=B-32:GOTO 360
350 IF J2=2 THEN PRINT@B,CHR$(128);:B=B+32
360 IF B<0 THEN B=6
370 IF B>320 THEN B=294
380 IF PEEK(1024+B)=162 THEN DA=DA+1:PRINT@448,"DAMAGE";DA;:PLAY"T222;O5;V31;GF"
390 IF DA=5 THEN 560
400 PRINT@B,N$;
410 IF C=254 OR C=126 THEN GOSUB 440
420 NEXT Y,X
430 GOTO 140
440 E=0
450 E=E+1
460 IF E=4 THEN 490
470 IF PEEK(1024+B+(E*5))=162 THEN 490
480 GOTO 450
490 PRINT@B+1,STRING$(E*5,179);
500 PLAY"T222O5V31B"
510 FOR L=1+U TO G
520 IF T(L)=B+(E*5)-5 THEN T(L)=0:HITS=HITS+1:PRINT@390,HITS;:PRINT@421,HITS*10;:GOTO 540
530 NEXT L
540 PRINT@B+1,STRING$(E*5,128);
550 RETURN
560 PRINT@171,"game";:PRINT@176,"over";
570 PRINT@265,"press";:PRINT@272,"button";
580 FOR HY=1 TO 500:NEXT HY
590 C=PEEK(65280):IF C=254 OR C=126 THEN RUN
600 GOTO 590
610 PRINT@265,"press";:PRINT@272,"button";
620 C=PEEK(65280):IF C=254 OR C=126 THEN 640
630 GOTO 620
640 PRINT@265,STRING$(14,128);
650 RETURN
