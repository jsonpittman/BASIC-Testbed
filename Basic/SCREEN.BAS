1 'SCREEN SETUP UTILITY
2 'WRITTEN BY JIM KNOPPOW
3 'COPYRIGHT (C) DECEMBER 1991
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
6 '*****************************
10 O=126:CLS:PRINT@134,"SCREEN DEFRUSTRATOR":PRINT@168,"BY JIM KNOPPOW":PRINT@229,"PRESS BREAK TO RETURN TO":PRINT@263,"MENU AT ANY TIME":FOR X=1TO900:NEXT:CLS:ON BRK GOTO20
20 WIDTH32:CLS:PRINT@14,"MENU":PRINT@66,"1. STOP CURSOR BLINK":PRINT@98,"2. CURSOR BLINK RATE":PRINT@130,"3. SCREEN COLORS":PRINT@162,"4. CURSOR TYPE":PRINT@194,"5. BORDER COLOR":PRINT@226,"6. SEE VALUES CHOSEN":PRINT@258,"7. ORIGINAL SCREEN
30 G=VAL(INKEY$):IFG>7 OR G<1 THEN 30 ELSE ON G GOSUB 50,60,70,80,90,100,110
40 GOTO20
50 WIDTH40:POKE&HF77F,&H12:PRINT"POKE &HF77F, &H12. DEFAULT VALUE= 7": PRINT"PRESS ANY KEY TO RETURN":EXEC44539:RETURN
60 WIDTH40:POKE&HF77F,7:INPUT"FEED ME A NUMBER FROM 1 TO 255";S:POKE&HF78C,S:LINE INPUT"HIT ENTER TO RETURN";D$:RETURN
70 WIDTH40:CLS:FORX=0TO255:POKE&HFE08,X:CLS:PRINT"TESTING POKE &HFE08, ";X;" DEFAULT=0":PRINT"HIT ENTER":EXEC44539:NEXT:RETURN
80 WIDTH40:CLS:FORX=0TO255:POKE&HF7A3,X:CLS:PRINT"TESTING POKE&HF7A3,";X;" DEFAULT=64":INPUT"PRESS ENTER";D$:NEXT:RETURN
90 WIDTH40:CLS:FORX=0TO63:POKE&HFF9A,X:O=X:CLS:PRINT"TESTING POKE &HFF9A,";X;" DEFAULTS ALWAYS TO 126 WHEN CHANGING WIDTH":PRINT"WORKS ONLY WHILE RUNNING PROGRAMS":INPUT"PRESS ENTER";D$:NEXT:RETURN
100 WIDTH40:CLS:H=PEEK(&HF78C):I=PEEK(&HFE08):J=PEEK(&HF7A3):PRINT"BLINK  F78C=";H:PRINT"SCREEN FE08=";I:PRINT"CURSOR F7A3=";J:PRINT"BORDER FF9A=";O:PRINT"QUIT PROGRAM":INPUT"TYPE <Y>ES OR <N>O";X$:IF X$="Y" OR X$="N"GOTO120ELSEGOTO100
110 WIDTH40:POKE&HF77F,7:POKE&HF78C,11:POKE&HFE08,0:POKE&HFF9A,126:POKE&HF7A3,64:RETURN
120 IF X$="Y" THEN END ELSE RETURN
