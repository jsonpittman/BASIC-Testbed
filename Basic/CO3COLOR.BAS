5 DIM IN(3,3):POKE65497,0:RD=3:GN=3:BL=3
10 PALETTE RGB:RR=36:RG=54:GG=18:BR=45:AL=63:GB=27:BB=9
20 HCOLOR11,8:HSCREEN2
30 HBUFF1,908:HGET(0,184)-(200,192),1
40 HBUFF2,98:HGET(0,0)-(20,8),2:HBUFF3,1025 : HGET (0,72)-(80,96),3
50 HCIRCLE(159,60),80,,.75:HCIRCLE(239,60),80,,.75:HCIRCLE(199,120),80,,.75
60 HPAINT(120,60),3,11:HPAINT(200,40),1,11:HPAINT(260,60),0,11
70 HPAINT(160,100),6,11:HPAINT(200,100),4,11:HPAINT(240,100),5,11
80 HPAINT(200,120),2,11
90 FORX=1TO3:FORY=0TO3:READIN(X,Y):NEXT Y,X
100 HPRINT(0,0),"ADDITIVE":HPRINT(0,1),"COLOR":HPRINT(0,2),"MIXTURES"
110 HPRINT(1,6),"COLOR" : HPRINT (1,7),"NUMBERS" : GOSUB600
120 HPRINT(0,18),"COLOR  INTENSITY"
130 HPRINT(0,20),"RED     ":HPRINT(6,20),RD
140 HPRINT(0,21),"GREEN":HPRINT(6,21),GN:HPRINT(0,22),"BLUE":HPRINT(6,22),BL
150 CL=20:LA$=CHR$(95):HPRINT(9,CL),LA$
160 HPRINT(9,23),"ENTER NEW INTENSITY 0 TO 3"
198 '
199 '
200 I$=INKEY$:IFI$=""THEN200 ELSE IF ASC(I$)<48 OR ASC(I$)>51  THEN I$=" "
210 IF I$=" " THEN HPUT(70,CL*8-1)-(90,CL*8+7),2,PSET:IF CL<22 THEN CL=CL+1 :HPRINT(9,CL),LA$:GOTO200  ELSE CL=20:HPRINT(9,CL),LA$:GOTO 200
220 AP=CL-19
230 IF AP=1 GOSUB 300 ELSE IF AP=2 GOSUB 400 ELSEGOSUB 500
240 GOTO200
298 '
299 '
300 RD=VAL(I$):HPUT(50,CL*8-1)-(70,CL*8+7),2,PSET:HPRINT(6,20),RD
310 RR=IN(1,RD) : RG=RR+GG : BR=RR+BB : AL=RR+GG+BB
320 PALETTE 3,RR:PALETTE 1,RG:PALETTE 6,BR:PALETTE 4,AL
330 GOSUB 600
390 RETURN
398 '
399 '
400 GN=VAL(I$):HPUT(50,CL*8-1)-(70,CL*8+7),2,PSET:HPRINT(6,21),GN
410 GG=IN(2,GN) : RG=RR+GG : GB=GG+BB : AL=RR+GG+BB
420 PALETTE 0,GG : PALETTE 1,RG : PALETTE 5,GB : PALETTE 4,AL
430 GOSUB 600
490 RETURN
498 '
499 '
500 BL=VAL(I$):HPUT(50,CL*8-1)-(70,CL*8+7),2,PSET:HPRINT(6,22),BL
510 BB=IN(3,BL) : BR=BB+RR : GB=GG+BB : AL=RR+GG+BB
520 PALETTE 2,BB : PALETTE 6,BR : PALETTE 5,GB : PALETTE 4,AL
530 GOSUB 600
590 RETURN
598 '
599 '
600 HPUT(0,72)-(80,96),3,PSET
610 HPRINT (0,9),RR : HPRINT (3,9),RG : HPRINT (6,9),GG
620 HPRINT (0,10),BR : HPRINT (3,10),AL : HPRINT (6,10),GB
630 HPRINT (3,11),BB
690 RETURN
1000 '
1010 DATA0,4,32,36,0,2,16,18,0,1,8,9
