10 'MAKE CIRCLES USING MOUSE OR
20 'JOYSTICK AND TANDY HI-RES
30 'INTERFACE ON COCO3. MACHINE
40 'LANGUAGE SUBROUTINE RESOLVES
50 '640X192 PIXELS.
60 CLEAR 200,&H7EFF
70 FOR A=&H7F00 TO &H7F73
80 READ H$:POKE A,VAL("&H"+H$)
90 NEXT A
100 DEF USR0=&H7F00
110 PALETTE 0,63:PALETTE 1,0
120 HBUFF 1,189
130 HSCREEN 4
140 HGET(0,0)-(21,17),1
150 GOSUB 360
160 IF(PEEK(&HFF00)AND1)>0 THEN 150
170 PX=X:PY=Y
180 HPUT(LX,LY)-(X+10,Y+4),1
190 C=HPOINT(PX,PY)
200 HSET(PX,PY)
210 HGET(LX,LY)-(X+10,Y+4),1
220 IF(PEEK(&HFF00)AND1)=0 THEN 220
230 GOSUB 360
240 IF(PEEK(&HFF00)AND1)>0 THEN 230
250 HPUT(LX,LY)-(X+10,Y+4),1
260 AX=ABS(X-PX):AY=2*ABS(Y-PY)
270 R=SQR(AX*AX+AY*AY)
280 R=INT(R+.5)
290 HCIRCLE(PX,PY),R
300 IF C=0 THEN HRESET(PX,PY)
310 LX=X-10:IF LX<0 THEN LX=0
320 LY=Y-4:IF LY<0 THEN LY=0
330 HGET(LX,LY)-(X+10,Y+4),1
340 IF(PEEK(&HFF00)AND1)=0 THEN 340
350 GOTO 150
360 H=X:V=Y:X=USR0(0):Y=USR0(1)
370 LH=H-10:IF LH<0 THEN LH=0
380 LV=V-4:IF LV<0 THEN LV=0
390 LX=X-10:IF LX<0 THEN LX=0
400 LY=Y-4:IF LY<0 THEN LY=0
410 HPUT(LH,LV)-(H+10,V+4),1
420 HGET(LX,LY)-(X+10,Y+4),1
430 HLINE(LX,Y)-(X+10,Y),PSET
440 HLINE(X,LY)-(X,Y+4),PSET
450 HRESET(X,Y)
460 RETURN
470 DATA B7,FF,D9,BD,B3,ED,4F,5D,27,02,86,08,B7,7F,74,B6,FF,01,84,F7,BA,7F,74,B7,FF,01,B6,FF,03,84,F7,B7,FF,03,8D,2E,BF,7F,75,8D,29,BC,7F,75,26,F4,1F,10,83,00,01,7D,7F,74,27,0D,44,56,10,83,00,BF,2F,0E,CC,00,BF,20,09,10,83,02,7F,2F,03,CC,02,7F,BD,B4
480 DATA F4,39,B6,FF,20,84,03,8A,FC,B7,FF,20,5F,5A,26,FD,8E,00,94,84,23,B7,FF,20,30,1F,26,FC,30,01,B6,FF,00,2A,F9,39
