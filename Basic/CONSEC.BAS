10 REM"CONSECUTIVE NUMBER PROBLEMS"
20 REM"STEVE BLYN,COMPUTER ISLAND,STATEN ISLAND,NY,1990"
30 X=RND(-TIMER):CLS:PLAY"O3L20GEC":CT=CT+1
40 R=RND(3):Z=Z+1:IF CT=11 THEN 250
50 IF R=1 THEN N=RND(20)+10:C=N+N+1+N+2
60 IF R=2 THEN N=RND(15)*2:C=N+N+2+N+4
70 IF R=3 THEN N=RND(15)*2-1:C=N+N+2+N+4
80 IF R=1 THEN B$="CONSECUTIVE" ELSE IF R=2 THEN B$="CONSECUTIVE EVEN" ELSE B$="CONSECUTIVE ODD"
90 PRINT@2,"CONSECUTIVE NUMBER PROBLEMS";
100 PRINT@32,STRING$(32,224);
110 PRINT@64,"#";Z;"FIND THREE ";B$
120 PRINT@96,"NUMBERS WHOSE SUM IS ";C
130 PRINT@160,"THE FIRST NUMBER IS ";:INPUT A1
140 IF A1=N THEN PLAY"O4L50CDEFGGG":SC=SC+1:PRINT@200,"CORRECT";
150 IF A1<>N THEN SOUND6,3:SOUND 10,3:PRINT@200,"SORRY,  IT'S ";N;
160 PRINT@227,"let's check now";:GOSUB 240
170 PRINT@256,"THE SECOND NUMBER IS ";:IF R=1 THEN A2=N+1 ELSE A2=N+2
180 PRINTA2;:GOSUB 240:PRINT@320,"THE THIRD NUMBER IS  ";:IF R=1 THEN A3=N+2 ELSE A3=N+4
190 PRINTA3;:GOSUB 240:PRINT@373,"----";
200 PRINT@394,"THE TOTAL =";C
210 PRINT@416,STRING$(32,224);:PRINT@452,"PRESS ENTER TO CONTINUE";
220 EN$=INKEY$
230 IF EN$=CHR$(13) THEN 30 ELSE 220
240 FOR T=1 TO 1000:NEXT T:PLAY"O3L20GC":RETURN
250 CLS:PRINT@6,"YOUR SCORE WAS";SC*10;"%"
260 PRINT@96,"PRESS e TO END OR a FOR ANOTHER";
270 EN$=INKEY$
280 IF EN$="E" THEN END ELSE IF EN$="A" THEN RUN ELSE 270
