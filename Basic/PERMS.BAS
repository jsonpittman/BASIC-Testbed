100 ' PROGRAM TO LIST ALL PERMUTATIONS OF A PHRASE
110 CLEAR 2000
120 PRINT
130 LINE INPUT "WORDS: "; WD$
140 A$ = ""
150 FOR I = 1 TO LEN( WD$ )
160 B$ = MID$( WD$, I, 1 )
170 IF ( B$ > CHR$( 64 ) ) AND ( B$ < CHR$( 91 ) ) THEN A$ = A$ + B$
180 NEXT I
190 ON LEN( A$ ) GOTO 190,360,340,320,300,280,260,240,220,200
200 FOR K = 1 TO 10: GOSUB 220: J = 10: GOSUB 380: NEXT
210 END
220 FOR L = 1 TO 9: GOSUB 240: J = 9: GOSUB 380: NEXT
230 IF LEN( A$ ) = 9 THEN GOTO 120 ELSE RETURN
240 FOR M = 1 TO 8: GOSUB 260: J = 8: GOSUB 380: NEXT
250 IF LEN( A$ ) = 8 THEN GOTO 120 ELSE RETURN
260 FOR N = 1 TO 7: GOSUB 280: J = 7: GOSUB 380: NEXT
270 IF LEN( A$ ) = 7 THEN GOTO 120 ELSE RETURN
280 FOR O = 1 TO 6: GOSUB 300: J = 6: GOSUB 380: NEXT
290 IF LEN( A$ ) = 6 THEN GOTO 120 ELSE RETURN
300 FOR P = 1 TO 5: GOSUB 320: J = 5: GOSUB 380: NEXT
310 IF LEN( A$ ) = 5 THEN GOTO 120 ELSE RETURN
320 FOR Q = 1 TO 4: GOSUB 340: J = 4: GOSUB 380: NEXT
330 IF LEN( A$ ) = 4 THEN GOTO 120 ELSE RETURN
340 FOR R = 1 TO 3: GOSUB 360: J = 3: GOSUB 380: NEXT
350 IF LEN( A$ ) = 3 THEN GOTO 120 ELSE RETURN
360 FOR S = 1 TO 2: J = 2: PRINT A$,: GOSUB 380: NEXT
370 IF LEN( A$ ) = 2 THEN GOTO 120 ELSE RETURN
380 ' SUBROUTINE TO ROTATE PORTION OF WORD
390 ' ENTER WITH J=# OF LETTERS IN GROUP
400 ' ENTER WITH A$=WORD
410 ' EXIT WITH A$=ROTATED WORD
420 J = LEN( A$ ) - J
430 B$ = RIGHT$( A$, LEN( A$ ) - J )
440 B$ = RIGHT$( B$, LEN( B$ ) - 1 ) + LEFT$( B$, 1 )
450 A$ = LEFT$( A$, J ) + B$
460 RETURN
