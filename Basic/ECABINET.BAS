1 'ELECTRONIC CABINET
2 'BY DANIEL D. DONER
3 'COPYRIGHT (C) 1992
4 'BY FALSOFT, INC.
5 'RAINBOW MAGAZINE
10 GOTO 10440
1000 '
1010 '  LIMITED INPUTER
1020 '
1030 C=15+A:T$="":B=C
1040 PRINT@C,STRING$(Z,".")
1050 PRINT@B,"";
1060 I$=INKEY$:IF I$="" THEN 1060
1065 IF ASC(I$)=8 AND B>C THEN PRINT@B-1,".";:B=B-1:T$=LEFT$(T$,LEN(T$)-1):GOTO 1050 ELSE IF ASC(I$)=21 THEN B=C:T$="":GOTO 1040 ELSE IF ASC(I$)=13 THEN RETURN
1070 IF B=C+Z THEN 1060
1080 W$="":T$=T$+I$
1090 W$=W$+I$:PRINT@B,W$;:IF B<C+Z THEN B=B+1
1100 GOTO 1050
2000 '
2010 '   PRINT DATA
2020 '
2030 Y=1
2040 CLS
2050 PRINT@5,"RECORD NAME: ";N$:PRINT@28,"#";I;
2060 PRINT@32, STRING$(32,131);
2070 PRINT@67,"DESCRIPTION: ";:PRINT@67+13,A$(I,Y):Y=Y+1
2080 PRINT@99,"SERIAL NO.: ";:PRINT@99+13,A$(I,Y):Y=Y+1
2090 PRINT@131,"MODEL: ";:PRINT@131+13,A$(I,Y):Y=Y+1
2100 PRINT@163,"NO. OWNED: ";:PRINT@163+12,A(I,Y):Y=Y+1
2110 PRINT@195,"VAL OF EACH: ";:PRINT@195+12,A(I,Y):Y=Y+1
2120 PRINT@224,STRING$(32,131);
2130 PRINT@260,"TOTAL VALUE: ";:PRINT USING"$$#####,.##";A(I,4)*A(I,5)
2140 PRINT@323,"<E> -- EDIT   <S> -- SAVE"
2150 PRINT@355,"<L> -- LOAD   <D> -- DELETE"
2160 PRINT@387,"<A> -- ADD    <X> -- EXIT"
2165 PRINT@419,"<P> -- PRINT"
2170 '
2180 '      FILE SELECT
2190 '
2200 IN$=INKEY$:IF IN$="" THEN 2200 ELSE IN=ASC(IN$)
2210 IF IN=94 AND I>=2 THEN I=I-1:GOTO 2000 ELSE IF IN=10 AND I<X THEN I=I+1:GOTO 2000 ELSE IF IN=69 THEN GOSUB 4000:GOTO 2000 ELSE IF IN=65 THEN GOSUB 10250:GOTO 2000 ELSE IF IN=88 THEN GOSUB 10140:GOTO 2000
2220 IF IN=83 THEN GOSUB 7000:GOTO 2000 ELSE IF IN=76 THEN GOSUB 6000 ELSE IF IN=68 THEN GOSUB 10000:GOTO 2000 ELSE IF IN=80 THEN GOSUB 8000:GOTO 2000 ELSE2200
4000 '
4010 '
4020 '  EDIT ROUTINE
4030 '
4040 L=64:P=1
4050 PRINT@L,P$;
4054 PRINT@429,"EDIT :";
4055 PRINT@450,"USE UP-DOWN ARROWS TO SELECT"
4056 PRINT@489,"<SPACE> TO EXIT";
4060 IN$=INKEY$:IF IN$="" THEN 4060 ELSE IN=ASC(IN$)
4070 IF IN=94 AND P>1 THEN L=L-32:P=P-1 ELSE IF IN=10 AND P<=4 THEN L=L+32:P=P+1 ELSE IF IN=32 THEN RETURN ELSE IF IN=13 THEN 4110 ELSE 4060
4080 IF L>64 THEN PRINT@L-32,"  ";
4090 IF L<192 THEN PRINT@L+32,"  ";
4100 GOTO 4050
4110 IF P=1 THEN A=65 ELSE IF P=2 THEN A=97 ELSE IF P=3 THEN A=129 ELSE IF P=4 THEN A=161 ELSE IF P=5 THEN A=193
4120 IF P<4 THEN Z=15 ELSE IF P>4 THEN Z=6 ELSE Z=4
4130 GOSUB 1000
4140 A$(I,P)=T$:IF P=1 AND A$(I,P)="" THEN 4120 ELSE IF P>3 THEN A(I,P)=VAL(A$(I,P)):IF P=4 AND A(I,P)<1 OR P=5 AND A(I,P)<.01 THEN 4120 ELSE A(I,6)=A(I,5)*A(I,4)
4150 RETURN
5910 '
5920 ' LOAD DATA FROM DISK
5930 '
6000 CLS
6010 PRINT@96,"RECORD NAME TO LOAD:";:Z=8:A=102:GOSUB 1000
6020 N$=T$:IF VAL(LEFT$(N$,1))>0 OR N$="" THEN 6010
6030 OPEN "I",#1,N$
6040 FOR X=1 TO 51:FOR Y=1 TO 5
6050 IF EOF(1) THEN 6080
6060 IF Y<4 THEN INPUT#1,A$(X,Y) ELSE INPUT#1,A(X,Y)
6070 NEXT Y,X
6080 CLOSE
6090 I=1:X=X-1:GOTO 2000
7000 '
7010 '  SAVE DATA- NAME CHECK
7020 '
7040 PRINT@448,"IS '";N$;"' CORRECT NAME?":PRINT@480,"(Y/N) ";
7050 IN$=INKEY$:IF IN$="" THEN 7050
7060 IF IN$="N" GOSUB 7500 ELSE IF IN$="Y" GOSUB 7600:RETURN ELSE IF IN$<>="Y" THEN 7050
7070 GOTO 7040
7500 PRINT@448,STRING$(45,32);:PRINT@448,"CORRECT NAME: ";:Z=8:A=451:GOSUB 1000
7510 N$=T$:IF N$="" OR VAL(LEFT$(N$,1))>0 THEN 7500 ELSE RETURN
7590 '
7600 '  SAVE
7610 '
7620 PRINT@448,STRING$(40,32);:PRINT@461,"SAVING"
7630 OPEN "O",#1,N$
7640 FOR F=1 TO X:FOR Y=1 TO 5
7650 IF Y<4 THEN PRINT#1,A$(F,Y) ELSE PRINT#1,A(F,Y)
7660 NEXT Y,F
7670 CLOSE:RETURN
8000 '
8010 '     PRINT ON PAPER
8020 '
8030 PRINT@457,"PRINT: SURE ?"
8040 IN$=INKEY$:IF IN$="" THEN 8040
8050 IF IN$="N" THEN RETURN ELSE IF IN$<>"Y" THEN 8040
8060 PRINT@449,"ALIGN PAPER==> PRESS <ENTER>";:LINE INPUT IN$
8070 PRINT#-2:PRINT#-2:PRINT#-2,"RECORD: ";N$
8080 PRINT#-2,"DESCRIPTION";TAB(23)"SERIAL#";TAB(38)"MODEL";TAB(51)"NO.";TAB(60)"VALUE";TAB(68)"TOTAL VALUE"
8085 PRINT#-2,STRING$(79,"-")
8090 FOR I=1 TO X
8100 PRINT#-2, A$(I,1);TAB(17) A$(I,2);TAB(35) A$(I,3);TAB(52) A(I,4);:PRINT#-2, TAB(59);:PRINT#-2,USING"$$####.##";A(I,5);:PRINT#-2,TAB(68);:PRINT#-2,USING"$$#####.##";A(I,4)*A(I,5)
8110 NEXT I
8120 RETURN
10000 '
10010 '    DELETE DATA
10020 '
10030 PRINT@457,"DELETE (Y/N)?"
10040 IN$=INKEY$:IF IN$="" THEN 10040
10050 IF IN$="N" OR X<1 THEN RETURN ELSE IF IN$<>"Y" THEN 10040
10060 FOR C=I TO X:FOR Y=1 TO 5
10070 A$(C,Y)=A$(C+1,Y):A(C,Y)=A(C+1,Y)
10080 NEXT Y,C
10090 X=X-1:IF I>1 THEN I=I-1
10100 RETURN
10110 '
10120 '  END
10130 '
10140 CLS:PRINT@237,"STOP!"
10150 PRINT@293,"ARE YOU SURE YOU WANT"
10160 PRINT@326,"TO DO THIS ? (Y/N) "
10170 IN$=INKEY$:IF IN$="" THEN 10170
10180 IF IN$="Y" THEN END ELSE IF IN$="N" THEN 2000 ELSE 10170
10190 END
10200 '
10210 '  INPUT DATA
10220 '
10230 CLS
10240 PRINT@131,"RECORD NAME: ";:A=131:Z=8:GOSUB 1000:N$=T$:IF N$="" OR VAL(LEFT$(N$,1))>0 THEN 10240
10250 CLS
10260 X=X+1:Y=1:IF X>50 THEN RETURN
10270 PRINT@6,"PRESS <ENTER> TO QUIT"
10280 PRINT@64,"DESCRIPTION: ";:A=64:Z=15:GOSUB 1000:A$(X,Y)=T$:IF A$(X,1)="" THEN I=X-1:X=X-1:RETURN
10290 PRINT@6,STRING$(21,32)
10300 Y=Y+1
10310 PRINT@96,"SERIAL NO.: ";:A=96:Z=15:GOSUB 1000:A$(X,Y)=T$
10320 Y=Y+1
10330 PRINT@128,"MODEL: ";:A=128:Z=15:GOSUB 1000:A$(X,Y)=T$
10340 Y=Y+1
10350 PRINT@160,"NO. OWNED: ";:A=160:Z=4:GOSUB 1000:A(X,Y)=VAL(T$):IF A(X,Y)<1 THEN 10350
10360 Y=Y+1
10370 PRINT@192,"VALUE OF EACH: ";:A=192:Z=6:GOSUB 1000:A(X,Y)=VAL(T$):IF A(X,Y)<.01 THEN 10370
10380 A(X,Y+1)=A(X,Y)*A(X,Y-1)
10390 GOTO 10250
10400 '
10410 '   INTRODUCTION
10420 '
10430 '
10440 CLS:CLEAR 1000:DIM A$(50,6),A(50,6):P$="=>"
10450 PRINT@97,STRING$(29,131):PRINT@97,CHR$(138);:PRINT@129,CHR$(138);:PRINT@161,CHR$(138);:PRINT@162,STRING$(28,140);:PRINT@125,CHR$(133);:PRINT@157,CHR$(133);:PRINT@189,CHR$(133);
10460 PRINT@130,"THE ELECTRONIC FILE CABINET";
10470 PRINT@266,"BY DAN DONER"
10480 PRINT@324,"NEW RECORD OR OLD RECORD":PRINT@364,"(N/0) ?"
10490 IN$=INKEY$:IF IN$="" THEN 10490
10500 IF IN$="O" THEN GOSUB 6000:GOTO 2000 ELSE IF IN$="N" THEN GOSUB 10200:GOTO 2000 ELSE 10490
